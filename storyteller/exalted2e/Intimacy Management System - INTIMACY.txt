th u(newcobj,STORYTELLER: Intimacy Management System <INTIMACY>,intimacy,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

&CMD`+INTIMACY`PENNMUSH [u(cobj,intimacy)]=$^(?s)\+intimacy(?\:/(\S+))?(?\: +(.*))?$:@attach %!/CMD`+INTIMACY`MAIN
@set [u(cobj,intimacy)]/CMD`+INTIMACY`PENNMUSH=regexp
&CMD`+INTIMACY`RHOSTMUSH [u(cobj,intimacy)]=$^(?s)\+intimacy(?\:/(\\S+))?(?\: +(.*))?$:@attach %!/CMD`+INTIMACY`MAIN
@set [u(cobj,intimacy)]/CMD`+INTIMACY`RHOSTMUSH=regexp
&CMD`+INTIMACY`MAIN [u(cobj,intimacy)]=@attach %!/INC`GETSWITCH=%1;@select/inline cand(strlen(%2),not(strlen(%1)),u(isadmin,%#))=1,{@attach %!/INC`CHECKPC=%2,1},0,{@attach %!/INC`CHECKPC=%#,1};@attach %!/INC`[strfirstof(%q<switch>,MAIN)]=%2,%q<t1>
@set [u(cobj,intimacy)]/CMD`+INTIMACY`[switch(v(game),PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SWITCHES`PLAYER [u(cobj,intimacy)]=BUILD|ERODE|GAIN|LOSE|HELP|URGE|MOTIVATION

&INC`CHECKINT [u(cobj,intimacy)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Intimacy field empty.;@check u(valnum,%0)=@attach %!/INC`MSG=ERROR: Intimacies may only be addressed by their slot number.;@check hasattr(%1/D`INTIMACIES`%0)=@attach %!/INC`MSG=ERROR: Intimacy not found.

&INC`BUILD [u(cobj,intimacy)]=@attach %!/INC`CHECKINT;@stop gt(u(setr,new,add(1,get(%1/D`INTIMACIES`%0`CUR))),u(getstat,%1,D`VIRTUES,Conviction))=@attach %!/INC`MSG=ERROR: That would exceed your Conviction.;&D`INTIMACIES`%0`CUR %1=%q<new>;@attach %!/INC`MSG=You reinforced your '[get(%1/D`INTIMACIES`%0`CONTEXT)]' Intimacy.

&INC`ERODE [u(cobj,intimacy)]=@attach %!/INC`CHECKINT;@select/inline u(setr,new,sub(bound(get(%1/D`INTIMACIES`%0`CUR),0,u(getstat,%1,D`VIRTUES,Conviction)),1))=0,{@attach %!/INC`MSG=Your '[get(%1/D`INTIMACIES`%0`CONTEXT)]' Intimacy was eroded to 0\, and removed.;@attach %!/WIPE=%1,D`INTIMACIES`%0},{&D`INTIMACIES`%0`CUR %1=%q<new>;@attach %!/INC`MSG=You have Eroded your '[get(%1/D`INTIMACIES`%0`CONTEXT)]' Intimacy.}

&INC`GAIN [u(cobj,intimacy)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Intimacy context field empty.;th u(attrib_set,%1,u(setr,attr,D`INTIMACIES`[u(nextslot,%1,D`INTIMACIES)])`CONTEXT,%0);&%q<attr>`CUR %1=u(getstat,%1,D`VIRTUES,Conviction);@attach %!/INC`MSG=Intimacy '%0' added.

&INC`LOSE [u(cobj,intimacy)]=@attach %!/INC`CHECKINT;@attach %!/INC`MSG=You have removed the Intimacy '[get(%1/D`INTIMACIES`%0`CONTEXT)]';@attach %!/WIPE=%1,D`INTIMACIES`%0

&INC`MOTIVATION [u(cobj,intimacy)]=@attach %!/INC`MOTURGE=%0,%1,MOTIVATION
&INC`URGE [u(cobj,intimacy)]=@attach %!/INC`MOTURGE=%0,%1,URGE
&INC`MOTURGE [u(cobj,intimacy)]=@select/inline strlen(%0)=0,{@attach %!/INC`MSG=Your [u(capnames,%2)] has been cleared.;@attach %!/WIPE=%1,D`INTIMACIES`%2},{@check strlen(%0)=@attach %!/INC`MSG=ERROR: [capnames(%1)] field empty.;&D`INTIMACIES`%2 %1=%0;@attach %!/INC`MSG=Your [capnames(%2)] has been set to '%0'}

&INC`MAIN [u(cobj,intimacy)]=@pemit %#=u(header,+intimacies for [name(%1)]);@dolist/inline MOTIVATION URGE={@select/inline gt(strlen(get(%1/D`INTIMACIES`%i0)),0)=1,{@pemit %#=rjust(ansi(h,%i0):,11) [get(%1/D`INTIMACIES`%i0)]}};@dolist/inline filterbool(#lambda/u(valnum,\%0),iter(u(lattr,%1/D`INTIMACIES`*),last(%i0,`)))=@pemit %#=ljust(rjust(ansi(h,%i0),3): [get(%1/D`INTIMACIES`%i0`CONTEXT)],60)[ljust([bound(get(%1/D`INTIMACIES`%i0`CUR),0,u(getstat,%1,D`VIRTUES,Conviction))]/[u(getstat,%1,D`VIRTUES,Conviction)],5)];@pemit %#=u(subheader)

@@ ROLEPLAYING - +INTIMACY
+help/add +intimacy=[u(cobj,intimacy)]/HLP`+INTIMACY
+help/category +intimacy=Roleplaying
&HLP`+INTIMACY [u(Cobj,intimacy)]=[ansi(hc,Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+intimacy)] - Lists current Motivation, Urge, and Intimacies.%R[ansi(h,+intimacy/gain <text>)] - Gain a new Intimacy.%R[ansi(h,+intimacy/lose <id>)] - Lose a specified Intimacy.%R[ansi(h,+intimacy/build <id>)] - Raise Intimacy Health by 1.%R[ansi(h,+intimacy/erode <id>)] - Erode Intimacy Health by 1. Removes at 0 Health.%R[ansi(h,+intimacy/motivation <text>)] - Sets your Motivation.%R[ansi(h,+intimacy/urge <text>)] - Sets your urge if applicable.)]

+shelp/add +intimacy=[u(cobj,intimacy)]/SHLP`+INTIMACY
+shelp/category +intimacy=Characters
&SHLP`+INTIMACY [u(Cobj,intimacy)]=[ansi(hc,Staff Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+intimacy <player>)] - Shows a player's Intimacy information.)]