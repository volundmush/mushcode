
th u(newcobj,Character Data Editor <DATA>,data,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)
@lock/use [u(cobj,data)]=ISADMIN2/1

&CMD`+EDITCHAR`PENNMUSH [u(cobj,data)]=$^\+editchar(?\:/(\S+))?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+EDITCHAR`MAIN
@set [u(cobj,data)]/CMD`+EDITCHAR`PENNMUSH=regexp
&CMD`+EDITCHAR`RHOSTMUSH [u(cobj,data)]=$^\+editchar(?\:/(\\S+))?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+EDITCHAR`MAIN
@set [u(cobj,data)]/CMD`+EDITCHAR`RHOSTMUSH=regexp
&CMD`+EDITCHAR`MAIN [u(cobj,data)]=@attach %!/INC`GETSWITCH=%1;th iter(before(%2,=),u(setq`%va,chr(add(inum(0),64)),%i0),|);@attach %!/INC`CHECKPC=%qa,1,,1;@check strlen(%qb)=@attach %!/INC`MSG=ERROR: No Type entered to %q<switch> Your choices are: [itemize(iter(u(lattr`%va,[u(cobj,data)]/INC`%q<switch>`*,|),last(%i0,`),|,|),|,and,\,)]!;@attach %!/INC`PARTIAL=%qb,iter(u(lattr`%va,%!/INC`%q<switch>`*,|),last(%i0,`),|,|),|,topic,option;@attach [u(cobj,data)]/INC`%q<switch>`%q<topic>=%q<t1>,after(iter(%2,trim(%i0),|,|),elements(%2,1,|)|),squish(squish(trim(%3)),|)
@set [u(cobj,data)]/CMD`+EDITCHAR`[switch(%va,PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&GETCLASS [u(cobj,data)]=u(strfirstof`%va,u(getstat,switch(type(%0),THING,get(%0/OWNER),%0),D`INFO,Class),Mortal)
&GETPOWER [u(cobj,data)]=u(strfirstof`%va,u(getstat,switch(type(%0),THING,get(%0/OWNER),%0),D`INFO,Power),1)
&GETBONUS [u(cobj,data)]=add(u(getstat,%0,D`BONUS,%1),u(GETBONUS`[u(getclass,%0)],%0,%1))

&STARTUP [u(cobj,data)]=@select/inline %va=PennMUSH,{@dolist/inline getclass getpower getbonus={@function %i0=%!,%i0}},RhostMUSH,{@dolist/inline getclass getpower getbonus={@function/privileged getclass=%!,GETCLASS}}

&SWITCHES`ADMIN [u(cobj,data)]=SET|REM|ADD
&SYSTEM`NAME [u(cobj,data)]=EDITCHAR

&DO`ANNOUNCE [u(cobj,data)]=@attach %!/INC`MSG`CHAN=%1;@select/inline strmatch(%#,%0)=0,{@attach %!/INC`MSG=%2,%0;@attach %!/INC`MSG=%3},1,{@attach %!/INC`MSG=%2,%0}

&DO`CHECKSTAT [u(cobj,data)]=@select/inline strlen(u(setr`%va,value,%2))=>0,{@attach %!/INC`VALID`POSINT=%2,%1}

&DO`RENDERSTAT [u(cobj,data)]=th u(setq`%va,msg,if(strlen(%q<value>),'[ansi(h,u(capnames,u(strfirstof`%va,%2,%q<find>)))]' %1 was set to: [ansi(h,%q<value>)].,'[ansi(h,u(capnames,u(strfirstof`%va,%2,%q<find>)))]' %1 was cleared.));@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))]'s %q<msg>},{Your %q<msg>},{[ansi(h,name(%0))]'s %q<msg>}

@@ %0 - player being modified. %1 - message for admin channel. %2 - message for player. %3 - message for admin.

@@ Advantages

&INC`SET`CLASS [u(cobj,data)]=@attach %!/DO`SET`CLASS;@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))]'s [u(setr`%va,msg,Class is now set to: [ansi(h,u(capnames,%q<find>))])]},{Your %q<msg>},{[ansi(h,name(%0))]'s %q<msg>}

&DO`VAL`CLASS [u(cobj,data)]=@check strlen(%2)=@attach %!/INC`MSG=ERROR: No Class name entered.;@attach %!/INC`PARTIAL=%2,u(game_config,STORYTELLER,CLASSES),|,find,sphere;
&DO`SET`CLASS [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`CLASS;@attach [u(cobj,data)]/KEYVALUE`SET=%0,D`INFO,CLASS,u(capnames,%q<find>)

th u(newconf,config,STORYTELLER,CLASSES,MORTAL,Available Classes,LIST)

&INC`SET`POWER [u(cobj,data)]=@attach %!/DO`SET`POWER;@attach %!/DO`ANNOUNCE=%0,{[u(setr`%va,msg,ansi(h,name(%0))'s [u(setr`%va,have,default(u(cobj,data)/POWER`[getclass(%0)],u(strfirstof`%va,v(POWER`DEFAULT),Power)) is now: [ansi(h,%q<entry>)])])]},{Your %q<have>},{%q<msg>}

&DO`VAL`POWER [u(cobj,data)]=@check u(valnum,%2)=@attach %!/INC`MSG=ERROR: [default(u(cobj,data)/POWER`[getclass(%0)],u(strfirstof`%va,v(POWER`DEFAULT),Power))] must be a positive integer.;th u(setq`%va,entry,%2)
&DO`SET`POWER [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`POWER;@attach [u(cobj,data)]/KEYVALUE`SET=%0,D`INFO,POWER,%q<entry>

&INC`SET`WILLPOWER [u(cobj,data)]=@attach %!/DO`SET`WILLPOWER;@attach %!/DO`ANNOUNCE=%0,{u(setr`%va,msg,ansi(h,name(%0))'s [u(setr`%va,have,Willpower is now: [ansi(h,%q<entry>)])])},{Your %q<have>},{%q<msg>}

&DO`VAL`WILLPOWER [u(cobj,data)]=@check u(valnum,%2)=@attach %!/INC`MSG=ERROR: Willpower must be a positive integer.;th u(setq`%va,entry,%2)
&DO`SET`WILLPOWER [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`WILLPOWER;@attach [u(cobj,data)]/KEYVALUE`SET=%0,D`INFO,WILLPOWER,%q<entry>

&INC`SET`INFO [u(cobj,data)]=@attach [u(cobj,data)]/DO`SET`INFO=%0,elements(%1,2,|),%2;@attach %!/DO`RENDERSTAT=%0,Ability

&DO`VAL`INFO [u(cobj,data)]=@check strlen(%1)=@attach %!/INC`MSG=ERROR: No Ability name entered.;@attach %!/INC`PARTIAL=%1,u(u(cobj,data)/FUN`GET`INFO),|,find,info;th u(setq`%va,value,%2)
&DO`SET`INFO [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`INFO;@attach [u(cobj,data)]/KEYVALUE`SET=%0,D`INFO,%q<find>,%q<value>

&FUN`GET`INFO [u(cobj,data)]=u(game_config,STORYTELLER,INFOTYPES)

&WORDLIST`VAL [u(cobj,data)]=@stop u(charsearch,%2,~)=@attach %!/INC`MSG=ERROR: ~ is a system-reserved character.;th u(setq`%va,filtered,u(filter,STRLEN,iter(ucstr(%2),squish(%i0),|,|),|,|))
&KEYVALUE`SET [u(cobj,data)]=@stop u(charsearch,%2 %3,| ~)=@attach %!/INC`MSG=ERROR: ~ and | are system-reserved characters.;@select/inline strlen(%3)=>0,{th u(setstat,%0,%1,ucstr(%2),%3)},{th u(delstat,%0,%1,ucstr(%2))}
&WORDLIST`ADD [u(cobj,data)]=@attach [u(cobj,data)]/WORDLIST`VAL;@dolist/inline/delimit | %q<filtered>={th if(u(valnum,last(switch(%va,PennMUSH,%i0,RhostMUSH,%d0))),u(setq`%va,add,last(switch(%va,PennMUSH,%i0,RhostMUSH,%d0)))[u(setq`%va,word,elements(switch(%va,PennMUSH,%i0,RhostMUSH,%d0),lnum(1,sub(words(switch(%va,PennMUSH,%i0,RhostMUSH,%d0)),1))))],u(setq`%va,word,switch(%va,PennMUSH,%i0,RhostMUSH,%d0))[u(setq`%va,add,1)]);th u(setq`%va,wordlist,setunion(%q<wordlist>,%q<word>,|,|));th u(setstat,%0,%1,%q<word>,add(%q<add>,u(getstat,%0,%1,%q<word>)))};th u(setq`%va,returnlist,iter(ucstr(%q<wordlist>),%i0~[u(getstat,%0,%1,%q<word>)],|,|));th u(setq`%va,returnformat,u(itemize`%va,iter(%q<returnlist>,u(capnames,before(%i0,~))%b%([add(,after(%i0,~),0)]%),|,|),|,and,\,))
&WORDLIST`REM [u(cobj,data)]=@attach [u(cobj,data)]/WORDLIST`VAL;@dolist/inline/delimit | %q<filtered>={th if(u(valnum,last(switch(%va,PennMUSH,%i0,RhostMUSH,%d0))),u(setq`%va,sub,last(switch(%va,PennMUSH,%i0,RhostMUSH,%d0)))[u(setq`%va,word,elements(switch(%va,PennMUSH,%i0,RhostMUSH,%d0),lnum(1,sub(words(switch(%va,PennMUSH,%i0,RhostMUSH,%d0)),1))))],u(setq`%va,word,switch(%va,PennMUSH,%i0,RhostMUSH,%d0))[u(setq`%va,sub,1)]);th u(setq`%va,wordlist,setunion(%q<wordlist>,%q<word>,|,|));@select/inline u(setr`%va,rank,sub(u(getstat,%0,%1,%q<word>),%q<sub>))=>1,{th u(setstat,%0,%1,%q<word>,sub(%q<rank>,%q<sub>))},{th u(delstat,%0,%1,%q<word>)}};th u(setq`%va,returnlist,iter(ucstr(%q<wordlist>),%i0~[bound(u(getstat,%0,%1,%i0),0)],|,|));th u(setq`%va,returnformat,u(itemize`%va,iter(%q<returnlist>,u(capnames,before(%i0,~))%b%([add(after(%i0,~),0)]%),|,|),|,and,\,))

@@ %0 - Target. %1 - Attr. %2 - List. %3 - Entry for keyvalue.
