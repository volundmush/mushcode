th u(newcobj,STORYTELLER: Roll Management System <ROLL>,ROLL,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

th u(newconf,CONFIG,ROLLER,TARGET_NUMBER,8,Target Number,POSINT)
th u(newconf,CONFIG,ROLLER,DOUBLE_NUMBER,,Double success numbers.,WORD)
th u(newconf,CONFIG,ROLLER,AGAIN_NUMBER,10,Default #-Again.,POSINT)
th u(newconf,CONFIG,ROLLER,REROLL_NUMBER,,Default Re-#.,POSINT)
th u(newconf,CONFIG,ROLLER,BOTCHES,0,Enable botches?,BOOL)
th u(newconf,CONFIG,ROLLER,CHANNEL,,Channel to report dice rolls?,LIST)
th u(newconf,CONFIG,ROLLER,CATEGORIES,ATTRIBUTES|SKILLS|ARCANA|DISCIPLINES|MANIFESTATIONS|RENOWN,D`Attributes to stat search?,LIST)

&CMD`+ROLL`PENNMUSH [u(cobj,roll)]=$^(?\:\+)?(st)?(d)?roll(?\:/(\S+))?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+ROLL`MAIN
@set [u(cobj,roll)]/CMD`+ROLL`PENNMUSH=regexp
&CMD`+ROLL`RHOSTMUSH [u(cobj,roll)]=$^(?\:\+)?(st)?(d)?roll(?\:/(\\S+))?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+ROLL`MAIN
@set [u(cobj,roll)]/CMD`+ROLL`RHOSTMUSH=regexp
&CMD`+ROLL`MAIN [u(cobj,roll)]=@stop strmatch(init,%4);@select/inline gt(words(%3,/),0)=1,{@check u(FUN`FLAGCHECK,%3)=@attach %!/INC`MSG=ERROR: [u(itemize`%va,iter(%q<errcheck>,ansi(h,%i0),|,|),|,and,\,)]};@select/inline cand(%q<jid>,isdbref(u(cobj,jms)))=1,{@attach %!/INC`JOBINIT};@select/inline ucstr(%1)=ST,{@check u(isadmin`%va,%#)=@attach %!/INC`MSG=ERROR: Permission denied.;@check strlen(%4)=@attach %!/INC`MSG=ERROR: Who will you roll for?;@attach %!/INC`CHECKPC=%4,1,,1};@check strlen(u(setr`%va,string,switch(%1,ST,%5,%4)))=@attach %!/INC`MSG=ERROR: You must enter a formula of stats and/or numbers to roll. Example: Dexterity + Melee - 3;@check strlen(u(setr`%va,inflate,u(FUN`NORMALIZE,%q<string>)))=@attach %!/INC`MSG=ERROR: Could not format roll formula. Please contact a coder.;@check strlen(u(setr`%va,findnames,u(FUN`FINDNAMES,%q<inflate>,u(strfirstof`%va,%q<t1>,get(%#/D`COVER),%#))))=@attach %!/INC`MSG=ERROR: Could not perform name finding. Please contact a coder.;@check gt(u(setr`%va,totaldice,ulocal(FUN`CALC,u(setr`%va,calcdice,u(FUN`CALCDICE,%q<findnames>,u(strfirstof`%va,%q<t1>,get(%#/D`COVER),%#))))),0)=@attach %!/INC`MSG=ERROR: You have no dice to roll! Check your spelling and ensure that penalties aren't reducing your roll to nothing.;@attach %!/INC`INIT=%1%2;@attach %!/INC`%1ROLL
@set [u(cobj,roll)]/CMD`+ROLL`[switch(%va,PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SYSTEm`NAME [u(cobj,roll)]=DICE

&FUN`FLAGCHECK [u(cobj,roll)]=u(lmin`%va,iter(ucstr(%0),regeditalli(%i0,^\(?P<switch>\\w+?\)\(?P<num>\\d+\)?$,if(hasattr(u(cobj,roll)/FUN`FLAGCHECK`$<switch>),u(FUN`FLAGCHECK`$<switch>,$<num>),u(setq`%va,errcheck,setunion(%q<errcheck>,Unrecognized switch: %i0,|,|))0)),/))

&FUN`FLAGCHECK`MUL [u(cobj,roll)]=if(u(valnum,%0),u(setq`%va,mul,%0)1,u(FUN`FLAGERROR,Multi-Rolls must use whole positive numbers.)0)
&FUN`FLAGCHECK`TN [u(cobj,roll)]=if(u(valnum,%0),u(setq`%va,tn,%0)1,u(FUN`FLAGERROR,Target Numbers must use whole positive numbers.)0)
&FUN`FLAGCHECK`DIF [u(cobj,roll)]=if(u(valnum,%0),if(not(%q<def>),u(setq`%va,dif,%0)1,u(FUN`FLAGERROR,Difficulty and Defense cannot be used together.)),u(FUN`FLAGERROR,Difficulty must use whole positive numbers.)0)
&FUN`FLAGCHECK`SUX [u(cobj,roll)]=if(u(valnum,%0),u(setq`%va,sux,%0)1,u(FUN`FLAGERROR,Successes must use whole positive numbers.)0)
&FUN`FLAGCHECK`PEN [u(cobj,roll)]=if(u(valnum,%0),u(setq`%va,pen,%0)1,u(FUN`FLAGERROR,Penalties must use whole positive numbers.)0)
&FUN`FLAGCHECK`AG [u(cobj,roll)]=if(u(valnum,%0),u(setq`%va,ag,%0)1,u(FUN`FLAGERROR,#-Again must use whole positive numbers.)0)
&FUN`FLAGCHECK`DEF [u(cobj,roll)]=if(u(valnum,%0),if(not(%q<dif>),u(setq`%va,def,%0)1,u(FUN`FLAGERROR,Difficulty and Defense cannot be used together.)),u(FUN`FLAGERROR,Defense Values must use whole positive numbers.)0)
&FUN`FLAGCHECK`RE [u(cobj,roll)]=if(u(valnum,%0),u(setq`%va,re,setunion(%0,%q<re>))1,u(FUN`FLAGERROR,Re-Roll # must use whole positive numbers.)0)
&FUN`FLAGCHECK`DBL [u(cobj,roll)]=if(u(valnum,%0),u(setq`%va,dbl,%0)1,u(FUN`FLAGERROR,Double-# must use whole positive numbers.)0)
&FUN`FLAGCHECK`PRIVATE [u(cobj,roll)]=u(setq`%va,private,1)1
&FUN`FLAGCHECK`PUBLIC [u(cobj,roll)]=u(setq`%va,public,1)1
&FUN`FLAGCHECK`JOB [u(cobj,roll)]=if(u(valnum,%0),u(setq`%va,jid,%0)1,u(FUN`FLAGERROR,Job IDs must be positive integers.)0)

&FUN`FLAGERROR [u(cobj,roll)]=u(setq`%va,errcheck,setunion(%q<errcheck>,%0,|,|))

&INC`INIT [u(cobj,roll)]=th switchall(%0,*d*,u(setq`%va,nodbl,1),*st*,u(setq`%va,dicemode,ST));th u(u(cobj,roll)/FUN`INIT)

&FUN`INIT [u(cobj,roll)]=null([if(%q<tn>,,u(setq`%va,tn,u(game_config,ROLLER,TARGET_NUMBER)))][if(%q<mul>,,u(setq`%va,mul,1))][if(%q<ag>,,u(setq`%va,ag,u(game_config,ROLLER,AGAIN_NUMBER)))][if(%q<re>,,u(setq`%va,re,u(game_config,ROLLER,REROLL_NUMBER)))][if(cor(%q<dbl>,%q<nodbl>),,u(setq`%va,dbl,u(game_config,ROLLER,DOUBLE_NUMBER)))][u(setq`%va,dicemap,u(FUN`DICEMAP,%q<tn>,%q<dbl>,u(game_config,ROLLER,BOTCHES)))])

&FUN`CALC [u(cobj,roll)]=u(ladd`%va,regeditalli(%0,(%[-+%])%B(%\d+),$1$2))
&FUN`CALCDICE [u(cobj,roll)]=regeditalli(%0,v(REG`FORMATROLL),if(isnum($0),floor($0),u(u(cobj,roll)/FUN`STATNUM,$0,%1)))

&FUN`FANCYDICE [u(cobj,roll)]=iter(%0,ansi(switchall(1,eq(u(setr`%va,res,elements(%q<dicemap>,%i0)),0.001),hx,eq(%q<res>,0),hr,eq(%q<res>,1),hg,eq(%q<res>,2),hy,gte(%i0,%q<ag>),u,t(match(%q<re>,%i0)),C),switch(%q<res>,2,<%i0>,0.001,-%i0-,%i0)))

&FUN`FINDNAMES [u(cobj,roll)]=regeditalli(%0,v(REG`FORMATROLL),localize(if(isnum($0),floor($0),switch(u(setr`%va,statcheck,$0),*/*,u(FUN`FINDSPECNAME,u(FUN`FINDSTATNAME,before(%q<statcheck>,/),%1)/[after(%q<statcheck>,/)],%1),u(FUN`FINDSTATNAME,%q<statcheck>,%1)))))

&FUN`FINDSTATNAME [u(cobj,roll)]=u(strfirstof`%va,if(match(v(ROLL`DYNAMICSTATS),%0,|),ucstr(%0)),u(FUN`FINDSTATNAME`SEARCH`EXACT,%0,u(strfirstof`%va,%1,%#)),grab(v(ROLL`DYNAMICSTATS),%0*,|),u(FUN`FINDSTATNAME`SEARCH`WILD,%0,u(strfirstof`%va,%1,%#)),%0)

&FUN`FINDSPECNAME [u(cobj,roll)]=u(strfirstof`%va,if(match(get(u(strfirstof`%va,%1,%#)/D`SPECIALTIES),%0,|),%0),grab(iter(get(u(strfirstof`%va,%1,%#)/D`SPECIALTIES),before(%i0,~),|,|),%0*,|),%0)

&FIL`ISINTSTAT [u(Cobj,roll)]=isint(after(%0,~))

&FUN`FINDSTATNAME`SEARCH`EXACT [u(cobj,roll)]=null(iter(u(game_config,ROLLER,CATEGORIES),if(match(iter(u(filter,ISINTSTAT,get(%1/D`%i0),|,|),first(%i0,~),|,|),%0,|),u(setr`%va,found,%0)[ibreak(1)]),|,|))[ucstr(%q<found>)]

&FUN`FINDSTATNAME`SEARCH`WILD [u(cobj,roll)]=null(iter(u(game_config,ROLLER,CATEGORIES),if(strlen(u(setr`%va,found,grab(iter(u(filter,ISINTSTAT,get(%1/D`%i0),|,|),first(%i0,~),|,|),%0*,|))),%q<found>[ibreak(1)]),|,|))[ucstr(%q<found>)]

&ROLL`CATEGORIES [u(cobj,roll)]=ATTRIBUTES|SKILLS|ARCANA|DISCIPLINES|MANIFESTATIONS|RENOWN

&FUN`STATNUM [u(cobj,roll)]=localize(if(strmatch(%0,*/*),add(u(FUN`STATNUM,before(stripansi(%0),/),%1),u(strfirstof`%va,last(grab(get(%#/D`SPECIALTIES),elements(stripansi(%0),1 2,/,/)*~*,|),~),0)),if(match(v(ROLL`DYNAMICSTATS),%0,|),add(u(ROLL`DYNAMICSTATS`[edit(stripansi(%0),%b,_)],u(strfirstof`%va,%1,%#)),getbonus(u(strfirstof`%va,%1,%#),stripansi(%0))),add(u(firstof`%va,u(FUN`STATNUM`SEARCH,stripansi(%0),u(strfirstof`%va,%1,%#)),0),getbonus(u(strfirstof`%va,%1,%#),stripansi(%0))))))

&FUN`STATNUM`SEARCH [u(cobj,roll)]=null(iter(u(game_config,ROLLER,CATEGORIES),if(strlen(u(setr`%va,found,grab(get(%1/D`%i0),%0~*,|))),%q<found>[ibreak(1)]),|,|))[after(%q<found>,~)]

&FUN`FORMATROLL [u(cobj,roll)]=regeditalli(%0,v(REG`FORMATROLL),u(u(cobj,roll)/FUN`FORMATNUM,$0,u(strfirstof`%va,%1,%#)),%b\\+%b,ansi(hg,%b+%b),%b\\-%b,ansi(hr,%b-%b))

&REG`FORMATROLL [u(cobj,roll)]=(\b\S+(?:\b\s\S+)*\b\)?)

&FUN`FORMATNUM [u(cobj,roll)]=localize(if(isnum(%0),floor(%0),if(u(setr`%va,stat,u(u(cobj,roll)/FUN`STATNUM,%0,%1))[u(setq`%va,bonus,getbonus(%1,before(%0,/)),spec,u(getstat,%1,D`SPECIALTIES,%0))],switch(%0,*/*,if(%q<spec>,ansi(h,ucstr(%0)),ansi(h,ucstr(before(%0,/))[ansi(hx,/[after(%0,/)])])),ansi(h,ucstr(%0)))[ansi(h,<)][ansi(h,sub(%q<stat>,%q<spec>,%q<bonus>))][if(%q<bonus>,ansi(hg,+%q<bonus>))][if(%q<spec>,ansi(hy,+%q<spec>))][ansi(h,>)],ansi(hx,%0<0>))))

&FUN`NORMALIZE [u(cobj,roll)]=squish(regeditall(%0,v(REG`NORMALIZE),%b$<sym>%b))

&REG`NORMALIZE [u(cobj,roll)]=(\s*)?(?P<sym>[\+|\-])(\s*)?

&FUN`ROLLDICE [u(cobj,roll)]=lmath(add,elements(%1,%0))

&FUN`DICEMAP [u(cobj,roll)]=iter(if(%2,0.001,0) [iter(lnum(sub(%0,2)),0)] [iter(lnum(sub(11,%0)),1)],if(cand(%1,gte(inum(0),%1)),2,%i0))

&FUN`INSREG [u(cobj,roll)]=u(setq`%va,%0,linsert(r(%0),-1,%1,|)
&FUN`GETREG [u(cobj,roll)]=elements(r(%0),%1,|,|)

&FUN`ROLL [u(cobj,roll)]=[u(FUN`INSREG,roll%1,%0)][u(FUN`INSREG,roll%1,sort(die(%0,10,1)))][u(FUN`INSREG,roll%1,u(FUN`ROLLDICE,u(FUN`GETREG,roll%1,2),%q<dicemap>))][u(FUN`INSREG,roll%1,u(FUN`FANCYDICE,u(FUN`GETREG,roll%1,2)))][u(FUN`REROLL,u(FUN`GETREG,roll%1,2),roll%1)][u(FUN`INSREG,roll%1,filterbool(#lambda/not(match(%q<re>,\%0)),sort(cat(u(FUN`GETREG,roll%1,2),iter(listq(roll%1-reroll*),u(FUN`GETREG,%i0,2))))))][u(FUN`AGAIN,u(FUN`GETREG,roll%1,2),roll%1)][u(FUN`INSREG,roll%1,add(u(FUN`GETREG,roll%1,3),r(roll%1-rerollsux),r(roll%1-agrollsux)))]

&FUN`REROLL [u(cobj,roll)]=if(words(setinter(%q<re>,%0)),u(setq`%va,%1-rerolls,add(r(%1-rerolls),1))[u(FUN`INSREG,%1-reroll[r(%1-rerolls)],words(filterbool(#lambda/match(%q<re>,\%0),%0)))][u(FUN`INSREG,%1-reroll[r(%1-rerolls)],sort(die(u(FUN`GETREG,%1-reroll[r(%1-rerolls)],1),10,1)))][u(FUN`INSREG,%1-reroll[r(%1-rerolls)],u(FUN`ROLLDICE,u(FUN`GETREG,%1-reroll[r(%1-rerolls)],2),%q<dicemap>))][u(FUN`INSREG,%1-reroll[r(%1-rerolls)],u(FUN`FANCYDICE,u(FUN`GETREG,%1-reroll[r(%1-rerolls)],2)))][u(setq`%va,%1-rerollsux,add(r(%1-rerollsux),u(FUN`GETREG,%1-reroll[r(%1-rerolls)],3)))][u(FUN`REROLL,u(FUN`GETREG,%1-reroll[r(%1-rerolls)],2),%1)])

&FUN`AGAIN [u(cobj,roll)]=if(u(setr`%va,agcount,words(filterbool(#lambda/gte(\%0,%q<ag>),%0))),u(setq`%va,%1-agrolls,add(r(%1-agrolls),1))[u(FUN`INSREG,%1-agroll[r(%1-agrolls)],%q<agcount>)][u(FUN`INSREG,%1-agroll[r(%1-agrolls)],sort(die(u(FUN`GETREG,%1-agroll[r(%1-agrolls)],1),10,1)))][u(FUN`INSREG,%1-agroll[r(%1-agrolls)],u(FUN`ROLLDICE,u(FUN`GETREG,%1-agroll[r(%1-agrolls)],2),%q<dicemap>))][u(FUN`INSREG,%1-agroll[r(%1-agrolls)],u(FUN`FANCYDICE,u(FUN`GETREG,%1-agroll[r(%1-agrolls)],2)))][u(setq`%va,%1-agrollsux,add(r(%1-agrollsux),u(FUN`GETREG,%1-agroll[r(%1-agrolls)],3)))][u(FUN`AGAIN,u(FUN`GETREG,%1-agroll[r(%1-agrolls)],2),%1)])

&FUN`ROLLRESULT [u(cobj,roll)]=if(and(not(isint(%0)),lt(%0,1)),ansi(hx,a botch!),switch(%1,0,if(lt(floor(%0),1),ansi(hr,a failure!),if(gt(floor(%0),1),ansi(hg,floor(%0) successes!),ansi(hg,a success!))),1,if(lte(floor(%0),%2),if(floor(%0),floor(%0) success[if(gt(floor(%0),1),es)]\, but [ansi(hr,a miss.)],no successes! [ansi(hr,a miss!)]),ansi(hg,floor(%0) success[if(gt(floor(%0),1),es)]! [sub(floor(%0),%2)] success[if(gt(sub(floor(%0),%2),1),es)] over DV!)),2,if(lt(floor(%0),%2),if(floor(%0),floor(%0) successes\, but [ansi(hr,a failure.)],no successes! [ansi(hr,a failure!)]),ansi(hg,floor(%0) Success[if(gt(floor(%0),1),es)]!%B[sub(floor(%0),%2)] Threshold Success[if(gt(sub(floor(%0),%2),1),es)]))))

&FUN`RESFORMAT [u(cobj,roll)]=u(setq`%va,formatted,u(FUN`FORMATROLL,%q<findnames>,%q<t1>))[null(iter(lnum(1,%q<mul>),u(FUN`ROLL,%q<totaldice>,%i0)))][switch(%q<dicemode>,ST,if(%q<public>,%n rolls with %q<t1name>'s Stats,You roll for %q<t1name>),job,%n Rolls,if(%q<private>,You Roll,%n rolls))]%b%q<formatted>[if(neq(%q<tn>,u(game_config,ROLLER,TARGET_NUMBER)),%bat TN%q<tn>)][if(%q<def>,\,%bVs DV%q<def>)][if(%q<dif>,\,%bat Difficulty %q<dif>)][if(neq(words(setinter(%q<dbl>,u(game_config,ROLLER,DOUBLE_NUMBER))),words(%q<dbl>)),\,%bWith Double [itemize(sort(%q<dbl>),%b,and,\,)])][if(neq(%q<ag>,u(game_config,ROLLER,AGAIN_NUMBER)),\,%bWith %q<ag>-Again)][if(words(%q<re>),\,%bRe-Rolling [itemize(%q<re>,%b,and,\,)])][if(gt(%q<mul>,1),\,%b%q<mul> times)]: %q<totaldice> Total dice!%R[u(trimlines`%va,iter(lnum(1,%q<mul>),ansi(h,if(gt(%q<mul>,1),u(numth,%i0) Roll,Roll):)%b[u(FUN`GETREG,roll%i0,4)][if(u(setr`%va,rerolls,r(roll%i0-rerolls)),%b[ansi(hx,|)] Re-Rolls:%b[iter(lnum(1,%q<rerolls>),u(FUN`GETREG,roll%i1-reroll%i0,4),%b,|)])][if(u(setr`%va,agains,r(roll%i0-agrolls)),%R[u(trimlines`%va,iter(lnum(1,%q<agains>),ansi(h,if(gt(%q<mul>,1),%t)%q<ag>-Again Roll %i0:)%b[u(FUN`GETREG,roll%i1-agroll%i0,4)],%b,%r))])]%R%TResulting in: [u(FUN`ROLLRESULT,add(sub(%q<sux>,%q<pen>),u(FUN`GETREG,roll%i0,6)),if(%q<def>,1,if(%q<dif>,2,0)),u(strfirstof`%va,%q<def>,%q<dif>))][if(%q<sux>,%R%TBonus Successes: %q<sux>)][if(%q<pen>,%R%TPenalty: %q<pen>)],%b,%r))]

&INC`ROLL [u(cobj,roll)]=th u(setq`%va,results,u(FUN`RESFORMAT));@select/inline t(%q<private>)=1,{@attach %!/INC`MSG=%q<results>},0,{@attach %!/INC`MSG`ROOM=%q<results>};@attach %!/INC`MSG`CHAN=if(%q<private>,\(PRIVATE\)%B)[if(%q<private>,edit(%q<results>,You Roll,%n Rolls),%q<results>)],u(game_config,ROLLER,CHANNEL);@select/inline %q<jid>=>0,{@trigger u(cobj,jms)/INC`JOBROLL=,%q<results>}

&INC`STROLL [u(cobj,roll)]=th u(setq`%va,results,u(FUN`RESFORMAT,,%q<t1>));@select/inline t(%q<public>)=0,{@attach %!/INC`MSG`ROOM=%q<results>},1,{@attach %!/INC`MSG`ROOM=%q<results>};@attach %!/INC`MSG`CHAN=edit(%q<results>,You roll for,%n rolls for),u(game_config,ROLLER,CHANNEL)

&CMD`+ROLLINIT [u(cobj,roll)]=$roll init:@check words(u(setr`%va,list,lvplayers(%l)))=@attach %!/INC`MSG=Nobody to roll init for!;th iter(%q<list>,u(setq`%va,dex,u(firstof`%va,u(getstat,%i0,D`ATTRIBUTES,DEXTERITY,BONUS),0),comp,u(firstof`%va,u(getstat,%i0,D`ATTRIBUTES,COMPOSURE,BONUS),0),bonus,u(firstof`%va,u(getstat,%i0,D`BONUS,initiative),0),roll,u(firstof`%va,rand(1,10),0))[u(attrib_set`%va,%i0,INITROLL,ansi(h,DEXTERITY\(%q<dex>\) + COMPOSURE\(%q<comp>\) + BONUS\(%q<bonus>\) + ROLL\(%q<roll>\) = )%B[ansi(hg,add(%q<dex>,%q<comp>,%q<bonus>,%q<roll>))])]);@attach %!/INC`MSG`ROOM=%n called for an Initiative Roll!;th iter(sortkey(#lambda/stripansi(last(get(\%0/INITROLL))),%q<list>,n),remit(%l,ljust(left(name(%i0):,add(1,config(player_name_len))),add(1,config(player_name_len)))%B[get(%i0/INITROLL)]))


@@ Job add-on

&INC`PLAYERID [u(cobj,roll)]=@check objid(%0)=@attach %!/INC`MSG=ERROR: Invalid Objid!;@select/inline gt(u(setr`%va,pid%1,u(u(cobj,jms)/mysql,get`playerid,%0)),0)=0,{@attach %!/INC`DOSQL=NEW`PLAYERID,%0,name(%0);@attach %!/INC`PLAYERID=%0,%1}

&Q`NEW`PLAYERID [u(cobj,roll)]=INSERT INTO $PLAYERS$ (objid,player_name) VALUES (?,?)
&SQL`TABLE`PLAYERS [u(cobj,roll)]=jobsys_players

&INC`JOBINIT [u(cobj,roll)]=@attach %!/INC`PLAYERID=%:;@check u(u(cobj,jms)/mysql,GET`JOBID,%q<jid>)=@attach %!/INC`MSG=ERROR: Job %0 not found.;@check or(strmatch(u(u(cobj,jms)/mysql,GET`OWNEROBJID,%q<jid>),%:),match(u(u(cobj,jms)/mysql,GET`ANYOBJIDS,%q<jid>),%:),match(u(u(cobj,jms)/mysql,GET`CLAIMOBJIDS,%q<jid>),%:),u(FUN`PERMCHECK,u(setr`%va,cat,u(u(cobj,jms)/mysql,GET`JOBCAT,%q<jid>)),ADMIN,%#))=@attach %!/INC`MSG=ERROR: Permission Denied.;@check eq(u(u(cobj,jms)/mysql,GET`JOBSTATUS,%q<jid>),0)=@attach %!/INC`MSG=The job is already finished.

&INC`JOBROLL [u(cobj,jms)]=@attach %!/INC`DOSQL=ADD`COMMENT,%q<jid>,%q<pid>,0,%1;@attach %!/INC`MSGJOB=%q<jid>,{[u(FUN`MASKNAME,%q<cat>)] sent a DICE ROLL in for [u(FUN`JOBNAME,%q<jid>,%q<cat>)].};@attach %!/INC`MAILJOB=%q<jid>,{[u(FUN`MASKNAME,%q<cat>)] sent a DICE ROLL in for [u(FUN`JOBNAME,%q<jid>,%q<cat>)].%R%RMessage:%R%1};@attach %!/INC`UPDJOB=0,%q<jid>;@attach %!/INC`DOSQL=SET`JOBATTN,not(strmatch(u(mysql,GET`OWNEROBJID,%q<jid>),%:)),%q<jid>;@attach %!/INC`DOSQL=SET`CHECKDATE,%q<jid>,%q<pid>


@@ ROLEPLAYING - +ROLL
+help/add +roll=[u(cobj,roll)]/HLP`+ROLL
+help/category +roll=Roleplaying
&HLP`+ROLL [u(cobj,roll)]=[ansi(h,+roll\[/switches\] <stats to roll>)]%R[ansi(h,+droll\[/switches\] <stats to roll>)]%R%R+roll is for virtually any situation that involves rolling X amount of d10.%R%R<stats to roll> can be any Attribute, Ability, or Stat+Specialty (addressed by attribute or ability followed by a slash and then the text-- example, Melee/Swords. This will ADD BOTH TOGETHER so you don't need to use Melee + Melee/Swords), Willpower, Essence, Crafts, or martial arts styles. It will also accept any whole numbers. Each entry can be seperated by a + or -.%R%RExample of a valid roll formula: +roll Dexterity + Melee + 10%R%RStat entries are not case sensitive, although they do support partial matches. Dex will be matched to Dexterity... however, int could be either Intelligence or Integrity, and it will pick the first its matching algorithm finds, so be careful to enter enough to not be vague.%R%R+droll is the same as +roll, though it does not count 10s as succeses. This is used for rolling extras or damage and so on.%R%RSWITCHES (which can be combined in any order and used repeatedly) are-%R%R/def# - Roll against a given Defense to determine hit or miss and threshold successes. (may not be used with /dif# simultaneously)%R/dif# - Roll against a given difficulty to determine threshold succeses. (may not be used with /def# simultaneously)%R/tn# - change target number.%R/sux# - adds automatic successes.%R/pen# - external penalties to subtract successes from rolls.%R/PRIVATE - only you see the roll's results.%R/mul# - multi-roll. Rolls the given <stats to roll> # amount of times. Note: does not support cumulative penalties for flurries.%R/dbl# - double #. This or above counts as two successes.%R/re# - Re-roll #. Can be used several times, such as /re6/re7/re8.

+shelp/add +roll=[u(cobj,roll)]/SHLP`+ROLL
+shelp/category +roll=ROLEPLAYING
&SHLP`+ROLL [u(cobj,roll)]=In addition to the normal commands, Storytellers may do the following:%R+stroll\[/<switches>\] <player>=<roll string>%R%RIt does exactly what it sounds like.