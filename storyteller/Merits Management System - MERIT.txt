@@ DEPENDENCIES - CORE
th u(newcobj,STORYTELLER: Merits Management System <MERIT>,merit,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

&SWITCHES`PLAYER [u(cobj,merit)]=ADD|RANK|GET|DESC|CONTEXT|DELETE|NOTES|AVAILABLE
&SWITCHES`ADMIN [u(cobj,merit)]=
&SYSTEM`NAME [u(cobj,merit)]=u(strfirstof,%q<sysname>,MERITS)

&DO`ANNOUNCE [u(cobj,merit)]=@select/inline t(strlen(%1))=1,{@attach %!/INC`MSG`CHAN=%1};@select/inline strmatch(%#,%0)=0,{@attach %!/INC`MSG`NOTICE=%2,%0;@attach %!/INC`MSG=%3},1,{@attach %!/INC`MSG`NOTICE=%2,%0}
@@ %0 - player being modified. %1 - message for admin channel. %2 - message for player. %3 - message for admin.

&INC`TARGET [u(cobj,merit)]=@select/inline strmatch(%0,*/*)=1,{@attach %!/INC`CHECKPC=before(%0,/),1;[u(setq,filename,after(%0,/))]},0,{@attach %!/INC`CHECKPC=%#,1;th [u(setq,filename,%0)]};@check u(setr,pers_id,%q<t1id>)=@attach %!/INC`MSG=ERROR: Target has no Persona!;th u(setq,target,%q<t1>)

&Q`LIST`MERIT_NAMES [u(cobj,merit)]=SELECt merit_name FROM vol_story_merits WHERE merit_category_id=? ORDER BY merit_name

&INC`AVAILABLE [u(cobj,merit)]=@attach %!/INC`MSG=AVAILABLE MERITS: [iter(u(mysql3,LIST`MERIT_NAMES,%q<mcatid>),%i0,u(rsep),\,%b)]

&INC`MERNAME [u(cobj,merit)]=@check strlen(u(setr,merit,trim(squish(before(%0,:)))))=@attach %!/INC`MSG=ERROR: [capstr(%q<type>)] field empty.;@check u(setr,merit_id,u(elements,u(setr,merit,u(strfirstof,u(mysql3,FIND`MERIT_ID`EXACT,%q<mcatid>,%q<merit>,%q<merit>),u(mysql3,FIND`MERIT_ID`WILD,%q<mcatid>,sqlescape(%q<merit>)))),2,u(fsep)))=@attach %!/INC`MSG=ERROR: %q<type> not found! Choices are: [ansi(h,iter(u(mysql3,LIST`MERIT,%q<mcatid>),before(%i0,u(fsep)),u(rsep),\,%b))];th u(setq,merit_name,before(%q<merit>,u(fsep)));@select/inline t(u(setr,reqcon,t(u(elements,%q<merit>,3,u(fsep)))))=1,{th u(setq,context,squish(trim(after(%0,:))));@check strlen(%q<context>)=@attach %!/INC`MSG=ERROR: This %q<type> must have a Context!;@stop u(charsearch,%q<context>,[u(rsep)] [u(fsep)] | =)=@attach %!/INC`MSG=ERROR: Contexts may not contain: [u(rsep)] [u(fsep)] | =.}

&Q`FIND`MERIT_ID`EXACT [u(cobj,merit)]=SELECT merit_name,merit_id,merit_require_context FROM vol_story_merits WHERE merit_category_id=? AND (merit_id=? OR merit_name=?)
&Q`FIND`MERIT_ID`WILD [u(Cobj,merit)]=SELECT merit_name,merit_id,merit_require_context FROM vol_story_merits WHERE merit_category_id=? AND merit_name LIKE "!%%"
&Q`LIST`MERIT [u(Cobj,merit)]=SELECT merit_name,merit_id FROM vol_story_merits WHERE merit_category_id=?

&INC`EXISTING [u(cobj,merit)]=@select/inline %q<reqcon>=1,{@check u(setr,persona_merit_id,u(setr,persona_merit,u(strfirstof,u(mysql3,FIND`PERSONA_MERIT_ID_CONTEXT`EXACT,%q<pers_id>,%q<merit_id>,%q<context>),u(mysql3,FIND`PERSONA_MERIT_ID_CONTEXT`WILD,%q<pers_id>,%q<merit_id>,sqlescape(%q<context>)))))=@attach %!/INC`MSG=ERROR: %q<type> not found!},0,{@check u(setr,persona_merit_id,u(mysql,FIND`PERSONA_MERIT_ID,%q<pers_id>,%q<merit_id>))=@attach %!/INC`MSG=ERROR: %q<type> %q<merit_name> not found!};@attach %!/INC`LOADMERIT=%q<persona_merit_id>,1

&Q`FIND`PERSONA_MERIT_ID [u(cobj,merit)]=SELECT persona_merit_id FROM volv_story_persona_merits WHERE persona_id=? AND merit_id=?

&Q`FIND`PERSONA_MERIT_ID_CONTEXT`EXACT [u(cobj,merit)]=SELECT persona_merit_id FROM volv_story_persona_merits WHERE persona_id=? AND merit_id=? AND merit_context=?
&Q`FIND`PERSONA_MERIT_ID_CONTEXT`WILD [u(cobj,merit)]=SELECT persona_merit_id FROM volv_story_persona_merits WHERE persona_id=? AND merit_id=? AND merit_context LIKE "!%%"

&INC`LOADMERIT [u(cobj,merit)]=th u(setq,data,u(mysql3,LOAD_MERIT,%q<persona_merit_id>));th iter(pmid id name context value,u(setr,mer%1%i0,u(elements,%q<data>,inum(0),u(fsep))));th u(setq,mer%1full,if(strlen(r(mer%1context)),r(mer%1name): [r(mer%1context)],r(mer%1name)))

&Q`LOAD_MERIT [u(Cobj,merit)]=SELECT persona_merit_id,merit_id,merit_name,merit_context,merit_value FROM volv_story_persona_merits WHERE persona_merit_id=?

&INC`BEGIN [u(cobj,merit)]=@attach %!/INC`TARGET;@select/inline strmatch(%#,%q<target>)=0,{@check u(isadmin,%#)=@attach %!/INC`MSG=Permission denied.};@stop cand(u(isapproved,%q<target>),not(u(isadmin,%#)))=@attach %!/INC`MSG=Permission denied.;@attach %!/INC`MERNAME

&INC`BEGIN2 [u(cobj,merit)]=@attach %!/INC`TARGET;@select/inline strmatch(%#,%q<target>)=0,{@check u(isadmin,%#)=@attach %!/INC`MSG=Permission denied.};@stop cand(u(isapproved,%q<target>),nor(u(isadmin,%#),%q<notes>))=@attach %!/INC`MSG=Permission denied.;@attach %!/INC`MERNAME;@attach %!/INC`EXISTING

&INC`ADD [u(cobj,merit)]=@attach %!/INC`BEGIN;@stop u(mysql,EXIST`PERSONA_MERIT[if(%q<reqcon>,_CONTEXT)],%q<pers_id>,%q<merit_id>,%q<context>)=@attach %!/INC`MSG=ERROR: Conflicting %q<type>.[if(%q<reqcon>,%bContexts must be unique.)];@check cand(isint(%1),gte(%1,0))=@attach %!/INC`MSG=[capstr(%q<type>)] must have a rank. Ranks must be 0 or greater.;@attach %!/INC`DOSQL=ADD`PERSONA_MERIT[if(%q<reqcon>,_CONTEXT)]/persona_merit_id,%q<pers_id>,%q<merit_id>,%1,%q<context>;@attach %!/DO`ADD=%q<attr>,%q<target>,%q<merit>,%q<context>,%1;th u(setq,msg,gained a new %q<type>: %q<merit_name>[if(%q<context>,:%B%q<context>)] (%1));@select/inline strmatch(%#,%q<target>)=0,{th u(setq,amsg,ansi(h,name(%q<target>)) %q<msg>)};@attach %!/DO`ANNOUNCE=%q<target>,{%q<amsg>},{You %q<msg>},{[ansi(h,name(%q<target>))] %q<msg>}

&Q`EXIST`PERSONA_MERIT_CONTEXT [u(cobj,merit)]=SELECT persona_merit_id FROM volv_story_persona_merits WHERE persona_id=? AND merit_id=? AND merit_context=?
&Q`EXIST`PERSONA_MERIT [u(cobj,merit)]=SELECT persona_merit_id FROM volv_story_persona_merits WHERE persona_id=? AND merit_id=?

&Q`ADD`PERSONA_MERIT_CONTEXT [u(cobj,merit)]=INSERT INTO vol_story_persona_merits (persona_id,merit_id,merit_value,merit_context) VALUES (?,?,?,?)
&Q`ADD`PERSONA_MERIT [u(cobj,merit)]=INSERT INTO vol_story_persona_merits (persona_id,merit_id,merit_value) VALUES (?,?,?)

&INC`DELETE [u(cobj,merit)]=@attach %!/INC`BEGIN2;@attach %!/INC`VERIFY={[ansi(hr,WARNING:)]%BDeleting the %q<mer1full> %q<type>. Are you sure? Enter the same command again within ten seconds to verify.},%q<type> DELETE %q<mer1pmid>,;th u(setq,msg,lost a %q<type>: %q<mer1full> (%q<mer1value>));@select/inline strmatch(%#,%q<target>)=0,{th u(setq,amsg,ansi(h,name(%q<target>)) %q<msg>)};@attach %!/DO`ANNOUNCE=%q<target>,{%q<amsg>},{You %q<msg>},{[ansi(h,name(%q<target>))] %q<msg>};@attach %!/INC`DOSQL=DELETE`PERSONA_MERIT,%q<persona_merit_id>

&Q`DELETE`PERSONA_MERIT [u(cobj,merit)]=DELETE FROM vol_story_persona_merits WHERE persona_merit_id=?

&INC`RANK [u(cobj,merit)]=@attach %!/INC`BEGIN2;@check cand(isint(%1),gte(%1,0))=@attach %!/INC`MSG=%q<mer1full> must have a rank. Ranks must be 0 or greater.;@attach %!/INC`DOSQL=SET`MERIT_RANK,%1,%q<persona_merit_id>;th u(setq,msg,had a %q<type> Rank Changed: %q<mer1full> to [ansi(h,%1)]);@select/inline strmatch(%#,%q<target>)=0,{th u(setq,amsg,ansi(h,name(%q<target>)) %q<msg>)};@attach %!/DO`ANNOUNCE=%q<target>,{%q<amsg>},{You %q<msg>},{[ansi(h,name(%q<target>))] %q<msg>}

&Q`SET`MERIT_RANK [u(Cobj,merit)]=UPDATE vol_story_persona_merits SET merit_value=? WHERE persona_merit_id=?

&INC`CONTEXT [u(cobj,merit)]=@attach %!/INC`BEGIN2;@check %q<reqcon>=@attach %!/INC`MSG=ERROR: This %q<type> does not support Contexts!;@check strlen(u(setr,newcon,squish(trim(%1))))=@attach %!/INC`MSG=ERROR: Context empty.;@stop u(charsearch,%q<newcon>,[u(rsep)] [u(fsep)] | =)=@attach %!/INC`MSG=ERROR: Contexts may not contain: [u(rsep)] [u(fsep)] | =.;@stop cand(u(setr,conflict,u(mysql,EXIST`PERSONA_MERIT[if(%q<reqcon>,_CONTEXT)],%q<pers_id>,%q<merit_id>,%q<newcon>)),neq(%q<conflict>,%q<persona_merit_id>))=@attach %!/INC`MSG=ERROR: Conflicting %q<type>.[if(%q<reqcon>,%bContexts must be unique.)];@attach %!/INC`DOSQL=SET`MERIT_CONTEXT,%q<newcon>,%q<persona_merit_id>;&%q<attr>`CONTEXT %q<target>=%q<newcon>;@attach %!/DO`UPD=%q<target>,%q<attr>;th u(setq,msg,had a %q<type> Context Changed: %q<mer1full> to %q<mer1name>: %q<newcon>);@select/inline strmatch(%#,%q<target>)=0,{th u(setq,amsg,ansi(h,name(%q<target>)) %q<msg>)};@attach %!/DO`ANNOUNCE=%q<target>,{%q<amsg>},{You %q<msg>},{[ansi(h,name(%q<target>))] %q<msg>}

&Q`SET`MERIT_CONTEXT [u(Cobj,merit)]=UPDATE vol_story_persona_merits SET merit_context=? WHERE persona_merit_id=?

&INC`DESC [u(cobj,merit)]=@attach %!/INC`BEGIN2;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Desc empty!;@attach %!/INC`DOSQL=SET`MERIT_DESCRIPTION,%1,%q<persona_merit_id>;th u(setq,msg,had a %q<type> Desc Changed: %q<mer1full>);@select/inline strmatch(%#,%q<target>)=0,{th u(setq,amsg,ansi(h,name(%q<target>)) %q<msg>)};@attach %!/DO`ANNOUNCE=%q<target>,{%q<amsg>},{You %q<msg>},{[ansi(h,name(%q<target>))] %q<msg>}

&Q`SET`MERIT_DESCRIPTION [u(cobj,merit)]=UPDATE vol_story_persona_merits SET merit_description=? WHERE persona_merit_id=?

&INC`NOTES [u(cobj,merit)]=th u(setq,notes,1);@attach %!/INC`BEGIN2;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Notes empty!;@attach %!/INC`DOSQL=SET`MERIT_NOTES,%1,%q<persona_merit_id>;th u(setq,msg,had a %q<type> Note Changed: %q<mer1full>);@select/inline strmatch(%#,%q<target>)=0,{th u(setq,amsg,ansi(h,name(%q<target>)) %q<msg>)};@attach %!/DO`ANNOUNCE=%q<target>,{%q<amsg>},{You %q<msg>},{[ansi(h,name(%q<target>))] %q<msg>}

&Q`SET`MERIT_NOTES [u(cobj,merit)]=UPDATE vol_story_persona_merits SET merit_notes=? WHERE persona_merit_id=?

&INC`GET [u(cobj,merit)]=@attach %!/INC`TARGET;@attach %!/INC`MERNAME;@attach %!/INC`EXISTING;@pemit %#=u(HEADER,name(%q<target>)'s [u(FUN`MERNAME,%q<target>,%q<attr>)] Desc);@pemit %#=decompose(get(%q<target>/%q<attr>`DESC));@select/inline strlen(u(setr,notes,get(%q<target>/%q<attr>`NOTES)))=>0,{@pemit %#=u(separator,Notes);@pemit %#=decompose(%q<notes>)};@pemit %#=u(FOOTER)

&INC`MAIN [u(cobj,merit)]=@attach %!/INC`TARGET;@select/inline t(strlen(%q<filename>))=1,{@attach %!/INC`MERNAME=%q<filename>;@attach %!/INC`EXISTING;@attach %!/INC`DISPLAY},0,{@attach %!/INC`LIST}

&INC`LIST [u(cobj,merit)]=@pemit %#=u(HEADER,name(%q<target>)'s [u(capnames,%q<sysname>)]);@pemit %#=ansi(u(color,%#,COLUMN_NAMES),align(40 30 6,Name,Desc,Rank));@pemit %#=u(SEPARATOR);@dolist/inline/delimit [u(rsep)] [u(mysql3,LIST`PERSONA_MERIT,%q<mcatid>,%q<pers_id>)]={th u(setq,data,u(choosegame,%i0,%d0));th u(setq,name,u(elements,%q<data>,1,u(fsep)));@pemit %#=align(40 30 6,u(pueblize,u(setr,fullname,if(strlen(u(setr,context,elements(%q<data>,2,u(fsep)))),%q<name>: %q<context>,%q<name>)),%q<comm> [name(%q<target>)]/%q<fullname>),left(u(mysql,GET`PERSONA_MERIT_DESCRIPTION,elements(%q<data>,4,u(fsep))),20),repeat(*,elements(%q<data>,3,u(fsep))))};@pemit %#=u(FOOTER)

&Q`LIST`PERSONA_MERIT [u(Cobj,merit)]=SELECT merit_name,merit_context,merit_value,persona_merit_id FROM volv_story_persona_merits WHERE merit_category_id=? AND persona_id=? ORDER BY merit_name,merit_context

&Q`GET`PERSONA_MERIT_DESCRIPTION [u(Cobj,merit)]=SELECT merit_description FROM volv_story_persona_merits WHERE persona_merit_id=?
&Q`GET`PERSONA_MERIT_NOTES [u(Cobj,merit)]=SELECT merit_notes FROM volv_story_persona_merits WHERE persona_merit_id=?

&INC`DISPLAY [u(cobj,merit)]=@attach %!/INC`EXISTING;@pemit %#=u(HEADER,name(%q<target>)'s %q<type>: %q<mer1full>);@pemit %#=u(mysql,GET`PERSONA_MERIT_DESCRIPTION,%q<persona_merit_id>);@select/inline strlen(u(setr,notes,u(mysql,GET`PERSONA_MERIT_NOTES,%q<persona_merit_id>)))=>0,{@pemit %#=u(SEPARATOR,Notes);@pemit %#=%q<notes>;@pemit %#=u(separator)},1,{@pemit %#=u(SEPARATOR)};@@ @pemit %#=ansi(u(color,%#,COLUMN_NAMES),align(20 20,Setby,Seton));@@ @pemit %#=u(separator);@@ @pemit %#=align(20 20,get(%q<target>/%q<attr>`BY),u(fancytime,get(%q<target>/%q<attr>`ON),%#));@pemit %#=u(FOOTER)

@@ CHARACTER - +INFO

&HLP`MERITS [u(cobj,merit)]=The Merit system allows Players to mark down their character's Merits, Flaws, and other notes.%R%R[ansi(hc,Concepts)]%R[align(5 [sub(width(%#),6)],,[ansi(h,Types)] - The actual type of the merit. Examples: Haven\, Resources\, Artifact\, Contacts\, Patron\, etc.%R[ansi(h,context)] - Merits and equivalents can have a CONTEXT\, such as the name of an Artifact or the source of one's resources. When a Merit name is neded\, the context is added in the following style: Artifact: Volcano Cutter. Contexts must be unique for each TYPE you have! You can have an Artifact: Volcano Cutter and an Artifact: Orichalcum Breastplate\, but not two Volcano cutters.)]%R%R[ansi(hc,Managing Merits)]%RNote: Merits may only be edited freely while in Chargen. Only admin may edit them otherwise.%R%R[align(5 [sub(width(%#),6)],,[ansi(h,+merits/available)] - Displays all Merits in the system that you can take.%R[ansi(h,+merits)] - Lists your current Merits.%R[ansi(h,+merits <merit>)] - View details about a Merit.%R[ansi(h,+merits/add <merit>=<value>)] - Creates new Merit. Value must be a number.%R[ansi(h,+merits/rank <merit>=<value>)] - Change's a merit's value.%R[ansi(h,+merits/desc <merit>=<text>)] - Describes a merit with a text entry visible when it's viewed. This is locked on approval.%R[ansi(h,+merits/notes <merit>=<text>)] - Similar to desc\, but doesn't get admin-locked on approval. Useful for keeping notes regarding a Merit.%R[ansi(h,+merits/context <merit>=<new context>)] - Change a merit's context.%R[ansi(h,+merits/get <merit>)] - Retrieve an unformatted version of a Merit's /desc for easy editing.%R[ansi(h,+merits/delete %q<merit>)] - Delete a merit.)]%R%R[u(HLP`MERITS`GAME)]
+help/add Character/+merits=[u(cobj,merit)]/HLP`MERITS


&SHLP`MERITS [u(cobj,merit)]=Staff commands with +merit are identical to the player commands. To target specific players, use <player>/ to view a player with +merit, and <player>/<merit> to use any other commands. For example, +merits/add <person>/Resources: Tribute=3
+shelp/add Character/+merits=[u(cobj,merit)]/SHLP`MERITS
