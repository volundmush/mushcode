th u(newcobj,STORYTELLER: Damage Management System <DAMAGE>,damage,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

&SYSTEM`NAME [u(cobj,damage)]=HEALTH

&CMD`DAMAGE [u(cobj,damage)]=$^(?\:\+)(damage|heal|bonushealth)(?\: +(.+?))?(?\: +(.*))?$:@attach %!/INC`%1=%2,%3
@set [u(cobj,damage)]/CMD`DAMAGE=regexp

&CMD`INIT [u(cobj,damage)]=$^(?\:\+)(init)(?\:/(\S+))?(?\: +(.+?))?$:@attach %!/INC`GETSWITCH=%2;@attach %!/INC`INIT`[u(strfirstof,%q<switch>,MAIN)]=%3
@set [u(cobj,damage)]/CMD`INIT=regexp

&SWITCHES`PLAYER [u(cobj,damage)]=RESET

&INC`DAMAGE [u(cobj,damage)]=@check strlen(%0)=@attach %!/INC`MSG=What kind of damage will you take?;@attach %!/INC`TYPE=%0;@check strlen(%1)=@attach %!/INC`MSG=How much damage will you take?;@check u(valnum,%1)=@attach %!/INC`MSG=Damage must be whole, positive numbers!;th iter(lnum(1,%1),u(FUN`INC`%q<find>,%#));@attach %!/INC`MSG`ROOM=[ansi(h,%n)] takes %1 %q<find> Damage! [switch(%q<find>|[t(%q<bashcount>)]|[t(%q<lethcount>)]|[t(%q<aggcount>)],BASHING|1|0|0,,BASHING|*|1|0,%q<lethcount> Upgraded to Lethal!%B,BASHING|*|1|1,%q<lethcount> Upgraded to Lethal then %q<aggcount> to Aggravated!%B,LETHAL|0|1|0,,LETHAL|0|*|1,%q<aggcount> Upgraded to Aggravated!%B)][capstr(%p)] %q<find> Total is: [u(FUN`GET`%q<find>,%#)]. Total Overall Damage is: [rjust(u(FUN`CURDAMAGE,%#),strlen(u(setr,maxhealth,u(FUN`MAX`HEALTH,%#))),0)]/%q<maxhealth>

&INC`HEAL [u(cobj,damage)]=@check strlen(%0)=@attach %!/INC`MSG=What kind of damage will you heal?;@attach %!/INC`TYPE=%0;@check strlen(%1)=@attach %!/INC`MSG=How much damage will you heal?;@check u(valnum,%1)=@attach %!/INC`MSG=Healing amount must be whole, positive numbers!;th u(setq,healed,min(u(setr,damage,u(FUN`GET`%q<find>,%#)),%1));@check %q<healed>=@attach %!/INC`MSG=You don't have any %q<find> Damage to Heal.;th u(setstat,%#,D`DAMAGE,%q<find>,bound(sub(%q<damage>,%q<healed>),0));@nsremit %l=msgheadroom(HEALTH) [ansi(h,%n)] heals %q<healed> %q<find> Damage. [capstr(%p)] %q<find> Total is: [u(FUN`GET`BASHING,%#)]. Total Overall Damage is: [rjust(u(FUN`CURDAMAGE,%#),strlen(u(setr,maxhealth,u(FUN`MAX`HEALTH,%#))),0)]/%q<maxhealth>

&INC`INIT`MAIN [u(cobj,damage)]=@select/inline strlen(%0)=>0,{@check cand(isint(%0),not(eq(%0,0)))=@attach %!/INC`MSG=ERROR: Initiative changes must be positive or negative integers. It cannot be 0.;th u(attrib_set,%#,D`INIT,u(setr,new,add(u(setr,old,default(%#/D`INIT,3)),%0)));@attach %!/INC`MSG`ROOM=%n [if(gt(%0,0),gained,lost)] [abs(%0)] Initiative. New value: %q<new>},{@nspemit %#=u(header,Initiative);@nspemit %#=ansi(u(color,%#,DAMAGE,COLUMN_NAMES),align(5 25 15,Order,Name,Initiative));@nspemit %#=u(separator);@dolist/inline u(sortinit,u(lvplayers,%l))={@nspemit %#=align(5 25 <15,inum(0),u(moniker,%i0),default(%i0/D`INIT,3))};@nspemit %#=u(subheader)}
&INC`INIT`RESET [u(cobj,damage)]=&D`INIT %#=3;@attach %!/INC`MSG`ROOM=%n has refreshed %p Initiative to 3!

&INC`TYPE [u(cobj,damage)]=@attach %!/INC`PARTIAL=%0,v(VAR`TYPES),|,find,Damage Track

&INC`VALID`HEALTH [u(Cobj,damage)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Health levels field empty.;@check lmath(min,iter(ucstr(%0),or(isnum(%i0),regmatchi(%i0,^\(I|D\)$))))=@attach %!/INC`MSG=ERROR: %0 contains an invalid value. Health values must be a negative number\, I\, or D.

&INC`BONUSHEALTH [u(cobj,damage)]=@select/inline strlen(%0)=>0,{@attach %!/INC`VALID`HEALTH=%0 %1;th u(attrib_set,%#,D`HEALTH,%0 %1);@attach %!/INC`MSG=Your current Bonus Health Levels are: %0 %1},{@wipe %#/D`HEALTH;@attach %!/INC`MSG=You cleared your Bonus Health Levels.}

&FUN`INC`BASHING [u(cobj,damage)]=switch(1,gt(u(FUN`GET`EMPTY,%0),0),u(setstat,%0,D`DAMAGE,Bashing,add(u(FUN`GET`BASHING,%0),1))[u(setq,bashcount,add(1,%q<bashcount>))],u(FUN`INC`LETHAL,%#))

&FUN`INC`LETHAL [u(cobj,damage)]=switch(1,gt(or(u(FUN`GET`EMPTY,%0),u(FUN`GET`BASHING,%0)),0),u(setstat,%0,D`DAMAGE,Lethal,add(u(FUN`GET`Lethal,%0),1))[u(setq,lethcount,add(1,%q<lethcount>))],u(FUN`INC`AGGRAVATED,%#))

&FUN`INC`AGGRAVATED [u(cobj,damage)]=u(setstat,%0,D`DAMAGE,Aggravated,add(u(FUN`GET`Aggravated,%0),1))[u(setq,aggcount,add(1,%q<aggcount>))]

&VAR`TYPES [u(cobj,damage)]=AGGRAVATED|LETHAL|BASHING

&FUN`HEALTHBOXES [u(cobj,damage)]=elements(cat(iter(lnum(add(u(getstat,%0,D`DAMAGE,Aggravated),%3)),A),iter(lnum(add(u(getstat,%0,D`DAMAGE,Lethal),%2)),L),iter(lnum(add(u(getstat,%0,D`DAMAGE,Bashing),%1)),B),iter(lnum(30),E)),lnum(1,%4))

&FUN`FORMATHEALTH [u(cobj,damage)]=localize([u(setq,health,u(FUN`GET`HEALTH,%0))][iter(u(FUN`HEALTHBOXES,%0,,,,words(%q<health>)),ansi(switch(%i0,A,hr,L,+orange,B,hy,E,n),[elements(%q<health>,inum(0))][switch(%i0,A,*,L,X,B,/,E,)]),%B)])

&FUN`CURDAMAGE [u(cobj,damage)]=bound(add(u(getstat,%0,D`DAMAGE,Bashing),u(getstat,%0,D`DAMAGE,Lethal),u(getstat,%0,D`DAMAGE,Aggravated)),0,u(FUN`MAX`HEALTH,%0))

&FUN`GET`BASHING [u(cobj,damage)]=words(u(filter,HEALTH,u(FUN`HEALTHBOXES,%0),%b,%b,B))
&FUN`GET`LETHAL [u(cobj,damage)]=words(u(filter,HEALTH,u(FUN`HEALTHBOXES,%0),%b,%b,L))
&FUN`GET`AGGRAVATED [u(cobj,damage)]=words(u(Filter,HEALTH,u(FUN`HEALTHBOXES,%0),%b,%b,A))
&FUN`GET`EMPTY [u(cobj,damage)]=words(u(filter,HEALTh,u(FUN`HEALTHBOXES,%0),%b,%b,E))

&FIL`HEALTH [u(cobj,damage)]=strmatch(%1,%0)

&FUN`MAX`HEALTH [u(cobj,damage)]=words(u(FUN`GET`HEALTH,%0))

&Q`SELECT`HEALTH_MERIT [u(Cobj,damage)]=SELECT merit_value FROM vol_story_persona_merits WHERE persona_id=? AND merit_id=?
&Q`SELECT`WORD_POWER [u(cobj,damage)]=SELECT wordpower_purchases FROM volv_story_persona_wordpower WHERE persona_id=? AND template_name=? AND stat_name=? AND wordpower_name=?

&TEMPLATE`BASE [u(cobj,damage)]=cat(if(u(mysql,SELECT`HEALTH_MERIT,%1,1016),-0),-0 -1 -1 -2 -2 -4 I,get(%0/D`HEALTH))

&TEMPLATE`MORTAL [u(cobj,damage)]=cat(u(TEMPLATE`BASE,%0,%1),)
&TEMPLATE`SOLAR [u(cobj,damage)]=cat(u(TEMPLATE`BASE,%0,%1),if(u(setr,oxbody,u(mysql,SELECT`WORD_POWER,%1,Solar,Resistance,Ox-Body Technique)),repeat(switch(get(%0/V`PERSONA`STAT`2002),1,-1 -2,2,-1 -2,3,-1 -2 -2,4,-1 -2 -2,5,-0 -1 -2)%B,%q<oxbody>)))
&TEMPLATE`ABYSSAL [u(cobj,damage)]=cat(u(TEMPLATE`BASE,%0,%1),if(u(setr,oxbody,u(mysql,SELECT`WORD_POWER,%1,Abyssal,Resistance,Ox-Body Technique)),repeat(switch(get(%0/V`PERSONA`STAT`2002),1,-1 -2,2,-1 -2,3,-1 -2 -2,4,-1 -2 -2,5,-0 -1 -2)%B,%q<oxbody>)))
&TEMPLATE`INFERNAL [u(cobj,damage)]=cat(u(TEMPLATE`BASE,%0,%1),if(u(setr,oxbody,u(mysql,SELECT`WORD_POWER,%1,218003)),repeat(switch(get(%0/V`PERSONA`STAT`2002),1,-1 -2,2,-1 -2,3,-1 -2 -2,4,-1 -2 -2,5,-0 -1 -2)%B,%q<oxbody>)))
&TEMPLATE`DRAGON-BLOODED [u(cobj,damage)]=cat(u(TEMPLATE`BASE,%0,%1),if(u(setr,oxbody,u(mysql,SELECT`WORD_POWER,%1,Dragon-Blooded,Resistance,Ox-Body Technique)),repeat(switch(get(%0/V`PERSONA`STAT`2002),1,-2 -2,2,-2 -2,3,-1 -2,4,-1 -2,5,-1 -2 -2)%B,%q<oxbody>)))
&TEMPLATE`LUNAR [u(cobj,damage)]=cat(u(TEMPLATE`BASE,%0,%1),if(u(setr,oxbody,u(mysql,SELECT`WORD_POWER,%1,Lunar,Stamina,Ox-Body Technique)),repeat(switch(get(%0/V`PERSONA`STAT`2002),1,-2 -2 -2,2,-2 -2 -2,3,-2 -2 -2 -2,4,-2 -2 -2 -2,5,-2 -2 -2 -2 -4)%B,%q<oxbody>)))

&FUN`GET`HEALTH [u(cobj,damage)]=u(sorthealth,localize(u(TEMPLATE`[get(%0/V`PERSONA`TEMPLATE)],%0,get(%0/D`ID))))

&FUN`HEALTHSORT [u(cobj,damage)]=comp(u(FUN`HEALTHSORT`GET,%0),u(FUN`HEALTHSORT`GET,%1),N)

&FUN`HEALTHSORT`GET [u(cobj,damage)]=switch(%0,-0,1,-1,2,-2,3,-4,4,I,5,D,6)

&SORTHEALTH [u(cobj,damage)]=sortby(FUN`HEALTHSORT,%0)

&SORTINIT [u(cobj,damage)]=revwords(u(SORTINIT`%va,%0,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B)))
&SORTINIT`PENNMUSH [u(cobj,damage)]=sortkey(#lambda/default(\%0/D`INIT,3),%0,n,%1,%2)
&SORTINIT`RHOSTMUSH [u(cobj,damage)]=sortby(#lambda/[lit([ncomp(default(%0/D`INIT,3),default(%1/D`INIT,3))])],%0,%1,%2)
