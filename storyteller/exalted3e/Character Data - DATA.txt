&CONFIG`OPTIONS [u(Cobj,data)]=CLASSES|INFOTYPES|ATTRIBUTES|ABILITIES|ABILITIES_EXTRA|STYLES|SPELLS

@@ Advantages
&POWER`DEFAULT [u(cobj,data)]=Essence

&CONFIG`CLASSES`DEFAULT [u(cobj,data)]=MORTAL|SOLAR|TERRESTRIAL|LUNAR

&DO`SET`ADVANTAGE [u(Cobj,data)]=@attach %!/DO`SET`STAT=%0,%1,%2,Advantage,1
&DO`SET`ATTRIBUTE [u(cobj,data)]=@attach %!/DO`SET`STAT=%0,%1,%2,Attribute,2
&DO`SET`ABILITY [u(cobj,data)]=@attach %!/DO`SET`STAT=%0,%1,%2,Ability,3
&DO`SET`STYLE [u(cobj,data)]=@attach %!/DO`SET`STAT=%0,%1,%2,Style,4
&DO`SET`CRAFT [u(cobj,data)]=@attach %!/DO`SET`STAT=%0,%1,%2,Craft,5

&CHAR`SET`ESSENCE [u(cobj,data)]=@attach [u(cobj,data)]/DO`SET`ADVANTAGE=%0,Essence,%2;@attach %!/INC`MSG=u(setr,msg,ansi(hw,name(%0)) now has the %q<msg>);@attach %!/INC`MSG`CHAN=%q<msg>


&CHAR`SET`CASTE [u(cobj,data)]=@attach %!/DO`SET`X_SPLAT=%0,%1,%2,%3,Caste;@attach [u(cobj,data)]/DO`ANNOUNCE=%0,{[u(setr,msg,ansi(h,name(%0))'s [u(setr,have,Caste is now: [ansi(h,%q<find>)])])]},{Your %q<have>},{%q<msg>}

&CHAR`SET`ASPECT [u(cobj,data)]=@attach %!/DO`SET`X_SPLAT=%0,%1,%2,%3,Aspect;@attach [u(cobj,data)]/DO`ANNOUNCE=%0,{[u(setr,msg,ansi(h,name(%0))'s [u(setr,have,Aspect is now: [ansi(h,%q<find>)])])]},{Your %q<have>},{%q<msg>}

&CHAR`SET`WILLPOWER [u(cobj,data)]=@attach %!/DO`SET`ADVANTAGE=%0,Willpower,%2;@attach %!/INC`MSG=u(setr,msg,ansi(hw,name(%0)) now has the %q<msg>);@attach %!/INC`MSG`CHAN=%q<msg>

&CHAR`SET`ABILITY [u(cobj,data)]=@attach %!/DO`SET`ABILITY=%0,%1,%2,%3;@attach %!/INC`MSG=u(setr,msg,ansi(hw,name(%0)) now has the %q<msg>);@attach %!/INC`MSG`CHAN=%q<msg>

&CHAR`SET`ATTRIBUTE [u(cobj,data)]=@attach %!/DO`SET`ATTRIBUTE=%0,%1,%2,%3;@attach %!/INC`MSG=u(setr,msg,ansi(hw,name(%0)) now has the %q<msg>);@attach %!/INC`MSG`CHAN=%q<msg>

&CHAR`SET`STYLE [u(cobj,data)]=@attach %!/DO`SET`STYLE=%0,%1,%2,%3;@attach %!/INC`MSG=u(setr,msg,ansi(hw,name(%0)) now has the %q<msg>);@attach %!/INC`MSG`CHAN=%q<msg>

&CHAR`SET`CRAFT [u(cobj,data)]=@attach %!/DO`SET`CRAFT=%0,%1,%2,%3;@attach %!/INC`MSG=u(setr,msg,ansi(hw,name(%0)) now has the %q<msg>);@attach %!/INC`MSG`CHAN=%q<msg>

&CHAR`TAG`ABILITY [u(cobj,data)]=@attach %!/DO`TAG`STAT`1=%0,%1,%2,Ability,3;@attach %!/INC`MSG=u(setr,msg,ansi(hw,name(%0))'s %q<msg>);@attach %!/INC`MSG`CHAN=%q<msg>

&CHAR`TAG`ATTRIBUTE [u(cobj,data)]=@attach %!/DO`TAG`STAT`1=%0,%1,%2,Attribute,2;@attach %!/INC`MSG=u(setr,msg,ansi(hw,name(%0))'s %q<msg>);@attach %!/INC`MSG`CHAN=%q<msg>

@@ Specialties
&CHAR`SET`SPECIALTY [u(cobj,data)]=@attach %!/DO`SET`SPECIALTY=%0,%1,%2;@attach %!/INC`MSG=u(setr,msg,[ansi(h,name(%0))]'s [ansi(h,%q<stat_name>/%q<specialty_name>)] Specialty is now: [ansi(h,if(%q<value>,%q<value>,DELETED))]);@attach %!/INC`MSG`CHAN=%q<msg>

&INC`VALID`SPECIALTY_NAME [u(cobj,data)]=@check strlen(u(setr,specialty_name,u(capnames,trim(squish(%0)))))=@attach %!/INC`MSG=ERROR: No specialty name entered!;@check regmatchi(%q<specialty_name>,^\(\\w+|%b\)+$)=@attach %!/INC`MSG=ERROR: Specialty names must be comprised purely of alphanumeric characters and spaces.;

&DO`VAL`SPECIALTY [u(cobj,data)]=@attach %!/DO`VAL`STAT=%0,before(%1,/),%2,Ability,3;@attach %!/INC`VALID`SPECIALTY_NAME=after(%1,/);
&DO`SET`SPECIALTY [u(Cobj,data)]=@attach %!/DO`VAL`SPECIALTY;@attach %!/DO`VAL`VALUE;@select/inline %q<value>=0,{@check u(setr,p_spec_id,u(mysql,EXIST`SPECIALTY,%q<pers>,%q<stat_id>,%q<specialty_name>))=@attach %!/INC`MSG=ERROR: [name(%0)] does not have this Specialty.;@attach %!/INC`DOSQL=DELETE`SPECIALTY,%q<p_spec_id>;@attach %!/INC`MSG=You no longer have the [ansi(h,%q<stat_name>/%q<specialty_name>)] Specialty,%0},{@attach %!/INC`DOSQL=SET`SPECIALTY,%q<pers>,%q<stat_id>,%q<specialty_name>,%q<value>;@attach %!/INC`MSG=You now have the [ansi(h,%q<stat_name>/%q<specialty_name>)] Specialty rated at [ansi(h,%q<value>)],%0}

&Q`EXIST`PERSONA_STAT [u(cobj,data)]=SELECT persona_stat_id FROM vol_story_persona_stats WHERE persona_id=? AND stat_id=?


@@ Native Charms
&DO`VAL`CHARM [u(cobj,data)]=@check u(setr,persona_id,get(%0/D`ID))=@attach %!/INC`MSG=ERROR: No Persona available for [name(%0)]!;@check strlen(u(setr,template,u(mysql,GET`TEMPLATE_NAME,%q<persona_id>)))=@attach %!/INC`MSG=ERROR: Template code error. Contact coder.;@attach %!/DO`VAL`WORDPOWER=%0,%q<template>/%1,%2
&DO`VAL`SORCERY [u(cobj,data)]=@attach %!/DO`VAL`WORDPOWER=%0,Sorcery/%1,%2

&DO`ADD`CHARM [u(cobj,data)]=@attach %!/DO`VAL`CHARM;@attach %!/DO`ADD`WORDPOWER=%0,%q<template>/%1,%2,Charm
&DO`REM`CHARM [u(Cobj,data)]=@attach %!/DO`VAL`CHARM;@attach %!/DO`REM`WORDPOWER=%0,%q<template>/%1,%2,Charm


&DO`ADD`SORCERY [u(cobj,data)]=@attach %!/DO`ADD`WORDPOWER=%0,Sorcery/%1,%2,Spell;
&DO`REM`SORCERY [u(Cobj,data)]=@attach %!/DO`REM`WORDPOWER=%0,Sorcery/%1,%2,Spell;
&DO`TAG`SORCERY [u(cobj,data)]=@attach %!/DO`TAG`WORDPOWER=%0,Sorcery/[before(%1,/)],after(%1,/),Spell,%2;
&DO`ADD`MARTIAL [u(cobj,data)]=@attach %!/DO`ADD`WORDPOWER=%0,Martial Arts/%1,%2,Martial Arts Charm;
&DO`REM`MARTIAL [u(cobj,data)]=@attach %!/DO`REM`WORDPOWER=%0,Martial Arts/%1,%2,Martial Arts Charm;

&CHAR`ADD`CHARM [u(cobj,data)]=@attach %!/DO`ADD`CHARM=%0,%1,%2;@attach %!/INC`MSG=u(setr,msg,ansi(hw,name(%0)) now has the %q<msg>);@attach %!/INC`MSG`CHAN=%q<msg>
&CHAR`REM`CHARM [u(cobj,data)]=@attach %!/DO`REM`CHARM=%0,%1,%2;@attach %!/INC`MSG=u(setr,msg,ansi(hw,name(%0)) now has the %q<msg>);@attach %!/INC`MSG`CHAN=%q<msg>
&CHAR`ADD`OCHARM [u(cobj,data)]=@attach %!/DO`ADD`WORDPOWER=%0,%1,%2;@attach %!/INC`MSG=u(setr,msg,ansi(hw,name(%0)) now has the %q<msg>);@attach %!/INC`MSG`CHAN=%q<msg>
&CHAR`REM`OCHARM [u(cobj,data)]=@attach %!/DO`REM`WORDPOWER=%0,%1,%2;@attach %!/INC`MSG=u(setr,msg,ansi(hw,name(%0)) now has the %q<msg>);@attach %!/INC`MSG`CHAN=%q<msg>

&CHAR`ADD`MARTIAL [u(cobj,data)]=@attach %!/DO`ADD`MARTIAL=%0,%1,%2;@attach %!/INC`MSG=u(setr,msg,ansi(hw,name(%0)) now has the %q<msg>);@attach %!/INC`MSG`CHAN=%q<msg>
&CHAR`REM`MARTIAL [u(cobj,data)]=@attach %!/DO`REM`MARTIAL=%0,%1,%2;@attach %!/INC`MSG=u(setr,msg,ansi(hw,name(%0)) now has the %q<msg>);@attach %!/INC`MSG`CHAN=%q<msg>

&CHAR`ADD`SORCERY [u(cobj,data)]=@attach %!/DO`ADD`SORCERY=%0,%1,%2;@attach %!/INC`MSG=u(setr,msg,ansi(hw,name(%0)) now has the %q<msg>);@attach %!/INC`MSG`CHAN=%q<msg>
&CHAR`REM`SORCERY [u(cobj,data)]=@attach %!/DO`REM`SORCERY=%0,%1,%2;@attach %!/INC`MSG=u(setr,msg,ansi(hw,name(%0)) now has the %q<msg>);@attach %!/INC`MSG`CHAN=%q<msg>
&CHAR`TAG`SORCERY [u(cobj,data)]=@attach %!/DO`TAG`SORCERY=%0,%1,%2;@attach %!/INC`MSG=u(setr,msg,ansi(hw,name(%0))'s %q<msg>);@attach %!/INC`MSG`CHAN=%q<msg>

@@ Evocations
&DO`ADD`EVOCATION [u(Cobj,data)]=@attach %!/DO`ADD`MERIT_WORDPOWER=%0,%1,%2,Artifact,1002;
&DO`REM`EVOCATION [u(cobj,data)]=@attach %!/DO`REM`MERIT_WORDPOWER=%0,%1,%2,Artifact,1002;

&CHAR`ADD`EVOCATION [u(cobj,data)]=@attach %!/DO`ADD`EVOCATION=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))] [u(setr,have,gained [ansi(h,u(u(cobj,data)/FUN`RET`EVOCNAME,%0,%q<slot>))] Evocations: [u(itemize,iter(u(capnames,%q<filtered>),%i0,|,|),|,and,\,)]. Result of addition: %q<returnformat>)]},{You %q<have>},{[ansi(h,name(%0))] gained %q<have>}

&CHAR`REM`EVOCATION [u(cobj,data)]=@attach %!/DO`REM`EVOCATION=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))] [u(setr,have,lost [ansi(h,u(u(cobj,data)/FUN`RET`EVOCNAME,%0,%q<slot>))] Evocations: [u(itemize,iter(u(capnames,%q<filtered>),%i0,|,|),|,and,\,)]. Result of removal: %q<returnformat>)]},{You %q<have>},{[ansi(h,name(%0))] lost %q<have>}

@@ Creation stuff!

&DO`CREATE`CRAFT [u(cobj,data)]=@attach %!/DO`CREATE`SYSSTAT=%0,%1,%2,Craft,5;
&DO`RENAME`CRAFT [u(cobj,data)]=@attach %!/DO`RENAME`SYSSTAT=%0,%1,%2,Craft,5;

&DO`CREATE`RITUAL [u(Cobj,data)]=@attach %!/DO`CREATE`SYSMERIT=%0,%1,%2,Ritual,4;

@@ Health

&CHAR`ADD`HEALTH [u(cobj,data)]=@attach %!/DO`ADD`HEALTH;th u(setq,have,additional Health Levels: [sortby(FUN`HEALTHSORT,ucstr(%2))]);@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))] has gained %q<have>},{You have gained %q<have>},{[ansi(h,name(%0))] has gained %q<have>}

&CHAR`REM`HEALTH [u(cobj,data)]=@attach %!/DO`REM`HEALTH;th u(setq,have,Health Levels: [sortby(FUN`HEALTHSORT,ucstr(%2))]);@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))] has lost %q<have>},{You have lost %q<have>},{[ansi(h,name(%0))] has lost %q<have>}

&DO`VAL`HEALTH [u(cobj,data)]=@check strlen(%2)=@attach %!/INC`MSG=ERROR: Health levels field empty.;@check lmath(min,iter(ucstr(%2),or(isnum(%i0),regmatchi(%i0,^(I|D)$))))=@attach %!/INC`MSG=ERROR: %q<newhealth> contains an invalid value. Health values must be a negative number, I, or D.
&DO`ADD`HEALTH [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`HEALTH;&D`HEALTH %0=sortby(u(cobj,data)/FUN`HEALTHSORT,cat(get(%0/D`HEALTH),ucstr(%2)))
&DO`REM`HEALTh [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`HEALTH;&D`HEALTH %0=sortby(u(cobj,data)/FUN`HEALTHSORT,remove(get(%0/D`HEALTH),ucstr(%2)))

&FUN`HEALTHSORT [u(cobj,data)]=comp(u(FUN`HEALTHSORT`GET,%0),u(FUN`HEALTHSORT`GET,%1),N)

&FUN`HEALTHSORT`GET [u(cobj,data)]=switch(%0,-0,1,-1,2,-2,3,-4,4,I,5,D,6)


@@ SHELP - Characters
&SHLP`CHARACTERS [u(cobj,data)]=+editchar is used to modify all parts of a character's +sheet!%R%R[ansi(h,+editchar/<flag> <player>|<categories|and|objects>\[=<modification>\])]%r%r[ansi(h,FLAG: SET)]%r[ansi(h,Splat)]%t%t=<splat>%r[ansi(h,Caste)]%T%T=<caste>%R[ansi(h,Essence)]%t%t=<value>%R[ansi(h,Willpower)]%t=<value>%R[ansi(h,Attribute)]%t<attribute>=<value>%r[ansi(h,Ability)]%t%t<ability>=<value>%r[ansi(h,Specialty)]%t<ability or attribute>/<specialty>=<value>%R[ansi(h,Style)]%t%t<style>=<value>%r[ansi(h,Craft)]%t%t<field>=<value> (Weaponsmithing, etc)%r%r[ansi(h,FLAG: ADD & REM)]%r[ansi(h,Charm)]%t%t<category>=<charm1>|<charm2>|etc (Native Charms)%r[ansi(h,MACharm)]%t%t<style>=<charm1>|<charm2>|etc (Martial Arts Charms)%r[ansi(h,Evocation)]%t<artifact>=<charm1>|<charm2>|etc (Evocations)%r[ansi(h,OCharm)]%t%t<splat>|<category>=<charm1>|<charm2>|etc (Cross-Splat Charms)%r[ansi(h,Spell)]%t%t<circle>=<spell1>|<spell2>|etc%r[ansi(h,Favored)]%t%t<Attribute/Ability>=<Item1>|<Item2>|etc%r[ansi(h,Supernal)]%t<Attribute/Ability>=<Item1>|<Item2>|etc%r[ansi(h,Health)]%t%t=<level1> <level2> etc (must be -0, -1, D, etc)%r%r[ansi(h,EXAMPLES)]%r%r[ansi(h,+editchar/set Isidoros|Attribute|Dexterity=4)]%r%tSets Isidoros' Dexterity to 4%r[ansi(h,+editchar/rem Isidoros|Health=-0 -1)]%r%tRemoves one -0 and one -1 from Isidoros' permanent Health Pool%R%R[ansi(h,CLEARING VALUES)]%RLeaves Values absolutely clear to remove them entirely! Example:%R+editchar/set Isidoros|Attribute|Dexterity=
+shelp/add Characters/+editchar=[u(cobj,data)]/SHLP`CHARACTERS