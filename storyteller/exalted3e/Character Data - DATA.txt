@@ Advantages
&POWER`DEFAULT [u(cobj,data)]=Essence

&INC`SET`ESSENCE [u(cobj,data)]=@attach %!/INC`SET`POWER
&INC`SET`SPLAT [u(cobj,data)]=@attach %!/INC`SET`CLASS
th u(newconf,config,STORYTELLER,CLASSES,MORTAL|SOLAR,Available Spheres,LIST)

&INC`SET`CASTE [u(cobj,data)]=@attach [u(cobj,data)]/DO`SET`CASTE;@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))]'s [u(setr`%va,msg,Caste is now set to: [ansi(h,u(capnames,%q<find>))])]},{Your %q<msg>},{[ansi(h,name(%0))]'s %q<msg>}

&DO`VAL`CASTE [u(cobj,data)]=th u(setq`%va,class,getclass(%0));@check words(u(setr`%va,castes,get(u(cobj,data)/CASTE`%q<class>)),|)=@attach %!/INC`MSG=[u(capnames,%q<class>)]s do not have Castes.;@check strlen(%2)=@attach %!/INC`MSG=ERROR: No Caste name entered.;@attach %!/INC`PARTIAL=%2,%q<castes>,|,find,caste
&DO`SET`CASTE [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`CASTE;@attach [u(cobj,data)]/KEYVALUE`SET=%0,D`INFO,CASTE,%q<find>

&CASTE`SOLAR [u(cobj,data)]=DAWN|ZENITH|TWILIGHT|NIGHT|ECLIPSE
&CASTE`LUNAR [u(cobj,data)]=FULL MOON|CHANGING MOON|NO MOON|CASTELESS
&CASTE`TERRESTRIAL [u(cobj,data)]=AIR|EARTH|FIRE|WATER|WOOD
&CASTE`ABYSSAL [u(cobj,data)]=DUSK|MIDNIGHT|DAYBREAK|DAY|MOONSHADOW
&CASTE`LIMINAL [u(cobj,data)]=BLOOD|BREATH|FLESH|MARROW|SOIL
&CASTE`SIDEREAL [u(cobj,data)]=JOURNEYS|ENDINGS|BATTLES|SECRETS|SERENITY

&CASTE`ALCHEMICAL [u(cobj,data)]=ORICHALCUM|MOONSILVER|JADE|SOULSTEEL|STARMETAL|ADAMANT
&CASTE`DRAGON-KING [u(cobj,data)]=ANKLOK|MOSOK|PTEROK|RAPTOK
&CASTE`GOD-BLOODED [u(cobj,data)]=DIVINE|GHOST|DEMON|FAE|SOLAR|LUNAR|SIDEREAL|ABYSSAL|INFERNAL
&CASTE`INFERNAL [u(cobj,data)]=SLAYER|FIEND|SCOURGE|MALEFACTOR|DEFILER
&CASTE`JADEBORN [u(cobj,data)]=ARTISAN|WORKER|WARRIOR
&CASTE`RAKSHA [u(cobj,data)]=DIPLOMAT|COURTIER|IMPERIAL RAKSHA|SCRIBE|ENTERTAINER|LUMINARY|ESHU|ORNAMENTAL RAKSHA|WARRIOR|ANARCH|XIA|CATAPHRACT|WORKER|PANJANDRUM|ARTISAN|STRATEGOS|GUIDE|HARBINGER|VAGABOND|NOMAD|FERRYMAN|HERALD|SKALD|DRAGOON|ATTENDANT
&CASTE`SPIRIT [u(cobj,data)]=GOD|DEMON

@@ Attributes

&INC`SET`ATTRIBUTE [u(cobj,data)]=@attach %!/DO`SET`ATTRIBUTE=%0,elements(%1,2,|),%2;@attach %!/DO`RENDERSTAT=%0,Attribute

&DO`VAL`ATTRIBUTE [u(cobj,data)]=@check strlen(%1)=@attach %!/INC`MSG=ERROR: No Attribute name entered.;@attach %!/INC`PARTIAL=%1,u(u(cobj,data)/FUN`GET`ATTRIBUTES),|,find,attribute;@attach [u(cobj,data)]/DO`CHECKSTAT=%0,Attributes,%2
&DO`SET`ATTRIBUTE [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`ATTRIBUTE;@attach [u(cobj,data)]/KEYVALUE`SET=%0,D`ATTRIBUTES,%q<find>,%q<value>

&FUN`GET`ATTRIBUTES [u(cobj,data)]=u(game_config,STORYTELLER,ATTRIBUTES)
&FUN`RET`ATTRIBUTES [u(cobj,data)]=iter(ucstr(get(%0/D`ATTRIBUTES)),before(%i0,~),|,|)

th u(newconf,config,STORYTELLER,ATTRIBUTES,STRENGTH|CHARISMA|PERCEPTION|DEXTERITY|MANIPULATION|INTELLIGENCE|STAMINA|APPEARANCE|WITS,Attributes list,list)

@@ Abilities

&INC`SET`ABILITY [u(cobj,data)]=@attach [u(cobj,data)]/DO`SET`ABILITY=%0,elements(%1,2,|),%2;@attach %!/DO`RENDERSTAT=%0,Ability

&DO`VAL`ABILITY [u(cobj,data)]=@check strlen(%1)=@attach %!/INC`MSG=ERROR: No Ability name entered.;@attach %!/INC`PARTIAL=%1,setdiff(u(u(cobj,data)/FUN`GET`ABILITIES),CRAFT,|,|),|,find,ability;@attach [u(cobj,data)]/DO`CHECKSTAT=%0,Abilities,%2
&DO`SET`ABILITY [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`ABILITY;@attach [u(cobj,data)]/KEYVALUE`SET=%0,D`ABILITIES,%q<find>,%q<value>

&FUN`GET`ABILITIES [u(cobj,data)]=u(game_config,STORYTELLER,ABILITIES)
&FUN`RET`ABILITIES [u(cobj,data)]=iter(ucstr(get(%0/D`ABILITIES)),before(%i0,~),|,|)
&FUN`GET`ABILITIES_EXTRA [u(cobj,data)]=u(game_config,STORYTELLER,ABILITIES_EXTRA)

th u(newconf,config,STORYTELLER,ABILITIES_EXTRA,ARCHERY|BRAWL|MARTIAL ARTS|MELEE|THROWN|WAR|BUREAUCRACY|LINGUISTICS|RIDE|SAIL|SOCIALIZE|ATHLETICS|AWARENESS|DODGE|LARCENY|STEALTH|CRAFT|INVESTIGATION|LORE|MEDICINE|OCCULT|INTEGRITY|PERFORMANCE|PRESENCE|RESISTANCE|SURVIVAL,Abilities list for display,LIST)
th u(newconf,config,STORYTELLER,ABILITIES,ARCHERY|BRAWL|MELEE|THROWN|WAR|BUREAUCRACY|LINGUISTICS|RIDE|SAIL|SOCIALIZE|ATHLETICS|AWARENESS|DODGE|LARCENY|STEALTH|INVESTIGATION|LORE|MEDICINE||CRAFT|OCCULT|INTEGRITY|PERFORMANCE|PRESENCE|RESISTANCE|SURVIVAL,Abilities list,list)

@@ Styles

&INC`SET`STYLES [u(cobj,data)]=@attach [u(cobj,data)]/DO`SET`STYLES=%0,elements(%1,2,|),%2;@attach %!/DO`RENDERSTAT=%0,Style

&DO`VAL`STYLES [u(cobj,data)]=@check strlen(%1)=@attach %!/INC`MSG=ERROR: No Style name entered.;@attach %!/INC`PARTIAL=%1,u(u(cobj,data)/FUN`GET`STYLES),|,find,style;@attach [u(cobj,data)]/DO`CHECKSTAT=%0,Styles,%2
&DO`SET`STYLES [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`STYLES;@attach [u(cobj,data)]/KEYVALUE`SET=%0,D`STYLES,%q<find>,%q<value>

&FUN`GET`STYLES [u(cobj,data)]=u(game_config,STORYTELLER,STYLES)
th u(newconf,CONFIG,STORYTELLER,STYLES,SNAKE STYLE|TIGER STYLE|SINGLE POINT SHINING INTO THE VOID STYLE|WHITE REAPER STYLE|EBON SHADOW STYLE|CRANE STYLE|SILVER-VOICED NIGHTINGALE STYLE|RIGHTEOUS DEVIL STYLE|BLACK CLAW STYLE|DREAMING PEARL COURTESAN STYLE|STEEL DEVIL STYLE,Available Martial Arts,LIST)

@@ Crafts

&INC`SET`CRAFTS [u(cobj,data)]=@attach [u(cobj,data)]/DO`SET`CRAFTS=%0,elements(%1,2,|),%2;@attach %!/DO`RENDERSTAT=%0,Craft

&DO`VAL`CRAFTS [u(cobj,data)]=@check strlen(%1)=@attach %!/INC`MSG=ERROR: No Craft name entered.;@@ @attach %!/INC`PARTIAL=%1,u(u(cobj,data)/FUN`GET`CRAFTS),|,find,Craft;th u(setq`%va,find,%1);@attach [u(cobj,data)]/DO`CHECKSTAT=%0,Crafts,%2
&DO`SET`CRAFTS [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`CRAFTS;@attach [u(cobj,data)]/KEYVALUE`SET=%0,D`CRAFTS,%q<find>,%q<value>

&FUN`RET`CRAFTS [u(cobj,data)]=iter(ucstr(get(%0/D`CRAFTS)),before(%i0,~),|,|)

&FUN`GET`CRAFTS [u(cobj,data)]=u(game_config,STORYTELLER,CRAFTS)
th u(newconf,CONFIG,STORYTELLER,CRAFTS,ARTIFACTS|GEOMANCY|FIRST AGE ARTIFICE,Available Craft Types,LIST)

@@ Specialties

&INC`SET`SPECIALTY [u(cobj,data)]=@attach [u(cobj,data)]/Do`SET`SPECIALTY=%0,elements(%1,2,|),%2;@attach %!/DO`RENDERSTAT=%0,Specialty,%q<find>/%q<spec>

&DO`VAL`SPECIALTY [u(cobj,data)]=@check strlen(u(setr`%va,enstat,before(%1,/)))=@attach %!/INC`MSG=ERROR: No stat entered to specialize in.;@attach %!/INC`PARTIAL=%q<enstat>,setunion(setunion(u(u(cobj,data)/FUN`GET`ABILITIES_EXTRA),u(u(cobj,data)/FUN`GET`ATTRIBUTES),|,|),u(u(cobj,data)/FUN`RET`STYLES,%0),|,|),|,find,stat;@check strlen(u(setr`%va,spec,after(%1,/)))=@attach %!/INC`MSG=ERROR: No Specialty name entered.;@attach [u(cobj,data)]/DO`CHECKSTAT=%0,Specialties,%2
&DO`SET`SPECIALTY [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`SPECIALTY;@attach [u(cobj,data)]/KEYVALUE`SET=%0,D`SPECIALTIES,%q<find>/%q<spec>,%q<value>

&FUN`RET`SPECIALTIES [u(cobj,data)]=iter(ucstr(get(%0/D`SPECIALTIES)),before(%i0,~),|,|)

@@ Native Charms



&CHARMTYPE [u(cobj,data)]=localize(switch(u(setr`%va,class,getclass(%0)),GOD-BLOODED,switch(1,t(match(DIVINE DEMON,u(setr`%va,caste,u(getstat`%va,%0,D`INFO,CASTE)))),Spirit,strmatch(%q<caste>,Fae),Raksha,t(match(SOLAR LUNAR ABYSSAL INFERNAL SIDEREAL)),%q<caste>,MORTAL),%q<class>))

&INC`ADD`CHARM [u(cobj,data)]=@attach [u(cobj,data)]/DO`ADD`CHARM=%0,u(charmtype,%0)/[elements(%1,2,|)],%2;@attach [u(cobj,data)]/INC`ADD`CHARM`REPORT
&INC`REM`CHARM [u(cobj,data)]=@attach [u(cobj,data)]/DO`REM`CHARM=%0,u(charmtype,%0)/[elements(%1,2,|)],%2;@attach [u(cobj,data)]/INC`REM`CHARM`REPORT

&INC`ADD`CHARM`REPORT [u(cobj,data)]=@attach %!/DO`ANNOUNCE=%0,{[u(setr`%va,msg,ansi(h,name(%0)) gained the [u(setr`%va,have,ansi(h,u(capnames,%q<find> %q<find2>)) Charms [u(itemize`%va,iter(u(capnames,%q<filtered>),%i0,|,|),|,and,\,)]. Result of additions: %q<returnformat>)])]},{You now have the %q<have>},{%q<msg>}
&INC`REM`CHARM`REPORT [u(cobj,data)]=@attach %!/DO`ANNOUNCE=%0,{[u(setr`%va,msg,ansi(h,name(%0)) lost the [u(setr`%va,have,ansi(h,u(capnames,%q<find> %q<find2>)) Charms: [u(itemize`%va,iter(u(capnames,%q<filtered>),%i0,|,|),|,and,\,)]. Result of removal: %q<returnformat>)])]},{You no longer have the %q<have>},{%q<msg>}

&INC`ADD`OCHARM [u(cobj,data)]=@attach %!/DO`ADD`CHARM=%0,elements(%1,2,|)/[elements(%1,3,|)],%2;@attach %!/INC`ADD`CHARM`REPORT
&INC`REM`OCHARM [u(cobj,data)]=@attach %!/DO`REM`CHARM=%0,elements(%1,2,|)/[elements(%1,3,|)],%2;@attach %!/INC`REM`CHARM`REPORT

&DO`VAL`CHARM [u(cobj,data)]=@check strlen(before(%1,/))=@attach %!/INC`MSG=ERROR: No Charmtype entered.;@attach %!/INC`PARTIAL=before(%1,/),u(u(cobj,data)/FUN`GET`CHARMTYPES),|,find,charmtype;@check strlen(after(%1,/))=@attach %!/INC`MSG=ERROR: No Charm Category entered.;@attach %!/INC`PARTIAL=after(%1,/),u(u(cobj,data)/FUN`GET`CHARMTYPES`%q<find>),|,find2,Charm Category;@check strlen(u(setr`%va,value,%2))=@attach %!/INC`MSG=ERROR: No Charms entered.
&DO`ADD`CHARM [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`CHARM;@attach [u(cobj,data)]/WORDLIST`ADD=%0,D`CHARMS`%q<find>`%q<find2>,%q<value>
&DO`REM`CHARM [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`CHARM;@attach [u(cobj,data)]/WORDLIST`REM=%0,D`CHARMS`%q<find>`%q<Find2>,%q<value>

&FUN`GET`CHARMTYPES [u(cobj,data)]=SOLAR|ABYSSAL|TERRESTRIAL|LUNAR|SIDEREAL|INFERNAL|SPIRIT

&FUN`GET`CHARMTYPES`SOLAR [u(cobj,data)]=u(FUN`GET`ABILITIES)
&FUN`GET`CHARMTYPES`ABYSSAL [u(cobj,data)]=u(FUN`GET`ABILITIES)
&FUN`GET`CHARMTYPES`TERRESTRIAL [u(cobj,data)]=u(FUN`GET`ABILITIES)
&FUN`GET`CHARMTYPES`LUNAR [u(cobj,data)]=u(FUN`GET`ATTRIBUTES)
&FUN`GET`CHARMTYPES`SIDEREAL [u(cobj,data)]=u(FUN`GET`ABILITIES)
&FUN`GET`CHARMTYPES`INFERNAL [u(cobj,data)]=u(FUN`GET`ABILITIES)
&FUN`GET`CHARMTYPES`SPIRIT [u(cobj,data)]=UNIVERSAL

@@ &VAR`CHARMS`ALCHEMICAL [u(cobj,data)]=COMBAT|SURVIVAL|SPEED_AND_MOBILITY|SOCIAL|STEALTH_AND_DISGUISE|ANALYTIC_AND_COGNITIVE|LABOR_AND_UTILITY|SUBMODULES|GENERAL|MASS_COMBAT|SPIRITUAL
@@ &VAR`CHARMS`RAKSHA [u(cobj,data)]=MASK|HEART|CUP|RING|STAFF|SWORD|WAY
@@ &VAR`CHARMS`SPIRIT [u(cobj,data)]=GENERAL|UNIVERSAL|AEGIS|BLESSINGS|CURSES|DIVINATIONS|DIVINE_WORKS|EDGES|EIDOLA|ENCHANTMENTS|INHABITINGS|RELOCATIONS|SENDINGS|TANTRA
@@ &VAR`CHARMS`MORTAL [u(cobj,data)]=UNIVERSAL
@@ &VAR`CHARMS`GHOST [u(cobj,data)]=UNIVERSAL
@@ &VAR`CHARMS`JADEBORN [u(cobj,data)]=FOUNDATION|WORKER|WARRIOR|ARTISAN|ENLIGHTENED|CHAOS

@@ Martial Arts Charms

&INC`ADD`MACHARM [u(cobj,data)]=@attach %!/DO`ADD`MACHARM=%0,elements(%1,2,|),%2;;@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))] [u(setr`%va,have,gained [ansi(h,u(u(cobj,data)/FUN`GET`STYLENAME,%0,%q<slot>))] Charms: [u(itemize`%va,iter(u(capnames,%q<filtered>),%i0,|,|),|,and,\,))]. Result of addition: %q<returnformat>)]},{You %q<have>},{[ansi(h,name(%0))] gained %q<have>}

&INC`REM`MACHARM [u(cobj,data)]=@attach %!/DO`REM`MACHARM=%0,elements(%1,2,|),%2;;@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))] [u(setr`%va,have,lost [ansi(h,u(u(cobj,data)/FUN`GET`STYLENAME,%0,%q<slot>))] Charms: [u(itemize`%va,iter(u(capnames,%q<filtered>),%i0,|,|),|,and,\,))]. Result of removal: %q<returnformat>)]},{You %q<have>},{[ansi(h,name(%0))] lost %q<have>}

&DO`VAL`MACHARM [u(cobj,data)]=@check strlen(%1)=@attach %!/INC`MSG=ERROR: No Style entered.;@check words(u(setr`%va,styles,u(u(cobj,data)/FUN`RET`STYLES,%0)),|)=@attach %!/INC`MSG=ERROR: [switch(%0,%#,You don't,name(%0) doesn't)] know any styles.;@attach %!/INC`PARTIAL=%1,%q<styles>,|,style,style;th u(setq`%va,slot,u(strfirstof`%va,elements(first(u(wildgrepi`%va,%0,D`CHARMS`MACHARMS`*`NAME,%q<style>)),4,`,`),u(nextslot,%0,D`CHARMS`MACHARMS)));@check strlen(u(setr`%va,value,%2))=@attach %!/INC`MSG=ERROR: No Charms entered.

&DO`ADD`MACHARM [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`MACHARM;@attach [u(cobj,data)]/WORDLIST`ADD=%0,D`CHARMS`MACHARMS`%q<slot>,%q<value>;&D`CHARMS`MACHARMS`%q<slot>`NAME %0=ucstr(%q<style>)

&DO`REM`MACHARM [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`MACHARM;@attach [u(cobj,data)]/WORDLIST`REM=%0,D`CHARMS`MACHARMS`%q<slot>,%q<value>;@select/inline strlen(get(%0/D`CHARMS`MACHARMS`%q<slot>))=0,{@attach %!/WIPE=%0,D`CHARMS`MACHARMS`%q<slot>}

&FUN`RET`STYLES [u(cobj,data)]=iter(ucstr(get(%0/D`STYLES)),before(%i0,~),|,|)
&FUN`GET`STYLENAME [u(cobj,data)]=u(capnames,default(%0/D`CHARMS`MACHARMS`%1`NAME,Unnamed Style))

@@ Evocations
&INC`ADD`EVOCATION [u(cobj,data)]=@attach %!/DO`ADD`EVOCATION=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))] [u(setr`%va,have,gained [ansi(h,u(u(cobj,data)/FUN`RET`EVOCNAME,%0,%q<slot>))] Evocations: [u(itemize`%va,iter(u(capnames,%q<filtered>),%i0,|,|),|,and,\,)]. Result of addition: %q<returnformat>)]},{You %q<have>},{[ansi(h,name(%0))] gained %q<have>}

&INC`REM`EVOCATION [u(cobj,data)]=@attach %!/DO`REM`EVOCATION=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))] [u(setr`%va,have,lost [ansi(h,u(u(cobj,data)/FUN`RET`EVOCNAME,%0,%q<slot>))] Evocations: [u(itemize`%va,iter(u(capnames,%q<filtered>),%i0,|,|),|,and,\,)]. Result of removal: %q<returnformat>)]},{You %q<have>},{[ansi(h,name(%0))] lost %q<have>}

&DO`VAL`EVOCATION [u(cobj,data)]=@check strlen(u(setr`%va,style,ucstr(%1)))=@attach %!/INC`MSG=ERROR: No Artifact Name entered.;th u(setq`%va,slot,u(strfirstof`%va,elements(first(u(wildgrepi`%va,%0,D`CHARMS`EVOCATIONS`*`NAME,%q<style>)),4,`,`),u(nextslot,%0,D`CHARMS`EVOCATIONS)));@check strlen(u(setr`%va,value,%2))=@attach %!/INC`MSG=ERROR: No Charms entered.

&DO`ADD`EVOCATION [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`EVOCATION;@attach [u(cobj,data)]/WORDLIST`ADD=%0,D`CHARMS`EVOCATIONS`%q<slot>,%q<value>;&D`CHARMS`EVOCATIONS`%q<slot>`NAME %0=ucstr(%q<style>)

&DO`REM`EVOCATION [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`EVOCATION;@attach [u(cobj,data)]/WORDLIST`REM=%0,D`CHARMS`EVOCATIONS`%q<slot>,%q<value>;@select/inline strlen(get(%0/D`CHARMS`EVOCATIONS`%q<slot>))=0,{@attach %!/WIPE=%0,D`CHARMS`EVOCATIONS`%q<slot>}

&FUN`RET`EVOCNAME [u(cobj,data)]=u(capnames,default(%0/D`CHARMS`EVOCATIONS`%1`NAME,Unnamed Artifact))

@@ Health

&INC`ADD`HEALTH [u(cobj,data)]=@attach %!/DO`ADD`HEALTH;th u(setq`%va,have,additional Health Levels: [sortby(FUN`HEALTHSORT,ucstr(%2))]);@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))] has gained %q<have>},{You have gained %q<have>},{[ansi(h,name(%0))] has gained %q<have>}

&INC`REM`HEALTH [u(cobj,data)]=@attach %!/DO`REM`HEALTH;th u(setq`%va,have,Health Levels: [sortby(FUN`HEALTHSORT,ucstr(%2))]);@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))] has lost %q<have>},{You have lost %q<have>},{[ansi(h,name(%0))] has lost %q<have>}

&DO`VAL`HEALTH [u(cobj,data)]=@check strlen(%2)=@attach %!/INC`MSG=ERROR: Health levels field empty.;@check lmath(min,iter(ucstr(%2),or(isnum(%i0),regmatchi(%i0,^(I|D)$))))=@attach %!/INC`MSG=ERROR: %q<newhealth> contains an invalid value. Health values must be a negative number, I, or D.
&DO`ADD`HEALTH [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`HEALTH;&D`HEALTH %0=sortby(u(cobj,data)/FUN`HEALTHSORT,cat(get(%0/D`HEALTH),ucstr(%2)))
&DO`REM`HEALTh [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`HEALTH;&D`HEALTH %0=sortby(u(cobj,data)/FUN`HEALTHSORT,remove(get(%0/D`HEALTH),ucstr(%2)))

&FUN`HEALTHSORT [u(cobj,data)]=comp(u(FUN`HEALTHSORT`GET,%0),u(FUN`HEALTHSORT`GET,%1),N)

&FUN`HEALTHSORT`GET [u(cobj,data)]=switch(%0,-0,1,-1,2,-2,3,-4,4,I,5,D,6)

@@ Spells

&INC`ADD`SPELL [u(cobj,data)]=@attach %!/DO`ADD`SPELL=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{[u(setr`%va,msg,ansi(h,name(%0)) now has the [u(setr`%va,have,ansi(h,u(capnames,%q<find>)) Spells [u(itemize`%va,iter(u(capnames,%q<filtered>),%i0,|,|),|,and,\,))]. Result of additions: %q<returnformat>)])]},{You now have the %q<have>},{%q<msg>}
&INC`REM`SPELL [u(cobj,data)]=@attach %!/DO`REM`SPELL=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{[u(setr`%va,msg,ansi(h,name(%0)) no longer have the [u(setr`%va,have,ansi(h,u(capnames,%q<find>)) Spells: [u(itemize`%va,iter(u(capnames,%q<filtered>),%i0,|,|),|,and,\,))]. Result of removal: %q<returnformat>)])]},{You no longer have the %q<have>},{%q<msg>}

&DO`VAL`SPELL [u(cobj,data)]=@check strlen(%1)=@attach %!/INC`MSG=ERROR: No Spell Category entered.;@attach %!/INC`PARTIAL=%1,u(u(cobj,data)/FUN`GET`SPELLS),|,find,Spell Category;@check strlen(u(setr`%va,value,%2))=@attach %!/INC`MSG=ERROR: No Spells entered.

&DO`ADD`SPELL [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`SPELL;@attach [u(cobj,data)]/WORDLIST`ADD=%0,D`SPELLS`%q<find>,%q<value>
&DO`REM`SPELL [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`SPELL;@attach [u(cobj,data)]/WORDLIST`REM=%0,D`SPELLS`%q<find>,%q<value>;@select/inline strlen(get(%0/D`SPELLS`%q<find>))=0,{@wipe %0/D`SPELLS`%q<find>;@select/inline strlen(get(%0/D`SPELLS))=0,{@wipe %0/D`SPELLS}}

&FUN`GET`SPELLS [u(cobj,data)]=u(game_config,STORYTELLER,SPELLS)
th u(newconf,CONFIG,STORYTELLER,SPELLS,TERRESTRIAL|CELESTIAL|SOLAR|SHADOWLANDS|LABYRINTH|VOID,Available Spell Categories,LIST)

@@ Favored

&INC`ADD`FAVORED [u(cobj,data)]=@attach %!/DO`ADD`FAVORED=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{[u(setr`%va,msg,ansi(h,name(%0)) now [u(setr`%va,have,[ansi(h,u(capnames,%q<find>))] Favors: [u(itemize`%va,iter(%q<value>,ansi(h,u(capnames,%i0)),|,|),|,and,\,)])])]},{You now %q<have>},{%q<msg>}
&INC`REM`FAVORED [u(cobj,data)]=@attach %!/DO`REM`FAVORED=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{[u(setr`%va,msg,ansi(h,name(%0)) no longer [u(setr`%va,have,[ansi(h,u(capnames,%q<find>))] Favor: [u(itemize`%va,iter(%q<value>,ansi(h,u(capnames,%i0)),|,|),|,and,\,)])])]},{You no longer %q<have>},{%q<msg>}

&DO`VAL`FAVORED [u(cobj,data)]=@check strlen(before(%1,/))=@attach %!/INC`MSG=ERROR: No Favored Type entered.;@attach %!/INC`PARTIAL=before(%1,/),u(u(cobj,data)/FUN`GET`FAVORED),|,find,Favored Type;@check strlen(%2)=@attach %!/INC`MSG=ERROR: No [u(capnames,%q<find>)] entered.;th u(setq`%va,find2,u(u(cobj,data)/FUN`GET`FAVORED`%q<find>));th u(setq`%va,value,iter(ucstr(%2),grab(%q<find2>,if(strlen(%i0),%i0*),|),|,|));@check lmath(min,iter(%q<value>,strlen(%i0),|,%b))=@attach %!/INC`MSG=ERROR: One or more [u(capnames,%q<find>)] didn't resolve. Please check your entry.

&DO`ADD`FAVORED [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`FAVORED;@stop words(u(setr`%va,conflict,setinter(u(setr`%va,oldcharms,ucstr(get(%0/D`FAVORED`%q<find>))),ucstr(%q<value>),|,|)),|)=@attach %!/INC`MSG=[switch(%0,%#,You already have,name(%0) already has)] the [u(itemize`%va,u(capnames,%q<conflict>),|,and,\,)] [ansi(h,u(capnames,%q<find>))] Favored.;&D`FAVORED`%q<find> %0=u(setr`%va,newcharms,setunion(%q<oldcharms>,ucstr(%q<value>),|,|))
&DO`REM`FAVORED [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`FAVORED;@stop words(u(setr`%va,conflict,setdiff(ucstr(%q<value>),u(setr`%va,oldcharms,ucstr(get(%0/D`FAVORED`%q<find>))),|,|)),|)=@attach %!/INC`MSG=[switch(%0,%#,You don't have have,name(%0) doesn't have)] the [u(itemize`%va,u(capnames,%q<conflict>),|,and,\,)] [ansi(h,u(capnames,%q<find>))] Favored.;&D`FAVORED`%q<find> %0=u(setr`%va,newcharms,setdiff(get(%0/D`FAVORED`%q<find>),ucstr(%q<value>),|,|));@select/inline strlen(%q<newcharms>)=0,{@attach %!/WIPE=%0,D`FAVORED`%q<find>}

&FUN`GET`FAVORED [u(cobj,data)]=ATTRIBUTES|ABILITIES|COLLEGES|PATHS
&FUN`GET`FAVORED`ATTRIBUTES [u(cobj,data)]=u(FUN`GET`ATTRIBUTES)
&FUN`GET`FAVORED`ABILITIES [u(cobj,data)]=u(FUN`GET`ABILITIES)|CRAFT

@@ Supernal

&INC`ADD`SUPERNAL [u(cobj,data)]=@attach [u(cobj,data)]/DO`ADD`SUPERNAL=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{u(setr`%va,msg,ansi(h,name(%0)) now [u(setr`%va,have,[ansi(h,u(capnames,%q<find>))] Supernals: [u(itemize`%va,iter(%q<value>,ansi(h,u(capnames,%i0)),|,|),|,and,\,)])])},{You now %q<have>},{%q<msg>}
&INC`REM`SUPERNAL [u(cobj,data)]=@attach [u(cobj,data)]/DO`REM`SUPERNAL=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{u(setr`%va,msg,ansi(h,name(%0)) no longer [u(setr`%va,have,[ansi(h,u(capnames,%q<find>))] Supernals: [u(itemize`%va,iter(%q<value>,ansi(h,u(capnames,%i0)),|,|),|,and,\,)])])},{You no longer %q<have>},{%q<msg>}

&DO`VAL`SUPERNAL [u(cobj,data)]=@check strlen(before(%1,/))=@attach %!/INC`MSG=ERROR: No Favored Type entered.;@attach %!/INC`PARTIAL=before(%1,/),u(u(cobj,data)/FUN`GET`SUPERNAL),|,find,Favored Type;@check strlen(%2)=@attach %!/INC`MSG=ERROR: No [u(capnames,%q<find>)] entered.;th u(setq`%va,find2,u(u(cobj,data)/FUN`GET`SUPERNAL`%q<find>));th u(setq`%va,value,iter(ucstr(%2),grab(%q<find2>,if(strlen(%i0),%i0*),|),|,|));@check lmath(min,iter(%q<value>,strlen(%i0),|,%b))=@attach %!/INC`MSG=ERROR: One or more [u(capnames,%q<find>)] didn't resolve. Please check your entry.

&DO`ADD`SUPERNAL [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`SUPERNAL;@stop words(u(setr`%va,conflict,setinter(u(setr`%va,oldcharms,ucstr(get(%0/D`SUPERNAL`%q<find>))),ucstr(%q<value>),|,|)),|)=@attach %!/INC`MSG=[switch(%0,%#,You already have,name(%0) already has)] the [u(itemize`%va,u(capnames,%q<conflict>),|,and,\,)] [ansi(h,u(capnames,%q<find>))] as Supernal.;&D`SUPERNAL`%q<find> %0=u(setr`%va,newcharms,setunion(%q<oldcharms>,ucstr(%q<value>),|,|))
&DO`REM`SUPERNAL [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`SUPERNAL;@stop words(u(setr`%va,conflict,setdiff(ucstr(%q<value>),u(setr`%va,oldcharms,ucstr(get(%0/D`SUPERNAL`%q<find>))),|,|)),|)=@attach %!/INC`MSG=[switch(%0,%#,You don't have have,name(%0) doesn't have)] the [u(itemize`%va,u(capnames,%q<conflict>),|,and,\,)] [ansi(h,u(capnames,%q<find>))] as Supernal.;&D`SUPERNAL`%q<find> %0=u(setr`%va,newcharms,setdiff(get(%0/D`SUPERNAL`%q<find>),ucstr(%q<value>),|,|));@select/inline strlen(%q<newcharms>)=0,{@wipe %0/D`SUPERNAL`%q<find>}

&FUN`GET`SUPERNAL [u(cobj,data)]=ATTRIBUTES|ABILITIES
&FUN`GET`SUPERNAL`ATTRIBUTES [u(cobj,data)]=u(FUN`GET`ATTRIBUTES)
&FUN`GET`SUPERNAL`ABILITIES [u(cobj,data)]=setunion(u(FUN`GET`ABILITIES),MARTIAL ARTS|CRAFT,|,|)

+shelp/addmain Characters/+editchar=+editchar is used to modify all parts of a character's +sheet!%R%R[ansi(h,+editchar/<flag> <player>|<categories|and|objects>\[=<modification>\])]%r%r[ansi(h,FLAG: SET)]%r[ansi(h,Splat)]%t%t=<splat>%r[ansi(h,Caste)]%T%T=<caste>%R[ansi(h,Essence)]%t%t=<value>%R[ansi(h,Willpower)]%t=<value>%R[ansi(h,Attribute)]%t<attribute>=<value>%r[ansi(h,Ability)]%t%t<ability>=<value>%r[ansi(h,Specialty)]%t<ability or attribute>/<specialty>=<value>%R[ansi(h,Style)]%t%t<style>=<value>%r[ansi(h,Craft)]%t%t<field>=<value> (Weaponsmithing, etc)%r%r[ansi(h,FLAG: ADD & REM)]%r[ansi(h,Charm)]%t%t<category>=<charm1>|<charm2>|etc (Native Charms)%r[ansi(h,MACharm)]%t%t<style>=<charm1>|<charm2>|etc (Martial Arts Charms)%r[ansi(h,Evocation)]%t<artifact>=<charm1>|<charm2>|etc (Evocations)%r[ansi(h,OCharm)]%t%t<splat>|<category>=<charm1>|<charm2>|etc (Cross-Splat Charms)%r[ansi(h,Spell)]%t%t<circle>=<spell1>|<spell2>|etc%r[ansi(h,Favored)]%t%t<Attribute/Ability>=<Item1>|<Item2>|etc%r[ansi(h,Supernal)]%t<Attribute/Ability>=<Item1>|<Item2>|etc%r[ansi(h,Health)]%t%t=<level1> <level2> etc (must be -0, -1, D, etc)%r%r[ansi(h,EXAMPLES)]%r%r[ansi(h,+editchar/set Isidoros|Attribute|Dexterity=4)]%r%tSets Isidoros' Dexterity to 4%r[ansi(h,+editchar/rem Isidoros|Health=-0 -1)]%r%tRemoves one -0 and one -1 from Isidoros' permanent Health Pool%R%R[ansi(h,CLEARING VALUES)]%RLeaves Values absolutely clear to remove them entirely! Example:%R+editchar/set Isidoros|Attribute|Dexterity=