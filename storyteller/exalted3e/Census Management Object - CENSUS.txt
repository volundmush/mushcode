@@ DEPENDENCIES - CORE
th u(newcobj,STORYTELLER: Census Management System <CENSUS>,census,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

&CMD`+CENSUS [u(cobj,census)]=$^\+census(?\:/(\S+))?(?\: +(.*))?$:@check words(u(setr,list,u(sortname,lsearch(all,eplayer,\[cand(nor(haspower(##,GUEST),u(isadmin,##)),u(approved,##))\]))))=@attach %!/INC`MSG=No active players in the +census!;@attach %!/INC`GETSWITCH=%1;@attach %!/INC`[u(strfirstof,%q<switch>,MAIN)]=%2
@set [u(cobj,census)]/CMD`+CENSUS=regexp

&SYSTEM`NAME [u(Cobj,census)]=CENSUS
&SWITCHES`PLAYER [u(cobj,census)]=SUPERNAL

&INC`MAIN [u(cobj,census)]=@select/inline strlen(%0)=>0,{@attach %!/INC`SUB},{@pemit %#=u(header,mudname() Census Data);@dolist/inline/delimit | [u(game_config,storyteller,CLASSES)]={@select/inline gt(words(u(setr,racelist,u(filter,INFO,%q<list>,%b,%b,Class,##))),0)=1,{@pemit %#=ljust(ansi(h,u(pueblize,u(capnames,%i0),+census [u(capnames,%i0)])),70,.)[rjust(words(%q<racelist>),2,0)];@select/inline words(%q<racelist>)=0,{},{@pemit %#=u(itemize,iter(%q<racelist>,name(%i0),%b,|),|,and,\,)%R}}};@pemit %#=u(header,Total Player Characters: [words(%q<list>)])}

&FIL`INFO [u(Cobj,census)]=strmatch(u(getstat,%0,D`INFO,%1),%2)

&INC`SUB [u(cobj,census)]=@attach %!/INC`PARTIAL=%0,u(game_config,storyteller,CLASSES),|,choice,Class;@check words(u(setr,racelist,filterbool(#lambda/strmatch(%q<choice>,u(getstat,\%0,D`INFO,Class)),%q<list>)))=@attach %!/INC`MSG=No active [u(capnames,%q<choice>)] Players!;@pemit %#=u(header,mudname() Census Data);@dolist/inline/delimit | [get(u(cobj,data)/CASTE`%q<choice>)]={@select/inline gt(words(u(setr,castelist,u(filter,INFO,%q<racelist>,%b,%b,Caste,##))),0)=1,{@pemit %#=ljust(ansi(h,u(capnames,%i0)),70,.)[rjust(words(%q<castelist>),2,0)];@select/inline words(%q<castelist>)=0,#FFFFFF#FFFFFF{},{@pemit %#=u(itemize,iter(%q<castelist>,name(%i0),%b,|),|,and,\,)%R}}};@pemit %#=u(header,Total [u(capnames,%q<Class>)] Player Characters: [words(%q<racelist>)])

&INC`SUPERNAL [u(cobj,census)]=@check words(u(setr,supers,filterbool(#lambda/match(Solar,getclass(\%0)),%q<list>)))=@attach %!/INC`MSG=Nobody active with Supernal abilities.;th iter(%q<supers>,iter(ucstr(get(%i0/D`SUPERNAL`ABILITIES)),u(setr,edit(%i0,%b,_),setunion(r(edit(%i0,%b,_)),%i1,|,|)),|,|));th u(setq,abils,filterbool(#lambda/strlen(r(edit(\%0,\%b,_))),u(game_config,storyteller,ABILITIES_EXTRA),|,|));@pemit %#=u(header,Supernal Census);@dolist/inline/delimit | %q<abils>={@pemit %#=ljust(ansi(h,u(capnames,%i0)),70,.)[rjust(words(u(setr,abil,r(edit(%i0,%b,_))),|),2,0)];@select/inline words(%q<abil>,|)=0,#FFFFFF#FFFFFF{},{@pemit %#=u(itemize,iter(%q<abil>,u(moniker,%i0),|,|),|,and,\,)%R}};@pemit %#=u(subheader)



@@ COMMUNITY - +CENSUS
+help/add +census=[u(cobj,census)]/HLP`+CENSUS
+help/category +census=Community
&HLP`+CENSUS [u(cobj,census)]=[ansi(hc,Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+census)] - Shows all approved players by Splat.%R[ansi(h,+census <splat>)] - Shows all players in a splat by Caste/Aspect/etc.)]
