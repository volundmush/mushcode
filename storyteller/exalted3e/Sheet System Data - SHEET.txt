@@ DEPENDENCIES - CORE

&Q`COUNT`PERSONA_STAT_CATEGORY [u(Cobj,sheet)]=SELECT count(persona_stat_id) FROM volv_story_persona_stats WHERE stat_category_id=? AND persona_id=? AND (stat_value>0 OR stat_flags_1 OR stat_flags_2)
&Q`COUNT`SPECIALTIES [u(cobj,sheet)]=SELECT count(specialty_name) FROM volv_story_persona_stats_specialties WHERE persona_id=?
&Q`COUNT`MERITS [u(cobj,sheet)]=SELECT count(persona_merit_id) FROM volv_story_persona_merits WHERE merit_category_id=? AND persona_id=?

&INC`MAIN [u(cobj,sheet)]=@pemit %#=u(FUN`SHEETHEADER,%0,%1,%2);@pemit %#=u(FUN`ATTRIBUTES,%0,%1,%2);@pemit %#=u(FUN`ABILITIES,%0,%1,%2);@select/inline t(u(mysql,COUNT`PERSONA_STAT_CATEGORY,5,%2))=1,{@pemit %#=u(FUN`CRAFTS,%0,%1,%2)};@select/inline t(u(mysql,COUNT`PERSONA_STAT_CATEGORY,4,%2))=1,{@pemit %#=u(FUN`STYLES,%0,%1,%2)};@select/inline t(u(mysql,COUNT`SPECIALTIES,%2))=1,{@pemit %#=u(FUN`SPECIALTIES,%0,%1,%2)};@select/inline t(u(mysql,COUNT`MERITS,1,%2))=1,{@pemit %#=u(FUN`MERITS,%0,%1,%2)};@select/inline t(u(mysql,COUNT`MERITS,2,%2))=1,{@pemit %#=u(FUN`SMERITS,%0,%1,%2)};@select/inline t(u(mysql,COUNT`MERITS,3,%2))=1,{@pemit %#=u(FUN`FLAWS,%0,%1,%2)};@select/inline t(u(mysql,COUNT`MERITS,4,%2))=1,{@pemit %#=u(FUN`RITUALS,%0,%1,%2)};@attach %!/INC`ADVANTAGES;@attach %!/INC`FOOTER

&FUN`CLASSINFO`MORTAL [u(cobj,sheet)]=u(FUN`CLASSINFO`MAIN,%0,%1,%2,NAME~Name|SEX~Sex,STAT_1000~Essence~1|SPLAT_X~Profession~N/A)
&FUN`CLASSINFO`SOLAR [u(cobj,sheet)]=u(FUN`CLASSINFO`MAIN,%0,%1,%2,NAME~Name|SEX~Sex,STAT_1000~Essence~1|SPLAT_X~Caste~N/A)
&FUN`CLASSINFO`LUNAR [u(cobj,sheet)]=u(FUN`CLASSINFO`MAIN,%0,%1,%2,NAME~Name|SEX~Sex|FIELD_50~Totem Animal~None,STAT_1000~Essence~1|SPLAT_X~Caste~N/A)
&FUN`CLASSINFO`DRAGON-BLOODED [u(cobj,sheet)]=u(FUN`CLASSINFO`MAIN,%0,%1,%2,NAME~Name|SEX~Sex|SPLAT_X~Aspect~N/A,STAT_1000~Essence~1|FIELD_40~Nation~Outcaste|FIELD_41~House~None)

&FUN`CLASSINFO`ABYSSAL [u(cobj,sheet)]=u(FUN`CLASSINFO`MAIN,%0,%1,NAME~Name|CASTE~Caste~N/A|[if(u(game_config,STORYTELLER,RESONANCE),FLAWED VIRTUE~Flawed Virtue~None,DOOM~Doom~None)],POWER~Essence~1|LIEGE~Liege~None)

&FUN`CLASSINFO`SIDEREAL [u(cobj,sheet)]=u(FUN`CLASSINFO`MAIN,%0,%1,NAME~Name|CASTE~Caste~N/A|VIRTUE FLAW~Virtue Flaw~None,POWER~Essence~1|FACTION~Faction~None)

&FUN`CLASSINFO`INFERNAL [u(cobj,sheet)]=u(FUN`CLASSINFO`MAIN,%0,%1,NAME~Name|CASTE~Caste~N/A|URGE~Urge~None,POWER~Essence~1|FAVORED YOZI~Favored Yozi~None)
&FUN`CLASSINFO`ALCHEMICAL [u(cobj,sheet)]=u(FUN`CLASSINFO`MAIN,%0,%1,NAME~Name|CASTE~Caste~N/A,POWER~Essence~1|NATION~Nation~None)
&FUN`CLASSINFO`RAKSHA [u(cobj,sheet)]=u(FUN`CLASSINFO`MAIN,%0,%1,NAME~Name|CASTE~Caste~N/A,POWER~Essence~1|LURE~Lure~None)
&FUN`CLASSINFO`DRAGON-KING [u(cobj,sheet)]=u(FUN`CLASSINFO`MAIN,%0,%1,NAME~Name|CASTE~Caste~N/A,POWER~Essence~1)
&FUN`CLASSINFO`JADEBORN [u(cobj,sheet)]=u(FUN`CLASSINFO`MAIN,%0,%1,NAME~Name|CASTE~Caste~N/A,POWER~Essence~1)
&FUN`CLASSINFO`GOD-BLOODED [u(cobj,sheet)]=u(FUN`CLASSINFO`MAIN,%0,%1,NAME~Name|CASTE~Caste~N/A,POWER~Essence~1)


&ATTRIBUTES`PHYSICAL [u(cobj,sheet)]=2000 2001 2002
&ATTRIBUTES`SOCIAL [u(cobj,sheet)]=2003 2004 2005
&ATTRIBUTES`MENTAL [u(cobj,sheet)]=2006 2007 2008

@@ u(FUN`COLOR,%1,BORDER,\}[repeat(-,8)])[u(FUN`COLOR,%1,SLASH,/)][u(FUN`COLOR,%1,PSM,Physical)][u(FUN`COLOR,%1,SLASH,/)][u(FUN`COLOR,%1,BORDER,repeat(-,15))][u(FUN`COLOR,%1,SLASH,/)][u(FUN`COLOR,%1,PSM,Social)][u(FUN`COLOR,%1,SLASH,/)][u(FUN`COLOR,%1,BORDER,repeat(-,16))][u(FUN`COLOR,%1,SLASH,/)][u(FUN`COLOR,%1,PSM,Mental)][u(FUN`COLOR,%1,SLASH,/)][u(FUN`COLOR,%1,BORDER,repeat(-,11)\{)]%R

&FUN`CHARMHEADER [u(cobj,sheet)]=u(FUN`COLOR,%1,BORDER,\})[center(u(FUN`COLOR,%1,SLASH,/)[u(FUN`COLOR,%1,CHARMHEADER,%2)][u(FUN`COLOR,%1,SLASH,/)],sub(u(width,%#),2),u(FUN`COLOR,%1,BORDER,-))][u(FUN`COLOR,%1,BORDER,\{)]

&FUN`CHARMSUBHEADER [u(cobj,sheet)]=u(FUN`COLOR,%1,BORDER,|)[center(u(FUN`COLOR,%1,CHARMSUBHEADERBORDER,====)[u(FUN`COLOR,%1,CHARMSUBHEADER,%2)][u(FUN`COLOR,%1,CHARMSUBHEADERBORDER,====)],sub(u(width,%#),2))][u(FUN`COLOR,%1,BORDER,|)]

&FUN`SECTIONHEADER [u(cobj,sheet)]=u(FUN`COLOR,%1,BORDER,\})[center(u(FUN`COLOR,%1,SLASH,/)[u(FUN`COLOR,%1,HEADER,edit(%2,_,%b))][u(FUN`COLOR,%1,SLASH,/)],sub(u(width,%#),2),u(FUN`COLOR,%1,BORDER,-))][u(FUN`COLOR,%1,BORDER,\{)]

&FUN`DOTFORMAT [u(cobj,sheet)]=localize(u(FUN`SECTIONHEADER,%0,%1,%4%5)%R[if(%7,u(setq,first,u(filter,LTSTRLEN,%6,chr(176),chr(176),%3))[u(setq,second,u(filter,GTESTRLEN,%6,chr(176),chr(176),sub(%3,10)))],u(setq,first,%6))][if(strlen(%q<first>),u(setq,firstsec,u(FUN`STATFORMAT,%0,%1,%2,%3,%6,%q<first>)))][if(strlen(%q<second>),u(setq,secondsec,u(FUN`STATFORMAT2,%0,%1,%2,sub(u(width,%#),4),%6,%q<second>)))][switch(t(strlen(%q<firstsec>))[t(strlen(%q<secondsec>))],11,%q<firstsec>%R%q<secondsec>,10,%q<firstsec>,01,%q<secondsec>)])

&FUN`ABILITIES [u(cobj,sheet)]=u(FUN`DOTFORMAT,%0,%1,%2,23,A,bilities,u(mysql3,LIST`PERSONA_STAT_CATEGORY,3,%2))

&Q`LIST`PERSONA_STAT_CATEGORY [u(Cobj,sheet)]=SELECT stat_name,stat_value,stat_flags_1,stat_flags_2 FROM volv_story_persona_stats WHERE stat_category_id=? AND persona_id=? AND (stat_value>0 OR stat_flags_1>0 OR stat_flags_2>0) ORDER BY stat_name

&FUN`CRAFTS [u(cobj,sheet)]=u(FUN`DOTFORMAT,%0,%1,%2,23,C,rafts,u(mysql3,LIST`PERSONA_STAT_CATEGORY,5,%2),1)
&FUN`STYLES [u(cobj,sheet)]=u(FUN`DOTFORMAT,%0,%1,%2,23,S,tyles,u(mysql3,LIST`PERSONA_STAT_CATEGORY,4,%2),1)

&Q`LIST`PERSONA_SPECIALTIES [u(cobj,sheet)]=SELECT CONCAT_WS('/',stat_name,specialty_name),specialty_value,0,0 FROM volv_story_persona_stats_specialties WHERE persona_id=?

&FUN`SPECIALTIES [u(cobj,sheet)]=u(FUN`DOTFORMAT,%0,%1,%2,36,S,pecialties,u(mysql3,LIST`PERSONA_SPECIALTIES,%2),1)

&FUN`MERITS [u(cobj,sheet)]=u(FUN`MERFORMAT,%0,%1,%2,u(mysql3,LIST`PERSONA_MERITS,1,%2),MERITS,+merits)
&FUN`SMERITS [u(cobj,sheet)]=u(FUN`MERFORMAT,%0,%1,%2,u(mysql3,LIST`PERSONA_MERITS,2,%2),SUPERNATURAL MERITS,+smerits)
&FUN`FLAWS [u(cobj,sheet)]=u(FUN`MERFORMAT,%0,%1,%2,u(mysql3,LIST`PERSONA_MERITS,3,%2),FLAWS,+flaws)
&FUN`RITUALS [u(cobj,sheet)]=u(FUN`MERFORMAT,%0,%1,%2,u(mysql3,LIST`PERSONA_MERITS,4,%2),RITUALS,+rituals)

&Q`LIST`PERSONA_MERITS [u(Cobj,sheet)]=SELECT CONCAT_WS(': ',merit_name,merit_context),merit_value FROM volv_story_persona_merits WHERE merit_category_id=? AND persona_id=? ORDER BY merit_name,merit_context

@@ &FUN`STATFORMAT`STAT [u(cobj,sheet)]=localize([u(setq,namedisp,if(%9,left(elements(%3,1,chr(177)),%9),elements(%3,1,chr(177))))][u(setq,val,u(strfirstof,%5,elements(%3,2,chr(177))))][u(setq,bon,u(strfirstof,%6,elements(%3,5,chr(177))))][u(setq,totval,add(%q<val>,if(%7,%q<bon>)))][if(%7,u(setq,valdisp,if(%7,u(FUN`COLOR,%1,STATDOT,repeat(*,%q<val>)))),[u(setq,astlen,if(lte(%q<bon>,-1),bound(add(%q<val>,%q<bon>),0),%q<val>))][u(setq,minlen,if(lte(%q<bon>,-1),abs(%q<bon>)))][u(setq,pluslen,if(gte(%q<bon>,1),%q<bon>))][u(setq,valdisp,[if(%q<astlen>,u(FUN`COLOR,%1,STATDOT,repeat(*,%q<astlen>)))][if(%q<minlen>,u(FUN`COLOR,%1,STATDOT,repeat(-,%q<minlen>)))][if(%q<pluslen>,u(FUN`COLOR,%1,STATDOT,repeat(+,%q<pluslen>)))])])][if(%8,,u(setq,fav,elements(%3,3,chr(177))))][u(setq,statlen,strlen(%q<namedisp>))][if(lte(sub(%4,1,%q<statlen>,strlen(%q<valdisp>)),0),u(setq,valdisp,%q<totval>,dotlen,strlen(%q<totval>)),u(setq,dotlen,strlen(%q<valdisp>)))][u(setq,linelen,sub(%4,1,%q<dotlen>,%q<statlen>))][switch(%q<fav>,3,u(FUN`COLOR,%1,SUPMARK,+),2,u(FUN`COLOR,%1,CASTEMARK,+),1,u(FUN`COLOR,%1,FAVMARK,+),%b)][u(FUN`COLOR,%1,STATNAME,if(%5,%q<namedisp>,u(capnames,%q<namedisp>)))][if(gt(%q<linelen>,0),repeat(u(FUN`COLOR,%1,STATFILL,.),%q<linelen>))]%q<valdisp>)


@@ %0 = Player. %1 = Splat. %2 = pers_id. %3 = Entry. %4 = Length. %5 = manual value. %6 = Manual bonus. %7 = Don't show bonus. %8 = No favoreds. %9 = Truncate.

&Q`GET`ADVANTAGES_TEMPLATES [u(cobj,sheet)]=SELECT DISTINCT template_id,template_name FROM volv_story_persona_wordpower WHERE persona_id=? ORDER BY template_sort
&Q`GET`ADVANTAGES_STATS [u(Cobj,sheet)]=SELECT DISTINCT stat_id,stat_name FROM volv_story_persona_wordpower WHERE persona_id=? AND template_id=? ORDER BY stat_name
&Q`GET`ADVANTAGES_POWERS [u(Cobj,sheet)]=SELECT wordpower_name,wordpower_purchases,wordpower_flags_1 FROM volv_story_persona_wordpower WHERE persona_id=? AND stat_id=? ORDER BY wordpower_name

&INC`ADVANTAGES [u(cobj,sheet)]=@dolist/inline/delimit [chr(176)] [u(mysql3,GET`ADVANTAGES_TEMPLATES,%2)]={th u(setq,tdata,u(choosegame,%i0,%d0));@pemit %#=u(FUN`CHARMHEADER,%0,%1,u(capnames,elements(%q<tdata>,2,chr(177))));@dolist/inline/delimit [chr(176)] [u(mysql3,GET`ADVANTAGES_STATS,%2,elements(%q<tdata>,1,chr(177)))]={th u(setq,sdata,u(choosegame,%i0,%d0));@pemit %#=u(FUN`CHARMSUBHEADER,%0,%1,elements(%q<sdata>,2,chr(177)));@pemit %#=u(FUN`BORDER,%0,%1,u(FUN`WORDLIST,%0,u(mysql3,GET`ADVANTAGES_POWERS,%2,elements(%q<sdata>,1,chr(177)))))}};@attach %!/INC`EVOCATIONS

&Q`GET`EVOCATION_ARTIFACTS [u(cobj,sheet)]=SELECT DISTINCT persona_merit_id,merit_context FROM volv_story_persona_merits_wordpowers WHERE persona_id=? ORDER BY merit_context
&Q`GET`EVOCATIONS [u(cobj,sheet)]=SELECT merit_wordpower_name,merit_wordpower_purchases,merit_wordpower_flags_1 FROM volv_story_persona_merits_wordpowers WHERE persona_merit_id=? ORDER BY merit_wordpower_name

&INC`EVOCATIONS [u(Cobj,sheet)]=@select/inline words(u(setr,arts,u(mysql3,GET`EVOCATION_ARTIFACTS,%2)),chr(176))=>0,{@pemit %#=u(FUN`CHARMHEADER,%0,%1,Evocations);@dolist/inline/delimit [chr(176)] %q<arts>={th u(setq,sdata,u(choosegame,%i0,%d0));@pemit %#=u(FUN`CHARMSUBHEADER,%0,%1,elements(%q<sdata>,2,chr(177)));@pemit %#=u(FUN`BORDER,%0,%1,u(FUN`WORDLIST,%0,u(mysql3,GET`EVOCATIONS,elements(%q<sdata>,1,chr(177)))))}}

&INC`FOOTER [u(cobj,sheet)]=@attach %!/INC`HEALTH;@attach %!/INC`POOLS;@pemit %#=u(FUN`FOOTER,%0,%1)

&INC`HEALTH [u(cobj,sheet)]=@pemit %#=u(FUN`SECTIONHEADER,%0,%1,Health);@pemit %#=u(FUN`BORDER,%0,%1,u(u(cobj,damage)/FUN`FORMATHEALTH,%0))

&INC`POOLS [u(cobj,sheet)]=@select/inline gt(words(u(u(cobj,pool)/FUN`LISTPOOLS,%0),|),0)=1,{@pemit %#=u(FUN`3HEADER,%0,%1,Pools|Tracks|Experience);@pemit %#=u(FUN`POOLS,%0,%1,%2)

&FUN`POOLS [u(cobj,sheet)]=localize(align(1. [u(setr,c1,floor(u(setr,c0,fdiv(sub(u(width,%#),4),3))))] 1. [u(setr,c2,ceil(%q<c0>))] 1. [u(setr,c3,sub(u(width,%#),4,%q<c1>,%q<c2>))] 1.,u(FUN`COLOR,%1,BORDER,|),u(FUN`FORMATPOOLS,%0,Pool),u(FUN`COLOR,%1,BORDER,|),u(FUN`FORMATPOOLS,%0,Track),u(FUN`COLOR,%1,BORDER,|),u(FUN`XP,%0,%1,%2),u(FUN`COLOR,%1,BORDER,|),,,%R))

&FUN`XP [u(cobj,sheet)]=u(trimlines,iter(u(u(cobj,xp)/mysql3,GET`TOTALS_PLAYER,%2),u(FUN`COLOR,%1,PSM,rjust(elements(%i0,1,chr(177)),13): [rjust(add(elements(%i0,2,chr(177)),0),3)]/[add(elements(%i0,3,chr(177)),0)]),chr(176),%R))

&FUN`FOOTER [u(cobj,sheet)]=u(FUN`COLOR,%1,BORDER,|----------------------------------------------------------------------------|)%R%B[u(FUN`COLOR,%1,BORDER,\\)]%B[center(u(FUN`GETSERIES,%1),72)]%B[u(FUN`COLOR,%1,BORDER,/)]%R%B%B[u(FUN`COLOR,%1,BORDER,'------------------------------------------------------------------------')]

&FUN`GETSERIES [u(cobj,sheet)]=switch(%0,SOLAR,ansi(hy,Solars):%B[ansi(hy,The Lawgivers)],ABYSSAL,ansi(hx,Abyssals):%B[ansi(hx,The Deathknights)],INFERNAL,ansi(hg,Infernals):%B[ansi(hg,The Green Sun Princes)],LUNAR,ansi(hc,Lunars):%B[ansi(hc,The Stewards)],SIDEREAL,ansi(hm,Sidereals):%B[ansi(hm,The Viziers)],DRAGON-BLOODED,ansi(hr,Terrestrials):%B[ansi(hr,The Dragon-Blooded)],ALCHEMICAL,ansi(c,Alchemicals):%B[ansi(c,The Champions)],JADEBORN,ansi(y,Jadeborn),RAKSHA,ansi(m,Raksha):%B[ansi(m,The Fair Folk)],MORTAL,Mortals:%BThe Heroes,GHOST,Ghosts,DRAGON-KING,Dragon-Kings,GOD-BLOODED,ansi(h,GOD-BLOODED):%B[ansi(h,The Children of the Mighty)],Exalted 3e)

&DEFAULT`STATNAME [u(cobj,sheet)]=
&DEFAULT`FAVMARK [u(cobj,sheet)]=c
&DEFAULT`SUPMARK [u(cobj,sheet)]=hr
&DEFAULT`CASTEMARK [u(cobj,sheet)]=n

&SOLAR`BORDER [u(cobj,sheet)]=y
&SOLAR`SLASH [u(cobj,sheet)]=hr
&SOLAR`HEADER [u(cobj,sheet)]=hy
&SOLAR`CHARMHEADER [u(cobj,sheet)]=hy
&SOLAR`3COLUMNNAME [u(cobj,sheet)]=hy
&SOLAR`CASTEMARK [u(cobj,sheet)]=y

&LUNAR`BORDER [u(cobj,sheet)]=+lightblue1
&LUNAR`SLASH [u(cobj,sheet)]=hb
&LUNAR`HEADER [u(cobj,sheet)]=hc
&LUNAR`CHARMHEADER [u(cobj,sheet)]=hc
&LUNAR`CASTEMARK [u(cobj,sheet)]=+lightblue1

&INFERNAL`BORDER [u(cobj,sheet)]=g
&INFERNAL`SLASH [u(cobj,sheet)]=y
&INFERNAL`HEADER [u(cobj,sheet)]=hg
&INFERNAL`CHARMHEADER [u(cobj,sheet)]=hg

&ABYSSAL`BORDER [u(cobj,sheet)]=+gray18
&ABYSSAL`SLASH [u(cobj,sheet)]=+grey93
&ABYSSAL`HEADER [u(cobj,sheet)]=+red3
&ABYSSAL`CHARMHEADER [u(cobj,sheet)]=+red3

&SIDEREAL`BORDER [u(cobj,sheet)]=hm
&SIDEREAL`SLASH [u(cobj,sheet)]=m
&SIDEREAL`HEADER [u(cobj,sheet)]=hw
&SIDEREAL`CHARMHEADER [u(cobj,sheet)]=hw

&DRAGON-BLOODED`BORDER [u(cobj,sheet)]=hr
&DRAGON-BLOODED`SLASH [u(cobj,sheet)]=c
&DRAGON-BLOODED`HEADER [u(cobj,sheet)]=hc
&DRAGON-BLOODED`CHARMHEADER [u(cobj,sheet)]=hc
&DRAGON-BLOODED`CASTEMARK [u(cobj,sheet)]=hr

&RAKSHA`BORDER [u(cobj,sheet)]=+springgreen3
&RAKSHA`SLASH [u(cobj,sheet)]=+powderblue
&RAKSHA`HEADER [u(cobj,sheet)]=+powderblue
&RAKSHA`CHARMHEADER [u(cobj,sheet)]=+powderblue
