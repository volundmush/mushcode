@@ DEPENDENCIES - Core

@switch/inline isdbref(u(wso))=0,{@tel create(Watch System Object <WSO>)=config(master_room)}
&wso u(coi)=locate(config(master_room),Watch System Object <WSO>,TXxi)
@parent u(wso)=u(coi)
@set u(wso)=WIZARD !NO_COMMAND

&CMD`+WATCH u(wso)=$^(?s)(?\:\+)?(?\:watch|wf|friend|friends)(?\:/(\S+))?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(wso)/VAR`PLAYFLAGS),if(isadmin(%#),get(u(wso)/VAR`ADMINFLAGS)),|,|),|,WATCH,switch,switch;@include u(wso)/INC`[strfirstof(%q<switch>,MAIN)]=%2,%3
@set u(wso)/CMD`+WATCH=regexp

&INC`MAIN u(wso)=@assert words(setr(list,filterbool(u(wso)/FILTER_ISOBJID,get(%#/D`WATCH),%B,|)),|)=@nspemit %#=ERROR: You have no friends on watch!;@nspemit %#=header(Your Friends);th iter(lnum(1,ceil(fdiv(words(%q<list>,|),30))),nspemit(%#,iter(elements(%q<list>,lnum(add(1,mul(sub(inum(0),1),30)),mul(inum(0),30)),|,|),ljust(pueblize(name(%i0),finger [name(%i0)]),60)[if(gte(conn(%i0),0),ansi(hg,ago(idle(%i0))),ansi(hx,elements(get(%i0/LAST),2 3)))],|,%R)));@nspemit %#=header();

&INC`ADD u(wso)=@assert strlen(%0)=@nspemit %#=ERROR: No player entered to add.;@assert isdbref(setr(p,pmatch(%0)))=@nspemit %#=ERROR: %0 does not match a player.;@break match(get(%#/D`WATCH),objid(%qp))=@nspemit %#=ERROR: They are already a friend.;&D`WATCH %#=filterbool(FILTER_ISOBJID,setunion(get(%#/D`WATCH),objid(%qp)));@nspemit %#=name(%qp) added to your Friends list!

&INC`DEL u(wso)=@assert strlen(%0)=@nspemit %#=ERROR: No player entered to remove.;@assert isdbref(setr(p,pmatch(%0)))=@nspemit %#=ERROR: %0 does not match a player.;@assert match(get(%#/D`WATCH),objid(%qp))=@nspemit %#=ERROR: They are not a friend.;&D`WATCH %#=filterbool(FILTER_ISOBJID,setdiff(get(%#/D`WATCH),objid(%qp)));@nspemit %#=name(%qp) removed from your Friends list!

&FILTER_ISOBJID u(wso)=isobjid(%0)

&VAR`PLAYFLAGS u(wso)=ADD|DEL

&PLAYER`CONNECT`WATCH u(wso)=@break or(hasflag(%0,DARK),hidden(%0));@break gt(%1,1);@nspemit/list lsearch(all,eplayer,\[match(get(##/D`WATCH),\%0)\])=announce(FRIEND)%B[name(%0)] has connected.

&PLAYER`DISCONNECT`WATCH u(wso)=@break or(hasflag(%0,DARK),hidden(%0));@break gte(%1,1);@nspemit/list lsearch(all,eplayer,\[match(get(##/D`WATCH),\%0)\])=announce(FRIEND)%B[name(%0)] has disconnected.

