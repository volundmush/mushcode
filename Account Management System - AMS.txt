@@ DEPENDENCIES - Core
@@ +banlist does not work AT ALL on RhostMUSH.
@@ +ip cannot track failed logins on RhostMUSH.
@@ +ip and +banlist requires MySQL.

th u(NEWCOBJ,Account Management System <AMS>,ams,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)
@power [u(cobj,ams)]=no_pay debit queue
th u(NEWCOBJ,Account Database <ACCOUNTS>,accounts,u(cobj,ams),,1,WIZARD SAFE,SIDEFX SAFE)
th u(NEWCOBJ,Ban Database <BANDB>,bandb,u(cobj,ams),,1,WIZARD SAFE,SIDEFX SAFE)
@power u(cobj,bandb)=many_attribs

&SYSTEM`SWITCHES [u(cobj,ams)]=setunion(setunion(u(SWITCHES`%q<sysname>`PLAYER),if(u(isadmin`%va,%#),u(SWITCHES`%q<sysname>`ADMIN)),|,|),if(cor(u(OPEN_CREATION),u(game_config,SYSTEM,OPEN_ACCOUNT)),v(SWITCHES`%q<sysname>`OPEN_CREATION)),|,|)
&SYSTEM`NAME [u(cobj,ams)]=u(strfirstof`%va,%q<sysname>,ACCOUNT)

@@ ****** ACCOUNT SECTION *******
&CMD`+ACCOUNT`PENNMUSH [u(cobj,ams)]=$^(?\:\+)?account(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+ACCOUNT`MAIN
@set [u(cobj,ams)]/CMD`+ACCOUNT`PENNMUSH=regexp
&CMD`+ACCOUNT`RHOSTMUSH [u(cobj,ams)]=$^(?\:\+)?account(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+ACCOUNT`MAIN
@set [u(cobj,ams)]/CMD`+ACCOUNT`RHOSTMUSH=regexp
&CMD`+ACCOUNT`MAIN [u(cobj,ams)]=th u(setq`%va,sysname,ACCOUNT);@attach %!/INC`GETSWITCH=%1;@attach %!/INC`ACCOUNT`[u(strfirstof`%va,%q<switch>,MAIN)]=%2,%3
@set [u(cobj,ams)]/CMD`+ACCOUNT`[switch(%va,PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SWITCHES`ACCOUNT`PLAYER [u(cobj,ams)]=
&SWITCHES`ACCOUNT`ADMIN [u(cobj,ams)]=BIND|UNBIND|UNBOUND|DELETE|CREATE|LIST|RENAME|INACTIVE
&SWITCHES`ACCOUNT`OPEN_CREATION [u(cobj,ams)]=REQUEST|ACCEPT|NEW|RENAME

&INC`ACCOUNT`FIND [u(cobj,ams)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Account Name field empty.;@check strlen(u(setr`%va,accid,namegraball(children(u(cobj,accounts)),%0)))=@attach %!/INC`MSG=ERROR: Account '%0' not found.;@stop gt(words(%q<accid>),1)=@attach %!/INC`MSG=ERROR: '%0' matched multiple accounts. Please be more specific.;th u(setq`%va,accname,name(%q<accid>))

&INC`ACCOUNT`TARGET [u(cobj,ams)]=@select/inline %0=#*,{@attach %!/INC`ACCOUNT`FIND=after(%0,#)},{@attach %!/INC`CHECKPC=%0,1;@check isdbref(%q<t1acc>)=@attach %!/INC`MSG=ERROR: %q<t1name> is not bound to an account.;th u(setq`%va,accid,%q<t1acc>);th u(setq`%va,accname,name(%q<accid>))}

&INC`ACCOUNT`MAIN [u(cobj,ams)]=@select/inline cand(u(isadmin`%va,%#),strlen(%0))=1,{@attach %!/INC`ACCOUNT`TARGET},0,{@check isdbref(u(setr`%va,accid,u(accid,%#)))=@attach %!/INC`MSG=ERROR: No account to display.;th u(setq`%va,accname,name(%q<accid>))};@attach %!/INC`ACCOUNT`SHOW

&INC`ACCOUNT`CREATE [u(cobj,ams)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: No account name entered.;@stop isdbref(u(find_in`%va,u(cobj,accounts),%0))=@attach %!/INC`MSG=ERROR: An account already exists with that name.;@check valid(name,%0)=@attach %!/INC`MSG=ERROR: That name can't be used for an account.;@check lte(strlen(%0),30)=@attach %!/INC`MSG=ERROR: 30 characters or less please.;@tel [u(setr`%va,accid,create(u(setr`%va,accname,%0)))]=u(cobj,accounts);@parent %q<accid>=u(cobj,accounts);@attach %!/INC`MSG`CHAN={Created NEW ACCOUNT: %q<accname>}

&INC`ACCOUNT`NEW [u(cobj,ams)]=@stop u(isguest,%#)=@attach %!/INC`MSG=ERROR: Permission denied.;@stop strmatch(%#,#1)=@attach %!/INC`MSG=ERROR: #1 cannot be bound to an Account.;@stop isdbref(u(setr`%va,accid,u(accid,%#,1)))=@attach %!/INC`MSG=ERROR: You are already bound to [name(%q<accid>)]!;@attach %!/INC`ACCOUNT`CREATE;@attach %!/INC`ACCOUNT`ADDTOACCOUNT=%q<accid>,%:;@attach %!/INC`MSG={You are now bound to ACCOUNT: %q<accname>!};@attach %!/INC`MSG`CHAN={Bound to NEW ACCOUNT: %q<accname> via +account/new}

&INC`ACCOUNT`LIST [u(cobj,ams)]=@check words(u(setr`%va,accounts,u(sortacc,u(filter,ACTIVE_ACCOUNT,children(u(cobj,accounts))))))=@attach %!/INC`MSG=ERROR: There are no active accounts entered in the system.;@attach %!/INC`ACCOUNT`MAINLIST=Accounts

&FIL`ACTIVE_ACCOUNT [u(cobj,ams)]=u(lmax`%va,iter(get(%0/CHARACTERS),isobjid(%i0)))
&FIL`INACTIVE_ACCOUNT [u(cobj,ams)]=not(u(FIL`ACTIVE_ACCOUNT,%0))

&INC`ACCOUNT`INACTIVE [u(cobj,ams)]=@check words(u(setr`%va,accounts,u(sortacc,u(filter,INACTIVE_ACCOUNT,children(u(cobj,accounts))))))=@attach %!/INC`MSG=ERROR: There are no inactive accounts to list.;@attach %!/INC`ACCOUNT`MAINLIST=Inactive Accounts

&SORTACC [u(cobj,mco)]=u(SORTACC`%va,%0,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))
&SORTACC`PENNMUSH [u(cobj,mco)]=sortkey(#lambda/get(\%0/LAST),%0,n,%1,%2)
&SORTACC`RHOSTMUSH [u(cobj,mco)]=sortby(#lambda/[lit([ncomp(last(name(%0)),last(name(%1)))])],%0,%1,%2)

&INC`ACCOUNT`MAINLIST [u(cobj,ams)]=@pemit %#=u(header,mudname() %0);@pemit %#=ansi(u(color`%va,%#,COLOR,COLUMN_NAMES),align(30 6 42,Name,Last,Characters));@pemit %#=u(separator);@dolist/inline %q<accounts>={@pemit %#=align(30 6 42,u(pueblize,name(##),+account #[name(##)]),if(get(##/LAST),elements(u(fancytime`%va,get(##/LAST),%#), 1 2),ansi(hx,N/A)),u(strfirstof`%va,iter(sortkey(#lambda/convtime(get(\%0/LAST)),u(filter,ISOBJID,get(##/CHARACTERS)),-n),u(pueblize,u(moniker`%va,%i0),+finger [name(%i0)]),%b,\,%b),OLD: [iter(get(##/OLD),ansi(c,after(%i0,~)),|,\,%b)]))};@pemit %#=u(footer,+account <id or player> for details)

&FUN`ACCOUNT`CHARLINE [u(cobj,ams)]=align(-6 3 -2 -3 -4 21 -4 21 6,num(%0),switch(1,u(iswizard`%va,%0),ansi(u(game_config,SYSTEM,WIZTAG),WIZ),hasflag(%0,ROYALTY),ansi(u(game_config,SYSTEM,ROYTAG),ROY),u(isadmin`%va,%0),ansi(hy,ADM),haspower(%0,BUILDER),ansi(hm,BUI),u(isapproved,%0),ansi(hg,APP),ansi(hr,---)),default(%0/D`ALTS,0),u(getproperty,%0,MAJORABBR),default(%0/D`FINGER`TYPE,??),u(pueblize,u(moniker`%va,%0),+finger [name(%0)]),elements(maildstats(%0),5),u(pueblize,get(%0/LASTIP),+ip [name(%0)]),u(lastidle,%0))

&INC`ACCOUNT`SHOW [u(cobj,ams)]=th u(setq`%va,pcs,u(filter,ISOBJID,get(%q<accid>/CHARACTERS)));@pemit %#=u(HEADER,ACCOUNT: [name(%q<accid>)]);@pemit %#=ansi(u(color`%va,%#,COLOR,COLUMN_NAMES),align(-6 3 2 3 4 21 4 21 6,Dbref,Sta,Al,Fac,Type,Name,Mail,IP,LastOn));@pemit %#=u(SEPARATOR);@dolist/inline %q<pcs>={@pemit %#=u(FUN`ACCOUNT`CHARLINE,##)};@select/inline cand(u(player_config`%va,%#,SYSTEM,OLD_ALTS),gt(words(u(setr`%va,old,u(filter,OLDOBJID,get(%q<accid>/OLD),|,|)),|),0))=1,{@pemit %#=u(SEPARATOR,Old Characters);@pemit %#=align(-6 3 3 21 8 21,Dbref,,,Name,,Objid);@dolist/inline/delimit | [u(sortoldid`%va,%q<old>,|,|)]={@pemit %#=align(-6 3 -3 21 8 21,before(##,:),,,after(##,~),,before(##,~))}};@pemit %#=u(footer)

&INC`ACCOUNT`REQUEST [u(cobj,ams)]=@stop isdbref(u(setr`%va,accid,u(accid,%#,1)))=@attach %!/INC`MSG=ERROR: You already belong to [name(%q<accid>)]!;@attach %!/INC`CHECKPC=%0,1;@check u(setr`%va,reqacc,u(accid,%q<t1>,1))=@attach %!/INC`MSG=ERROR: %q<t1name> does not have an account!;@attach %!/INC`MSG=Request sent! Please use +request/accept %n on any character for that account to confirm.;th u(attrib_set`%va,%q<reqacc>,REQUESTS,setunion(get(%q<reqacc>/REQUESTS),%:));@attach %!/INC`MSG=%n has requested to join this account. Use [u(pueblize,+account/accept %n)] to confirm.,u(alts,%q<t1>);@attach %!/INC`MSG`CHAN=Sent add request to [name(%q<reqacc>)].

&INC`ACCOUNT`ACCEPT [u(cobj,ams)]=@check isdbref(u(setr`%va,accid,u(accid,%#,1)))=@attach %!/INC`MSG=ERROR: You do not have an account!;@attach %!/INC`CHECKPC=%0,1;@stop u(setr`%va,reqacc,u(accid,%q<t1>,1))=@attach %!/INC`MSG=ERROR: %q<t1name> already has an account!;@check match(get(%q<accid>/REQUESTS),%q<t1objid>)=@attach %!/INC`MSG=ERROR: No request pending from %q<t1name>.;th u(attrib_set`%va,%q<accid>,REQUESTS,setdiff(get(%q<accid>/REQUESTS),%q<t1objid>));@attach %!/INC`MSG=%q<t2name> has been added to [name(%q<accid>)],u(alts,%#);@attach %!/INC`MSG`CHAN=Account request for %q<t1name> to [name(%q<accid>)] confirmed.;@attach %!/INC`ACCOUNT`ADDTOACCOUNT=%q<accid>,%q<t1objid>

&INC`ACCOUNT`BIND [u(cobj,ams)]=@attach %!/INC`CHECKPC=%0,2;@stop strmatch(%q<t2>,#1)=@attach %!/INC`MSG=ERROR: #1 cannot be bound to an Account.;@select/inline %1=NEW,{@attach %!/INC`ACCOUNT`CREATE=%q<t2name>},{@attach %!/INC`ACCOUNT`TARGET=%1};@select/inline isdbref(%q<t2acc>)=1,{@attach %!/INC`ACCOUNT`REMFROMACCOUNT=%q<t2acc>,%q<t2objid>};@attach %!/INC`ACCOUNT`ADDTOACCOUNT=%q<accid>,%q<t2objid>;@attach %!/INC`MSG`NOTICE=You have been Account-Bound to [u(pueblize,%q<accname>,+account)].,%q<t2>;@attach %!/INC`MSG=You have Bound %q<t1name> to [u(pueblize,name(%q<accid>),+account [last(name(%q<accid>))])];@attach %!/INC`MSG`CHAN=Account-Bound %q<t2name> to [u(pueblize,%q<accname>,+account #%q<accname>)]

&INC`ACCOUNT`ADDTOACCOUNT [u(cobj,ams)]=th u(attrib_set`%va,%0,CHARACTERS,setunion(get(%0/CHARACTERS),objid(%1)));th u(attrib_set`%va,%1,D`ACCOUNT,%0);th u(setstat,%0,OLD,objid(%1),name(%1))
&INC`ACCOUNT`REMFROMACCOUNT [u(cobj,ams)]=th u(attrib_set`%va,%0,CHARACTERS,setdiff(get(%0/CHARACTERS),objid(%1)));@wipe %1/D`ACCOUNT;@select/inline t(%1)=1,{th u(delstat,%0,OLD,objid(%1))}

&INC`ACCOUNT`UNBIND [u(cobj,ams)]=@attach %!/INC`CHECKPC=%0,1;@check u(setr`%va,accid,%q<t1acc>)=@attach %!/INC`MSG={ERROR: %q<t1name> is not a member of any accounts.};@attach %!/INC`ACCOUNT`REMFROMACCOUNT=%q<accid>,%q<t1objid>;@attach %!/INC`MSG={You have removed %q<t1name> from Account [name(%q<accid>)]};@attach %!/INC`MSG`NOTICE={You were removed you from [name(%q<accid>)]},%q<t1>;@attach %!/INC`MSG`CHAN={Removed %q<t1name> from [name(%q<accid>)]}

&INC`ACCOUNT`UNBOUND [u(cobj,ams)]=@check words(u(setr`%va,unbound,switch(%va,PennMUSH,lsearch(all,type,player,elock,!D`ACCOUNT:?*),search(eplayer=!hasattr(##,D`ACCOUNT)))))=@attach %!/INC`MSG={There are no accountless PCs in the Database. Hooray!};@pemit %#=u(HEADER,Unbound Characters);@pemit %#=align(-6 3 3 27 25 9,Dbref,Sta,Alt,Name,IP,Last On);@pemit %#=u(SEPARATOR);@dolist/inline %q<unbound>={@pemit %#=u(FUN`ACCOUNT`CHARLINE,##)};@pemit %#=u(SUBHEADER)

&INC`ACCOUNT`RENAME [u(cobj,ams)]=@select/inline cor(u(OPEN_CREATION),u(game_config,SYSTEM,ACCOUNT_RENAME))=1,{@check isdbref(u(setr`%va,accid,u(accid,%#,1)))=@pemit %#=ERROR: This character is not bound to an Account.;@attach %!/INC`ACCOUNT`CHECKRENAME;@name %q<accid>=%0;@attach %!/INC`MSG=Account name changed to: %0.;@attach %!/INC`MSG`CHAN=Changed Account name to: %0},{@check u(isadmin`%va,%#)=@attach %!/INC`MSG={Only Staff may rename accounts!};@attach %!/INC`ACCOUNT`TARGET=%0;@attach %!/INC`ACCOUNT`CHECKRENAME=%1;@name %q<accid>=%1;@attach %!/INC`MSG=Account renamed to: %1.;@attach %!/INC`MSG`CHAN=Renamed Account '%q<accname>' to: %1}

&INC`ACCOUNT`CHECKRENAME [u(cobj,ams)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Name field empty.;@check valid(name,%0)=@attach %!/INC`MSG=ERROR: That is not a good name for an account.;@stop cand(isdbref(u(setr`%va,found,u(find_in`%va,u(cobj,accounts),%0))),not(strmatch(before(%q<accid>,:),before(%q<found>,:))))=@attach %!/INC`MSG=ERROR: An account already has that name. Names must be unique. Pick another!

&INC`ACCOUNT`DELETE [u(cobj,ams)]=@check strlen(%0)=@attach %!/INC`MSG={ERROR: Account ID empty.};@attach %!/INC`ACCOUNT`FIND=%0;@attach %!/INC`VERIFY={ansi(hr,WARNING:) This will delete Account %0. All records will be deleted. This will not delete any characters! Are you sure? Enter the command again within ten seconds to continue.},ACCOUNT DELETE %q<accid>;@attach %!/INC`MSG={[name(%q<accid>)] deleted!};@attach %!/INC`MSG`NOTICE={Your account was deleted.},u(setr`%va,chars,get(%q<accid>/CHARACTERS));@attach %!/INC`MSG`CHAN={[name(%q<accid>)] deleted!};@dolist/inline %q<chars>={@wipe ##/D`ACCOUNT};@attach %!/DELETE=%q<accid>

&PLAYER`CONNECT [u(cobj,ams)]=@dolist/inline u(lattr`%va,%!/PLAYER`CONNECT`*)={@trigger %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
&PLAYER`CREATE [u(cobj,ams)]=@dolist/inline u(lattr`%va,%!/PLAYER`CREATE`*)={@trigger %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
&PLAYER`DISCONNECT [u(cobj,ams)]=@dolist/inline u(lattr`%va,%!/PLAYER`DISCONNECT`*)={@trigger %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}

&OBJECT`RENAME [u(cobj,ams)]=@dolist/inline u(lattr`%va,%!/OBJECT`RENAME`*)={@trigger %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
&OBJECT`DESTROY [u(cobj,ams)]=@dolist/inline u(lattr`%va,%!/OBJECT`DESTROY`*)={@trigger %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
&OBJECT`CREATE [u(cobj,ams)]=@dolist/inline u(lattr`%va,%!/OBJECT`CREATE`*)={@trigger %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
&OBJECT`MOVE [u(cobj,ams)]=@dolist/inline u(lattr`%va,%!/OBJECT`MOVE`*)={@trigger %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}

&PLAYER`CONNECT`ACCOUNT [u(cobj,ams)]=@stop u(isguest,%0);@stop isdbref(u(setr`%va,acc,u(accid,%0,1)))=&LAST %q<acc>=secs();@stop strmatch(num(%0),#1);@select/inline u(game_config,SYSTEM,AUTO_ACCOUNT)=1,{@select/inline words(u(setr`%va,accid,u(filter,isdbref,setunion(iter(u(filter,NOTGUEST,u(mysql,GET`OBJIDS_FROM_IPS,u(sql`in`string,get(%0/LASTIP)))),u(accid,%i0,1)),))))=>1,{@attach %!/INC`MSG=Welcome to [mudname()]! Login records show multiple accounts for this site. Auto-Matching cannot continue. [if(u(open_creation),%BPlease use +account/request on an existing alt if you have one. Else\, you may +account/new to generate a new account.,%BPlease request an account from staff.)],%0},1,{@attach %!/INC`MSG=Welcome to [mudname()]! We found one account matching your login records. You have been auto-bound to [name(%q<accid>)]! Please contact staff if this is in error.,%0;@attach %!/INC`ACCOUNT`ADDTOACCOUNT=%q<accid>,%0},0,{@attach %!/INC`MSG=Welcome to [mudname()]! No account was found for this character.[if(u(open_creation),%BPlease use +account/request on an existing alt if you have one. Else\, you may +account/new to generate a new account.,%BPlease request an account from staff.)],%0}},0,{@attach %!/INC`MSG=Welcome to [mudname())]! No account was found for this character.[if(u(open_creation),%BPlease use +account/request on an existing alt if you have one. Else\, you may +account/new to generate a new account.,%BPlease request an account from staff.)],%0}

th u(NEWCONF,config,SYSTEM,AUTO_ACCOUNT,1,Auto-Bind accounts based on IP?,BOOL)
th u(NEWCONF,config,SYSTEM,OPEN_ACCOUNT,0,Players can +account /new and /rename?,BOOL)
th u(NEWCONF,config,SYSTEM,ACCOUNT_RENAME,0,Allow players to rename their account?,BOOL)

th u(NEWCONF,pconf,SYSTEM,OLD_ALTS,1,+account details include old alts?,BOOL)

&PLAYER`DISCONNECT`ACCOUNT [u(cobj,ams)]=@check isdbref(u(setr`%va,acc,u(accid,%0,1)));&LAST %q<acc>=secs();

@@ @dolist/inline/nobreak children(u(cobj,accounts))={@check u(setr`%va,last,u(lmax,iter(u(filter,ISOBJID,get(%i0/CHARACTERS)),convtime(get(%i0/LAST)))));&LAST %i0=%q<last>;@name %i0=get(%i0/EMAIL)}

&OBJECT`RENAME`FIXACCOUNT [u(cobj,ams)]=@select/inline %2=PLAYER,{@check isdbref(u(setr`%va,accid,u(accid,%0)));th u(setstat,%q<accid>,OLD,%0,%1)}

&OBJECT`DESTROY`CLEANACCOUNTS [u(cobj,ams)]=@select/inline %2=PLAYER,{@check isdbref(u(setr`%va,accid,u(accid,%0,1)));@attach %!/INC`ACCOUNT`REMFROMACCOUNT=%q<accid>,%0}

@@ ****** ALTS SECTION *******

&CMD`+ALTS`PENNMUSH [u(cobj,ams)]=$^(?\:\+)?alts(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+ALTS`MAIN
@set [u(cobj,ams)]/CMD`+ALTS`PENNMUSH=regexp
&CMD`+ALTS`RHOSTMUSH [u(cobj,ams)]=$^(?\:\+)?alts(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+ALTS`MAIN
@set [u(cobj,ams)]/CMD`+ALTS`RHOSTMUSH=regexp
&CMD`+ALTS`MAIN [u(cobj,ams)]=th u(setq`%va,sysname,ALTS);@attach %!/INC`GETSWITCH=%1;@attach %!/INC`ALTS`[u(strfirstof`%va,%q<switch>,MAIN)]=%2,%3
@set [u(cobj,ams)]/CMD`+ALTS`[switch(%va,PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SWITCHES`ALTS`PLAYER [u(cobj,ams)]=LIST|PRIORITY
&SWITCHES`ALTS`ADMIN [u(cobj,ams)]=

&INC`ALTS`MAIN [u(cobj,ams)]=@attach %!/INC`CHECKPC=u(strfirstof`%va,%0,%#),1;@check get(%q<t1>/D`ALTS)=@attach %!/INC`MSG=[name(%q<t1>)] is not on any alts lists.;@attach %!/INC`MSG=Alts of [ansi(h,%q<t1name>)] ([u(hideidle,%q<t1>)]): [u(FUN`ALTS`LIST,%q<t1>)]

&INC`ALTS`LIST [u(cobj,ams)]=@check u(setr`%va,accid,u(accid,%#,1))=@attach %!/INC`MSG=ERROR: This character is not bound to an account.;@check strlen(%0)=@attach %!/INC`MSG=ERROR: List or target field empty. Please enter a name or a number to list this alt under.;@select/inline cand(strlen(%0),strlen(%1))=1,{@attach %!/INC`CHECKPC=%0,1;@check strmatch(u(accid,%q<t1objid>),%q<accid>)=@attach %!/INC`MSG=ERROR: %q<t1name> is not one of your registered characters.;@check cand(gte(u(setr`%va,list,%1),0),isint(%1))=@attach %!/INC`MSG=ERROR: Alts list must be a whole number 0 or greater.},0,{@attach %!/INC`CHECKPC=%#,1;@check cand(gte(u(setr`%va,list,%0),0),isint(%0))=@attach %!/INC`MSG=ERROR: Alts list must be a whole number 0 or greater.};&D`ALTS %q<t1>=%q<list>;@attach %!/INC`MSG=Okay! %q<t1name> will [if(%q<list>,now appear in Alts List #%q<list>.,not appear in any Alts lists.)]

&FUN`ALTS`LIST [u(cobj,ams)]=if(get(%0/D`ALTS),u(strfirstof`%va,u(itemize`%va,iter(u(sortname,squish(iter(get(u(accid,%0)/CHARACTERS),if(eq(default(%0/D`ALTS,0),default(%i0/D`ALTS,0)),%i0)))),u(moniker`%va,%i0)[if(not(%1),\([u(hideidle,%i0)]\))],%b,|),|,and,\,),None),None)

&INC`ALTS`PRIORITY [u(cobj,ams)]=@check u(setr`%va,accid,u(accid,%#,1))=@attach %!/INC`MSG=ERROR: This character is not bound to an account.;@select/inline t(strlen(%0))=1,{@select/inline cand(strlen(%0),strlen(%1))=1,{@attach %!/INC`CHECKPC=%0,1;@check strmatch(u(accid,%q<t1objid>),%q<accid>)=@attach %!/INC`MSG=ERROR: %q<t1name> is not one of your registered characters.;@check cand(gte(u(setr`%va,list,%1),0),isint(%1))=@attach %!/INC`MSG=ERROR: Alts priority must be a whole number 0 or greater.},0,{@attach %!/INC`CHECKPC=%#,1;@check cand(gte(u(setr`%va,list,%0),0),isint(%0))=@attach %!/INC`MSG=ERROR: Alts priority must be a whole number 0 or greater.};&D`ALTS`PRIORITY %q<t1>=%q<list>;@attach %!/INC`MSG=Okay! %q<t1name>'s Priority is now: %q<list>},0,{@attach %!/INC`ALTS`PRIORITY`LIST}

&INC`ALTS`PRIORITY`LIST [u(cobj,ams)]=@attach %!/INC`MSG=Your Alts by Priority: [iter(revwords(u(sortpriority`%va,u(alts,%#))),u(moniker`%va,%i0) ([default(%i0/D`ALTS`PRIORITY,0)]),%b,\,%b)]

@@ ******* IP SECTION *********

&CMD`+IP`PENNMUSH [u(cobj,ams)]=$^(?\:\+)?(?\:ip|sitematch|matchsite|site)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+IP`MAIN
@set [u(cobj,ams)]/CMD`+IP`PENNMUSH=regexp
&CMD`+IP`RHOSTMUSH [u(cobj,ams)]=$^(?\:\+)?(?\:ip|sitematch|matchsite|site)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+IP`MAIN
@set [u(cobj,ams)]/CMD`+IP`RHOSTMUSH=regexp
&CMD`+IP`MAIN [u(cobj,ams)]=th u(setq`%va,sysname,IP);@attach %!/INC`GETSWITCH=%1;@attach %!/INC`IP`[u(strfirstof`%va,%q<switch>,MAIN)]=%2,%3
@set [u(cobj,ams)]/CMD`+IP`[switch(%va,PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SWITCHES`IP`PLAYER [u(cobj,ams)]=
&SWITCHES`IP`ADMIN [u(cobj,ams)]=IP|GUESTS

&INC`IP`MAIN [u(cobj,ams)]=@stop u(isguest,%#)=@attach %!/INC`MSG=ERROR: Guests cannot use +ip.;@select/inline u(isadmin`%va,%#)=0,{@attach %!/INC`CHECKPC=%#,1,1;@attach %!/INC`IP`IPSHOW=%q<t1>,0},{@attach %!/INC`CHECKPC=u(strfirstof`%va,%0,%#),1,1;@@ @stop u(isguest,%q<t1>)=@attach %!/INC`MSG=ERROR: To match Guests\, please use +ip/guests;@attach %!/INC`IP`IPSHOW=%q<t1>}

&INC`IP`IP [u(cobj,ams)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: An IP address is required.;@attach %!/INC`IP`IPSHOW=%0,1,1

&INC`IP`IPSHOW [u(cobj,ams)]=@check gt(words(u(setr`%va,ips,if(%2,%0,u(mysql,GET`LOGIN,%q<t1id>)))),0)=@attach %!/INC`MSG=ERROR: No IP logs found to match against.;th u(setq`%va,pcs,u(sortname,u(filter,if(u(isadmin`%va,%#),ISOBJID,OBJNOTGUEST),u(mysql,GET`OBJIDS_FROM_IPS,u(SQL`IN`STRING,%q<ips>)))));@check words(%q<pcs>)=@attach %!/INC`MSG=ERROR: No Matching Logs found.;@pemit %#=u(header,Site matches for [if(%2,%0,name(%0))]);@dolist/inline %q<pcs>={@pemit %#=u(separator,ansi(h,u(pueblize,u(moniker`%va,##),+finger [name(##)]))[ansi(h,\(##\))] - [u(lastidle,##)]);@pemit %#=ansi(u(color`%va,%#,%q<sysname>,COLUMN_NAMES),align(20 22 25,Date,Address,Result));@pemit %#=iter(revwords(iter(u(mysql2,get`logins,objid(##)),align(20 22 25,u(fancytime`%va,elements(%i0,3,^),%#),elements(%i0,2,^),if(elements(%i0,1,^),Success,elements(%i0,4,^))),|,beep()),beep()),%i0,beep(),%R)};@pemit %#=u(subheader)

&Q`GET`LOGIN [u(cobj,ams)]=SELECT DISTINCT login_address FROM $LOGINS$ WHERE player_id=? AND login_result=1

&FIL`OBJNOTGUEST [u(cobj,ams)]=cand(isobjid(%0),not(u(isguest`%va,%0)))

&Q`GET`OBJIDS_FROM_IPS [u(cobj,ams)]=SELECT DISTINCT objid from $LOGINS$ as logins LEFT JOIN $PLAYERS$ as players ON players.player_id=logins.player_id WHERE logins.login_address IN (!)

&Q`GET`LOGINS [u(cobj,ams)]=SELECT login_result,login_address,UNIX_TIMESTAMP(login_date),login_reason from $LOGINS$ as logins LEFT JOIN $PLAYERS$ as players ON players.player_id=logins.player_id WHERE players.objid=? ORDER BY login_date DESC LIMIT 10

&TRG`IP`CLEANCONNLOGS [u(cobj,ams)]=@attach %!/INC`IDCHECK=%0;@check %q<id>;th u(setq`%va,keepids,u(mysql,GET`GOOD_LOGINS,%q<id>));@attach %!/INC`DOSQL=DELETE`OLD_LOGINS,%q<id>,u(SQL`IN`NUMBER,%q<keepids>)

&Q`GET`GOOD_LOGINS [u(cobj,ams)]=SELECT login_id FROM $LOGINS$ WHERE player_id=? ORDER BY login_date DESC LIMIT 20
&Q`DELETE`OLD_LOGINS [u(cobj,ams)]=DELETE FROM $LOGINS$ WHERE player_id=? AND login_id NOT IN (!)

&PLAYER`CONNECT`IPLOG [u(cobj,ams)]=@attach %!/INC`IDCHECK=%0;@check %q<id>;@attach %!/INC`DOSQL=INSERT`LOGIN,%q<id>,secs(),1,success,get(%0/LASTIP),get(%0/LASTSITE),u(isguest,%0);@trigger %!/TRG`IP`CLEANCONNLOGS=%0

&Q`INSERT`LOGIN [u(cobj,ams)]=INSERT INTO $LOGINS$ (player_id,login_date,login_result,login_reason,login_address,login_site,login_guest) VALUES (?,FROM_UNIXTIME('?'),?,?,?,?,?)

&PLAYER`CONNECTFAIL`IPLOG [u(cobj,ams)]=@attach %!/INC`IDCHECK=%0;@check %q<id>;@attach %!/INC`DOSQL=INSERT`LOGIN,%q<id>,secs(),0,failure: %1,get(%0/LASTIP),get(%0/LASTSITE);@@ @trigger %!/TRG`IP`CLEANCONNLOGS=%0

&SOCKET`LOGINFAIL`IPLOG [u(cobj,ams)]=@select/inline isdbref(%4)=1,{@trigger %!/PLAYER`CONNECTFAIL`IPLOG=%4,%3;th u(setq`%va,isgod,strmatch(#1,%4))[u(setq`%va,admin,u(isadmin`%va,%4))];@attach %!/INC`MSG`SYSCHAN={[if(cor(%q<isgod>,%q<admin>),ansi(hr,WARNING:),ansi(hy,ALERT:))]%BFailed [switch(1,%q<isgod>,ansi(hr,GOD),%q<admin>,ansi(hr,ADMIN),Player)] ([name(%4)]) login attempt from IP '%1' HOST '[host(%0)]' PORT '%0'. Consecutive Failure %2: %3. [if(strmatch(get(%4/LASTSITE),host(%0)),,ansi(hr,ALERT:)%BAttempt not from previous LASTSITE!)]};@select/inline cor(%q<isgod>,%q<admin>)=1,{@pemit/port %0=[ansi(hrf,WARNING:)]%BAdmin login failures are logged.};@select/inline %2=>1,{@pemit/port %0=If you have forgotten your password, ask an admin for a reset. Too many login attempts will cause a temporary lockout.}}

&OBJECT`DESTROY`IPLOGS [u(cobj,ams)]=@select/inline %2=PLAYER,{@attach %!/INC`DOSQL=DELETE`ALL_LOGS,%0}
&Q`DELETE`ALL_LOGS [u(cobj,ams)]=DELETE logins FROM $LOGINS$ INNER JOIN $PLAYERS$ ON logins.player_id=players.player_id WHERE players.objid=?

@@ ***** BANLIST SECTION *******
&CMD`+BANLIST`PENNMUSH [u(cobj,ams)]=$^(?\:\+)?banlist(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+BANLIST`MAIN
@set [u(cobj,ams)]/CMD`+BANLIST`PENNMUSH=regexp
&CMD`+BANLIST`RHOSTMUSH [u(cobj,ams)]=$^(?\:\+)?banlist(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+BANLIST`MAIN
@set [u(cobj,ams)]/CMD`+BANLIST`RHOSTMUSH=regexp
&CMD`+BANLIST`MAIN [u(cobj,ams)]=@check u(isadmin`%va,%#)=@force/inline %#=HUH_COMMAND;th u(setq`%va,sysname,BANLIST);@attach %!/INC`GETSWITCH=%1;@attach %!/INC`BANLIST`[u(strfirstof`%va,%q<switch>,MAIN)]=%2,%3
@set [u(cobj,ams)]/CMD`+BANLIST`[switch(%va,PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SWITCHES`BANLIST`PLAYER [u(cobj,ams)]=
&SWITCHES`BANLIST`ADMIN [u(cobj,ams)]=BANIP|BANPLAYER|DELETE|DURATION|COMMENT|MESSAGE

&INC`BANLIST`MAIN [u(cobj,ams)]=@select/inline t(strlen(%0))=0,{@attach %!/INC`BANLIST`LIST},1,{@attach %!/INC`BANLIST`DETAILS}

&INC`BANLIST`ID [u(cobj,ams)]=@check u(valnum,%0)=@attach %!/INC`MSG=Entry must be in ID number format.;@check hasattr(u(cobj,bandb)/BAN`%0)=@attach %!/INC`MSG=Entry '%0' not found.

&INC`BANLIST`LIST [u(cobj,ams)]=@pemit %#=u(HEADER,mudname() Ban List);@pemit %#=align(4 21 25 25,ID,IP,Affected,Until);@dolist/inline u(setr`%va,list,u(sortattr`%va,u(lattr`%va,u(cobj,bandb)/BAN`*)))={@pemit %#=align(4 21 25 25,##,get(u(cobj,bandb)/##),get(u(cobj,bandb)/##`AFFNAMES),u(setq`%va,col,if(gte(get(u(cobj,bandb)/##`UNTIL),secs()),n,hr))[ansi(%q<col>,u(fancytime`%va,convsecs(get(u(cobj,bandb)/##`UNTIL))))])};@pemit %#=u(SUBHEADER,words(%q<list>) Entries)

&INC`BANLIST`DETAILS [u(cobj,ams)]=@attach %!/INC`BANLIST`ID;@pemit %#=u(HEADER,Banlist Entry %0);@pemit %#=IP: [get(u(cobj,bandb)/BAN`%0)];@pemit %#=AFFECTED: [get(u(cobj,bandb)/%0`AFFNAMES)];@pemit %#=Set by [name(get(u(cobj,bandb)/BAN`%0`SETBY))] on [u(fancytime`%va,convsecs(get(u(cobj,bandb)/BAN`%0`SETON)))];@dolist/inline u(sortattr`%va,u(lattr`%va,u(cobj,bandb)/BAN`%0`COMMENT`*))={@pemit %#=u(SEPARATOR,Comment [last(##,`)]);@pemit %#=name(get(u(cobj,bandb)/##`BY)) commented on [u(fancytime`%va,convsecs(get(u(cobj,bandb)/##`ON)))]:%R[get(u(cobj,bandb)/##)]};@pemit %#=u(HEADER)

&INC`BANLIST`BANIP [u(cobj,ams)]=@check u(iswizard`%va,%#)=@attach %!/INC`MSG=Permission denied.;@check strlen(u(setr`%va,ip,%0))=@attach %!/INC`MSG=ERROR: No IP entered to ban.;@attach %!/INC`BANLIST`BAN=%0,trim(before(%1,/)),trim(after(%1,/))

&INC`BANLIST`BANPLAYER [u(cobj,ams)]=@check u(iswizard`%va,%#)=@attach %!/INC`MSG=Permission denied.;@attach %!/INC`CHECKPC=%0,1,BANLIST;th u(setq`%va,ip,get(%q<t1>/lastip));@attach %!/INC`BANLIST`BAN=%0,trim(before(%1,/)),trim(after(%1,/))

&INC`BANLIST`BAN [u(cobj,ams)]=@attach %!/INC`BANLIST`BADMATCH=%q<ip>;@stop words(u(setr`%va,banned,u(FUN`CHECKBAN,%q<ip>)))=@attach %!/INC`MSG=The IP matches (without wildcards) existing entries in the Ban Database: [u(itemize`%va,%q<banned>)];@check strlen(%1)=@attach %!/INC`MSG=ERROR: Duration value empty.;@check isint(u(setr`%va,dur,u(stringsecs`%va,%1)))=@attach %!/INC`MSG=ERROR: Duration did not compute to a time value. see help stringsecs() for proper syntax.;@check strlen(%2)=@attach %!/INC`MSG=ERROR: A comment is necessary to apply a ban.;th u(setq`%va,affects,u(FUN`BANAFFECTS,%q<ip>));@attach %!/INC`VERIFY={WARNING: This will affect sites used by [u(setr`%va,affnames,u(strfirstof`%va,u(itemize`%va,iter(%q<affects>,name(%i0),%b,|),|,and,\,),<nobody>))] for [etime(%q<dur>)]. These characters will be @booted upon completion. Are you sure? Enter again within ten seconds to verify.},BAN %q<affects> %q<dur>,BANLIST;th u(attrib_set`%va,u(cobj,bandb),u(setr`%va,banid,BAN`[u(nextslot,u(cobj,bandb),BAN)]),%q<ip>);@attach %!/INC`BANLIST`COMMENT`MAKE=%q<banid>,%2;th u(attrib_set`%va,u(cobj,bandb),%q<banid>`AFFECTED,%q<affects>);th u(attrib_set`%va,u(cobj,bandb),%q<banid>`AFFNAMES,%q<affnames>);th u(attrib_set`%va,u(cobj,bandb),%q<banid>`UNTIL,u(setr`%va,end,add(secs(),%q<dur>)));th u(attrib_set`%va,u(cobj,bandb),%q<banid>`SETON,secs());th u(attrib_set`%va,u(cobj,bandb),%q<banid>`SETBY,%:);@attach %!/INC`MSG=You have been banned from [mudname()] until [convsecs(%q<end>)].,%q<affects>;@dolist/inline %q<affects>={@boot %i0};@attach %!/INC`MSG`SYSCHAN={[ansi(h,\[%n\])] banned %q<ip> ([ansi(h,%q<affnames>)]) until [convsecs(%q<end>)] for: %2};@attach %!/INC`MSG=Banned.

&INC`BANLIST`BADMATCH [u(cobj,ams)]=th foreach(#lambda/u(setr`%va,quest,add(%q<quest>,strmatch(?,\%0))),%0);@stop %q<quest>=@attach %!/INC`MSG=ERROR: Question marks are not allowed in IP entries.;th foreach(#lambda/u(setr`%va,aster,add(%q<aster>,strmatch(*,\%0))),%0);@stop gt(%q<aster>,1)=@attach %!/INC`MSG=ERROR: no more than one asterisk per match.;@stop cor(strmatch(*.,%0),strmatch(*.,%0))=@attach %!/INC`MSG=ERROR: That would ban EVERYONE.;@stop and(%q<aster>,not(strmatch(*,right(%0,1))))=@attach %!/INC`MSG=ERROR: Asterisks may only be at the end of an IP entry.

&INC`BANLIST`DELETE [u(cobj,ams)]=@check u(iswizard`%va,%#)=@attach %!/INC`MSG=Permission denied.;@attach %!/INC`BANLIST`ID;@attach %!/INC`VERIFY={[ansi(hr,WARNING:)] This will delete Banlist entry %0. Are you sure? Enter again within ten seconds to verify.},BANLIST DELETE %0;@attach %!/INC`MSG`SYSCHAN={[ansi(h,\[%n\])] lifted BanID %0: [get(u(cobj,bandb)/%0)] ([get(u(cobj,bandb)/%0`AFFNAMES)]).};@attach %!/INC`MSG=Unbanned.;@attach %!/WIPE=u(cobj,bandb),%0

&INC`BANLIST`DURATION [u(cobj,ams)]=@check u(iswizard`%va,%#)=@attach %!/INC`MSG=Permission denied.;@attach %!/INC`BANLIST`ID;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Duration value empty.;@check isint(u(setr`%va,dur,stringsecs(%1)))=@attach %!/INC`MSG=ERROR: Duration did not compute to a time value. see help stringsecs() for proper syntax.;@attach %!/INC`MSG`SYSCHAN={[ansi(h,\[%n\])] reset ban duration for BanID %0: [get(u(cobj,bandb)/%0)] ([get(u(cobj,bandb)/%0`AFFNAMES)]) to [etime(%q<dur>)] - until [convsecs(add(secs(),%q<dur>))]};th u(attrib_set`%va,u(cobj,bandb),BAN`%0`UNTIL,add(secs(),%q<dur>));@attach %!/INC`MSG=Duration reset.

&INC`BANLIST`COMMENT [u(cobj,ams)]=@attach %!/INC`BANLIST`ID;@check strlen(%1)=@attach %!/INC`MSG=Comment text field empty.;@attach %!/INC`BANLIST`COMMENT`MAKE;@attach %!/INC`MSG=Comment posted.;@attach %!/INC`MSG`SYSCHAN={[ansi(h,\[%n\])] new comment posted to [pueblize(+banlist %0,+banlist %0)]}

&INC`BANLIST`COMMENT`MAKE [u(cobj,ams)]=th u(attrib_set`%va,u(cobj,bandb),u(setr`%va,attr,BAN`%0`COMMENT`[u(nextslot,u(cobj,bandb),BAN`%0`COMMENT)]),%1);th u(attrib_set`%va,u(cobj,bandb),%q<attr>`ON,secs());th u(attrib_set`%va,u(cobj,bandb),%q<attr>`BY,%:)

&INC`BANLIST`MESSAGE [u(cobj,ams)]=@check u(iswizard`%va,%#)=@attach %!/INC`MSG=Permission denied.;@attach %!/INC`BANLIST`ID

&FUN`BANAFFECTS [u(cobj,ams)]=u(sortname,setunion(iter(wildgrepi(u(ipdb),*`*,%0),first(%i0,`)),))

&FUN`CHECKBAN [u(cobj,ams)]=filterbool(#lambda/eq(comp(%0,get(u(cobj,bandb)/\%0),I),0),reglattr(u(cobj,bandb)/^\\d+$))

&FUN`CHECKLOGINS [u(cobj,ams)]=filterbool(#lambda/strmatch(%0,get(u(cobj,bandb)/\%0)),reglattr(u(cobj,bandb)/^\\d+$))

&SOCKET`CONNECT`BANLIST [u(cobj,ams)]=@check u(setr`%va,banid,first(u(FUN`CHECKLOGINS,%1)));@select/inline gt(secs(),get(u(cobj,bandb)/%q<banid>`UNTIL))=1,{@attach %!/INC`MSG`SYSCHAN={[ansi(hr,WARNING:)] EXPIRED Banned address %1 (match: BanID %q<banid>: [get(u(cobj,bandb)/%q<banid>)]) has connected.}},0,{@pemit/port %0=u(setr`%va,message,strfirstof(get(u(cobj,bandb)/%q<banid>`MESSAGE),u(VAR`BANLIST`MESSAGE)));@boot/port %0;@attach %!/INC`MSG`SYSCHAN={[ansi(hr,WARNING:)] Banned address %1 (match: BanID %q<banid>: [get(u(cobj,bandb)/%q<banid>)]) attempted to connect. Message sent to them: '%q<message>'}}

&VAR`BANLIST`MESSAGE [u(cobj,ams)]=Your site has been banned from [mudname()]. Contact staff at [game_config(SYSTEM`PUBLIC_EMAIL)] with any inquiries.

@@ ****** APPROVAL SECTION *********

&CMD`+APPROVE`PENNMUSH [u(cobj,ams)]=$^(?\:\+)?(approve|unapprove)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+APPROVE`MAIN
@set [u(cobj,ams)]/CMD`+APPROVE`PENNMUSH=regexp
&CMD`+APPROVE`RHOSTMUSH [u(cobj,ams)]=$^(?\:\+)?(approve|unapprove)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+APPROVE`MAIN
@set [u(cobj,ams)]/CMD`+APPROVE`RHOSTMUSH=regexp
&CMD`+APPROVE`MAIN [u(cobj,ams)]=th u(setq`%va,sysname,APPROVE);@attach %!/INC`GETSWITCH=%2;@attach %!/INC`%1`[u(strfirstof`%va,%q<switch>,MAIN)]=%3,%4
@set [u(cobj,ams)]/CMD`+APPROVE`[switch(%va,PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&INC`APPROVE`MAIN [u(cobj,ams)]=@select/inline cand(u(isadmin`%va,%#),strlen(%0))=1,{@attach %!/INC`CHECKPC=%0,1;@stop u(isapproved,%q<t1>)=@attach %!/INC`MSG=ERROR: They are already approved!;@attach %!/INC`APPROVE`DOAPPROVE=%q<t1>;@attach %!/INC`MSG=You have approved %q<t1name> for play!;@attach %!/INC`MSG`CHAN=%q<t1name> approved for play!},{@attach %!/INC`MSG=You [if(u(isapproved,%#),ansi(hg,are),ansi(hr,are not))] are approved for play!}

th u(NEWCONF,config,CHANNELS,APPROVAL,,Channel to announce Approvals on.,LIST)

&INC`APPROVE`DOAPPROVE [u(cobj,ams)]=@set %0=D`APPROVED:1;@attach %!/INC`MSG`CHAN=[ansi(h,u(moniker`%va,%0))] is now approved for play!,u(game_config,CHANNELS,APPROVAL),,1,,1;@trigger u(cobj,mco)/PLAYER`APPROVE=%0,%:
&INC`APPROVE`DOUNAPPROVE [u(cobj,ams)]=@set %0=D`APPROVED:0



&INC`UNAPPROVE`MAIN [u(cobj,ams)]=@select/inline u(isadmin`%va,%#)=1,{@attach %!/INC`CHECKPC=%0,1;@check u(isapproved,%q<t1>)=@attach %!/INC`MSG=ERROR: They are not approved!;@attach %!/INC`APPROVE`DOUNAPPROVE=%q<t1>;@attach %!/INC`MSG=You have unapproved %q<t1name> for play!;@attach %!/INC`MSG`CHAN=%q<t1name> unapproved for play.;@attach %!/INC`MSG`PUBCHAN=%q<t1name> unapproved for play.},{@attach %!/INC`MSG=You [if(u(isapproved,%#),ansi(hg,are),ansi(hr,are not))] are approved for play!}


@@ ********* STAFF SECTION ********
&CMD`+STAFF`PENNMUSH [u(cobj,ams)]=$^(?\:\+)?(staff|admin|wizlist)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+STAFF`MAIN
@set [u(cobj,ams)]/CMD`+STAFF`PENNMUSH=regexp
&CMD`+STAFF`RHOSTMUSH [u(cobj,ams)]=$^(?\:\+)?(staff|admin|wizlist)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+STAFF`MAIN
@set [u(cobj,ams)]/CMD`+STAFF`RHOSTMUSH=regexp
&CMD`+STAFF`MAIN [u(cobj,ams)]=th u(setq`%va,sysname,STAFF);@attach %!/INC`GETSWITCH=%2;@attach %!/INC`STAFF`[u(strfirstof`%va,%q<switch>,MAIN)]=%3,%4
@set [u(cobj,ams)]/CMD`+STAFF`[switch(%va,PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SWITCHES`STAFF`ADMIN [u(cobj,ams)]=ADD|REMOVE|ORDER|POSITION|DUTY|VACATION

&INC`STAFF`MAIN [u(cobj,ams)]=@check words(u(setr`%va,staff,u(FUN`FINDSTAFF)))=@attach %!/INC`MSG=ERROR: There are no Staff registered!;@pemit %#=u(header,mudname() Staff);@pemit %#=ansi(u(color`%va,%#,%q<sysname>,COLUMN_NAMES),align(3 20 3 40 8,Idl,Name,Bit,Position,Duty));@pemit %#=u(separator);@dolist/inline %q<staff>=@pemit %#=u(FUN`WIZLIST,##,%#);@select/inline t(strlen(u(setr`%va,smsg,u(game_config,STAFF,WIZLIST_FOOTER))))=1,{@pemit %#=u(separator);@pemit %#=%q<smsg>};@pemit %#=u(subheader)

th u(newconf,config,STAFF,WIZLIST_FOOTER,,Displayed at the bottom of +admin.,WORD)

&FUN`FINDSTAFF [u(cobj,ams)]=u(sortstaff,switch(%va,PennMUSH,lsearch(all,type,player,elock,D`STAFF:>0),RhostMUSH,search(EPLAYER=[lit(hasattr(##/D`STAFF))])))

&SORTSTAFF [u(cobj,ams)]=u(SORTSTAFF`%va,%0,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))
&SORTSTAFF`PENNMUSH [u(cobj,ams)]=sortkey(#lambda/default(\%0/D`STAFF`ORDER,99),%0,n,%1,%2)
&SORTSTAFF`RHOSTMUSH [u(cobj,ams)]=sortby(#lambda/[lit([ncomp(default(%0/D`STAFF`ORDER,99),default(%1/D`STAFF`ORDER,99))])],%0,%1,%2)

&FUN`WIZLIST [u(cobj,ams)]=align(3 20 3 40 8,u(hideidle,%0),u(moniker`%va,%0),u(choosegame,switch(1,u(iswizard`%va,%0),ansi(hr,WIZ),hasflag(%0,ROYALTY),ansi(hc,ROY),ansi(hx,N/A)),bittype(%0)),default(%0/D`STAFF`POSITION,???),if(objeval(%#,conn(%0)),switch(1,get(%0/D`STAFF`VACATION),ansi(hx,Vac),get(%0/D`STAFF`DUTY),ansi(if(u(ishidden`%va,%0),hx,hg),On),ansi(hx,Off)),switch(1,get(%0/D`STAFF`VACATION),ansi(hx,Vac),ansi(hx,Off))))

&INC`STAFF`ADD [u(cobj,ams)]=@check u(iswizard`%va,%#)=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`CHECKPC=%0,1;@stop get(%q<t1>/D`STAFF)=@attach %!/INC`MSG=ERROR: %q<t1name> is already on the Staff list!;@select/inline gt(strlen(%1),0)=1,{@check u(valnum,%1)=@attach %!/INC`MSG=ERROR: Order Must be a whole positive number.;th u(attrib_set`%va,%q<t1>,D`STAFF`ORDER,%1)};th u(attrib_set`%va,%q<t1>,D`STAFF,1);@attach %!/INC`MSG=You have added %q<t1name> to Staff!;@attach %!/INC`MSG`NOTICE=You have been added to Staff!,%q<t1>;@attach %!/INC`MSG`CHAN=%q<t1name> added to Staff.;@dolist/inline/delimit | [u(game_config,CHANNELS,JOIN_ADMIN)]={@chan/on %i0=%q<t1>}

&INC`STAFF`REMOVE [u(cobj,ams)]=@check u(iswizard`%va,%#)=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`CHECKPC=%0,1;@check get(%q<t1>/D`STAFF)=@attach %!/INC`MSG=ERROR: %q<t1name> is not on the Staff list!;@attach %!/INC`MSG=You have removed %q<t1name> from Staff!;@attach %!/INC`MSG`NOTICE=You have been removed from Staff!,%q<t1>;@attach %!/WIPE=%q<t1>,D`STAFF;@attach %!/INC`MSG`CHAN=%q<t1name> removed from Staff.;@dolist/inline/delimit | [u(game_config,CHANNELS,JOIN_ADMIN)]={@chan/off %i0=%q<t1>}

&INC`STAFF`VACATION [u(cobj,ams)]=@select/inline u(setr`%va,vac,not(default(%#/D`STAFF`VACATION,0)))=1,{@attach %!/INC`MSG=You are now On Vacation!},0,{@attach %!/INC`MSG=You are now Off Vacation!};&D`STAFF`VACATION %#=%q<vac>

&INC`STAFF`ORDER [u(cobj,ams)]=@attach %!/INC`CHECKPC=%0,1;@check t(get(%q<t1>/D`STAFF))=@attach %!/INC`MSG=ERROR: They are not on the Staff list!;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Order field empty.;@check u(valnum,%1)=@attach %!/INC`MSG=ERROR: Order Must be a whole positive number.;&D`STAFF`ORDER %q<t1>=%1;@attach %!/INC`MSG=You have set %q<t1name>'s Staff order to %1!;@attach %!/INC`MSG`NOTICE=Your staff order is now %1!;@attach %!/INC`MSG`CHAN=%q<t1name>'s Staff Order is now: %1

&INC`STAFF`POSITION [u(cobj,ams)]=@attach %!/INC`CHECKPC=%0,1;@check get(%q<t1>/D`STAFF)=@attach %!/INC`MSG=ERROR: They are not on the Staff list!;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Position field empty.;&D`STAFF`POSITION %q<t1>=%1;@attach %!/INC`MSG=You have set %q<t1name>'s Staff Position to %1!;@attach %!/INC`MSG`NOTICE=Your Staff Position is now %1!;@attach %!/INC`MSG`CHAN=%q<t1name>'s Staff Position is now: %1

&INC`STAFF`DUTY [u(cobj,ams)]=@select/inline u(setr`%va,duty,not(default(%#/D`STAFF`DUTY,0)))=1,{@attach %!/INC`MSG=You are now On duty!},0,{@attach %!/INC`MSG=You are now Off Duty!};&D`STAFF`DUTY %#=%q<duty>

&PLAYER`CONNECT`STAFFDUTY [u(cobj,ams)]=@check cand(get(%0/D`STAFF),u(player_config`%va,%0,LOGIN,STATUS));@wait 3={@attach %!/INC`MSG=You are [if(hasflag(%0,DARK),DARK,not Dark)], [if(hidden(%0),Hidden,not Hidden)] and [if(get(%0/D`STAFF`VACATION),on Vacation,if(default(%0/D`STAFF`DUTY,0),On Duty,Off Duty))].}

@@ ******** IDLERS SECTION ******
&CMD`+IDLERS`PENNMUSH [u(cobj,ams)]=$^(?\:\+)?idlers(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+IDLERS`MAIN
@set [u(cobj,ams)]/CMD`+IDLERS`PENNMUSH=regexp
&CMD`+IDLERS`RHOSTMUSH [u(cobj,ams)]=$^(?\:\+)?idlers(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+IDLERS`MAIN
@set [u(cobj,ams)]/CMD`+IDLERS`RHOSTMUSH=regexp
&CMD`+IDLERS`MAIN [u(cobj,ams)]=th u(setq`%va,sysname,IDLERS);@attach %!/INC`GETSWITCH=%1;@attach %!/INC`IDLERS`MAIN=%2,%3,%q<switch>
@set [u(cobj,ams)]/CMD`+IDLERS`[switch(%va,PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&INC`IDLERS`MAIN [u(cobj,ams)]=@select/inline gt(strlen(%0),0)=1,{@attach %!/INC`VALID`POSINT=%0,Days},{th u(setq`%va,value,30)};@check words(u(setr`%va,idlers,sortkey(#lambda/convtime(get(\%0/LASTLOGOUT)),setdiff(lsearch(all,eplayer,\[cand(gt(sub(secs(),convtime(get(##/LASTLOGOUT))),mul(86400,%q<value>)),not(u(isguest`%va,##)),not(u(isadmin`%va,##)),not(gte(conn(##),0)))\]),num(u(cobj,ancestor_player))),n)))=@attach %!/INC`MSG=Nobody's been offline longer than %q<value>.;@select/inline strlen(%2)=>0,{@check words(u(setr`%va,idlers,u(filter,CHARTYPE,%q<idlers>,%b,%b,%2)))=@attach %!/INC`MSG=No idling characters are type %2.};@pemit %#=u(header,if(strlen(%2),%2%B)Players over %q<value> Days Idle)%R[ansi(u(color`%va,%#,COLOR,COLUMN_NAMES),align(-6 3 2 3 4 21 4 21 6,Dbref,Sta,Al,Fac,Type,Name,Mail,IP,LastOn))]%R[u(separator)];@dolist/inline %q<idlers>=@pemit %#=u(FUN`ACCOUNT`CHARLINE,%i0);@pemit %#=u(footer)

&SWITCHES`IDLERS`ADMIN [u(cobj,ams)]=
&SWITCHES`IDLERS`PLAYER [u(cobj,ams)]=u(game_config,FCLIST,TYPES)

&FIL`CHARTYPE [u(cobj,ams)]=strmatch(default(%0/D`FINGER`TYPE,OC),%1)

@@ SQL SECTION

&SQL`INSTALL [u(cobj,ams)]=PLAYERS|LOGINS
&SQL`TABLE`LOGINS [u(cobj,ams)]=mushcode_logins
&SQL`TABLE`PLAYERS [u(cobj,ams)]=mushcode_players

&Q`INSTALL`PLAYERS [u(cobj,ams)]=CREATE TABLE IF NOT EXISTS $PLAYERS$ (player_id BIGINT(31) UNSIGNED NOT NULL AUTO_INCREMENT,objid VARCHAR(30) NOT NULL,player_name VARCHAR(60) NOT NULL DEFAULT 'Unset',PRIMARY KEY(player_id),UNIQUE KEY player_objid (objid)) ENGINE=InnoDB DEFAULT CHARSET=latin1 AUTO_INCREMENT=1

&Q`INSTALL`LOGINS [u(cobj,ams)]=CREATE TABLE IF NOT EXISTS $LOGINS$ (login_id BIGINT(31) UNSIGNED NOT NULL AUTO_INCREMENT, player_id BIGINT(31) UNSIGNED NOT NULL,login_date DATETIME,login_result BOOL NOT NULL DEFAULT TRUE,login_reason VARCHAR(80),login_address VARCHAR(39) NOT NULL,login_site varchar(100),login_guest BOOL NOT NULL DEFAULT FALSE,PRIMARY KEY(login_id),FOREIGN KEY(player_id) REFERENCES $PLAYERS$(player_id) ON UPDATE CASCADE ON DELETE CASCADE) ENGINE=InnoDB DEFAULT CHARSET=latin1 AUTO_INCREMENT=1

&TRG`INSTALL [u(cobj,ams)]=@dolist/inline/delimit | [v(SQL`INSTALL)]={@attach %!/INC`DOSQL=INSTALL`##}
@trigger u(cobj,ams)/TRG`INSTALL

@@ CHARACTER - +ACCOUNT
+help/add +account=[u(cobj,ams)]/HLP`+ACCOUNT
+help/category +account=Character
&HLP`+ACCOUNT [u(cobj,ams)]=The account system tracks players (and their alts) and their current status in the game (unapproved, approved, builder, admin, etc.).%R%R[ansi(hc,Player Commands)]%R[align(5 [sub(u(width`%va,%#),6)],,[ansi(h,+account)] - Shows your account if you have one.%R[ansi(h,+account/new <name>)] - For new players. Enter this to start a new account on our game. You should preferably use your email address!%R[if(u(OPEN_CREATION),[ansi(h,+account/rename <newname>)] - Renames your account.%R)][ansi(h,@password <old>=<new>)] - Change your password. If you forget your password\, staff will send a new one to your <email> on request - or if your Guest IP matches your previous logins.%R[ansi(h,+ip)] - Shows your recent logins. Please report any suspicious login attempts to staff!)]

+shelp/add +account=[u(cobj,ams)]/SHLP`+ACCOUNT
+shelp/category +account=Character
&SHLP`+ACCOUNT [u(cobj,ams)]=[ansi(hc,Staff Commands)]%R[align(5 [sub(u(width`%va,%#),6)],,[ansi(h,+account/list)] - Shows all accounts in the system.%R[ansi(h,+account/inactive)] - List inactive accounts.%R[ansi(h,+account <account>)] - Shows a specific account. Target can be a player\, OR an account name preceded by # such as #MushDude.%R[ansi(h,+account/create <name>)] - Create a new account.%R[ansi(h,+account/rename <account>=<newname>)] - Target as with +account. Renames account to <newname>.%R[ansi(h,+account/bind <player>=<account>)] - Binds <player> to an account\, removing them from any other account they're in. <account> can be the name of an character \(if they have an account\)\, the word NEW \(makes a new account using the character's name\)\, or the name of an account preceded by # - such as #MushDude.%R[ansi(h,+account/unbind <player>)] - Removes a player from any account they're in.%R[ansi(h,+account/unbound)] - Displays all unbound characters. Some are best left that way.%R[ansi(h,@newpass *<name>=<newpassword>)] - Set a new password for <name>. Note the asterisk\, this is necessary! This command is WIZARD only.%R[ansi(h,+ip <player>)] - Checks a player's logins and anyone with matching IPs.%R[ansi(h,+ip/guests <player>)] - Includes Guests in the IP lookup.%R[ansi(h,+ip/ip <ip>)] - Checks an IP for any matching players. Note that ::ffff: must be prepended to any IPv4 addresses if they're showing up in normal uses of +ip!)]

@@ SHELP - CHARACTER - APPROVAL
+shelp/add +approve=[u(cobj,ams)]/SHLP`+APPROVE
+shelp/category +approve=Character
&SHLP`+APPROVE [u(cobj,ams)]=[ansi(h,+approve <name>)]%RThis will approve <name>%R%R[ansi(h,+unapprove <name>)]%RThis will unapprove <name>%R%RStaff are always considered 'approved' but may need to have the UNREGISTERED flag removed with @set if they intend to use @powers.

@@ CHARACTER - +ALTS
+help/add +alts=[u(cobj,ams)]/HLP`+ALTS
+help/category +alts=Character
&HLP`+ALTS [u(cobj,ams)]=The Alts system allows you to specify what characters of yours other players can associate with you. It also allows you to configure which alts take precedent (if any) for receiving messages from channels and BBS notifications.%R%R[ansi(hc,Commands)]%R[align(5 [sub(u(width`%va,%#),6)],,[ansi(h,+alts <target>)] - Check someone's alts. Alts are any characters with the same ALT ID. Without a target it will use yourself\, showing you what others would see.%R[ansi(h,+alts/list <#>)] or [ansi(h,+alts/list <alt>=<#>)] - Set your current character or an alt of yours to use a specific ALT ID. Characters with the same ALT ID are linked on +alts displays. Alt IDs are numbers.%R%RAll Alts begin with Alt ID 0\, which is special. Characters with Alt ID 0 have no alts period.%R%R[ansi(h,+alts/priority <#>)] or [ansi(h,+alts/priority <alt>=<#>)] - Set your Alt PRIORITY.%R[ansi(h,+alts/priority)] - Show the priority settings of all your alts.%R%RThe PRIORITY affects channel reception. In both cases\, the HIGHEST PRIORITY ALT who is eligible to receive the message \(has the channel turned on and not gagged\) will receive it. Where multiple alts have the same priority\, all with that priority will receive it.%R%RFor example\, if Alts A and B are set to Priority 1 and have the Public channel on\, and Alt C also has it on but is priority 0\, only A and B will see messages from the Public channel. If A and B gag or turn it off\, C will once again receive Public messages.)]

@@ SHELP - ADMINISTRATION
+shelp/add +staff=[u(cobj,ams)]/SHLP`+STAFF
+shelp/category +Staff=Administration
&SHLP`+STAFF [u(cobj,ams)]=Keep in mind that anyone on this list is able to use all softcoded admin commands!%R%R[ansi(hc,Staff Commands)]%R[align(5 [sub(u(width`%va,%#),6)],,[ansi(h,+staff)] - Lists staff. Identical to wizlist.%R[ansi(h,+staff/add <name>\[=<order>\])] and [ansi(h,+Staff/rem <name>)] - Hires or Removes Staff list. This does NOT affect flags or @powers! An optional Order can be provided for how they display. \(Tip: Use large numbers like 10\, 20\, 30\, for your staff to make re-organization easy.\)%R[ansi(h,+staff/order <name>=<order>)] - Changes the staff display order. Wizard only. Lower numbers show first. Equal order numbers sort by DBREF.%R[ansi(h,+staff/position <name>=<text>)] - Change a staff member's position. Wizard only.)]%R%R[ansi(hc,Staff List Display)]%R[align(5 [sub(u(width`%va,%#),6)],,[ansi(h,+staff/duty)] - Toggle duty status on or off.%R[ansi(h,+staff/vacation)] - Go 'on vacation' or toggle it off.)]

+shelp/add Permissions=[u(cobj,ams)]/SHLP`%va
+shelp/category Permissions=Administration
+shelp/metatags Permissions=wizard royalty bitlevel
&SHLP`PENNMUSH [u(cobj,ams)]=[ansi(hc,See Also:)] [u(pueblize,help CONTROL)], [u(pueblize,help WIZARD)], [u(pueblize,help ROYALTY)], [u(pueblize,help @set)], [u(pueblize,help @power)]%R%RStaff are characters able to use Admin commands. For this package anyone who is either WIZARD\, ROYALTY\, or on the +staff list is able to use admin commands\, but Penn's built-in commands use other standards.%R%RPennMUSH has 3 Staff tiers.%R%R1. The [ansi(hr,GOD)] (#1) is a special WIZARD capable of granting or removing the WIZARD flag to other characters. It can use @config/save and several other dangerous commands. Only login as #1 when truly necessary. Since even WIZARDS cannot control() it\, code objects cannot set attributes on #1 and it cannot use many softcoded systems properly.%R%R2. [ansi(hy,WIZARD)] flagged characters have command of everything in the game including dangerous commands such as @force (on players)\, @shutdown\, @hook\, and @config/set. It should be reserved for staff who absolutely need these privileges\, typically the coder(s) and owner. Some potentially dangerous softcoded commands are restricted to Wizard.%R%R3. [ansi(hc,ROYALTY)] have 'see but not touch' compared to WIZARDS - they have several implicit @powers like being able to @tel anywhere but can't change the game's code. This is a good flag for general staff.
&SHLP`RHOSTMUSH [u(cobj,ams)]=

+shelp/add Player Management=[u(cobj,ams)]/SHLP`PMANAGE
+shelp/category Player Management=Administration
&SHLP`PMANAGE [u(cobj,ams)]=WIZARDS have a great deal of hardcoded control over players for security and management. Most of these commands can only be used by them.%R%R[align(5 [sub(width(%#),6)],,[ansi(h,@pcreate <name>=<password>)] - Creates a player.%R%R[ansi(h,@newpass *<name>=<newpassword>)] - Changes a password. the * is necessary for hardcoded command lookups.%R%R[ansi(h,@sitelock/ban/player <name>)] - Bans a player. Be very careful with this! Check [u(pueblize,ansi(h,help @sitelock),help @sitelock)] for more info.%R%R[ansi(h,@nuke *<name>)] - Deletes a character AND EVERYTHING THEY OWN\, which by default includes EVERYTHING THEY MADE. use [ansi(h,@search *<name>)] to see what that would be before you @nuke! use [ansi(h,@chown <dbref>=me)] to save objects.)]

+shelp/add +banlist=[u(cobj,ams)]/SHLP`BANLIST
+shelp/category +banlist=Administration
&SHLP`BANLIST [u(cobj,ams)]=[ansi(hc,See Also:)] [pueblize(help @sitelock)]%RThe BANLIST is a softcoded alternative to Penn's hardcoded @sitelock, offering similar functionality with a more convenient command display and interface. Bans set in this system automatically expire when their duration has passed. Connections from EXPIRED bans will send up warnings on admin channels but not impede the player at all.%R%RThe system has no connection to @sitelock. @sitelock settings will be processed before Banlist is. Additionally, Banlist can only decide whether to allow a connection or not - it cannot restrict certain sites from logging on as guests but allow them to logon normally, for instance. This requires @sitelock. Use +banlist for any general bans and @sitelock for anything more complicated.%R%R[ansi(hc,Basic Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+banlist)] - Show all Banlist entries.%R[ansi(h,+banlist <id>)] - Checks details for a specific banlist entry.%R[ansi(h,+banlist/comment <id>=<comment>)] - Adds a comment to a Banlist entry.})]%R%R[ansi(hc,Wizard Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+banlist/banip <ip>=<duration>/<comment>)] - Bans an IP (easily retrieved using the +ip command) for <duration> for <comment> reason. <duration> must be a string accepted by stringsecs (help stringsecs) such as 5d or 2w (5 days, 2 weeks, etc). See details below about entering IPs.%R[ansi(h,+banlist/banplayer <name>=<duration>/<comment>)] - Like /banip but attempts to automatically retrieve the proper IP address.%R[ansi(h,+banlist/duration <id>=<duration>)] - Reset a duration with a new one from now onwards.%R[ansi(h,+banlist/delete <id>)] - lift a ban.%R[ansi(h,+banlist/message <id>=<text>)] - Send a custom message to banned connections when they attempt to connect.})]%R%RWhen banning PLAYERS, the system will pull from the target's LASTIP attribute. This is set when they DISCONNECT, so in a few situations it may not be the IP they are currently using or use most.%R%RWhen banning IPs, you must enter an IP as it's displayed in the +IP command. this includes any IPv6 ::ffff:. The system uses text matching so will catch IPv4 or IPv6 addresses equally. Wildcards are only allowed for restricting entire Class C IPv4 Addresses (example: ::ffff:150.33.5.*). Be VERY careful when dealing with wildcards. The system will attempt to warn about who else a Class C ban will catch but it can only automatically check so much.%R%RNote that IP addresses for commercial ISP customers change semi-regularly.%R%RWhen bans are set down, anyone it affects will be IMMEDIATELY BOOTED.%R%RThe Default message displayed to banned connectors: [u(u(cao)/VAR`BANLIST`MESSAGE)]


@@ COMMUNITY - +STAFF

+help/add +staff=[u(cobj,ams)]/HLP`+STAFF
+help/category +staff=Community
+help/metatags +staff=imms wizlist admin
&HLP`+STAFF [u(cobj,ams)]=+staff lists the game's administrators and their current status.%R[ansi(hc,See Also:)] [u(pueblize,help JUDGE)], [u(pueblize,help ROYALTY)], [u(pueblize,help WIZARD)]%R%R[align(5 [sub(u(width`%va,%#),6)],,[ansi(h,+staff)] - Show game's staff members.)]

@@ MIGRATION TOOL
&MIGRATE`ACCOUNTS [u(cobj,migrate)]=@check isdbref(u(cobj,accounts))=@attach %!/INC`MSG=ERROR: Cannot locate 2.0 Account Database. Cannot continue.;@check isdbref(u(setr`%va,adb,u(%q<index>/adb)))=@attach %!/INC`MSG=ERROR: Cannot locate 1.0 Account Database <ADB>. Cannot continue.;@check words(u(setr`%va,oldids,sort(u(lattr`%va,%q<adb>/*))))=@attach %!/INC`MSG=ERROR: No accounts to import.;@dolist %q<oldids>={@attach [u(cobj,ams)]/INC`ACCOUNT`MAKENUM=##;th u(attrib_set`%va,%q<accid>,CHARACTERS,u(setr`%va,chars,get(%q<adb>/##)));@attach %!/INC`CPTREE=%q<adb>,##`BB,%q<accid>,D`BB;@attach %!/INC`CPTREE=%q<adb>,##`COLOR,%q<accid>,D`COLOR;@cpattr %q<adb>/##`OLD=%q<accid>/OLD;@cpattr %q<adb>/##`EMAIL=%q<accid>/EMAIL;@cpattr %q<adb>/##`TZ=%q<accid>/D`CONFIG`SYSTEM`TIMEZONE;th iter(%q<chars>,u(attrib_set`%va,%i0,D`ACCOUNT,%q<accid>))}

&MIGRATE`STAFF [u(cobj,migrate)]=@check isdbref(u(setr`%va,pco,u(%q<index>/pco)))=@attach %!/INC`MSG=ERROR: Cannot locate 1.0 PennMUSH Configuration Object. Cannot continue.;@dolist/inline/delimit | [get(%q<pco>/WIZLIST)]={&D`STAFF [before(##,~)]=1;&D`STAFF`ORDER [before(##,~)]=after(##,~);&D`STAFF`POSITION [before(##,~)]=u(strfirstof`%va,get(before(##,~)/POSITION),get(before(##,~)/JOB))};&CONFIG`STAFF`WIZLIST_FOOTER [u(cobj,config)]=u(%q<pco>/WIZLIST`MSG)

&MIGRATE`APPROVAL [u(cobj,migrate)]=@dolist/inline u(setr`%va,unapproved,lsearch(all,type,player,elock,!POWER^GUEST&!FLAG^WIZARD^&!FLAG^ROYALTY&FLAG^UNREGISTERED))={&D`APPROVED %i0=0};@dolist/inline u(setr`%va,approved,lsearch(all,type,player,elock,!POWER^GUEST&!FLAG^WIZARD&!FLAG^ROYALTY&!FLAG^UNREGISTERED))={&D`APPROVED %i0=1};@dolist/inline %q<unapproved>={@set %i0=!UNREGISTERED}

&MIGRATE`BANS [u(cobj,migrate)]=@check isdbref(u(setr`%va,bandb,u(cobj,bandb))=@attach %!/INC`MSG=ERROR: Cannot locate 2.0 Ban Database. Cannot continue.;@check isdbref(u(setr`%va,olddb,u(%q<index>/bandb)))=@attach %!/INC`MSG=ERROR: Cannot locate 1.0 Ban Database <BANDB>. Cannot continue.;@check words(u(setr`%va,oldids,sort(u(lattr`%va,%q<adb>/*)))=@attach %!/INC`MSG=ERROR: No bans to import.;@dolist/inline %q<oldids>={@attach %!/INC`CPTREE=%q<olddb>,##,%q<bandb>,BAN};@attach %!/INC`MSG=Successfully converted [words(%q<oldids>)] bans!

&CONFLICT`ACCOUNTS [u(cobj,migrate)]=@check isdbref(u(setr`%va,cao,u(%q<index>/cao)))=@attach %!/INC`MSG=ERROR: Cannot locate 1.0 Character Accounts Object <CAO>. Cannot continue.;@halt %q<cao>;@set %q<cao>=NO_COMMAND;@tel %q<cao>=[u(cobj,oldbox)];@attach %!/INC`MSG=Successfully disabled old Account System.
