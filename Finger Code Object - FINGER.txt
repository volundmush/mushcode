@switch/inline isdbref(u(finger))=0,{@teleport create(Finger Code Object <FINGER>)=config(master_room)}
&finger u(coi)=locate(config(master_room),Finger Code Object <FINGER>,TXxi)
@parent u(finger)=u(coi)
@set u(finger)=WIZARD SAFE !NO_COMMAND

&CMD`FINGER u(finger)=$^(?s)(?\:\+)?(oocfinger|finger|icfinger)(?\:/(\S+))?(?\: +(.+?))?(?\:=(.*?))?$:@include u(ccs)/INC`PARTIAL=%2,setunion(get(u(finger)/VAR`[edit(%1,ic,)]`PLAYFLAGS),if(isadmin(%#),get(u(finger)/VAR`[edit(%1,ic,)]`ADMINFLAGS)),|,|),|,ucstr([edit(%1,ic,)]),switch,switch;@include u(finger)/INC`[edit(%1,ic,)]`[strfirstof(%q<switch>,MAIN)]=%3,%4
@set u(finger)/CMD`FINGER=regexp

&VAR`FINGER`PLAYFLAGS u(finger)=SET
&VAR`FINGER`ADMINFLAGS u(finger)=

&RFN`MSGHEAD u(finger)=msghead(v(VAR`MSGHEAD))
&VAR`MSGHEAD u(finger)=FINGER

&RFN`HEADER u(finger)=header(%0,,FINGER`BORDER,FINGER`BORDERDOT,FINGER`BORDERTEXT,1)
&RFN`SEPARATOR u(finger)=separator(%0,,FINGER`BORDER,FINGER`BORDERDOT,FINGER`BORDERTEXT,1)

th attrib_set(u(cco-db)/VAR`CATEGORIES,setunion(get(u(cco-db)/VAR`CATEGORIES),FINGER,|,|))
&VAR`CATEGORIES`FINGER u(cco-db)=BORDER|BORDERTEXT|BORDERDOT

&INC`FINGER`MAIN u(finger)=@include u(ccs)/INC`CHECKPC=strfirstof(%0,%#),1,FINGER;th setq(mode,default(me/VAR`FINGER`MODE,DEFAULT));@nspemit %#=u(RFN`HEADER,u(GET`FINGER`%q<mode>,%q<t1>,NAME));th setq(col1,u(VAR`FINGER`%q<mode>`COLUMN1,%q<t1>));th setq(rjust1,lmath(max,iter(%q<col1>,strlen(last(%i0,~)),|,%b)));th setq(col2,u(VAR`FINGER`%q<mode>`COLUMN2,%q<t1>));th setq(rjust2,lmath(max,iter(%q<col2>,strlen(last(%i0,~)),|,%b)));@nspemit %#=align(1. [floor(fdiv(sub(width(%#),7),2))] 1. [ceil(fdiv(sub(width(%#),7),2))] 1.,ansi(firstcolor(%#,FINGER`BORDER,DEFAULT`BORDER),|),iter(%q<col1>,rjust(elements(%i0,2,~),%q<rjust1>):%B[u(GET`FINGER`%q<mode>,%q<t1>,elements(%i0,1,~))],|,%R),ansi(firstcolor(%#,FINGER`BORDER,DEFAULT`BORDER),|),iter(%q<col2>,rjust(elements(%i0,2,~),%q<rjust2>):%B[u(GET`FINGER`%q<mode>,%q<t1>,elements(%i0,1,~))],|,%R),ansi(firstcolor(%#,FINGER`BORDER,DEFAULT`BORDER),|));@dolist/inline/delimit | [v(VAR`FINGER`%q<mode>`EXTRA)]=@switch/inline t(strlen(u(GET`FINGER`%q<mode>,%q<t1>,%i0)))=1,{@nspemit %#=u(RFN`SEPARATOR,capnames(%i0));@nspemit %#=align(1. [sub(width(%#),4)] 1.,ansi(firstcolor(%#,FINGER`BORDER,DEFAULT`BORDER),|),u(GET`FINGER`%q<mode>,%q<t1>,%i0),ansi(firstcolor(%#,FINGER`BORDER,DEFAULT`BORDER),|))};@nspemit %#=u(RFN`HEADER)

@@ [left(u(GET`FINGER`%q<mode>,%q<t1>,elements(%i0,1,~)),sub(ceil(fdiv(sub(width(%#),7),2)),add(2,default(%q<col2>`RJUST,6))))]

&INC`FINGER`SET u(finger)=@include u(ccs)/INC`CHECKPC=if(cand(isadmin(%#),strmatch(%0,*/*)),before(%0,/),%#),1,FINGER;@include u(ccs)/INC`PARTIAL=if(cand(isadmin(%#),strmatch(%0,*/*)),after(%0,/),%0),setunion(get(u(finger)/VAR`FINGER`[default(me/VAR`FINGER`MODE,DEFAULT)]`PLAYERSET),if(isadmin(%#),get(u(finger)/VAR`FINGER`[default(me/VAR`FINGER`MODE,DEFAULT)]`ADMINSET)),|,|),|,FINGER,field,field;@switch/inline strlen(%1)=0,{@include u(ccs)/INC`VERIFY={[ansi(hr,WARNING:)] Setting %q<field> to nothing will clear it. Are you sure? Enter that again to verify!},CLEAR FINGER %q<field>,FINGER};th u(u(finger)/SET`FINGER`[default(me/VAR`FINGER`MODE,DEFAULT)],%q<t1>,%q<field>,trimpenn(%1));@nspemit %#=u(RFN`MSGHEAD,FINGER) You set [if(strmatch(%#,%q<t1>),your,%q<t1name>'s)] [capnames(%q<field>)] Finger Field to: %1;@switch/inline strmatch(%#,%q<t1>)=0,{@nspemit %q<t1>=u(RFN`MSGHEAD,FINGER) [ansi(h,%n)] set your [capnames(%q<field>)] Finger Field to: %1}

&VAR`FINGER`MODE u(finger)=DEFAULT
&VAR`FINGER`DEFAULT`COLUMN1 u(finger)=SEX~Sex|RACE~Race
&VAR`FINGER`DEFAULT`COLUMN2 u(finger)=JOB~Job
&VAR`FINGER`DEFAULT`EXTRA u(finger)=QUOTE|PROFILE|SKILLS
&VAR`FINGER`DEFAULT`PLAYERSET u(finger)=SEX|JOB|RACE|QUOTE|PROFILE|SKILLS
&VAR`FINGER`DEFAULT`ADMINSET u(finger)=

&SET`FINGER`DEFAULT u(finger)=if(strlen(%2),if(hasattr(u(finger)/SET`FINGER`DEFAULT`%1),u(u(finger)/SET`FINGER`DEFAULT`%1,%0,%2),attrib_set(%0/D`FINGER`%1,%2)),if(hasattr(u(finger)/SET`FINGER`DEFAULT`%1`CLEAR),u(u(finger)/SET`FINGER`DEFAULT`%1`CLEAR,%0),attrib_set(%0/D`FINGER`%1)))

&SET`FINGER`DEFAULT`SEX u(finger)=attrib_set(%0/SEX,%1)

&GET`FINGER`DEFAULT u(finger)=if(hasattr(u(finger)/GET`FINGER`DEFAULT`%1),u(u(finger)/GET`FINGER`DEFAULT`%1,%0),get(%0/D`FINGER`%1))

&GET`FINGER`DEFAULT`NAME u(finger)=name(%0)
&GET`FINGER`DEFAULT`SEX u(finger)=switch(get(%0/SEX),m*,Male,f*,Female,n*,Neuter,???)
&GET`FINGER`DEFAULT`RACE u(finger)=default(%0/D`FINGER`RACE,Unset)
&GET`FINGER`DEFAULT`JOB u(finger)=default(%0/D`FINGER`JOB,Wanderer)

&VAR`OOCFINGER`PLAYFLAGS u(finger)=SET
&VAR`OOCFINGER`ADMINFLAGS u(finger)=

&INC`OOCFINGER`MAIN u(finger)=@include u(ccs)/INC`CHECKPC=strfirstof(%0,%#),1,OOCFINGER;@nspemit %#=u(RFN`HEADER,%q<t1name>);@dolist/inline/delimit | [v(VAR`OOCFINGER`[setr(mode,default(me/VAR`OOCFINGER`MODE,DEFAULT))]`COLUMN)]={@nspemit %#=align(1. >[v(VAR`OOCFINGER`%q<mode>`COLUMN`WIDTH)] 1. [sub(width(%#),v(VAR`OOCFINGER`%q<mode>`COLUMN`WIDTH),7)] 1.,ansi(firstcolor(%#,FINGER`BORDER,DEFAULT`BORDER),|),elements(%i0,2,~),ansi(firstcolor(%#,FINGER`BORDER,DEFAULT`BORDER),|),u(u(finger)/GET`OOCFINGER`%q<mode>,%q<t1>,elements(%i0,1,~)),ansi(firstcolor(%#,FINGER`BORDER,DEFAULT`BORDER),|))};@dolist/inline/delimit | [v(VAR`OOCFINGER`%q<mode>`EXTRA)]={@switch/inline t(strlen(u(u(finger)/GET`OOCFINGER`%q<mode>,%q<t1>,%i0)))=1,{@nspemit %#=subheader(capnames(%i0));@nspemit %#=align(1. [sub(width(%#),4)] 1.,ansi(firstcolor(%#,FINGER`BORDER,DEFAULT`BORDER),|),u(u(finger)/GET`OOCFINGER`%q<mode>,%q<t1>,%i0),ansi(firstcolor(%#,FINGER`BORDER,DEFAULT`BORDER),|))}};@nspemit %#=u(RFN`HEADER)

&INC`OOCFINGER`SET u(finger)=@include u(ccs)/INC`CHECKPC=if(cand(isadmin(%#),strmatch(%0,*/*)),before(%0,/),%#),1,OOCFINGER;@include u(ccs)/INC`PARTIAL=if(cand(isadmin(%#),strmatch(%0,*/*)),after(%0,/),%0),setunion(get(u(finger)/VAR`OOCFINGER`[default(me/VAR`OOCFINGER`MODE,DEFAULT)]`PLAYERSET),if(isadmin(%#),get(u(finger)/VAR`OOCFINGER`[default(me/VAR`OOCFINGER`MODE,DEFAULT)]`ADMINSET)),|,|),|,OOCFINGER,field,field;@switch/inline strlen(%1)=0,{@include u(ccs)/INC`VERIFY={[ansi(hr,WARNING:)] Setting %q<field> to nothing will clear it. Are you sure? Enter that again to verify!},CLEAR OOCFINGER %q<field>,OOCFINGER};th u(u(finger)/SET`OOCFINGER`[default(me/VAR`OOCFINGER`MODE,DEFAULT)],%q<t1>,%q<field>,%1);@nspemit %#=u(RFN`MSGHEAD,OOCFINGER) You set [if(strmatch(%#,%q<t1>),your,%q<t1name>'s)] [capnames(%q<field>)] Finger Field to: %1;@switch/inline strmatch(%#,%q<t1>)=0,{@nspemit %q<t1>=u(RFN`MSGHEAD,OOCFINGER) [ansi(h,%n)] set your [capnames(%q<field>)] Finger Field to: %1}

&VAR`OOCFINGER`MODE u(finger)=DEFAULT
&VAR`OOCFINGER`DEFAULT`COLUMN u(finger)=ALIAS~Alias|MAIL~Mush Mail|LAST~Last On|EMAIL~Email|CONTACT~Contact|HOMEPAGE~Homepage|THEME~Theme Song|ALTS~Alts|VOICEACTOR~Voice Actor
&VAR`OOCFINGER`DEFAULT`COLUMN`WIDTH u(finger)=11
&VAR`OOCFINGER`DEFAULT`EXTRA u(finger)=NOTES
&VAR`OOCFINGER`DEFAULT`PLAYERSET u(finger)=EMAIL|CONTACT|HOMEPAGE|THEME|ALTS|VOICEACTOR|NOTES
&VAR`OOCFINGER`DEFAULT`ADMINSET u(finger)=

&SET`OOCFINGER`DEFAULT u(finger)=if(strlen(%2),if(hasattr(u(finger)/SET`OOCFINGER`DEFAULT`%1),u(u(finger)/SET`OOCFINGER`DEFAULT`%1,%0,%2),attrib_set(%0/D`OOCFINGER`%1,%2)),if(hasattr(u(finger)/SET`OOCFINGER`DEFAULT`%1`CLEAR),u(u(finger)/SET`OOCFINGER`DEFAULT`%1`CLEAR,%0),attrib_set(%0/D`OOCFINGER`%1)))

&GET`OOCFINGER`DEFAULT u(finger)=if(hasattr(u(finger)/GET`OOCFINGER`DEFAULT`%1),u(u(finger)/GET`OOCFINGER`DEFAULT`%1,%0),get(%0/D`OOCFINGER`%1))

&GET`OOCFINGER`DEFAULT`ALIAS u(finger)=fullalias(%0)
&GET`OOCFINGER`DEFAULT`MAIL u(finger)=elements(mail(%0),2) Unread Messages
&GET`OOCFINGER`DEFAULT`LAST u(finger)=if(gt(conn(%0),0),ansi(hg,Connected!),get(%0/LAST))
&GET`OOCFINGER`DEFAULT`EMAIL u(finger)=switch(cor(strmatch(%0,%#),isadmin(%#))[strmatch(get(%0/D`OOCFINGER`EMAIL),!*)],00,get(%0/D`OOCFINGER`EMAIL),11,get(%0/D`OOCFINGER`EMAIL) (private),01,Private,get(%0/D`OOCFINGER`EMAIL))
&GET`OOCFINGER`DEFAULT`HOMEPAGE u(finger)=weblink(get(%0/D`OOCFINGER`HOMEPAGE))
&GET`OOCFINGER`DEFAULT`ALTS u(finger)=u(u(cao)/FUN`ALTS`LIST,%0,1)

@@ HELP SECTION
+help/addmain Character/+finger=[u(finger)]/HLP`FINGER
+help/addmain Character/+oocfinger=[u(finger)]/HLP`OOCFINGER

&HLP`FINGER u(finger)=The +finger system keeps track of and presents in-character information for some community flavor. By reading someone's finger, you can gain a brief overview of what the character is about!%R%R[ansi(hc,Commands)]%R[align(5 72,,{[ansi(h,+finger)] - Show your own finger.%R[ansi(h,+finger <name>)] - Show someone else's +finger.%R[ansi(h,+finger/set <field>=<text>)] - Set a finger field. Setting to nothing may clear it.})]%R%RFields settable by players: [itemize(get(u(finger)/VAR`FINGER`[default(me/VAR`FINGER`MODE,DEFAULT)]`PLAYERSET),|,and,\,)]%RFields Only settable by Admin: [itemize(get(u(finger)/VAR`FINGER`[default(me/VAR`FINGER`MODE,DEFAULT)]`ADMINSET),|,and,\,)]%R%RFields not listed here are controlled by other factors.[if(isadmin(%#),%R%R[subheader(Admin Section)]%RAdmin may set the fields of other characters using [ansi(h,+finger/set <name>/<field>=<text>)])]

&HLP`OOCFINGER u(finger)=The +oocfinger system keeps track of and presents out of character information for your fellow players' convenience.%R%R[ansi(hc,Commands)]%R[align(5 72,,{[ansi(h,+oocfinger)] - Show your own OOCfinger.%R[ansi(h,+oocfinger <name>)] - Show someone else's +oocfinger.%R[ansi(h,+oocfinger/set <field>=<text>)] - Set an oocfinger field. Setting to nothing may clear it.})]%R%RFields settable by players: [itemize(get(u(finger)/VAR`OOCFINGER`[default(me/VAR`OOCFINGER`MODE,DEFAULT)]`PLAYERSET),|,and,\,)]%RFields Only settable by Admin: [itemize(get(u(finger)/VAR`OOCFINGER`[default(me/VAR`OOCFINGER`MODE,DEFAULT)]`ADMINSET),|,and,\,)]%R%RFields not listed here are controlled by other factors.%RThe EMAIL field is special. If you wish to hide your email from everyone but admin, prefix it with a ! - for example, !user@site.com[if(isadmin(%#),%R%R[subheader(Admin Section)]%RAdmin may set the fields of other characters using [ansi(h,+oocfinger/set <name>/<field>=<text>)])]