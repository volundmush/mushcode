@@ DEPENDENCIES - CORE

@switch/inline isdbref(u(who))=0,{@tel create(Who Function Object <WHO>)=config(master_room)}
&who u(coi)=locate(config(master_room),Who Function Object <WHO>,TXxi)
@parent u(who)=u(coi)
@set u(who)=WIZARD !NO_COMMAND

@wipe u(pgo)/CMD`+WHO
@wipe u(pgo)/FUN`WHO
@mvattr u(pgo)/VAR`MAXPLAYERS=u(who)/VAR`MAXPLAYERS

&CMD`+WHO u(who)=$^(?\:\+)?who(?\:/(\S+))?$:@switch/inline gt(strlen(%1),0)=1,{@include u(ccs)/INC`PARTIAL=%1,v(DAT`MODES),|,WHO,mode,mode},{th setq(mode,name)};@nspemit %#=header(mudname()'s Online Players);@nspemit %#=trim(iter(u(DAT`FIELDS`%q<mode>),[switch(1,strmatch(elements(%i0,4,~),-),center,strmatch(elements(%i0,4,~),<),ljust,strmatch(elements(%i0,4,~),>),rjust,ljust)](capnames(elements(%i0,2,~)),elements(%i0,3,~)),|,%b));@nspemit %#=subheader();th setq(list,filtervis(%#,setunion(lwho(%#),),WHO));th setq(sort,getstat(u(who)/DAT`MODES`SORT,%q<mode>));th setq(sortlist,sortkey(#lambda/u(FUN`GET,\%0,before(%q<sort>,~)),%q<list>,after(%q<sort>,~)));@dolist/inline %q<sortlist>={@nspemit %#=u(FUN`WHO,%i0,%#,%q<mode>)};@switch/inline gt(setr(max,words(setunion(iter(%q<list>,get(%i0/LASTIP)),))),v(VAR`MAXPLAYERS))=1,{&VAR`MAXPLAYERS u(who)=%q<max>};@nspemit %#=header(words(%q<list>) Characters from %q<max> IPs Connected - Record is [v(VAR`MAXPLAYERS)])
@set u(who)/CMD`+WHO=regexp

&FUN`WHO u(who)=trim(iter(u(DAT`FIELDS`%2),[setq(get,u(FUN`GET[if(hasattr(me,FUN`GET`[elements(%i0,1,~)]`FORMAT),`[elements(%i0,1,~)]`FORMAT)],%0,elements(%i0,1,~),%1,1))][if(elements(%i0,7,~),setq(get,left(%q<get>,elements(%i0,7,~))))][setq(color,color(%1,%q<get>,WHO`[elements(%i0,1,~)],strfirstof(u(DAT`COLOR`[elements(%i0,1,~)],%0),n)))][setq(show,if(hasattr(me,DAT`PUEBLIZE`[elements(%i0,1,~)]),pueblize(%q<color>,u(DAT`PUEBLIZE`[elements(%i0,1,~)],%0)),%q<color>))][ljust([switch(1,strmatch(elements(%i0,6,~),-),center,strmatch(elements(%i0,6,~),<),ljust,strmatch(elements(%i0,6,~),>),rjust,ljust)](%q<show>,elements(%i0,5,~)),elements(%i0,5,~))],|))

&FUN`GET u(who)=switch(%1,NAME,name(%0),ALIAS,alias(%0),SEX,switch(get(%0/SEX),M*,Male,F*,Female,N*,Neuter,?),CONN,conn(%0),IDLE,idle(%0),u(FUN`GET`%1,%0))
&FUN`GET`IDLE`FORMAT u(who)=edit(elements(etimefmt($zxD $zxH $zxM $xS,idle(%0)),1 2),%b,)
&FUN`GET`CONN`FORMAT u(who)=edit(elements(etimefmt($zxD $zxH $zxM $xS,conn(%0)),1 2),%b,)
&FUN`GET`LOCATION`FORMAT u(who)=switch(1,cand(cor(hasflag(%0,UNFINDABLE),hasflag(room(loc(%0)),UNFINDABLE)),isadmin(%#)),name(room(loc(%0))),cor(hasflag(%0,UNFINDABLE),hasflag(room(loc(%0)),UNFINDABLE)),*UNFINDABLE*,name(room(loc(%0))))

&DAT`MODES u(who)=NAME|SEX|IDLE|CONN
&DAT`MODES`SORT u(who)=NAME~NAME~i|SEX~SEX~i|IDLE~IDLE~-n|CONN~CONN~-n
&DAT`FIELDS`NAME u(who)=NAME~NAME~30~<~30~<~29|ALIAS~ALIAS~10~<~10~<~10|SEX~SEX~3~<~3~-~1|IDLE~IDLE~6~>~6~>~6|CONN~CONN~6~>~6~>~6|LOCATION~LOCATION~18~<~18~<~18
&DAT`FIELDS`SEX u(who)=u(DAT`FIELDS`NAME)
&DAT`FIELDS`IDLE u(who)=u(DAT`FIELDS`NAME)
&DAT`FIELDS`CONN u(who)=u(DAT`FIELDS`NAME)
&DAT`PUEBLIZE`LOCATION u(who)=join [name(%0)]
&DAT`PUEBLIZE`NAME u(who)=+finger [name(%0)]
&DAT`COLOR`IDLE u(who)=ryg(round(mul(fdiv(bound(idle(%0),0,3600),3600),100),0))
&DAT`COLOR`CONN u(who)=hg

&STARTUP u(who)=@hook/override WHO=u(who),CMD`+WHO
