th u(NEWCOBJ,Login Tracker <LOGIN>,login,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

&CMD`+IP`PENNMUSH [u(cobj,login)]=$^(?\:\+)?(?\:ip|sitematch|matchsite|site|login)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+IP`MAIN
@set [u(cobj,login)]/CMD`+IP`PENNMUSH=regexp
&CMD`+IP`RHOSTMUSH [u(cobj,login)]=$^(?\:\+)?(?\:ip|sitematch|matchsite|site|login)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+IP`MAIN
@set [u(cobj,login)]/CMD`+IP`RHOSTMUSH=regexp
&CMD`+IP`MAIN [u(cobj,login)]=@attach %!/INC`GETSWITCH=%1;@attach %!/INC`[u(strfirstof,%q<switch>,MAIN)]=%2,%3
@set [u(cobj,login)]/CMD`+IP`[u(choosegame,RHOSTMUSH,PENNMUSH)]=no_command

&SYSTEM`NAME [u(cobj,login)]=LOGIN

&SWITCHES`PLAYER [u(cobj,login)]=
&SWITCHES`ADMIN [u(cobj,login)]=IP|GUESTS|CLEAR

&INC`MAIN [u(cobj,login)]=@stop u(isguest,%#)=@attach %!/INC`MSG=ERROR: Guests cannot use +ip.;@select/inline u(isadmin,%#)=0,{@attach %!/INC`CHECKPC=%#,1,1;@attach %!/INC`IPSHOW=%q<t1>,0},{@attach %!/INC`CHECKPC=u(strfirstof,%0,%#),1,1;@@ @stop u(isguest,%q<t1>)=@attach %!/INC`MSG=ERROR: To match Guests\, please use +ip/guests;@attach %!/INC`IPSHOW=%q<t1>}

&INC`IP [u(cobj,login)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: An IP address is required.;@attach %!/INC`IPSHOW=%0,1,1

&INC`CLEAR [u(cobj,login)]=@attach %!/INC`CHECKPC=%0,1;@attach %!/INC`DOSQL=DELETE`ALL_LOGS,%q<t1id>;@attach %!/INC`MSG=Cleared all login records for %q<t1name>!;@attach %!/INC`MSG`CHAN=Cleared all login records for %q<t1name>!

&Q`DELETE`ALL_LOGS [u(cobj,login)]=DELETE from vol_login WHERE character_id=?

&INC`IPSHOW [u(cobj,login)]=@check gt(words(u(setr,ips,if(%2,%0,u(mysql,GET`LOGIN_IPS,%q<t1id>)))),0)=@attach %!/INC`MSG=ERROR: No IP logs found to match against.;th u(setq,pcs,u(filter,if(u(isadmin,%#),ISOBJID,NOTGUEST),u(mysql,GET`OBJIDS_FROM_IPS,u(SQL`IN`STRING,%q<ips>))));@check words(%q<pcs>)=@attach %!/INC`MSG=ERROR: No Matching Logs found.;@pemit %#=u(header,Site matches for [if(%2,%0,name(%0))]);@dolist/inline %q<pcs>={@pemit %#=u(separator,ansi(h,u(pueblize,u(getmoniker,##),+finger [name(##)]))[ansi(h,\(##\))] - [u(lastidle,##)]);@pemit %#=ansi(u(color,%#,COLUMN_NAMES),align(20 22 25,Date,Address,Result));@pemit %#=iter(revwords(u(mysql2,GET`LOGINS,objid(##)),|),align(20 22 25,u(fancytime,elements(%i0,3,^),%#),elements(%i0,2,^),if(elements(%i0,1,^),Success,elements(%i0,4,^))),|,%R)};@pemit %#=u(footer)

&Q`GET`LOGIN_IPS [u(cobj,login)]=SELECT DISTINCT ip_address FROM volv_login WHERE character_id=? AND login_is_success=1

&Q`GET`OBJIDS_FROM_IPS [u(cobj,login)]=SELECT DISTINCT character_objid from volv_login WHERE ip_address IN (!) ORDER BY character_name DESC

&Q`GET`LOGINS [u(cobj,login)]=SELECT login_is_success,ip_address,login_date_secs FROM volv_login WHERE character_objid=? ORDER BY login_date_secs DESC LIMIT 10

&PLAYER`CONNECT [u(cobj,login)]=@attach %!/INC`CHECKPC=%0,1;th u(call`3,volp_login_record,0,%q<t1id>,1,'[sqlescape(get(%0/LASTIP))]')

&PLAYER`CONNECTFAIL [u(cobj,login)]=@attach %!/INC`CHECKPC=%0,1;th u(call`3,volp_login_record,0,%q<t1id>,0,'[sqlescape(get(%0/LASTIP))]')

&SOCKET`LOGINFAIL [u(cobj,login)]=@select/inline isdbref(%4)=1,{@trigger %!/PLAYER`CONNECTFAIL`IPLOG=%4,%3;th u(setq,isgod,strmatch(#1,%4))[u(setq,admin,u(isadmin,%4))];@attach %!/INC`MSG`SYSCHAN={[if(cor(%q<isgod>,%q<admin>),ansi(hr,WARNING:),ansi(hy,ALERT:))]%BFailed [switch(1,%q<isgod>,ansi(hr,GOD),%q<admin>,ansi(hr,ADMIN),Player)] ([name(%4)]) login attempt from IP '%1' HOST '[host(%0)]' PORT '%0'. Consecutive Failure %2: %3. [if(strmatch(get(%4/LASTSITE),host(%0)),,ansi(hr,ALERT:)%BAttempt not from previous LASTSITE!)]};@select/inline cor(%q<isgod>,%q<admin>)=1,{@pemit/port %0=[ansi(hrf,WARNING:)]%BAdmin login failures are logged.};@select/inline %2=>1,{@pemit/port %0=If you have forgotten your password\, ask an admin for a reset. Too many login attempts will cause a temporary lockout.}}

&HLP`IP [u(cobj,login)]=[ansi(h,+ip)] - Shows your recent logins. Please report any suspicious login attempts to staff!
+help/add Character/+ip=[u(cobj,login)]/HLP`IP

&SHLP`IP [u(cobj,login)]=[ansi(h,+ip <player>)] - Checks a player's logins and anyone with matching IPs.%R[ansi(h,+ip/guests <player>)] - Includes Guests in the IP lookup.%R[ansi(h,+ip/ip <ip>)] - Checks an IP for any matching players. Note that ::ffff: must be prepended to any IPv4 addresses if they're showing up in normal uses of +ip!
+shelp/add Character/+ip=[u(cobj,login)]/SHLP`IP
