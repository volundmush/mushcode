@@ This is a VERY BASIC pose tracker!

@switch/inline isdbref(u(pot))=0,{@tel create(Pose Order Tracker <POT>)=config(master_room)}
&pot u(coi)=locate(config(master_room),Pose Order Tracker <POT>,TXxi)
@parent u(pot)=u(coi)
@set u(pot)=WIZARD SAFE !NO_COMMAND

@switch/inline isdbref(u(potdb))=0,{@tel create(Pose Tracker Database <POTDB>)=u(pot)}
&potdb u(coi)=locate(u(pot),Pose Tracker Database <POTDB>,TXxi)
@parent u(potdb)=u(coi)
@set u(potdb)=WIZARD SAFE !NO_COMMAND
@power u(potdb)=many_attribs

&CMD`+SUMMARY u(pot)=$+summary *:@break gt(strlen(%0),1024)=@pemit %#=ansi(hr,That summary is too long.);@remit %l=ansi(h,>>> SUMMARY - [fullname(%#)] <<< - %0);&D`LASTPOSE`SUMMARY %#=%0;@assert cor(isic(%#),get(%l/POT));&[setr(potattr,setr(loc,uldefault(%l/D`CUSTLOC,%l,%#))`[nextslot(setr(potdb,u(potdb)),%q<loc>)])]`TYPE %q<potdb>=SUMMARY;&%q<potattr> %q<potdb>=%0;&%q<potattr>`BY %q<potdb>=%#;&%q<potattr>`WHEN %q<potdb>=secs();@switch/inline gt(nattr(%q<potdb>/%q<loc>`*),300)=1,{@dolist/inline elements(sortkey(#lambda/last(\%0,`),lattr(%q<potdb>/%q<loc>`*),n),lnum(1,sub(%q<count>,300)))=@wipe %q<potdb>/%i0}

&CMD`+POT u(pot)=$^(?\:\+)?pot(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.+))?$:@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(pot)/VAR`PLAYFLAGS),if(isadmin(%#),get(u(pot)/VAR`ADMINFLAGS)),|,|),|,POT,switch,switch;@include u(pot)/INC`[strfirstof(%1,MAIN)]=%2,%3
@set u(pot)/CMD`+pot=regexp

&VAR`PLAYFLAGS u(pot)=LAST|MAX|LIST|BRIEF|MODE|SUMMARY|HEADER|FOOTER

&RFN`MSGHEAD u(pot)=msghead(v(VAR`MSGHEAD),%1)
&RFN`HEADER u(pot)=header(%0,,POT`BORDER,POT`BORDERDOT,POT`BORDERTEXT,,%1)
&RFN`SUBHEADER u(pot)=subheader(%0,,POT`BORDER,POT`BORDERDOT,POT`BORDERTEXT,,%1)
&RFN`SEPARATOR u(pot)=separator(%0,,POT`BORDER,POT`BORDERDOT,POT`BORDERTEXT,,%1)
&VAR`MSGHEAD u(pot)=POT

&INC`MAIN u(pot)=@switch/inline default(%#/D`POT`MODE,0)=0,{@include u(pot)/INC`LIST},1,{@include u(pot)/INC`BRIEF}

&INC`BRIEF u(pot)=@assert words(setr(posers,sort(filterbool(#lambda/strmatch(get(\%0/D`LASTPOSE`WHERE),%l),lvplayers(%l)),attr:D`LASTPOSE`WHEN)))=@nspemit %#=u(RFN`MSGHEAD) No active RP in the room!;@nspemit %#=u(RFN`HEADER,RP Activity for [name(%l)]);@nspemit %#=ansi(firstcolor(%#,POT`COLNAME,DEFAULT`COLNAME),align(20 4 4 [sub(width(%#),31)],Name,Pose,Idle,Summary));@nspemit %#=separator();@dolist/inline %q<posers>=@nspemit %#=align(20 4 4 [sub(width(%#),31)],pueblize(name(%i0),+pot/last [name(%i0)]),ansi(ryg(round(mul(fdiv(bound(sub(secs(),get(%i0/D`LASTPOSE`WHEN)),0,3600),3600),100),0)),etime(sub(secs(),get(%i0/D`LASTPOSE`WHEN)),4)),ansi(ryg(round(mul(fdiv(bound(idle(%i0),0,3600),3600),100),0)),etime(idle(%i0),4)),get(%i0/D`LASTPOSE`SUMMARY));@nspemit %#=u(RFN`HEADER)

&INC`LAST u(pot)=@include u(ccs)/INC`CHECKPC=%0,1,v(VAR`MSGHEAD);@assert strmatch(loc(%q<t1>),%l)=@nspemit %#=u(RFN`MSGHEAD) %q<t1name> is not in the room!;@assert cand(hasattrval(%q<t1>/D`LASTPOSE),lte(setr(ago,sub(secs(),get(%q<t1>/D`LASTPOSE`WHEN))),mul(60,60,2)))=@nspemit %#=u(RFN`MSGHEAD) %q<t1name> hasn't posed recently.;@assert strmatch(get(%q<t1>/D`LASTPOSE`WHERE),%l)=@nspemit %#=u(RFN`MSGHEAD) %q<t1name> hasn't posed in this room.;@nspemit %#=header(%q<t1name>'s Last Pose [etime(%q<ago>,3)] Ago);@nspemit %#=u(u(pot)/FUN`FORMATPOSE,%#,%q<t1>,get(%q<t1>/D`LASTPOSE`TYPE),trim(get(%q<t1>/D`LASTPOSE),%R));@nspemit %#=header()

&INC`MODE u(pot)=&D`POT`MODE %#=setr(mode,if(default(%#/D`POT`MODE,0),0,1));@nspemit %#=u(RFN`MSGHEAD) +pot will now default to +pot/[if(%q<mode>,summary,list)]

&INC`LIST u(pot)=@switch/inline t(strlen(%0))=1,{@assert valnum(%0)=@nspemit %#=announce(POT) ERROR: Last number of poses must be a whole, positive number.};@assert words(setr(poses,revwords(elements(revwords(sortkey(#lambda/last(\%0,`),lattr(u(potdb)/[uldefault(%l/D`CUSTLOC,%l,%#)]`*))),lnum(1,firstof(%0,get(%#/D`POT`MAX),15))))))=@nspemit %#=No recent poses tracked in this location.;@nspemit %#=header(Recent Poses);@dolist %q<poses>={@switch/inline get(u(potdb)/%i0`TYPE)=SUMMARY,{@nspemit %#=ansi(h,>>> SUMMARY [trim(etime(sub(secs(),get(u(potdb)/%i0`WHEN)),3))] Ago - [fullname(get(u(potdb)/%i0`BY))] <<< - [get(u(potdb)/%i0)])},{@nspemit %#=[ansi(firstcolor(%#,POT`BORDER,DEFAULT`BORDER),<)][colornames(name(setr(spk,get(u(potdb)/%i0`BY))),%#,lplayers(%l),IC)] posed [ansi(c,trim(etime(sub(secs(),get(u(potdb)/%i0`WHEN)),3)))] Ago[ansi(firstcolor(%#,POT`BORDER,DEFAULT`BORDER),>)];@nspemit %#=u(u(pot)/FUN`FORMATPOSE,%#,get(u(potdb)/%i0`BY),get(u(potdb)/%i0`TYPE),get(u(potdb)/%i0))};@switch/inline eq(inum(0),words(%q<poses>))=1,{@nspemit %#=header(End of Poses)}}

&INC`SUMMARY u(pot)=@switch/inline t(strlen(%0))=1,{@assert valnum(%0)=@nspemit %#=announce(POT) ERROR: Last number of poses must be a whole, positive number.};@assert words(setr(poses,revwords(elements(revwords(sortkey(#lambda/last(\%0,`),filterbool(#lambda/strmatch(get(u(potdb)/\%0`TYPE),SUMMARY),lattr(u(potdb)/[uldefault(%l/D`CUSTLOC,%l,%#)]`*)))),lnum(1,firstof(%0,get(%#/D`POT`MAX),15))))))=@nspemit %#=No recent poses tracked in this location.;@nspemit %#=header(Recent Summaries);@nspemit %#=ansi(firstcolor(%#,POT`COLNAME,DEFAULT`COLNAME),align(20 4 4 [sub(width(%#),31)],Name,Ago,Idle,Summary));@nspemit %#=separator();@dolist %q<poses>={@nspemit %#=align(20 4 4 [sub(width(%#),31)],pueblize(name(get(u(potdb)/%i0`BY)),+pot/last [name(get(u(potdb)/%i0`BY))]),ansi(ryg(round(mul(fdiv(bound(sub(secs(),get(u(potdb)/%i0`WHEN)),0,3600),3600),100),0)),etime(sub(secs(),get(u(potdb)/%i0`WHEN)),4)),ansi(ryg(round(mul(fdiv(bound(idle(%i0),0,3600),3600),100),0)),etime(idle(get(u(potdb)/%i0`BY)),4)),get(u(potdb)/%i0));@switch/inline eq(inum(0),words(%q<poses>))=1,{@nspemit %#=header(End of Poses)}}

&INC`MAX u(pot)=@assert valnum(%0)=@nspemit %#=You must enter a whole number greater than 0 for your maximum poses!;&D`POT`MAX %#=%0;@nspemit %#=announce(POT) You will now see only the last %0 poses in +pot.

&INC`HEADER u(pot)=@include u(pot)/INC`CHHEAD=HEADER
&INC`FOOTER u(pot)=@include u(pot)/INC`CHHEAD=FOOTER
&INC`CHHEAD u(pot)=th setq(change,if(t(get(%#/D`POT`%0)),0,1));@nspemit %#=u(RFN`MSGHEAD) You will [if(%q<change>,now see,no longer see)] a %0 for all poses!;&D`POT`%0 %#=%q<change>

&TRG`SELFCLEAN u(pot)=@switch/inline gt(v(VAR`SELFCLEAN),0)=1,{@dolist/inline lattr(u(potdb)/*)=@trigger u(pot)/TRG`DOCLEAN=%i0;@wait mul(60,v(var`checkminutes))=@trigger u(pot)/TRG`SELFCLEAN

&TRG`DOCLEAN u(pot)=@dolist/inline filterbool(#lambda/gte(sub(secs(),get(u(potdb)/\%0`WHEN)),mul(60,60,v(VAR`TIMEOUTHOURS))),lattr(u(potdb)/%0`*))=@wipe u(potdb)/%i0

&VAR`SELFCLEAN u(pot)=1
&VAR`TIMEOUTHOURS u(pot)=2
&VAR`CHECKMINUTES u(pot)=30

&STARTUP u(pot)=@trigger u(pot)/TRG`SELFCLEAN;@break cand(isdbref(u(scene)),not(hasflag(u(scene),HALT)));@dolist/inline @EMIT POSE SAY SEMIPOSE={@hook/override/inline %i0=u(pot),OVERRIDE`IC}

&OVERRIDE`IC u(pot)=$^(?s)(pose|semipose|say|@emit)(?\:/noeval)?(?\: +(.*))?$:@assert strlen(%2)=@nspemit %#=Huh?  (Type "help" for help.);@include/nobreak me/include`lastpose`prepose;@include/nobreak u(pot)/INC`ALTMAIN=%1,%2;@message/spoof uldefault(%l/D`CUSTRECP,lcon(%l),%#)=POSE FORMATTER FAIL!,u(pot)/FUN`FORMATPOSE,##,%#,switch(%1,pose,:,semipose,;,say,",@emit,|),%2;@include me/include`lastpose`postpose;@@ @include me/include`lastpose`catchpose=trimpenn(trimpenn(%qr,%t,r),%r),%:
@set u(pot)/OVERRIDE`IC=regexp

&FUN`FORMATPOSE u(pot)=colornames(speak(%1,%2%3,says\,,%0/SAY_FN,%0/NONE_FN),firstof(%4,%0),lplayers(loc(%0)),IC)
&SAY_FN u(ap)=ansi(custcolor(%!,IC`QUOTES),")[ansi(custcolor(%!,IC`SPEECH),colornames(%0,%!,lplayers(loc(%!)),IC))][ansi(custcolor(%!,IC`QUOTES),")]

&SAY_FN u(at)=ansi(custcolor(%!,IC`QUOTES),")[ansi(custcolor(%!,IC`SPEECH),colornames(%0,%!,lplayers(loc(%!)),IC))][ansi(custcolor(%!,IC`QUOTES),")]

&INC`ALTMAIN u(pot)=@assert cor(isic(%#),get(%l/POT));&[setr(potattr,setr(loc,uldefault(%l/D`CUSTLOC,%l,%#))`[nextslot(setr(potdb,u(potdb)),%q<loc>)])]`TYPE %q<potdb>=switch(%0,@EMIT,|,SAY,",SEMIPOSE,;,POSE,:);&D`LASTPOSE`TYPE %#=switch(%0,@EMIT,|,SAY,",SEMIPOSE,;,POSE,:);&%q<potattr> %q<potdb>=%1;&D`LASTPOSE %#=%1;&D`LASTPOSE`SUMMARY %#;&%q<potattr>`BY %q<potdb>=%#;&%q<potattr>`WHEN %q<potdb>=secs();&D`LASTPOSE`WHEN %#=secs();&D`LASTPOSE`WHERE %#=%l;@switch/inline gt(nattr(%q<potdb>/%q<loc>`*),300)=1,{@dolist/inline elements(sortkey(#lambda/last(\%0,`),lattr(%q<potdb>/%q<loc>`*),n),lnum(1,sub(%q<count>,300)))=@wipe %q<potdb>/%i0}

&D`CUSTLOC u(vroom)=%!_[default(me/LOC-%0,v(default-startingroom))]
&D`CUSTRECP u(vroom)=setinter(edit(wildgrep(me,LOC-*,default(me/LOC-%0,v(default-startingroom))),LOC-,),lcon(me))

&INCLUDE`LASTPOSE`PREPOSE u(pot)=@assert isic(%#);@dolist/inline filterbool(#lambda/get(\%0/D`POT`HEADER),uldefault(%l/D`CUSTRECP,lcon(%l),%#))={@nspemit %i0=u(RFN`HEADER,%n has Posed,%i0)}

&INCLUDE`LASTPOSE`POSTPOSE u(pot)=@assert isic(%#);@dolist/inline filterbool(#lambda/get(\%0/D`POT`FOOTER),uldefault(%l/D`CUSTRECP,lcon(%l),%#))={@nspemit %i0=u(RFN`SUBHEADER,End of %n's Pose,%i0)}

th attrib_set(u(cco-db)/VAR`CATEGORIES,setunion(get(u(cco-db)/VAR`CATEGORIES),IC,|,|))
&VAR`CATEGORIES`IC u(cco-db)=SPEECH|QUOTES
&COLOR`IC`SPEECH u(pco)=h
&COLOR`IC`QUOTES u(pco)=h


@@ POSE ORDER TRACKER - POT
+help/addmain Communications/+pot=[u(pot)]/HLP`+POT
&HLP`+POT u(pot)=+pot, or [ansi(h,Po)]se [ansi(h,T)]racker, is a system that stores poses within the room for easy retrieval for review. Sometimes, poses are lost in spam or scroll, or a player enters late and needs to see what's going on, or was disconnected when someone else posed, and that's what this system's here to aid with.%R%R[ansi(hc,Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+pot)] - Show either +pot/list or +pot/brief, depending on /mode. (See below.)%R[ansi(h,+pot/list)] - show the last 15 (or set max) poses.%R[ansi(h,+pot/list <number>)] - Show a specified amount of poses.%R[ansi(h,+pot/max <number>)] - Set a new default number of poses to show.%R[ansi(h,+pot/brief)] - Show when people last posed and their last +summary made after that pose if they have one.%R[ansi(h,+pot/summary)] - Just like +pot/list, but only shows +summary's. Give a number argument to list a specific amount!%R[ansi(h,+pot/mode)] - Change the behavior of +pot. By default, +pot uses +pot/list display style. Change mode and it will use +pot/brief instead.%R[ansi(h,+pot/header)] - Make a separator header appear for you when someone poses. Use again to toggle off.%R[ansi(h,+pot/footer)] - Like header, but makes a footer appear instead/too.})]