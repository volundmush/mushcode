
@tel create(Meet Code Object <MCO>)=config(master_room)
&mco u(coi)=locate(config(master_room),Meet Code Object <MCO>,TXxi)
@parent u(mco)=u(coi)
@set u(mco)=WIZARD !NO_COMMAND

&CMD`JOIN u(mco)=$^(?\:\+)?join(?\: +(.+))?$:@switch/inline gt(strlen(%1),0)=1,{@include u(ccs)/INC`CHECKPC=%1,1,MEET;@assert hasflag(%q<t1>,CONNECTED)=@nspemit %#=announce(MEET) ERROR: %q<t1name> is not online.;@switch/inline or(if(v(VAR`ADMINNEEDINVITES),0,isadmin(%#)),V(VAR`NOINVITES))=0,{@switch/inline hasattr(u(mco)/MEETME`SUMMON`%q<t1>`%#)=1,{@switch/inline strmatch(%l,loc(%q<t1>))=0,{@tel %#=loc(%q<t1>)},1,{@nspemit %#=announce(MEET) ERROR: You are already there!;@break 1=@wipe u(mco)/MEETME`SUMMON`%q<t1>`%#};@wipe u(mco)/MEETME`SUMMON`%q<t1>`%#},0,{&MEETME`JOIN`%#`%q<t1> u(mco)=secs();@nspemit %#=announce(MEET) You have sent %q<t1name> a Join request!;@nspemit %q<t1>=announce(MEET) %n sent you a JOIN request. To bring %n to you, use '[pueblize(summon %n,summon %n)]'.;@break 1}},1,{@tel %#=loc(%q<t1>)};@nspemit %#=announce(MEET) You have joined %q<t1name>!;@nsoemit %# %q<t1>=announce(MEET) %n has joined %q<t1name>!;@nspemit %q<t1>=announce(MEET) %n has joined you!},0,{}
@set u(mco)/CMD`JOIN=regexp

&CMD`SUMMON u(mco)=$^(?\:\+)?summon(?\: +(.+))?$:@switch/inline gt(strlen(%1),0)=1,{@include u(ccs)/INC`CHECKPC=%1,1,MEET;@assert hasflag(%q<t1>,CONNECTED)=@nspemit %#=announce(MEET) ERROR: %q<t1name> is not online.;@switch/inline or(if(v(VAR`ADMINNEEDINVITES),0,isadmin(%#)),V(VAR`NOINVITES))=0,{@switch/inline hasattr(u(mco)/MEETME`JOIN`%q<t1>`%#)=1,{@switch/inline strmatch(%l,loc(%q<t1>))=0,{@tel %q<t1>=%l)},1,{@nspemit %#=announce(MEET) ERROR: You are already there!;@break 1=@wipe u(mco)/MEETME`JOIN`%q<t1>`%#};@wipe u(mco)/MEETME`JOIN`%q<t1>`%#},0,{&MEETME`SUMMON`%#`%q<t1> u(mco)=secs();@nspemit %#=announce(MEET) You have sent %q<t1name> a Summon request!;@nspemit %q<t1>=announce(MEET) %n sent you a SUMMON request. To go to %n, use '[pueblize(join %n,join %n)]'.;@break 1}},1,{@tel %q<t1>=%l};@nspemit %#=announce(MEET) You have summoned %q<t1name>!;@nsoemit %# %q<t1>=announce(MEET) %n has summoned %q<t1name>!;@nspemit %q<t1>=announce(MEET) %n has summoned you!}
@set u(mco)/CMD`SUMMON=regexp

&VAR`NOINVITES u(mco)=0
&VAR`ADMINNEEDINVITES u(mco)=1

&FILTER_TIMEOUT u(mco)=gt(sub(secs(),v(%0)),600)
&STARTUP u(mco)=@trigger me/TRG`DOCLEANUP
&TRG`DOCLEANUP u(mco)=@dolist filterbool(FILTER_TIMEOUT,lattr(me/MEETME`*))={@wipe me/%i0};@wait 60=@trigger u(mco)/TRG`DOCLEANUP