@@ DEPENDENCIES - CORE
@@ RECOMMENDED: Grid and Navigation System. If installed, clickable +port links will be created to available rooms.
@@ RECOMMENDED: Group System. The Fac column is almost meaningless without it.

th u(NEWCOBJ,Who System <WHO>,who,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

&CMD`+WHO`PENNMUSH [u(cobj,who)]=$^(?s)(?\:\+)?who(?\:/(\S+))?(?\: +(.+?))?$:@attach %!/CMD`+WHO`MAIN
@set [u(cobj,who)]/CMD`+WHO`PENNMUSH=regexp
&CMD`+WHO`RHOSTMUSH [u(cobj,who)]=$^(?s)(?\:\+)?who(?\:/(\\S+))?(?\: +(.+?))?$:@attach %!/CMD`+WHO`MAIN
@set [u(cobj,who)]/CMD`+WHO`RHOSTMUSH=regexp
&CMD`+WHO`MAIN [u(cobj,who)]=@attach %!/INC`GETSWITCH=%1;th u(setq`%va,portable,u(u(cobj,grid)/FUN`LISTROOMS));@attach %!/INC`WHO`[u(strfirstof`%va,%q<switch>,MAIN)]=%2
@set [u(cobj,who)]/CMD`+WHO`[switch(%va,PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SYSTEM`NAME [u(cobj,who)]=WHO
&SWITCHES`PLAYER [u(cobj,who)]=IDLE

&INC`WHO`MAIN [u(cobj,who)]=@attach %!/INC`WHO`SORT`MAIN;@select/inline gt(strlen(%0),0)=1,{@attach %!/INC`WHO`SEARCH=%0};@attach %!/INC`WHO`LIST=,%0

&FUN`WHO`MAIN [u(cobj,who)]=u(sortname,setunion(u(lwhoid`%va,%#),))

&INC`WHO`SORT`MAIN [u(cobj,who)]=@check words(u(setr`%va,who,u(FUN`WHO`MAIN)))=@attach %!/INC`MSG=No players to show!

&INC`WHO`SEARCH [u(cobj,who)]=@check words(u(setr`%va,who,namegraball(%q<who>,%0)))=@attach %!/INC`MSG=Search for '%0' turned up with nothing.

&INC`WHO`IDLE [u(cobj,who)]=@attach %!/INC`WHO`SORT`IDLE;@select/inline gt(strlen(%1),0)=1,{@attach %!/INC`WHO`SEARCH=%1};@attach %!/INC`WHO`LIST=Idle,%1
&INC`WHO`SORT`IDLE [u(cobj,who)]=@check words(u(setr`%va,who,revwords(u(sortidle`%va,u(FUN`WHO`MAIN)))))=@attach %!/INC`MSG=No players to show!

&INC`WHO`LIST [u(cobj,who)]=@pemit %#=u(HEADER,Who[if(strlen(%0),%b-%b%0)]);@attach %!/INC`WHO`HEADER`[strfirstof(%0,MAIN)];@pemit %#=u(separator);@dolist %q<who>={@pemit %#=u(FUN`WHO`PLAYERLINE`[strfirstof(%0,MAIN)],##);@select/inline eq(#@,words(%q<who>))=1,{&MAXPLAYERS %!=u(setr`%va,max,max(get(%!/MAXPLAYERS),words(%q<who>)));@pemit %#=u(SUBHEADER,words(%q<who>) Characters[if(u(game_config,WHO,SHOW_IPS),%Bfrom [words(setunion(iter(%q<who>,get(%i0/LASTSITE),,|),,|),|)] IPs)]%BConnected - Record is %q<max>)}}

th u(NEWCONF,config,WHO,SHOW_IPS,1,Show # of unique IPs?,BOOL)

th u(newconf,CONFIG,WHO,HEADER,Name~20|Alias~11|Fac~3|Idle~4|Conn~4|G~1|Location~29,check +shelp +who,LIST)
th u(newconf,CONFIG,WHO,COLUMNS,NAMELINK~20|ALIAS~11|MAJORABBR~3|HIDEIDLE~>4|HIDECONN~>4|SEXLETTER~1|LOCATION~29,Check +shelp +who,LIST)

&INC`WHO`HEADER`MAIN [u(cobj,who)]=@pemit %#=ansi(u(color`%va,%#,WHO,COLUMN_NAMES),lalign(iter(u(setr`%va,hdr,u(game_config,WHO,HEADER)),after(%i0,~),|,%b),iter(%q<hdr>,before(%i0,~),|,beep()),beep()))
&INC`WHO`HEADER`IDLE [u(cobj,who)]=@attach %!/INC`WHO`HEADER`MAIN

&FUN`WHO`PLAYERLINE`MAIN [u(cobj,who)]=lalign(iter(u(setr`%va,col,u(game_config,WHO,COLUMNS)),after(%i0,~),|,%b),iter(%q<col>,u(getproperty,%0,before(%i0,~)),|,beep()),beep())
&FUN`WHO`PLAYERLINE`IDLE [u(cobj,who)]=u(FUN`WHO`PLAYERLINE`MAIN,%0)

@@ &STARTUP [u(cobj,who)]=@hook/override WHO=%!,CMD`+WHO`%va

@@ COMMUNITY - WHO
+help/add +who=[u(cobj,who)]/HLP`+WHO
+help/category +who=Community
+help/metatags +who=online players
&HLP`+WHO [u(cobj,who)]=[ansi(hc,Commands)]%R[align(5 [sub(u(width`%va,%#),6)],,[ansi(h,+who)] - Show all visible\, connected players.%R[ansi(h,+who/idle)] - Like above\, but sorts by idle times.%R%R[ansi(h,+who <text>)] - Search for online players starting with <text>.)]


@@ WHERE COMMAND

&CMD`+WHERE`PENNMUSH [u(cobj,who)]=$^(?s)(?\:\+)?where(?\:/(\S+))?(?\: +(.+?))?$:@attach %!/CMD`+WHERE`MAIN
@set [u(cobj,who)]/CMD`+WHERE`PENNMUSH=regexp
&CMD`+WHERE`RHOSTMUSH [u(cobj,who)]=$^(?s)(?\:\+)?where(?\:/(\\S+))?(?\: +(.+?))?$:@attach %!/CMD`+WHERE`MAIN
@set [u(cobj,who)]/CMD`+WHERE`RHOSTMUSH=regexp
&CMD`+WHERE`MAIN [u(cobj,who)]=@attach %!/INC`GETSWITCH=%1;th u(setq`%va,portable,u(u(cobj,grid)/FUN`LISTROOMS));@attach %!/INC`WHERE`[u(strfirstof`%va,%q<switch>,MAIN)]=%2
@set [u(cobj,who)]/CMD`+WHERE`[switch(%va,PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&VAR`WHERE`PLAYFLAGS [u(cobj,who)]=ROOMS

&INC`WHERE`ROOMS [u(cobj,who)]=@attach %!/INC`WHERE`MAIN=%0,1

&INC`WHERE`MAIN [u(cobj,who)]=th u(setq`%va,portable,u(u(cobj,grid)/FUN`LISTROOMS));@select/inline strlen(%0)=0,{@attach %!/INC`WHERE`ALL},{@attach %!/INC`WHERE`PLAYER}

&INC`WHERE`ALL [u(cobj,who)]=@pemit %#=u(HEADER,Where);@pemit %#=ansi(u(color`%va,%#,WHERE,COLUMN_NAMES),align(25 1 [sub(u(width`%va,%#),33)],Location,,Players));th u(setq`%va,showic,0);th u(setq`%va,showooc,0);th u(setq`%va,ooc,u(filter,NOTIC,u(setr`%va,where,u(sortname,setunion(iter(u(filter,FINDABLE,u(lwho`%va,%#),%b,%b,%#),loc(%i0)),)))));th u(setq`%va,ic,u(filter,IC,%q<where>));th u(setq`%va,firstic,first(%q<ic>));th u(setq`%va,firstooc,first(%q<ooc>));@dolist cat(%q<ooc>,%q<ic>)={@select/inline %i0=%q<firstic>,{@pemit %#=u(separator,IC)},%q<firstooc>,{@pemit %#=u(separator,OOC)};@select/inline gt(words(u(setr`%va,show,u(filter,FINDABLE,u(lvplayers`%va,##),%B,%B,%#))),0)=1,{@pemit %#=u(FUN`WHERELINE%1,##,%q<show>)};@select/inline eq(#@,words(%q<where>))=1,{@pemit %#=u(FOOTER)}}

&INC`WHERE`PLAYER [u(cobj,who)]=@attach %!/INC`CHECKPC=%0,1;@check cand(conn(%q<t1>),not(u(ishidden`%va,%q<t1>)))=@attach %!/INC`MSG=%q<t1name> is not online!;@check findable(%#,%q<t1>)=@attach %!/INC`MSG=%q<t1name> or their location is set UNFINDABLE.;@pemit %#=u(HEADER,%q<t1name> - Location: [if(match(%q<portable>,loc(%q<t1>)),u(pueblize,left(name(loc(%q<t1>)),sub(u(width`%va,%#),49)),+port [loc(%q<t1>)]),left(name(loc(%q<t1>)),sub(u(width`%va,%#),49)))]);@attach %!/INC`WHERE`HEADER`MAIN;@pemit %#=u(separator);@dolist/inline u(filter,FINDABLE,u(sortname,setinter(u(lwho`%va,%#),lcon(loc(%q<t1>)))),%B,%B,%#)={@pemit %#=u(FUN`WHERELINE`PLAYER,##)};@pemit %#=u(subheader,if(not(strmatch(loc(%q<t1>),room(%q<t1>))),Room: [if(match(%q<portable>,room(%q<t1>)),u(pueblize,u(moniker`%va,room(%q<t1>)),+port [room(%q<t1>)]),u(moniker`%va,room(%q<t1>)))]))

&FIL`FINDABLE [u(cobj,who)]=cor(findable(%1,%0),u(isadmin`%va,%1))
&FIL`IC [u(cobj,who)]=t(get(%0/D`IC))
&FIL`NOTIC [u(cobj,who)]=not(get(%0/D`IC))

&FUN`WHERELINE [u(cobj,who)]=align(25 1 [sub(u(width`%va,%#),38)],if(match(%q<portable>,%0),u(pueblize,u(moniker`%va,%0),+port %0),u(moniker`%va,%0)),-,u(itemize`%va,iter(u(sortname,u(filter,FINDABLE,u(lvplayers`%va,%0),%b,%b,%#)),u(pueblize,u(moniker`%va,%i0),+finger [name(%i0)])[if(hasflag(%i0,UNFINDABLE),\(Unfindable\))],,|),|,and,\,))

&INC`WHERE`HEADER`MAIN [u(cobj,who)]=@pemit %#=ansi(u(color`%va,%#,WHO,COLUMN_NAMES),lalign(iter(u(setr`%va,hdr,u(game_config,WHERE,HEADER)),after(%i0,~),|,%b),iter(%q<hdr>,before(%i0,~),|,beep()),beep()))

&FUN`WHERELINE`PLAYER [u(cobj,who)]=lalign(iter(u(setr`%va,col,u(game_config,WHERE,COLUMNS)),after(%i0,~),|,%b),iter(%q<col>,u(getproperty,%0,before(%i0,~)),|,beep()),beep())

th u(newconf,CONFIG,WHERE,HEADER,Name~20|Alias~11|Fac~3|Idle~4|Conn~4|G~1|Location~29,check +shelp +who,LIST)
th u(newconf,CONFIG,WHERE,COLUMNS,NAMELINK~20|ALIAS~11|MAJORABBR~3|HIDEIDLE~>4|HIDECONN~>4|SEXLETTER~1|LOCATION~29,Check +shelp +who,LIST)

&FUN`WHERELINE1 [u(cobj,who)]=align(30 1 [sub(u(width`%va,%#),33)],if(match(%q<portable>,%0),pueblize(name(%0),+port %0),name(%0))[if(not(strmatch(%0,room(%0))),%R(IN [if(match(%q<portable>,room(%0)),u(pueblize,name(room(%0)),+port [room(%0)]),name(room(%0)))]))],ansi(u(color`%va,%#,WHO,BORDER),-),u(itemize`%va,iter(u(sortname,u(filter,FINDABLE,u(lvplayers`%va,%0))),u(pueblize,u(moniker`%va,%i0),+finger [name(%i0)])[if(hasflag(%i0,UNFINDABLE),\(Unfindable\))],,|),|,and,\,))

+help/add +where=[u(cobj,who)]/HLP`+WHERE
+help/category +where=Community
+help/metatags +where=online players
&HLP`+WHERE [u(cobj,who)]=[ansi(hc,Commands)]%R[align(5 [sub(u(width`%va,%#),6)],,[ansi(h,+where)] - Show all visible\, connected players by location.%R[ansi(h,+where <player>)] - See details about a specific player's location.)]
