
@tel create(Character Data Object <EX-CDO>)=config(master_room)
&ex-cdo u(coi)=locate(config(master_room),Character Data Object <EX-CDO>,TXxi)
@parent u(ex-cdo)=u(coi)
@set u(ex-cdo)=WIZARD !NO_COMMAND
@lock/use u(ex-cdo)=@[u(sgo)]/Use

&GFN`GETSPLAT u(ex-cdo)=strfirstof(getstat(%0/D`INFO,Splat),Mortal)

&FUN`HEALTHSORT u(ex-cdo)=switch(%0,-0,switch(%1,-0,0,-1,-1,-2,-1,-4,-1,I,-1,D,-1),-1,switch(%1,-0,1,-1,0,-2,-1,-4,-1,I,-1,D,-1),-2,switch(%1,-0,1,-1,1,-2,0,-4,-1,I,-1,D,-1),-4,switch(%1,-0,1,-1,1,-2,1,-4,0,I,-1,D,-1),I,switch(%1,-0,1,-1,1,-2,1,-4,1,I,0,D,-1),D,switch(%1,-0,1,-1,1,-2,1,-4,1,I,1,D,0))

&CMD`+EDITCHAR u(ex-cdo)=$^\+editchar(?\:/(\S+))?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%1,setunion(v(VAR`PLAYFLAGS),if(isadmin(%#),v(VAR`ADMINFLAGS)),|,|),|,EDITCHAR,switch,switch;th iter(before(%2,=),setq(chr(add(inum(0),64)),%i0),|);@include u(ccs)/INC`CHECKPC=%qa,1,EDITCHAR;@assert strlen(%qb)=@nspemit %#=announce(EDITCHAR) ERROR: No Type entered to %q<switch> Your choices are: [itemize(iter(lattr(u(ex-cdo)/INC`%q<switch>`*,|),last(%i0,`),|,|),|,and,\,)]!;@include u(ccs)/INC`PARTIAL=%qb,iter(lattr(u(ex-cdo)/INC`%q<switch>`*,|),last(%i0,`),|,|),|,EDITCHAR,topic,option;@include u(ex-cdo)/INC`%q<switch>`%q<topic>=%q<t1>,after(%2,elements(%2,1,|)|),%3
@set u(ex-cdo)/CMD`+EDITCHAR=regexp

&VAR`PLAYFLAGS u(ex-cdo)=
&VAR`ADMINFLAGS u(ex-cdo)=SET|REM|ADD

&INC`SET`ATTRIBUTE u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=announce(EDITCHAR) ERROR: No Attribute name entered.;@include u(ccs)/INC`PARTIAL=%qc,v(VAR`ATTRIBUTES),|,EDITCHAR,find,attribute;@switch/inline strlen(%2)=0,{@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))]'s Attribute [ansi(h,capnames(%q<find>))] was cleared.;@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0))'s Attribute [ansi(h,capnames(%q<find>))] was cleared, by [ansi(h,%n)];@nspemit %0=announce(EDITCHAR) Your Attribute [capnames(%q<find>)] was cleared.;th delstat(%0/D`ATTRIBUTES,capnames(%q<find>))},{@assert and(gte(%2,0),lte(%2,10))=@nspemit %#=announce(EDITCHAR) Attributes must be between 0 and 10!;@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] now has the [setr(message,ansi(h,%q<find>) Attribute at [ansi(h,%2)])];@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0)) now has the %q<message>, set by [ansi(h,%n)];@nspemit %0=announce(EDITCHAR) You now have the %q<message>;th setstat(%0/D`ATTRIBUTES,%q<find>,%2)}

&INC`SET`ABILITY u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=announce(EDITCHAR) ERROR: No Ability name entered.;@include u(ccs)/INC`PARTIAL=%qc,v(VAR`ABILITIES),|,EDITCHAR,find,Ability;@switch/inline strlen(%2)=0,{@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))]'s Ability [ansi(h,capnames(%q<find>))] was cleared.;@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0))'s Ability [ansi(h,capnames(%q<find>))] was cleared, by [ansi(h,%n)];@nspemit %0=announce(EDITCHAR) Your Ability [capnames(%q<find>)] was cleared.;th delstat(%0/D`ABILITIES,capnames(%q<find>))},{@assert and(gte(%2,0),lte(%2,10))=@nspemit %#=announce(EDITCHAR) Attributes must be between 0 and 10!;@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] now has the [setr(message,ansi(h,%q<find>) Ability at [ansi(h,%2)])];@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0)) now has the %q<message>, set by [ansi(h,%n)];@nspemit %0=announce(EDITCHAR) You now have the %q<message>;th setstat(%0/D`ABILITIES,%q<find>,%2)}

&INC`SET`SPLAT u(ex-cdo)=@assert strlen(%2)=@nspemit %#=ERROR: No Splat name entered.;@include u(ccs)/INC`PARTIAL=%2,v(VAR`SPLATS),|,EDITCHAR,find,splat;@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] is now a [setr(message,[ansi(h,%q<find>)])];@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0)) is now a %q<message>, set by [ansi(h,%n)];@nspemit %0=announce(EDITCHAR) You are now a %q<message>;th setstat(%0/D`INFO,Splat,%q<find>)

&INC`SET`ESSENCE u(ex-cdo)=@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: No Essence value entered.;@assert valnum(%2)=@nspemit %#=announce(EDITCHAR) ERROR: Essence values must be a whole, positive number.;@assert and(gte(%2,1),lte(%2,10))=@nspemit %#=announce(EDITCHAR) ERROR: Essence must be between 1 and 10!;@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))]'s [setr(message,Essence is now [ansi(h,%2)])];@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0))'s %q<message>, set by [ansi(h,%n)];@nspemit %0=announce(EDITCHAR) Your %q<message>;th setstat(%0/D`INFO,Essence,%2)

&INC`SET`WILLPOWER u(ex-cdo)=@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: No Willpower value entered.;@assert valnum(%2)=@nspemit %#=announce(EDITCHAR) ERROR: Willpower values must be a whole, positive number.;@assert and(gte(%2,1),lte(%2,10))=@nspemit %#=announce(EDITCHAR) ERROR: Willpower must be between 1 and 10!;@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))]'s [setr(message,Willpower is now [ansi(h,%2)])];@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0))'s %q<message>, set by [ansi(h,%n)];@nspemit %0=announce(EDITCHAR) Your %q<message>;th setstat(%0/D`INFO,Willpower,%2)

&INC`SET`INFO u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=ERROR: No Info name entered.;@include u(ccs)/INC`PARTIAL=%qc,v(VAR`INFO),|,EDITCHAR,find,Info;@switch/inline strlen(%2)=0,{@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))]'s [ansi(h,%q<find>)] was cleared.;@nscemit/noisy v(VAR`CHANNEL)=[ansi(h,name(%0))]'s [ansi(h,%q<find>)] was cleared, by [ansi(h,%N)];@nspemit %0=announce(EDITCHAR) Your %q<find> was cleared.;th delstat(%0/D`INFO,%q<find>)},{@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))]'s [ansi(h,%q<find>)] is now [ansi(h,%2)];@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0))'s [ansi(h,%q<find>)] is now [ansi(h,%2)], set by [ansi(h,%n)];@nspemit %0=announce(EDITCHAR) Your %q<find> is now: %2;th setstat(%0/D`INFO,%q<find>,%2)}

&INC`SET`BACKGROUND u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=ERROR: No Background name entered.;@switch/inline strlen(%2)=0,{@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))]'s Background [ansi(h,capnames(%qc))] was cleared.;@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0))'s Background [ansi(h,capnames(%qc))] was cleared, by [ansi(h,%N)];@nspemit %0=Your Background [capnames(%qc)] was cleared.;th delstat(%0/D`BACKGROUNDS,capnames(%qc))},{@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] now has the [ansi(h,capnames(%qc))] Background at [ansi(h,%2)];@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0)) now has the [ansi(h,capnames(%qc))] Background at [ansi(h,%2)], set by [ansi(h,%n)];@nspemit %0=announce(EDITCHAR) You now have the Background [capnames(%qc)] at [ansi(h,%2)];th setstat(%0/D`BACKGROUNDS,capnames(%qc),%2)}

&INC`SET`POOL u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=ERROR: No Pool Bonus name entered.;@include u(ccs)/INC`PARTIAL=%qc,iter(lattr(u(ex-pso)/DAT`*,|),last(%i0,`),|,|),|,EDITCHAR,pool,Pool Name;@switch/inline strlen(%2)=0,{@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))]'s Pool Bonus [ansi(h,capnames(%qc))] was cleared.;@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0))'s [ansi(h,capnames(%q<pool>))] Pool Bonus was cleared, by [ansi(h,%N)];@nspemit %0=announce(EDITCHAR) Your [ansi(h,capnames(%q<pool>))] Pool Bonus was cleared.;th delstat(%0/D`POOLBONUS,%q<pool>)},{@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] now has a [ansi(h,capnames(%q<pool>))] Pool Bonus at [ansi(h,%2)];@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0)) now has a [ansi(h,capnames(%q<pool>))] Pool Bonus at [ansi(h,%2)], set by [ansi(h,%n)];@nspemit %0=announce(EDITCHAR) You now have a [ansi(h,capnames(%q<pool>))] Pool Bonus at [ansi(h,%2)];th setstat(%0/D`POOLBONUS,%q<pool>,%2)}

&INC`SET`MERIT u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=ERROR: No Merit name entered.;@switch/inline strlen(%2)=0,{@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))]'s Merit [ansi(h,capnames(%qc))] was cleared.;@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0))'s Merit [ansi(h,capnames(%qc))] was cleared, by [ansi(h,%N)];@nspemit %0=Your Merit [capnames(%qc)] was cleared.;th delstat(%0/D`MERITS,capnames(%qc))},{@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] now has the [ansi(h,capnames(%qc))] Merit at [ansi(h,%2)];@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0)) now has the [ansi(h,capnames(%qc))] Merit at [ansi(h,%2)], set by [ansi(h,%n)];@nspemit %0=announce(EDITCHAR) You now have the Merit [capnames(%qc)] at [ansi(h,%2)];th setstat(%0/D`MERITS,capnames(%qc),%2)}

&INC`SET`FLAW u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=ERROR: No Flaw name entered.;@switch/inline strlen(%2)=0,{@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))]'s Flaw [ansi(h,capnames(%qc))] was cleared.;@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0))'s Flaw [ansi(h,capnames(%qc))] was cleared, by [ansi(h,%N)];@nspemit %0=announce(EDITCHAR) Your Flaw [capnames(%qc)] was cleared.;th delstat(%0/D`Flaws,capnames(%qc))},{@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] now has the [ansi(h,capnames(%qc))] Flaw at [ansi(h,%2)];@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0)) now has the [ansi(h,capnames(%qc))] Flaw at [ansi(h,%2)], set by [ansi(h,%n)];@nspemit %0=announce(EDITCHAR) You now have the Flaw [capnames(%qc)] at [ansi(h,%2)];th setstat(%0/D`Flaws,capnames(%qc),%2)}

&INC`SET`SPECIALTY u(ex-cdo)=@assert strlen(before(%qc,/))=@nspemit %#=ERROR: No Attribute or Ability name entered.;@include u(ccs)/INC`PARTIAL=before(%qc,/),setunion(v(VAR`ABILITIES),v(VAR`ATTRIBUTES),|,|),|,editchar,find,Attribute or Ability;@assert strlen(setr(spec,after(%qc,/)))=@nspemit %#=announce(EDITCHAR) ERROR: No Specialty name entered.;@switch/inline strlen(%2)=0,{@assert match(get(%0/D`SPECIALTIES),%q<find>/%q<spec>~*,|)=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] does not have that specialty!;@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))]'s %q<find> Specialty [ansi(h,capnames(%q<spec>))] was cleared.;@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0))'s %q<find> Specialty [ansi(h,capnames(%q<spec>))] was cleared, by [ansi(h,%N)];@nspemit %0=announce(EDITCHAR) Your %q<find> Specialty [ansi(h,capnames(%q<spec>))] was cleared.;th delstat(%0/D`SPECIALTIES,%q<find>/%q<spec>)},{@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] now has the %q<find> Specialty [ansi(h,capnames(%q<spec>))] at [ansi(h,%2)];@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0)) now has the %q<find> Specialty [ansi(h,capnames(%q<spec>))] at [ansi(h,%2)], set by [ansi(h,%n)];@nspemit %0=announce(EDITCHAR) You now have the %q<find> Specialty [ansi(h,capnames(%q<spec>))] at [ansi(h,%2)];th setstat(%0/D`SPECIALTIES,%q<find>/%q<spec>,%2)}

&INC`SET`POSMUT u(ex-cdo)=@include u(ex-cdo)/INC`SET`POSMUT`DOMUTATE=%0,%1,%2,POSITIVE
&INC`SET`NEGMUT u(ex-cdo)=@include u(ex-cdo)/INC`SET`POSMUT`DOMUTATE=%0,%1,%2,NEGATIVE
&INC`SET`NEUMUT u(ex-cdo)=@include u(ex-cdo)/INC`SET`POSMUT`DOMUTATE=%0,%1,%2,NEUTRAL
&INC`SET`WARMUT u(ex-cdo)=@include u(ex-cdo)/INC`SET`POSMUT`DOMUTATE=%0,%1,%2,WARFORM

&INC`SET`POSMUT`DOMUTATE u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=announce(EDITCHAR) ERROR: No [capnames(%3)] Mutation name entered.;@switch/inline strlen(%2)=0,{@assert match(get(%0/D`%3_MUTATIONS),%qc~*,|)=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] does not have that [capnames(%3)] Mutation!;@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))]'s [capnames(%3)] Mutation [ansi(h,capnames(%qc))] was cleared.;@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0))'s [capnames(%3)] Mutation [ansi(h,capnames(%qc))] was cleared, by [ansi(h,%N)];@nspemit %0=announce(EDITCHAR) Your [capnames(%3)] Mutation [capnames(%qc)] was cleared.;th delstat(%0/D`%3_MUTATIONS,capnames(%qc))},{@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] now has the [ansi(h,capnames(%qc))] [capnames(%3)] Mutation at [ansi(h,%2)];@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0)) now has the [ansi(h,capnames(%qc))] [capnames(%3)] Mutation at [ansi(h,%2)], set by [ansi(h,%n)];@nspemit %0=announce(EDITCHAR) You now have the [capnames(%3)] Mutation [capnames(%qc)] at [ansi(h,%2)];th setstat(%0/D`%3_MUTATIONS,capnames(%qc),%2)}

&INC`SET`COLLEGE u(ex-cdo)=@include u(ex-cdo)/INC`SET`COLLEGE`DOSET=%0,%1,%2,COLLEGES,College
&INC`SET`SLOT u(ex-cdo)=@include u(ex-cdo)/INC`SET`COLLEGE`DOSET=%0,%1,%2,CHARM_SLOTS,Charm Slot
&INC`SET`PATH u(ex-cdo)=@include u(ex-cdo)/INC`SET`COLLEGE`DOSET=%0,%1,%2,PATHS,Path
&INC`SET`GRACE u(ex-cdo)=@include u(ex-cdo)/INC`SET`COLLEGE`DOSET=%0,%1,%2,GRACES,Grace
&INC`SET`VIRTUES u(ex-cdo)=@include u(ex-cdo)/INC`SET`COLLEGE`DOSET=%0,%1,%2,VIRTUES,Virtue

&INC`SET`COLLEGE`DOSET u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=ERROR: No %4 name entered.;@include u(ccs)/INC`PARTIAL=%qc,v(VAR`%3),|,EDITCHAR,find,%4;@switch/inline strlen(%2)=0,{@assert match(get(%0/D`%3),%q<find>~*,|)=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] does not have that %4!;@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))]'s %4 [ansi(h,capnames(%qc))] was cleared.;@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0))'s %4 [ansi(h,capnames(%qc))] was cleared, by [ansi(h,%N)];@nspemit %0=announce(EDITCHAR) Your %4 [capnames(%qc)] was cleared.;th delstat(%0/D`%3,%q<find>)},{@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] now has the [ansi(h,capnames(%qc))] %4 at [ansi(h,%2)];@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0)) now has the [ansi(h,capnames(%qc))] %4 at [ansi(h,%2)], set by [ansi(h,%n)];@nspemit %0=announce(EDITCHAR) You now have the %4 [capnames(%qc)] at [ansi(h,%2)];th setstat(%0/D`%3,%q<find>,%2)}

&INC`SET`STYLE u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=announce(EDITCHAR) ERROR: No Tier entered.;@include u(ccs)/INC`PARTIAL=%qc,TERRESTRIAL|CELESTIAL|SIDEREAL,|,EDITCHAR,tier,tier;@assert strlen(%qd)=@nspemit %#=announce(EDITCHAR) ERROR: No Style ID entered.;@assert valnum(%qd)=@nspemit %#=announce(EDITCHAR) ERROR: Martial Arts Styles must be addressed by their slot IDs.;@assert strlen(get(%0/D`CHARMS`%q<tier>_MARTIAL_ARTS`%qd))=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] has no style by that ID.;@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: Style name field empty.;@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))]'s [numth(%qd)] [ansi(h,%q<tier>)] Martial Arts Style is now called [ansi(h,%2)];@nscemit/noisy v(VAR`CHANNEL)=ansi(h,name(%0))'s [numth(%qd)] [ansi(h,%q<tier>)] Martial Arts Style is now called [ansi(h,%2)], set by [ansi(h,%n)];@nspemit %0=announce(EDITCHAR) Your [numth(%qd)] [ansi(h,%q<tier>)] Martial Arts Style is now called [ansi(h,%2)];&D`CHARMS`%q<tier>_MARTIAL_ARTS`%qd`NAME %0=%2

&GFN`CHARMTYPE u(ex-cdo)=reswitchi(getsplat(%0),^(SOLAR|LUNAR|ABYSSAL|ALCHEMICAL|INFERNAL|DRAGON-BLOODED|RAKSHA|SIDEREAL|GHOST|SPIRIT|MORTAL|JADEBORN)$,getsplat(%0),^(GOD-BLOODED)$,reswitchi(getstat(%0/D`INFO,Caste),^(Divine)$,Spirit,^(Demon)$,Spirit,^(Fae)$,Raksha,^(SOLAR|LUNAR|ABYSSAL|INFERNAL|SIDEREAL)$,getstat(%0/D`INFO,Caste),Mortal),MORTAL)

&INC`ADD`CHARM u(ex-cdo)=@include u(ex-cdo)/INC`ADD`CHARM`DOADD=%0,%1,%2,charmtype(%0),%qc

&INC`ADD`OCHARM u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=announce(EDITCHAR) ERROR: Splat field empty.;@include u(ccs)/INC`PARTIAL=%qc,v(VAR`SPLATS),|,EDITCHAR,splat,Splat;@include u(ex-cdo)/INC`ADD`CHARM`DOADD=%0,%1,%2,%q<splat>,%qd

&INC`ADD`CHARM`DOADD u(ex-cdo)=@assert strlen(v(VAR`CHARMS`%3))=@nspemit %#=announce(EDITCHAR) ERROR: That Splat does not have Charms!;@assert strlen(%4)=@nspemit %#=announce(EDITCHAR) ERROR: Charm Category field empty.;@include u(ccs)/INC`PARTIAL=%4,v(VAR`CHARMS`%3),|,EDITCHAR,cat,category;@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: No Charms entered to Add.;@break words(setinter(%2,get(%0/D`CHARMS`%3`%q<cat>),|,i,|),|)=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] already has %3 %q<cat> Charm(s): [itemize(setinter(%2,get(%0/D`CHARMS`%3`%q<cat>),|,i,|),|,and,\,)];&D`CHARMS`%3`%q<cat> %0=iter(setunion(%2,get(%0/D`CHARMS`%3`%q<cat>),|,i,|),capnames(%i0),|,|);@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] now has [setr(have,the [capnames(%3)] [capnames(%q<cat>)] Charm(s): [ansi(h,itemize(iter(%2,capnames(%i0),|,|),|,and,\,))])];@nspemit %0=announce(EDITCHAR) You now have %q<have>;@nscemit/noisy v(VAR`CHANNEL)={[ansi(h,name(%0))] now has %q<have>, set by [ansi(h,%n)]}

&INC`REM`CHARM u(ex-cdo)=@include u(ex-cdo)/INC`REM`CHARM`DOREM=%0,%1,%2,charmtype(%0),%qc

&INC`REM`OCHARM u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=announce(EDITCHAR) ERROR: Splat field empty.;@include u(ccs)/INC`PARTIAL=%qc,v(VAR`SPLATS),|,EDITCHAR,splat,Splat;@include u(ex-cdo)/INC`REM`CHARM`DOREM=%0,%1,%2,%q<splat>,%qd

&INC`REM`CHARM`DOREM u(ex-cdo)=@assert strlen(v(VAR`CHARMS`%3))=@nspemit %#=announce(EDITCHAR) ERROR: That Splat does not have Charms!;@assert strlen(%4)=@nspemit %#=announce(EDITCHAR) ERROR: Charm Category field empty.;@include u(ccs)/INC`PARTIAL=%4,v(VAR`CHARMS`%3),|,EDITCHAR,cat,category;@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: No Charms entered to Remove.;@assert eq(words(setinter(%2,get(%0/D`CHARMS`%3`%q<cat>),|,i,|),|),words(%2,|))=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] doesn't have has %3 %q<cat> Charm(s): [itemize(setdiff(%2,get(%0/D`CHARMS`%3`%q<cat>),|,|),|,and,\,)];&D`CHARMS`%3`%q<cat> %0=iter(setdiff(get(%0/D`CHARMS`%3`%q<cat>),%2,|,i,|),capnames(%i0),|,|);@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] no longer has [setr(have,the [capnames(%3)] [capnames(%q<cat>)] Charm(s): [ansi(h,itemize(iter(%2,capnames(%i0),|,|),|,and,\,))])];@nspemit %0=announce(EDITCHAR) You no longer have %q<have>;@nscemit/noisy v(VAR`CHANNEL)={[ansi(h,name(%0))] no longer has %q<have>, set by [ansi(h,%n)]}

&INC`ADD`MACHARM u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=announce(EDITCHAR) ERROR: No Tier entered.;@include u(ccs)/INC`PARTIAL=%qc,TERRESTRIAL|CELESTIAL|SIDEREAL,|,EDITCHAR,tier,tier;@assert strlen(%qd)=@nspemit %#=announce(EDITCHAR) ERROR: No Style ID entered.;@assert valnum(%qd)=@nspemit %#=announce(EDITCHAR) ERROR: Martial Arts Styles must be addressed by their slot IDs.;@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: Charms field empty.;@break words(setinter(%2,get(%0/D`CHARMS`%q<tier>_MARTIAL_ARTS`%qd),|,i,|),|)=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] already has [default(%0/D`CHARMS`%q<tier>_MARTIAL_ARTS`%qd`NAME,Unnamed Style)] Charm(s): [itemize(setinter(%2,get(%0/D`CHARMS`%q<tier>_MARTIAL_ARTS`%qd),|,i,|),|,and,\,)];&D`CHARMS`%q<tier>_MARTIAL_ARTS`%qd %0=iter(setunion(%2,get(%0/D`CHARMS`%q<tier>_MARTIAL_ARTS`%qd),|,i,|),capnames(%i0),|,|);@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] now has [setr(have,the [default(%0/D`CHARMS`%q<tier>_MARTIAL_ARTS`%qd`NAME,Unnamed Style)] [capnames(%q<tier>)] Charm(s): [ansi(h,itemize(iter(%2,capnames(%i0),|,|),|,and,\,))])];@nspemit %0=announce(EDITCHAR) You now have %q<have>;@nscemit/noisy v(VAR`CHANNEL)={[ansi(h,name(%0))] now has %q<have>, set by [ansi(h,%n)]}

&INC`REM`MACHARM u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=announce(EDITCHAR) ERROR: No Tier entered.;@include u(ccs)/INC`PARTIAL=%qc,TERRESTRIAL|CELESTIAL|SIDEREAL,|,EDITCHAR,tier,tier;@assert strlen(%qd)=@nspemit %#=announce(EDITCHAR) ERROR: No Style ID entered.;@assert valnum(%qd)=@nspemit %#=announce(EDITCHAR) ERROR: Martial Arts Styles must be addressed by their slot IDs.;@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: Charms field empty.;@assert eq(words(setinter(%2,get(%0/D`CHARMS`%q<tier>_MARTIAL_ARTS`%qd),|,i,|),|),words(%2,|))=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] does not have the [default(%0/D`CHARMS`%q<tier>_MARTIAL_ARTS`%qd`NAME,Unnamed Style)] Charm(s): [itemize(setdiff(%2,get(%0/D`CHARMS`%q<tier>_MARTIAL_ARTS`%qd),|,|),|,and,\,)];&D`CHARMS`%q<tier>_MARTIAL_ARTS`%qd %0=iter(setdiff(get(%0/D`CHARMS`%q<tier>_MARTIAL_ARTS`%qd),%2,|,i,|),capnames(%i0),|,|);@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] no longer has [setr(have,the [default(%0/D`CHARMS`%q<tier>_MARTIAL_ARTS`%qd`NAME,Unnamed Style)] [capnames(%q<tier>)] Charm(s): [ansi(h,itemize(iter(%2,capnames(%i0),|,|),|,and,\,))])];@nspemit %0=announce(EDITCHAR) You no longer have %q<have>;@nscemit/noisy v(VAR`CHANNEL)={[ansi(h,name(%0))] no longer has %q<have>, set by [ansi(h,%n)]}

&INC`ADD`HEALTH u(ex-cdo)=@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: Health levels field empty.;@assert lmath(min,iter(ucstr(%2),or(isnum(%i0),regmatchi(%i0,^(I|D)$))))=@nspemit %#=%q<newhealth> contains an invalid value. Health values must be a negative number, I, or D.;&D`HEALTH %0=sortby(u(ex-cdo)/FUN`HEALTHSORT,cat(get(%0/D`HEALTH),ucstr(%2)));@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] has gained [setr(have,additional Health Levels: [sortby(u(ex-cdo)/FUN`HEALTHSORT,ucstr(%2))])];@nspemit %0=announce(EDITCHAR) You have gained %q<have>;@nscemit/noisy v(VAR`CHANNEL)={ansi(h,name(%0)) has gained %q<have>,set by [ansi(h,%n)]}

&INC`REM`HEALTH u(ex-cdo)=@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: Health levels field empty.;@assert lmath(min,iter(ucstr(%2),or(isnum(%i0),regmatchi(%i0,^(I|D)$))))=@nspemit %#=%q<newhealth> contains an invalid value. Health values must be a negative number, I, or D.;&D`HEALTH %0=sortby(u(ex-cdo)/FUN`HEALTHSORT,remove(get(%0/D`HEALTH),ucstr(%2)));@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] has lost [setr(have,Health Levels: [sortby(u(ex-cdo)/FUN`HEALTHSORT,ucstr(%2))])];@nspemit %0=announce(EDITCHAR) You have lost %q<have>;@nscemit/noisy v(VAR`CHANNEL)={ansi(h,name(%0)) has lost %q<have>,set by [ansi(h,%n)]}

&INC`ADD`SPELL u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=announce(EDITCHAR) ERROR: Spell Category field empty.;@include u(ccs)/INC`PARTIAL=%qc,v(VAR`SPELLS),|,EDITCHAR,cat,category;@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: No Spells entered to Add.;@break words(setinter(%2,get(%0/D`SPELLS`%q<cat>),|,i,|),|)=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] already has %q<cat> Spells(s): [itemize(setinter(%2,get(%0/D`SPELLS`%q<cat>),|,i,|),|,and,\,)];&D`SPELLS`%q<cat> %0=iter(setunion(%2,get(%0/D`SPELLS`%q<cat>),|,i,|),capnames(%i0),|,|);@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] now has [setr(have,the [capnames(%q<cat>)] Spells(s): [ansi(h,itemize(iter(%2,capnames(%i0),|,|),|,and,\,))])];@nspemit %0=announce(EDITCHAR) You now have %q<have>;@nscemit/noisy v(VAR`CHANNEL)={ansi(h,name(%0)) now has %q<have>, set by [ansi(h,%n)]}

&INC`REM`SPELL u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=announce(EDITCHAR) ERROR: Spell Category field empty.;@include u(ccs)/INC`PARTIAL=%qc,v(VAR`SPELLS),|,EDITCHAR,cat,category;@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: No Spells entered to Add.;@assert eq(words(%2,|),words(setinter(%2,get(%0/D`SPELLS`%q<cat>),|,i,|),|))=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] already has %q<cat> Spells(s): [itemize(setinter(%2,get(%0/D`SPELLS`%q<cat>),|,i,|),|,and,\,)];&D`SPELLS`%q<cat> %0=iter(setunion(%2,get(%0/D`SPELLS`%q<cat>),|,i,|),capnames(%i0),|,|);@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] now has [setr(have,the [capnames(%q<cat>)] Spells(s): [ansi(h,itemize(iter(%2,capnames(%i0),|,|),|,and,\,))])];@nspemit %0=announce(EDITCHAR) You now have %q<have>;@nscemit/noisy v(VAR`CHANNEL)={ansi(h,name(%0)) now has %q<have>, set by [ansi(h,%n)]}

&INC`ADD`PROTOCOL u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=announce(EDITCHAR) ERROR: Protocol Category field empty.;@include u(ccs)/INC`PARTIAL=%qc,v(VAR`PROTOCOLS),|,EDITCHAR,cat,category;@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: No Protocols entered to Add.;@break words(setinter(%2,get(%0/D`PROTOCOLS`%q<cat>),|,i,|),|)=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] already has %q<cat> Protocol(s): [itemize(setinter(%2,get(%0/D`PROTOCOLS`%q<cat>),|,i,|),|,and,\,)];&D`PROTOCOLS`%q<cat> %0=iter(setunion(%2,get(%0/D`PROTOCOLS`%q<cat>),|,i,|),capnames(%i0),|,|);@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] now has [setr(have,the [capnames(%q<cat>)] Protocol(s): [ansi(h,itemize(iter(%2,capnames(%i0),|,|),|,and,\,))])];@nspemit %0=announce(EDITCHAR) You now have %q<have>;@nscemit/noisy v(VAR`CHANNEL)={ansi(h,name(%0)) now has %q<have>, set by [ansi(h,%n)]}

&INC`REM`PROTOCOL u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=announce(EDITCHAR) ERROR: Protocol Category field empty.;@include u(ccs)/INC`PARTIAL=%qc,v(VAR`PROTOCOLS),|,EDITCHAR,cat,category;@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: No Protocols entered to Remove.;@assert eq(words(%2,|),words(setinter(%2,get(%0/D`PROTOCOLS`%q<cat>),|,i,|),|))=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] does not have %q<cat> Protocol(s): [itemize(setdiff(get(%0/D`PROTOCOLS`%q<cat>),|,|),%2,|,and,\,)];&D`PROTOCOLS`%q<cat> %0=iter(setdiff(%2,get(%0/D`PROTOCOLS`%q<cat>),|,i,|),capnames(%i0),|,|);@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] no longer has [setr(have,the [capnames(%q<cat>)] Protocol(s): [ansi(h,itemize(iter(%2,capnames(%i0),|,|),|,and,\,))])];@nspemit %0=announce(EDITCHAR) You no longer have %q<have>;@nscemit/noisy v(VAR`CHANNEL)={ansi(h,name(%0)) no longer has %q<have>, set by [ansi(h,%n)]}

&INC`ADD`LANGUAGE u(ex-cdo)=@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: Language field empty.;@break words(setinter(%2,get(%0/D`LANGUAGES),|,i,|),|)=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] already has Languages(s): [itemize(setinter(%2,get(%0/D`LANGUAGES),|,i,|),|,and,\,)];&D`LANGUAGES %0=iter(setunion(%2,get(%0/D`LANGUAGES),|,i,|),capnames(%i0),|,|);@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] now has [setr(have,the LANGUAGES(s): [ansi(h,itemize(iter(%2,capnames(%i0),|,|),|,and,\,))])];@nspemit %0=announce(EDITCHAR) You now have %q<have>;@nscemit/noisy v(VAR`CHANNEL)={ansi(h,name(%0)) now has %q<have>, set by [ansi(h,%n)]}

&INC`REM`LANGUAGE u(ex-cdo)=@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: Language field empty.;@assert eq(words(%2,|),words(setinter(%2,get(%0/D`LANGUAGES),|,i,|),|))=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] does not have Languages(s): [itemize(setdiff(%2,get(%0/D`LANGUAGES),|,|),|,and,\,)];&D`LANGUAGES %0=iter(setdiff(get(%0/D`LANGUAGES),%2,|,i,|),capnames(%i0),|,|);@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] no longer has [setr(have,the LANGUAGES(s): [ansi(h,itemize(iter(%2,capnames(%i0),|,|),|,and,\,))])];@nspemit %0=announce(EDITCHAR) You no longer have %q<have>;@nscemit/noisy v(VAR`CHANNEL)={ansi(h,name(%0)) no longer has %q<have>, set by [ansi(h,%n)]}

&INC`ADD`THAUMATURGY u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=announce(EDITCHAR) ERROR: Practice (Arts or Sciences) field empty.;@include u(ccs)/INC`PARTIAL=%qc,ARTS|SCIENCES,|,EDITCHAR,practice,Practice;@assert strlen(%qd)=@nspemit %#=announce(EDITCHAR) ERROR: Type (Degrees or Procedures) Field empty.;@include u(ccs)/INC`PARTIAL=%qd,PROCEDURES|DEGREES,|,EDITCHAR,type,Type;@break words(setinter(%2,get(%0/D`%q<practice>`%q<type>),|,i,|),|)=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] already has [capnames(%q<practice> %q<type>)](s): [itemize(setinter(%2,get(%0/D`%q<practice>`%q<type>),|,i,|),|,and,\,)];&D`%q<practice>`%q<type> %0=iter(setunion(%2,get(%0/D`%q<practice>`%q<type>),|,i,|),capnames(%i0),|,|);@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] now has [setr(have,the [capnames(%q<practice> %q<type>)](s): [ansi(h,itemize(iter(%2,capnames(%i0),|,|),|,and,\,))])];@nspemit %0=announce(EDITCHAR) You now have %q<have>;@nscemit/noisy v(VAR`CHANNEL)={ansi(h,name(%0)) now has %q<have>, set by [ansi(h,%n)]}

&INC`REM`THAUMATURGY u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=announce(EDITCHAR) ERROR: Practice (Arts or Sciences) field empty.;@include u(ccs)/INC`PARTIAL=%qc,ARTS|SCIENCES,|,EDITCHAR,practice,Practice;@assert strlen(%qd)=@nspemit %#=announce(EDITCHAR) ERROR: Type (Degrees or Procedures) Field empty.;@include u(ccs)/INC`PARTIAL=%qd,PROCEDURES|DEGREES,|,EDITCHAR,type,Type;@assert eq(words(%2,|),words(setinter(%2,get(%0/D`%q<practice>`%q<type>),|,i,|),|))=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] does not have [capnames(%q<practice> %q<type>)](s): [itemize(setdiff(%2,get(%0/D`%q<practice>`%q<type>),|,|),|,and,\,)];&D`%q<practice>`%q<type> %0=iter(setdiff(get(%0/D`%q<practice>`%q<type>),%2,|,i,|),capnames(%i0),|,|);@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] no longer has [setr(have,the [capnames(%q<practice> %q<type>)](s): [ansi(h,itemize(iter(%2,capnames(%i0),|,|),|,and,\,))])];@nspemit %0=announce(EDITCHAR) You no longer have %q<have>;@nscemit/noisy v(VAR`CHANNEL)={ansi(h,name(%0)) no longer has %q<have>, set by [ansi(h,%n)]}

&INC`ADD`FAVORED u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=announce(EDITCHAR) ERROR: Favored Type field empty.;@include u(ccs)/INC`PARTIAL=%qc,ATTRIBUTES|ABILITIES|COLLEGES|PATHS,|,EDITCHAR,type,Favored Type;@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: Favored %q<type> Field empty.;@include u(ccs)/INC`PARTIAL=%2,v(VAR`%q<type>),|,EDITCHAR,choice,%q<type>;@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: No Favoreds entered to Add.;@break words(setinter(%q<choice>,get(%0/D`%q<type>`FAVORED),|,i,|),|)=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] already has [capnames(%q<choice>)] Favored in [capnames(%q<type>)];&D`%q<type>`FAVORED %0=iter(setunion(%q<choice>,get(%0/D`%q<type>`FAVORED),|,i,|),capnames(%i0),|,|);@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] now has [setr(have,capnames(%q<choice>) in [capnames(%q<type>)] Favored)];@nspemit %0=announce(EDITCHAR) You now have %q<have>;@nscemit/noisy v(VAR`CHANNEL)={ansi(h,name(%0)) now has %q<have>, set by [ansi(h,%n)]}

&INC`REM`FAVORED u(ex-cdo)=@assert strlen(%qc)=@nspemit %#=announce(EDITCHAR) ERROR: Favored Type field empty.;@include u(ccs)/INC`PARTIAL=%qc,ATTRIBUTES|ABILITIES|COLLEGES|PATHS,|,EDITCHAR,type,Favored Type;@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: Favored %q<type> Field empty.;@include u(ccs)/INC`PARTIAL=%2,v(VAR`%q<type>),|,EDITCHAR,choice,%q<type>;@assert strlen(%2)=@nspemit %#=announce(EDITCHAR) ERROR: No Favoreds entered to Add.;@assert words(setinter(%q<choice>,get(%0/D`%q<type>`FAVORED),|,i,|),|)=@nspemit %#=announce(EDITCHAR) ERROR: [ansi(h,name(%0))] does not have [capnames(%q<choice>)] Favored in [capnames(%q<type>)];&D`%q<type>`FAVORED %0=iter(setdiff(get(%0/D`%q<type>`FAVORED),%q<choice>,|,i,|),capnames(%i0),|,|);@nspemit %#=announce(EDITCHAR) [ansi(h,name(%0))] no longer has [setr(have,capnames(%q<choice>) in [capnames(%q<type>)] Favored)];@nspemit %0=announce(EDITCHAR) You no longer have %q<have>;@nscemit/noisy v(VAR`CHANNEL)={ansi(h,name(%0)) no longer has %q<have>, set by [ansi(h,%n)]}

&CMD`+CHARMNAMES u(pgo)=$^\+charmnames(?\: +(.+))?$:@assert strlen(%1)=@nspemit %#=What Splat's Charm names will you check?;@include u(ccs)/INC`PARTIAL=%1,get(u(ex-cdo)/VAR`SPLATS),|,CHARMS,splat,Splat;@assert words(lattr(u(ex-pso)/DAT`%q<splat>`*))=@nspemit %#=That Splat has no pool data to display.;@nspemit %#=[header(capnames(%q<splat>) Pool Charms)];th null(if(strlen(lattr(u(ex-pso)/DAT`%q<splat>`EXPANDER`*)),nspemit(%#,ansi(h,* POOL EXPANDERS *))[null(iter(sortkey(#lambda/last(\%0,`),lattr(u(ex-pso)/DAT`%q<splat>`EXPANDER`*),n),nspemit(%#,last(%i0,`) Motes: [itemize(get(u(ex-pso)/%i0),|,and,\,)])))]));th null(if(strlen(lattr(u(ex-pso)/DAT`%q<splat>`OVERDRIVE`*)),nspemit(%#,ansi(h,* OVERDRIVE CHARMS *))[null(iter(sortkey(#lambda/last(\%0,`),lattr(u(ex-pso)/DAT`%q<splat>`OVERDRIVE`*),n),nspemit(%#,last(%i0,`) Motes: [itemize(get(u(ex-pso)/%i0),|,and,\,)])))]));@nspemit %#=%RTo codedly take advantage of these Charms so that they affect your pools, they must be entered exactly as spelled above. Expanders must have a number on the end such as Immanent Solar Glory 4 to denote how many times they were purchased (without such a number, it defaults to 1) and will be collectively checked in all Charm categories and added together. For Infernal Expanders, simply having them listed is enough. God-Blooded use the expanders of their parent Splats.;@nspemit %#=header()
@set u(pgo)/CMD`+CHARMNAMES=regexp

+shelp/addmain Characters/+editchar=+editchar is used to modify all parts of a character's +sheet!%R%R[ansi(h,+editchar/<flag> <player>|<categories|and|objects>\[=<modification>\])]%r%r[ansi(h,FLAG: SET)]%r[ansi(h,Splat)]%t%t=<splat>%R[ansi(h,Essence)]%t%t=<value>%R[ansi(h,Willpower)]%t=<value>%R[ansi(h,Attribute)]%t<attribute>=<value>%r[ansi(h,Ability)]%t%t<ability>=<value>%r[ansi(h,Specialty)]%t<ability or attribute>/<specialty>=<value>%r[ansi(h,Background)]%t<background>=<value>%r[ansi(h,Virtue)]%t%t<virtue>=<value>%r[ansi(h,Grace)]%t%t<Grace>=<value>%r[ansi(h,Path)]%t%t<Path>=<value> (Dragon King Steps, Blazing_Fire etc)%r[ansi(h,College)]%t%t<College>=<value> (Sidereal Astrology, The_Lovers etc)%r[ansi(h,Slot)]%t%t<Type>=<value>%r[ansi(h,Merit)]%t%t<merit>=<value>%r[ansi(h,Flaw)]%t%t<flaw>=<value>%r[ansi(h,PosMut)]%t%t<mutation>=<value> (Positive Mutation)%r[ansi(h,NeuMut)]%t%t<mutation>=<value> (Neutral Mutation)%r[ansi(h,NegMut)]%t%t<mutation>=<value> (Negative Mutation)%r[ansi(h,WarMut)]%t%t<mutation>=<value> (Lunar Warform Mutation)%r[ansi(h,Style)]%t%t<tier>|<slot>=<value> (used to set the style's display name)%r[ansi(h,Pool)]%t%t<pool>=<value> (added to normal calc for max size, can create pool without charms)%r%r[ansi(h,FLAG: ADD & REM)]%r[ansi(h,Charm)]%t%t<category>=<charm1>|<charm2>|etc (Native Charms)%r[ansi(h,MACharm)]%t%t<tier>|<slot>=<charm1>|<charm2>|etc (Martial Arts Charms)%r[ansi(h,OCharm)]%t%t<splat>|<category>=<charm1>|<charm2>|etc (Cross-Splat Charms)%r[ansi(h,Spell)]%t%t<circle>=<spell1>|<spell2>|etc%r[ansi(h,Protocol)]%t<tier>=<protocol1>|<protocol2>|etc%r[ansi(h,Favored)]%t%t<Attribute/Ability>=<Item1>|<Item2>|etc%r[ansi(h,Degree)]%t%t<Arts/Sciences>=<Degree1>|<Degree2>|etc%r[ansi(h,Procedure)]%t<Arts/Sciences>=<Procedure1>|<Procedure2>|etc%r[ansi(h,Language)]%t=<language1>|<language2>|etc%r[ansi(h,Health)]%t%t=<level1> <level2> etc (must be -0, -1, D, etc)%r%r[ansi(h,EXAMPLES)]%r%r[ansi(h,+editchar/set Isidoros|Attribute|Dexterity=4)]%r%tSets Isidoros' Dexterity to 4%r[ansi(h,+editchar/add Isidoros|Language=High Realm|Low Realm)]%r%tAdds High Realm & Low Realm to Isidoros' Languages%r[ansi(h,+editchar/rem Isidoros|Health=-0 -1)]%r%tRemoves one -0 and one -1 from Isidoros' permanent Health Pool%R%R[ansi(h,MARTIAL ARTS)]%RMartial Arts Charms are stored in Slots which are addressed by numbers, such as:%R+editchar/add Isidoros|MACharm|Celestial|1=Searing Fist Attack|Flash-Fire Technique%R+editchar/set Isidoros|Style|Celestial|1=Fire Dragon Style%R%RRemoving all Charms in a style removes it.%R%R[ansi(h,EXPANSION/OVERDRIVE CHARMS)]%RPlease be sure to use EXACT Charmnames as shown in +charmnames or these will NOT function. The results should be immediately visible in a +sheet if added correctly.%R%R[ansi(h,EXTRA INFO)]%RChanging VAR`MORTALESSENCE on the EX-PSO in the master room to 1 will give all mortals an Essence pool. With this set to 0, they'll only have one if the pool's set manually or they have the Awakened Essence merit or positive mutation.%R%RDissonance tracks are automatically added to characters with the Gremlin Syndrome negative mutation.%R%R[ansi(h,CLEARING VALUES)]%RLeaves Values absolutely clear to remove them entirely! Example:%R+editchar/set Isidoros|Attribute|Dexterity=

&VAR`ABILITIES u(ex-cdo)=ARCHERY|MARTIAL_ARTS|MELEE|THROWN|WAR|BUREAUCRACY|LINGUISTICS|RIDE|SAIL|SOCIALIZE|ATHLETICS|AWARENESS|DODGE|LARCENY|STEALTH|CRAFT|CRAFT_AIR|CRAFT_FIRE|CRAFT_WATER|CRAFT_EARTH|CRAFT_WOOD|CRAFT_GLAMOUR|CRAFT_FATE|CRAFT_MAGITECH|CRAFT_GENESIS|INVESTIGATION|LORE|MEDICINE|OCCULT|INTEGRITY|PERFORMANCE|PRESENCE|RESISTANCE|SURVIVAL|DRIVE|FIREARMS
&VAR`ATTRIBUTES u(ex-cdo)=STRENGTH|CHARISMA|PERCEPTION|DEXTERITY|MANIPULATION|INTELLIGENCE|STAMINA|APPEARANCE|WITS
&VAR`COLLEGES u(ex-cdo)=THE_CORPSE|THE_CROW|THE_HAYWAIN|THE_RISING_SMOKE|THE_SWORD|THE_CAPTAIN|THE_GULL|THE_MAST|THE_MESSENGER|THE_SHIP'S_WHEEL|THE_EWER|THE_LOVERS|THE_MUSICIAN|THE_PEACOCK|THE_PILLAR|THE_GUARDIANS|THE_KEY|THE_MASK|THE_SORCERER|THE_TREASURE_TROVE|THE_BANNER|THE_GAUNTLET|THE_QUIVER|THE_SHIELD|THE_SPEAR|THE_COMET|THE_LIGHTNING_BOLT
&VAR`GRACES u(ex-cdo)=WAY|SWORD|STAFF|CUP|RING|HEART
&VAR`LANGUAGES u(ex-cdo)=HIGH REALM|LOW REALM|OLD REALM|RIVERSPEAK|SKYTONGUE|FLAMETONGUE|SEATONGUE|FOREST-TONGUE|GUILD CANT|HIGH HOLY SPEECH
&VAR`PATHS u(ex-cdo)=CELESTIAL_AIR|CLEAR_AIR|SOLID_EARTH|YIELDING_EARTH|BLAZING_FIRE|FLICKERING_FIRE|FLOWING_WATER|SHIMMERING_WATER|GROWING_WOOD|SHAPING_WOOD|GLORIOUS_CONSUMPTION|COAGULATED_EUCHARIST|TECHNOMORPHIC_TRANSCENDANCE|ECSTATIC_ARMAGEDDON|TORMENTED_BODHISATTVA

&VAR`SPLATS u(ex-cdo)=SOLAR|LUNAR|ABYSSAL|ALCHEMICAL|INFERNAL|DRAGON-BLOODED|RAKSHA|SIDEREAL|SPIRIT|MORTAL|JADEBORN|DRAGON-KING|GOD-BLOODED
&VAR`SPLATS`ABYSSAL u(ex-cdo)=DUSK|MIDNIGHT|DAYBREAK|DAY|MOONSHADOW
&VAR`SPLATS`ALCHEMICAL u(ex-cdo)=ORICHALCUM|MOONSILVER|JADE|SOULSTEEL|STARMETAL|ADAMANT
&VAR`SPLATS`DRAGON-BLOODED u(ex-cdo)=AIR|EARTH|FIRE|WATER|WOOD
&VAR`SPLATS`DRAGON-KING u(ex-cdo)=ANKLOK|MOSOK|PTEROK|RAPTOK
&VAR`SPLATS`GOD-BLOODED u(ex-cdo)=DIVINE|GHOST|DEMON|FAE|SOLAR|LUNAR|SIDEREAL|ABYSSAL|INFERNAL
&VAR`SPLATS`INFERNAL u(ex-cdo)=SLAYER|FIEND|SCOURGE|MALEFACTOR|DEFILER
&VAR`SPLATS`JADEBORN u(ex-cdo)=ARTISAN|WORKER|WARRIOR
&VAR`SPLATS`LUNAR u(ex-cdo)=FULL MOON|CHANGING MOON|NO MOON|CASTELESS|WANING MOON|WAXING MOON|HALF MOON
&VAR`SPLATS`RAKSHA u(ex-cdo)=DIPLOMAT|COURTIER|IMPERIAL RAKSHA|SCRIBE|ENTERTAINER|LUMINARY|ESHU|ORNAMENTAL RAKSHA|WARRIOR|ANARCH|XIA|CATAPHRACT|WORKER|PANJANDRUM|ARTISAN|STRATEGOS|GUIDE|HARBINGER|VAGABOND|NOMAD|FERRYMAN|HERALD|SKALD|DRAGOON|ATTENDANT
&VAR`SPLATS`SIDEREAL u(ex-cdo)=JOURNEYS|ENDINGS|BATTLES|SECRETS|SERENITY
&VAR`SPLATS`SOLAR u(ex-cdo)=DAWN|ZENITH|TWILIGHT|NIGHT|ECLIPSE
&VAR`SPLATS`SPIRIT u(ex-cdo)=GOD|DEMON
&VAR`VIRTUES u(ex-cdo)=COMPASSION|CONVICTION|TEMPERANCE|VALOR

&VAR`INFO u(ex-cdo)=

&VAR`PROTOCOLS u(ex-cdo)=GOD-MACHINE|MAN-MACHINE

&VAR`SPELLS u(ex-cdo)=TERRESTRIAL|CELESTIAL|SOLAR|SHADOWLANDS|LABYRINTH|VOID

&VAR`CHARMS`SOLAR u(ex-cdo)=ARCHERY|MARTIAL_ARTS|MELEE|THROWN|WAR|BUREAUCRACY|LINGUISTICS|RIDE|SAIL|SOCIALIZE|ATHLETICS|AWARENESS|DODGE|LARCENY|STEALTH|CRAFT|INVESTIGATION|LORE|MEDICINE|OCCULT|INTEGRITY|PERFORMANCE|PRESENCE|RESISTANCE|SURVIVAL|DRIVE|FIREARMS

&VAR`CHARMS`ABYSSAL u(ex-cdo)=ARCHERY|MARTIAL_ARTS|MELEE|THROWN|WAR|BUREAUCRACY|LINGUISTICS|RIDE|SAIL|SOCIALIZE|ATHLETICS|AWARENESS|DODGE|LARCENY|STEALTH|CRAFT|INVESTIGATION|LORE|MEDICINE|OCCULT|INTEGRITY|PERFORMANCE|PRESENCE|RESISTANCE|SURVIVAL|DRIVE|FIREARMS

&VAR`CHARMS`SIDEREAL u(ex-cdo)=ARCHERY|MARTIAL_ARTS|MELEE|THROWN|WAR|BUREAUCRACY|LINGUISTICS|RIDE|SAIL|SOCIALIZE|ATHLETICS|AWARENESS|DODGE|LARCENY|STEALTH|CRAFT|INVESTIGATION|LORE|MEDICINE|OCCULT|INTEGRITY|PERFORMANCE|PRESENCE|RESISTANCE|SURVIVAL|DRIVE|FIREARMS|THE_CORPSE|THE_CROW|THE_HAYWAIN|THE_RISING_SMOKE|THE_SWORD|THE_CAPTAIN|THE_GULL|THE_MAST|THE_MESSENGER|THE_SHIP'S_WHEEL|THE_EWER|THE_LOVERS|THE_MUSICIAN|THE_PEACOCK|THE_PILLAR|THE_GUARDIANS|THE_KEY|THE_MASK|THE_SORCERER|THE_TREASURE_TROVE|THE_BANNER|THE_GAUNTLET|THE_QUIVER|THE_SHIELD|THE_SPEAR|THE_COMET|THE_LIGHTNING_BOLT

&VAR`CHARMS`DRAGON-BLOODED u(ex-cdo)=ARCHERY|MARTIAL_ARTS|MELEE|THROWN|WAR|BUREAUCRACY|LINGUISTICS|RIDE|SAIL|SOCIALIZE|ATHLETICS|AWARENESS|DODGE|LARCENY|STEALTH|CRAFT|INVESTIGATION|LORE|MEDICINE|OCCULT|INTEGRITY|PERFORMANCE|PRESENCE|RESISTANCE|SURVIVAL|DRIVE|FIREARMS

&VAR`CHARMS`LUNAR u(ex-cdo)=STRENGTH|CHARISMA|PERCEPTION|DEXTERITY|MANIPULATION|INTELLIGENCE|STAMINA|APPEARANCE|WITS|KNACKS

&VAR`CHARMS`ALCHEMICAL u(ex-cdo)=COMBAT|SURVIVAL|SPEED_AND_MOBILITY|SOCIAL|STEALTH_AND_DISGUISE|ANALYTIC_AND_COGNITIVE|LABOR_AND_UTILITY|SUBMODULES

&VAR`CHARMS`RAKSHA u(ex-cdo)=MASK|HEART|CUP|RING|STAFF|SWORD

&VAR`CHARMS`INFERNAL u(ex-cdo)=MALFEAS|CECELYNE|SWLIHN|ADORJAN|EBON_DRAGON|KIMBERY|THEION|MARTIAL_ARTS

&VAR`CHARMS`SPIRIT u(ex-cdo)=UNIVERSAL

&VAR`CHARMS`MORTAL u(ex-cdo)=UNIVERSAL

&VAR`CHARMS`GHOST u(ex-cdo)=UNIVERSAL

&VAR`CHARMS`JADEBORN u(ex-cdo)=FOUNDATION|WORKER|WARRIOR|ARTISAN|ENLIGHTENED

&VAR`CHANNEL u(ex-cdo)=Staff Reports