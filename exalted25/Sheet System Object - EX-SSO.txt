@@ DEPENDENCIES - CORE

@switch/inline isdbref(U(ex-sso))=0,{@tel create(Sheet System Object <EX-SSO>)=config(master_room)}
&ex-sso u(coi)=locate(config(master_room),Sheet System Object <EX-SSO>,TXxi)
@set u(ex-sso)=WIZARD !NO_COMMAND
@parent u(ex-sso)=u(coi)

&CMD`+SHEET u(ex-sso)=$^\+sheet(?\:/(\S+))?(?\: +(.*))?:@include u(ccs)/INC`PARTIAL=%1,setunion(v(VAR`PLAYFLAGS),if(isadmin(%#),v(VAR`ADMINFLAGS)),|,|),|,SHEET,choice;@switch/inline strlen(%2)=0,{@include u(ex-sso)/INC`[strfirstof(%q<choice>,MAIN)]=%#,getsplat(%#)},{@include u(ccs)/INC`CHECKPC=%2,1,SHEET;@assert or(isadmin(%#),gt(get(%q<t1>/SHEETPUBLIC),0),strmatch(%q<t1>,%#))=@nspemit %#=announce(SHEET) ERROR: %q<t1name>'s Sheet is not public.;@include u(ex-sso)/INC`[strfirstof(%q<choice>,MAIN)]=%q<t1>,getsplat(%q<t1>)}
@set u(ex-sso)/CMD`+SHEET=regexp

&VAR`PLAYFLAGS u(ex-sso)=EXTRA|STYLE|PUBLIC

&FUN`COLOR u(ex-sso)=ansi(strfirstof(v(VAR`COLOR`%0`%1),v(VAR`COLOR`DEFAULT`%1),n),%2)

&INC`STYLE u(ex-sso)=@switch/inline t(default(%#/SHEETSTYLE,0))=0,{@nspemit %#=announce(SHEET) Charms and Spells will now display in an alternate style!;&SHEETSTYLE %#=1},1,{@nspemit %#=announce(SHEET) Charms and Spells will now display in ordinary style!;&SHEETSTYLE %#=0}

&INC`PUBLIC u(ex-sso)=@switch/inline t(default(%#/SHEETPUBLIC,0))=0,{@nspemit %#=announce(SHEET) Your sheet is now shared publically!;&SHEETPUBLIC %#=1},1,{@nspemit %#=announce(SHEET) Your sheet is now private!;&SHEETPUBLIC %#=0}

&INC`EXTRA u(ex-sso)=@nspemit %#=repeat(u(FUN`COLOR,%1,BORDER,-),0)%R%b%b%b[ansi(strfirstof(v(VAR`COLOR`%1`ATTRNAME),n),--------------------<-)][ansi(strfirstof(v(VAR`COLOR`%1`PSM),n),Merits)][ansi(strfirstof(v(VAR`COLOR`%1`ATTRNAME),n),->----------------)][if(words(setr(merits,get(%0/D`MERITS)),|),%R%b%b%b- [iter(%q<merits>,ansi(strfirstof(v(VAR`COLOR`%1`SKILLNAME),n),elements(%i0,1,~))[repeat(ansi(strfirstof(v(VAR`COLOR`%1`SKILLLINE),n),_),sub(43,strlen(elements(%i0,1,~)),strlen(elements(%i0,2,~))))][ansi(strfirstof(v(VAR`COLOR`%1`SKILLDOT),n),elements(%i0,2,~))],|,%R%b%b%b-%b )])]%r%r;@nspemit %#=[ansi(strfirstof(v(VAR`COLOR`%1`ATTRNAME),n),%b%b%b-----------------<-)][ansi(strfirstof(v(VAR`COLOR`%1`PSM),n),Specialties)][ansi(strfirstof(v(VAR`COLOR`%1`ATTRNAME),n),->--------------)][if(words(setr(specialties,get(%0/D`SPECIALTIES)),|),%R%b%b%b- [iter(%q<specialties>,ansi(strfirstof(v(VAR`COLOR`%1`SKILLNAME),n),capnames(elements(%i0,1,~))\,%B[elements(%i0,2,~)])[repeat(ansi(strfirstof(v(VAR`COLOR`%1`SKILLLINE),n),_),sub(41,strlen(elements(%i0,1,~)),strlen(elements(%i0,2,~)),strlen(elements(%i0,3,~))))][ansi(strfirstof(v(VAR`COLOR`%1`SKILLDOT),n),elements(%i0,3,~))],|,%R%b%b%b-%b)])]%r%r;@nspemit %#=[ansi(strfirstof(v(VAR`COLOR`%1`ATTRNAME),n),%b%b%b--------------------<-)][ansi(strfirstof(v(VAR`COLOR`%1`PSM),n),Notes)][ansi(strfirstof(v(VAR`COLOR`%1`ATTRNAME),n),->-----------------)][if(words(setr(notes,lattr(%0/D`NOTES`*,|)),|),%R%b%b%b-[iter(%q<notes>,rjust(inum(0),2).%B[get(%0/%i0)],|,%R%b%b%b-)])][u(FUN`COLOR,%1,BORDER,)]%R[repeat(u(FUN`COLOR,%1,BORDER,-),0)]


&INC`MAIN u(ex-sso)=@nspemit %#=u(u(ex-sso)/FUN`INFO,%0,setr(splat,getsplat(%0)));@switch/inline gt(strlen(get(%0/D`ATTRIBUTES)),0)=1,{@nspemit %#=u(u(ex-sso)/FUN`ATTRIBUTES,%0,%q<splat>)};@switch/inline gt(strlen(get(%0/D`ABILITIES)),0)=1,{@nspemit %#=u(u(ex-sso)/FUN`ABILITIES,%0,%q<splat>)};@switch/inline gt(strlen(get(%0/D`SPECIALTIES)),0)=1,{@nspemit %#=u(u(ex-sso)/FUN`SPECIALTIES,%0,%q<splat>)};@switch/inline gt(strlen(get(%0/D`BACKGROUNDS)),0)=1,{@nspemit %#=u(u(ex-sso)/FUN`BACKGROUNDS,%0,%q<splat>)};@switch/inline gt(strlen(get(%0/D`GRACES)),0)=1,{@nspemit %#=u(u(ex-sso)/FUN`GRACES,%0,%q<splat>)};@include u(ex-sso)/INC`ADVANTAGES;@include u(ex-sso)/INC`FOOTER



@@ @nspemit %#=u(u(ex-sso)/FUN`ADVANTAGES`%q<splat>,%0,%q<splat>);@nspemit %#=u(u(ex-sso)/FUN`OTHER,%0,%q<splat>);@nspemit %#=u(u(ex-sso)/FUN`FOOTER,%0,%q<splat>)

&FUN`INFO u(ex-sso)=[space(1)][u(FUN`COLOR,%1,BORDER,.)][center(u(FUN`COLOR,%1,FRONTSLASH,/)[u(FUN`COLOR,%1,HEADER,Info)][u(FUN`COLOR,%1,FRONTSLASH,/)],74,u(FUN`COLOR,%1,BORDER,-))][u(FUN`COLOR,%1,BORDER,.)]%R[u(FUN`COLOR,%1,BORDER,/)][space(2)][u(FUN`COLOR,%1,TEXTHEAD,Name)][space(2)][u(FUN`COLOR,%1,COLON,:)]%B[ljust(left(u(FUN`COLOR,%1,TEXTFIELD,name(%0)),22),23)][u(FUN`COLOR,%1,TEXTHEAD,Title)][u(FUN`COLOR,%1,COLON,:)]%B[ljust(left(u(FUN`COLOR,%1,TEXTFIELD,default(%0/Title,the unknown)),35),36)][u(FUN`COLOR,%1,BORDER,\\)]%R[u(FUN`BORDER,%0,%1,%B[u(FUN`COLOR,%1,TEXTHEAD,Splat)][space(1)][u(FUN`COLOR,%1,COLON,:)]%B[ljust(left(u(FUN`COLOR,%1,TEXTFIELD,capnames(%1)),21),22)][if(match(MORTAL,%1),ljust(,21),u(FUN`COLOR,%1,TEXTHEAD,if(strmatch(%1,DRAGON-BLOODED),Aspect,Caste%B))[u(FUN`COLOR,%1,COLON,:)]%B[ljust(left(u(FUN`COLOR,%1,TEXTFIELD,getstat(%0/D`INFO,Caste)),20),21)])]%R[u(FUN`COLOR,%1,TEXTHEAD,Essence)][u(FUN`COLOR,%1,COLON,:)]%B[ljust(left(u(FUN`COLOR,%1,TEXTFIELD,getstat(%0/D`INFO,Essence)),7),21)][u(FUN`COLOR,%1,TEXTHEAD,Concept)][u(FUN`COLOR,%1,COLON,:)]%B[ljust(left(u(FUN`COLOR,%1,TEXTFIELD,getstat(%0/D`INFO,Concept)),35),36)])][u(FUN`INFO`%1,%0,%1)]

&FUN`INFO`SOLAR u(ex-sso)=%R[u(FUN`BORDER,%0,%1,u(FUN`COLOR,%1,TEXTHEAD,Virtue Flaw)[u(FUN`COLOR,%1,COLON,:)]%B[ljust(left(u(FUN`COLOR,%1,TEXTFIELD,getstat(%0/D`INFO,Virtue Flaw)),69),70)])]

&FUN`INFO`LUNAR u(ex-sso)=%R[u(FUN`BORDER,%0,%1,u(FUN`COLOR,%1,TEXTHEAD,Virtue Flaw)[u(FUN`COLOR,%1,COLON,:)]%B[ljust(left(u(FUN`COLOR,%1,TEXTFIELD,getstat(%0/D`INFO,Virtue Flaw)),25),26)][u(FUN`COLOR,%1,TEXTHEAD,Totem Animal)][u(FUN`COLOR,%1,COLON,:)]%B[ljust(left(u(FUN`COLOR,%1,TEXTFIELD,getstat(%0/D`INFO,Totem Animal)),69),70)])]

&FUN`INFO`INFERNAL u(ex-sso)=%R[u(FUN`BORDER,%0,%1,u(FUN`COLOR,%1,TEXTHEAD,Urge)[u(FUN`COLOR,%1,COLON,:)]%B[ljust(left(u(FUN`COLOR,%1,TEXTFIELD,getstat(%0/D`INFO,Urge)),25),26)][u(FUN`COLOR,%1,TEXTHEAD,Favored Yozi)][u(FUN`COLOR,%1,COLON,:)]%B[ljust(left(u(FUN`COLOR,%1,TEXTFIELD,getstat(%0/D`INFO,Favored Yozi)),69),70)])]

&VAR`TITLE u(ex-sso)=ansi(h,Exalted MUSH)

&FUN`BORDER u(ex-sso)=align(1. 74 1.,u(FUN`COLOR,%1,BORDER,|),%2,u(FUN`COLOR,%1,BORDER,|)%R)

&FUN`ATTRIBUTES u(ex-sso)=u(FUN`DOTFORMAT,%0,%1,ATTRIBUTES,23,A,ttributes,,)

@@ u(FUN`COLOR,%1,BORDER,\}[repeat(-,8)])[u(FUN`COLOR,%1,FRONTSLASH,/)][u(FUN`COLOR,%1,PSM,Physical)][u(FUN`COLOR,%1,FRONTSLASH,/)][u(FUN`COLOR,%1,BORDER,repeat(-,15))][u(FUN`COLOR,%1,FRONTSLASH,/)][u(FUN`COLOR,%1,PSM,Social)][u(FUN`COLOR,%1,FRONTSLASH,/)][u(FUN`COLOR,%1,BORDER,repeat(-,16))][u(FUN`COLOR,%1,FRONTSLASH,/)][u(FUN`COLOR,%1,PSM,Mental)][u(FUN`COLOR,%1,FRONTSLASH,/)][u(FUN`COLOR,%1,BORDER,repeat(-,11)\{)]%R

&FUN`ABILITIES u(ex-sso)=u(FUN`DOTFORMAT,%0,%1,ABILITIES,23,A,bilities,D`ABILITIES)

&VAR`COLOR`SOLAR`BORDER u(ex-sso)=y
&VAR`COLOR`SOLAR`PSM u(ex-sso)=hy

&FUN`DOTFORMAT u(ex-sso)=u(FUN`SECTIONHEADER,%0,%1,%4%5)%R%7[u(FUN`BORDER,%0,%1,table(iter(if(%9,filterbool(#lambda/lt(strlen(before(\%0,~)),sub(%3,10)),setr(preextra,switch(1,and(strlen(get(u(ex-cdo)/VAR`%2)),strlen(%6),strlen(get(%0/%6))),filterbool(#lambda/match(iter(get(%0/%6),before(\%i0,~),|,|),\%0,|),get(u(ex-cdo)/VAR`%2),|,|),t(strlen(%6)),sort(iter(get(%0/%6),before(%i0,~),|,|),i,|,|),get(u(ex-cdo)/VAR`%2))),|,|),switch(1,and(strlen(get(u(ex-cdo)/VAR`%2)),strlen(%6),strlen(get(%0/%6))),filterbool(#lambda/match(iter(get(%0/%6),before(\%i0,~),|,|),\%0,|),get(u(ex-cdo)/VAR`%2),|,|),t(strlen(%6)),sort(iter(get(%0/%6),before(%i0,~),|,|),i,|,|),get(u(ex-cdo)/VAR`%2))),u(FUN`COLOR,%1,STATNAME,if(and(strmatch(%2,ABILITIES),strmatch(%i0,Craft*),match(get(%0/D`%2`FAVORED),Craft,|)),*,if(match(get(%0/D`%2`FAVORED),%i0,|),u(FUN`COLOR,%1,STATNAME,*),%B))[capnames(setr(statname,%i0))])[if(or(%8,not(lte(setr(linelength,sub(sub(%3,1),strlen(%q<statname>),firstof(setr(val,max(getstat(%0/D`%2,%i0),add(getstat(%0/D`%2,%i0),getstat(%0/D`BONUS,%i0)))),1))),0))),repeat(u(FUN`COLOR,%1,STATLINE,.),%q<linelength>)[u(FUN`COLOR,%1,STATDOT,if(%8,getstat(%0/D`%2,%i0,BONUS),switch(1,gt(getstat(%0/D`BONUS,%i0),0),repeat(+,getstat(%0/D`BONUS,%i0))[repeat(*,getstat(%0/D`%2,%i0))],lt(getstat(%0/D`BONUS,%i0),0),repeat(-,bound(abs(getstat(%0/D`BONUS,%i0)),,sub(getstat(%0/D`%2,%i0),1)))[repeat(*,bound(getstat(%0/D`%2,%i0,BONUS),1))],strfirstof(repeat(*,getstat(%0/D`%2,%i0)),x))))],repeat(u(FUN`COLOR,%1,STATLINE,.),sub(sub(%3,1),strlen(%q<statname>),strlen(switch(getstat(%0/D`BONUS,%i0),<0,-,>0,+)[firstof(%q<val>,1)])))[u(FUN`COLOR,%1,STATDOT,switch(getstat(%0/D`BONUS,%i0),<0,-,>0,+)[firstof(%q<val>,1)])])],|,|),%3,74,|))][if(and(%9,words(setr(extra,filterbool(#lambda/gte(strlen(before(\%0,~)),sub(%3,10)),%q<preextra>,|,|)),|)),%R[u(FUN`BORDER,%0,%1,iter(%q<extra>,u(FUN`COLOR,%1,STATNAME,if(and(strmatch(%2,ABILITIES),strmatch(%i0,Craft*),match(get(%0/D`%2`FAVORED),Craft,|)),*,if(match(get(%0/D`%2`FAVORED),%i0,|),u(FUN`COLOR,%1,STATNAME,*),%B))[capnames(setr(statname,%i0))])[if(or(%8,not(lte(setr(linelength,sub(sub(73,1),strlen(%q<statname>),firstof(setr(val,max(getstat(%0/D`%2,%i0),add(getstat(%0/D`%2,%i0),getstat(%0/D`BONUS,%i0)))),1))),0))),repeat(u(FUN`COLOR,%1,STATLINE,.),%q<linelength>)[u(FUN`COLOR,%1,STATDOT,if(%8,getstat(%0/D`%2,%i0,BONUS),switch(1,gt(getstat(%0/D`BONUS,%i0),0),repeat(+,getstat(%0/D`BONUS,%i0))[repeat(*,getstat(%0/D`%2,%i0))],lt(getstat(%0/D`BONUS,%i0),0),repeat(-,bound(abs(getstat(%0/D`BONUS,%i0)),,sub(getstat(%0/D`%2,%i0),1)))[repeat(*,bound(getstat(%0/D`%2,%i0,BONUS),1))],strfirstof(repeat(*,getstat(%0/D`%2,%i0)),x))))],repeat(u(FUN`COLOR,%1,STATLINE,.),sub(sub(73,1),strlen(%q<statname>),strlen(switch(getstat(%0/D`BONUS,%i0),<0,-,>0,+)[firstof(%q<val>,1)])))[u(FUN`COLOR,%1,STATDOT,switch(getstat(%0/D`BONUS,%i0),<0,-,>0,+)[firstof(%q<val>,1)])])],|,%R))])]





&INC`ADVANTAGES u(ex-sso)=@switch/inline gt(strlen(get(%0/D`COLLEGES)),0)=1,{@nspemit %#=u(u(ex-sso)/FUN`COLLEGES,%0,%q<splat>)};@switch/inline gt(strlen(get(%0/D`PATHS)),0)=1,{@nspemit %#=u(u(ex-sso)/FUN`PATHS,%0,%q<splat>)};@include u(ex-sso)/INC`MUTATIONS;@include u(ex-sso)/INC`CHARMS;@include u(ex-sso)/INC`MACHARMS;@include u(ex-sso)/INC`SPELLS;@include u(ex-sso)/INC`CHARM_SLOTS;@@ @include u(ex-sso)/INC`VIRTUES

&FUN`BACKGROUNDS u(ex-sso)=u(FUN`DOTFORMAT,%0,%1,BACKGROUNDS,36,B,ackgrounds,D`BACKGROUNDS,,,1)
&FUN`SPECIALTIES u(ex-sso)=u(FUN`DOTFORMAT,%0,%1,SPECIALTIES,36,S,pecialties,D`SPECIALTIES,,,1)
&FUN`COLLEGES u(ex-sso)=u(FUN`DOTFORMAT,%0,%1,COLLEGES,36,C,olleges,D`COLLEGES)
&FUN`PATHS u(ex-sso)=u(FUN`DOTFORMAT,%0,%1,PATHS,36,P,aths,D`PATHS)
&FUN`GRACES u(ex-sso)=u(FUN`DOTFORMAT,%0,%1,GRACES,23,G,races,D`GRACES)

&INC`MUTATIONS u(ex-sso)=@dolist/inline filterbool(#lambda/strlen(get(%0/D`\%0)),POSITIVE_MUTATIONS NEUTRAL_MUTATIONS NEGATIVE_MUTATIONS WARFORM_MUTATIONS)={@nspemit %#=u(FUN`DOTFORMAT,%0,%1,%i0,36,left(capnames(edit(%i0,_,%B)),1),after(capnames(edit(%i0,_,%B)),left(capnames(edit(%i0,_,%B)),1)),D`%i0)}

&INC`CHARMS u(ex-sso)=@dolist/inline filterbool(#lambda/strlen(regrepi(%0,D`\%0`*,.+)),CHARMS`SOLAR CHARMS`LUNAR CHARMS`ABYSSAL CHARMS`ALCHEMICAL CHARMS`INFERNAL CHARMS`DRAGON-BLOODED CHARMS`JADEBORN CHARMS`RAKSHA CHARMS`SIDEREAL CHARMS`SPIRIT CHARMS`MORTAL ARCANOI`GHOST)={@switch/inline default(%#/SHEETSTYLE,0)=0,{@nspemit %#=u(FUN`CHARMHEADER,%0,%1,capnames(before(%i0,`))\, [capnames(after(%i0,`))]);@dolist/inline filterbool(#lambda/strlen(get(%0/\%0)),lattr(%0/D`%i0`*))={@nspemit %#=u(FUN`CHARMSUBHEADER,%0,%1,capnames(last(%i0,`)));@nspemit %#=u(FUN`BORDER,%0,%1,table(sort(iter(get(%0/%i0),capnames(%i0),|,|),i,|,|),36,74,|))}},1,{@nspemit %#=u(FUN`CHARMHEADER,%0,%1,capnames(before(%i0,`))\, [capnames(after(%i0,`))]);@dolist/inline filterbool(#lambda/strlen(get(%0/\%0)),lattr(%0/D`%i0`*))={@nspemit %#=u(FUN`BORDER,%0,%1,ansi(h,capnames(last(%i0,`))): [itemize(sort(iter(get(%0/%i0),capnames(%i0),|,|),i,|,|),|,and,\,)])}}}

&INC`MACHARMS u(ex-sso)=@dolist/inline filterbool(#lambda/strlen(regrepi(%0,D`\%0`*,.+)),CHARMS`TERRESTRIAL_MARTIAL_ARTS CHARMS`CELESTIAL_MARTIAL_ARTS CHARMS`SIDEREAL_MARTIAL_ARTS)={@switch/inline default(%#/SHEETSTYLE,0)=0,{@nspemit %#=u(FUN`CHARMHEADER,%0,%1,capnames(edit(elements(%i0,2,`),_,%b)));@dolist/inline filterbool(#lambda/strlen(get(%0/\%0)),lattr(%0/D`%i0`*))={@nspemit %#=u(FUN`CHARMSUBHEADER,%0,%1,last(%i0,`): [default(%0/%i0`NAME,Unnamed Style)]);@nspemit %#=u(FUN`BORDER,%0,%1,table(sort(iter(get(%0/%i0),capnames(%i0),|,|),i,|,|),36,74,|))}},1,{@nspemit %#=u(FUN`CHARMHEADER,%0,%1,capnames(edit(elements(%i0,2,`),_,%b)));@dolist/inline filterbool(#lambda/strlen(get(%0/\%0)),lattr(%0/D`%i0`*))={@nspemit %#=u(FUN`BORDER,%0,%1,ansi(h,last(%i0,`): [default(%0/%i0`NAME,Unnamed Style)]:) [itemize(sort(iter(get(%0/%i0),capnames(%i0),|,|),i,|,|),|,and,\,)])}}}

&INC`SPELLS u(ex-sso)=@dolist/inline filterbool(#lambda/strlen(get(%0/D`\%0)),SPELLS`TERRESTRIAL SPELLS`CELESTIAL SPELLS`SOLAR SPELLS`SHADOWLANDS SPELLS`LABYRINTH SPELLS`VOID PROTOCOLS`MAN-MACHINE PROTOCOLS`GOD-MACHINE ARTS`DEGREES ARTS`PROCEDURES SCIENCES`DEGREES SCIENCES`PROCEDURES)={@switch/inline default(%#/SHEETSTYLE,0)=0,{@nspemit %#=u(FUN`CHARMHEADER,%0,%1,capnames(before(%i0,`)));@nspemit %#=u(FUN`CHARMSUBHEADER,%0,%1,capnames(after(%i0,`)));@nspemit %#=u(FUN`BORDER,%0,%1,table(sort(iter(get(%0/D`%i0),capnames(%i0),|,|),i,|,|),36,74,|))},1,{@nspemit %#=u(FUN`CHARMHEADER,%0,%1,capnames(before(%i0,`)));@nspemit %#=u(FUN`BORDER,%0,%1,ansi(h,capnames(after(%i0,`))): [itemize(sort(iter(get(%0/D`%i0),capnames(%i0),|,|),i,|,|),|,and,\,)])}}

&INC`CHARM_SLOTS u(ex-sso)=@dolist/inline filterbool(#lambda/strlen(get(%0/D`\%0)),CHARM_SLOTS)={@nspemit %#=u(FUN`DOTFORMAT,%0,%1,%i0,36,left(capnames(edit(%i0,_,%B)),1),after(capnames(edit(%i0,_,%B)),left(capnames(edit(%i0,_,%B)),1)),D`%i0,,if(strmatch(%i0,CHARM_SLOTS),1))}

&INC`GRACES u(ex-sso)=@dolist/inline filterbool(#lambda/strlen(get(%0/D`\%0)),GRACES)={@nspemit %#=u(FUN`DOTFORMAT,%0,%1,%i0,36,left(capnames(edit(%i0,_,%B)),1),after(capnames(edit(%i0,_,%B)),left(capnames(edit(%i0,_,%B)),1)),D`%i0,,if(strmatch(%i0,CHARM_SLOTS),1))}

&FUN`CHARMHEADER u(ex-sso)=u(FUN`COLOR,%1,BORDER,\})[center(u(FUN`COLOR,%1,FRONTSLASH,/)[u(FUN`COLOR,%1,CHARMHEADER,%2)][u(FUN`COLOR,%1,FRONTSLASH,/)],76,u(FUN`COLOR,%1,BORDER,-))][u(FUN`COLOR,%1,BORDER,\{)]

&FUN`CHARMSUBHEADER u(ex-sso)=u(FUN`COLOR,%1,BORDER,|)[center(u(FUN`COLOR,%1,CHARMSUBHEADERBORDER,====)[u(FUN`COLOR,%1,CHARMSUBHEADER,%2)][u(FUN`COLOR,%1,CHARMSUBHEADERBORDER,====)],76)][u(FUN`COLOR,%1,BORDER,|)]

&FUN`SECTIONHEADER u(ex-sso)=u(FUN`COLOR,%1,BORDER,\})[center(u(FUN`COLOR,%1,FRONTSLASH,/)[u(FUN`COLOR,%1,HEADER,%2)][u(FUN`COLOR,%1,FRONTSLASH,/)],76,u(FUN`COLOR,%1,BORDER,-))][u(FUN`COLOR,%1,BORDER,\{)]

&INC`FOOTER u(ex-sso)=@switch/inline gt(strlen(get(%0/D`LANGUAGES)),0)=1,{@nspemit %#=u(FUN`SECTIONHEADER,%0,%1,Languages);@nspemit %#=u(FUN`BORDER,%0,%1,itemize(sort(get(%0/D`LANGUAGES),a,|),|,and,\,))};@switch/inline gt(words(u(u(ex-pso)/FUN`LISTPOOLS,%0)),0)=1,{@nspemit %#=u(FUN`COLOR,%1,BORDER,\}[repeat(-,8)])[u(FUN`COLOR,%1,FRONTSLASH,/)][u(FUN`COLOR,%1,PSM,Pools)][u(FUN`COLOR,%1,FRONTSLASH,/)][u(FUN`COLOR,%1,BORDER,repeat(-,16))][u(FUN`COLOR,%1,FRONTSLASH,/)][u(FUN`COLOR,%1,PSM,Channels)][u(FUN`COLOR,%1,FRONTSLASH,/)][u(FUN`COLOR,%1,BORDER,repeat(-,16))][u(FUN`COLOR,%1,FRONTSLASH,/)][u(FUN`COLOR,%1,PSM,Tracks)][u(FUN`COLOR,%1,FRONTSLASH,/)][u(FUN`COLOR,%1,BORDER,repeat(-,11)\{)]%R[u(FUN`BORDER,%0,%1,align(23 23 23,u(FUN`FORMATPOOLS,%0,Pool),u(FUN`FORMATPOOLS,%0,Channel),u(FUN`FORMATPOOLS,%0,Track)))]};@nspemit %#=u(FUN`BORDER,%0,%1,Health: [iter(get(%0/D`HEALTH),switch(1,lte(inum(0),get(%0/D`HEALTH`AGGRAVATED)),ansi(hx,%i0*),lte(inum(0),get(%0/D`HEALTH`LETHAL)),ansi(hr,%i0X),lte(inum(0),get(%0/D`HEALTH`BASHING)),ansi(hc,%i0/),%i0))]);@nspemit %#=u(FUN`FOOTER,%0,%1)

&FUN`FORMATPOOLS u(ex-sso)=iter(u(u(ex-pso)/FUN`LISTPOOLS,%0,%1),rjust(%i0: [rjust(u(u(ex-pso)/FUN`CUR,%0,%i0),switch(%1,Pool,3,Channel,1,Track,2,3),0)]/[rjust(u(u(ex-pso)/FUN`MAX,%0,%i0),switch(%1,Pool,3,Channel,1,Track,2,3),0)],switch(%1,Pool,21,Channel,19,Track,19,21)),%B,%R)





&FUN`FOOTER u(ex-sso)=u(FUN`COLOR,%1,BORDER,|----------------------------------------------------------------------------|)%R[u(FUN`COLOR,%1,BORDER,\\)]%B[ansi(strfirstof(v(VAR`COLOR`%1`PSM),n),XP:)]%B[u(FUN`COLOR,%1,PSM,rjust(sub(setr(g,u(u(xp)/FUN`COUNT,%0,G)),u(u(xp)/FUN`COUNT,%0,S)),4,0))][u(FUN`COLOR,%1,BORDER,/)][u(FUN`COLOR,%1,PSM,rjust(%qg,4,0))][rjust(u(u(ex-sso)/FUN`GETSERIES,%1),61)]%B[u(FUN`COLOR,%1,BORDER,/)]%R%B[u(FUN`COLOR,%1,BORDER,'--------------------------------------------------------------------------')]

&FUN`GETSERIES u(ex-sso)=switch(%0,SOLAR,ansi(hy,Solars):%B[ansi(hy,The Lawgivers)],ABYSSAL,ansi(hx,Abyssals):%B[ansi(hx,The Deathknights)],INFERNAL,ansi(hg,Infernals):%B[ansi(hg,The Green Sun Princes)],LUNAR,ansi(hc,Lunars):%B[ansi(hc,The Stewards)],SIDEREAL,ansi(hm,Sidereals):%B[ansi(hm,The Viziers)],DRAGON-BLOODED,ansi(hr,Terrestrials):%B[ansi(hr,The Dragon-Blooded)],ALCHEMICAL,ansi(c,Alchemicals):%B[ansi(c,The Champions)],JADEBORN,ansi(y,Jadeborn),RAKSHA,ansi(m,Raksha):%B[ansi(m,The Fair Folk)],MORTAL,Mortals:%BThe Heroes,GHOST,Ghosts,DRAGON-KING,Dragon-Kings,GOD-BLOODED,ansi(h,GOD-BLOODED):%B[ansi(h,The Children of the Mighty)],Exalted 2.5e)


&VAR`COLOR`SOLAR`BORDER u(ex-sso)=y
&VAR`COLOR`SOLAR`FRONTSLASH u(ex-sso)=hr
&VAR`COLOR`SOLAR`HEADER u(ex-sso)=hy
&VAR`COLOR`SOLAR`CHARMHEADER u(ex-sso)=hy

&VAR`COLOR`LUNAR`BORDER u(ex-sso)=+lightblue1
&VAR`COLOR`LUNAR`FRONTSLASH u(ex-sso)=hb
&VAR`COLOR`LUNAR`HEADER u(ex-sso)=hc
&VAR`COLOR`LUNAR`CHARMHEADER u(ex-sso)=hc

&VAR`COLOR`INFERNAL`BORDER u(ex-sso)=g
&VAR`COLOR`INFERNAL`FRONTSLASH u(ex-sso)=y
&VAR`COLOR`INFERNAL`HEADER u(ex-sso)=hg
&VAR`COLOR`INFERNAL`CHARMHEADER u(ex-sso)=hg

&VAR`COLOR`ABYSSAL`BORDER u(ex-sso)=+gray18
&VAR`COLOR`ABYSSAL`FRONTSLASH u(ex-sso)=+grey93
&VAR`COLOR`ABYSSAL`HEADER u(ex-sso)=+red3
&VAR`COLOR`ABYSSAL`CHARMHEADER u(ex-sso)=+red3


&VAR`COLOR`SIDEREAL`BORDER u(ex-sso)=hm
&VAR`COLOR`SIDEREAL`FRONTSLASH u(ex-sso)=m
&VAR`COLOR`SIDEREAL`HEADER u(ex-sso)=hw
&VAR`COLOR`SIDEREAL`CHARMHEADER u(ex-sso)=hw