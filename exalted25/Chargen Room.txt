@@ DEPENDENCIES - CORE

@switch/inline isdbref(u(cg))=0,{@dig/teleport Chargen}
@switch/inline isdbref(u(cg))=0,{th attrib_set(u(coi)/cg,%l)}
@parent u(cg)=u(coi)
@set u(cg)=WIZARD !NO_COMMAND

&CMD`+PAGE u(cg)=$^\+page(?\: +(.+))?$:@assert strlen(%1)=@nspemit %#=msghead(CHARGEN) ERROR: +page requires a page to turn to. Your options are: [itemize(filterbool(#lambda/isnum(\%0),lattr(me/*,|),|,|),|,and,\,)];@assert valnum(%1)=@nspemit %#=msghead(CHARGEN) ERROR: Pages must be whole, positive numbers.;@assert match(filterbool(#lambda/isnum(\%0),lattr(me/*,|),|,|),%1,|)=@nspemit %#=msghead(CHARGEN) ERROR: Page not found!;th setstat(%#/D`CHARGEN,Page,%1);@force/inline %#=look;@nspemit %#=msghead(CHARGEN) You are now viewing Page %1!
@set here/CMD`+PAGE=regexp

@desc u(cg)=%TWelcome to [mudname()] Chargen! In this room you will setup your sheet and other aspects and prepare for admin review for approval. Remember to check out any House Rules at [weblink(mudurl(),mudurl())]%R%R%TTo see your sheet, type [ansi(h,+sheet)]%R%R[u(strfirstof(getstat(%#/D`CHARGEN,Page),1),getsplat(%#))]%R%R%TNote that many options such as Splat, Caste, Attribute, and Ability names support partial matching!%R%R%TYou are currently viewing Page [ansi(h,strfirstof(getstat(%#/D`CHARGEN,Page),1))] of [ansi(h,lmath(max,filterbool(#lambda/isnum(\%0),lattr(me/*))))]. To change pages, type [ansi(h,+page <number>)] (example: +page 2)%R%R%TIf you'd like to undo certain steps, use [ansi(h,+reset)] to view your options.

&1 u(cg)=ansi(h,+splat <type>)%R%TYour choices are: [itemize(get(u(ex-cdo)/VAR`SPLATS),|,and,\,)][if(strlen(getstat(%#/D`INFO,Splat)),%R%R[ansi(h,+caste <type>)] to set your [if(strmatch(getsplat(%#),Dragon-Blooded),Aspect,Caste)]%R%TYour choices are: [itemize(get(u(ex-cdo)/VAR`SPLATS`[getsplat(%#)]),|,and,\,)][if(hasattr(me/1`[getsplat(%#)]),%R%R[u(1`[getsplat(%#)])])])]%R%R[ansi(h,&sex me=<gender>)] to set your Gender (Male, Female, or None)%R%R[ansi(h,+xp)] to view starting XP.

&1`SOLAR u(cg)=ansi(h,+virtueflaw <flaw>) to set your Limit Break.
&1`LUNAR u(cg)=u(1`SOLAR)%R%R[ansi(h,+totem <animal>)] to set your Spirit Shape.
&1`ABYSSAL u(cg)=ansi(h,+flawedvirtue <flaw>) to set your Flawed Virtue (MoEP: Abyssals)%ROR%R[ansi(h,+doom <doom>)] to set your Doom (Shards hack)%R%R[ansi(h,+liege <name>)] to set your Liege (or Renegade)
&1`ALCHEMICAL u(cg)=ansi(h,+nation <name>)] to set your Nation.
&1`INFERNAL u(cg)=ansi(h,+yozi <name>) to set your Favored Yozi.%R%R[ansi(h,+urge <archetype>)] to set your Urge model.%R%R[ansi(h,+intimacy/urge <urge>)] to set its specifics.
&1`DRAGON-BLOODED u(cg)=ansi(h,+house <name>)] to set your dynastic house or equivalent.
&1`RAKSHA u(cg)=ansi(h,+lure <virtue and condition>)] to set your Lure.
&1`MORTAL u(cg)=Mortals have nothing race-specific to set!
&1`JADEBORn u(cg)=This covers both Enlightened and Unenlightened!

&CMD`+SPLAT u(cg)=$^\+splat(?\: +(.+))?$:@assert strlen(%1)=@nspemit %#=msghead(CHARGEN) What Splat will you be?;@include u(ccs)/INC`PARTIAL=%1,setdiff(get(u(ex-cdo)/VAR`SPLATS),GOD-BLOODED|JADEBORN|ALCHEMICAL|DRAGON-KING,|),|,CHARGEN,choice,Splat;th setstat(%#/D`INFO,Splat,capnames(%q<choice>));@force/inline %#=look;@nspemit %#=msghead(CHARGEN) You are now a [ansi(h,capnames(%q<choice>))].
@set here/CMD`+SPLAT=regexp

&CMD`+CASTE u(cg)=$^\+caste(?\: +(.+))?$:@assert match(get(u(ex-cdo)/VAR`SPLATS),setr(splat,getstat(%#/D`INFO,Splat)),|)=@nspemit %#=msghead(CHARGEN) ERROR: You do not have a valid Splat set! Please use +splat to set one.;@assert strlen(setr(castes,get(u(ex-cdo)/VAR`SPLATS`%q<splat>)))=@nspemit %#=msghead(CHARGEN) Your Splat has no Castes entered.;@assert strlen(%1)=@nspemit %#=msghead(CHARGEN) What Caste will you use?;@include u(CCS)/INC`PARTIAL=%1,%q<castes>,|,CHARGEN,choice,Caste;@nspemit %#=msghead(CHARGEN) Your Caste is now [ansi(h,capnames(%q<choice>))].;th setstat(%#/D`INFO,Caste,capnames(%q<choice>))
@set here/CMD`+CASTE=regexp

@@ &CMD`+POINTS u(cg)=$+points:@nspemit %#=%r%tHouse Rules grants you experience points in place of Bonus Points to flesh out your character with. To spend these points, use the chargen rooms to set your sheet to what it will be after spending the points. Keep a record of where you spend each point and enter it into an +info when you're finished- "+info/set XP=6 for Appearance 3>4\%R4 on Sail 1->2...." Etc. Formatting with newlines with \%R and categorized lists are really appreciated. You may find it useful to design your character in a text file or the like before entering them in chargen, too. If designing a God-Blooded, please be sure and put how Inheritance is spent in an Inheritance Info file!;@switch/inline gt(setr(diff,sub(u(u(wxc)/FUN`STARTING),u(u(xp)/FUN`COUNT,%#,G))),0)=0,{@nspemit %#=You have already been awarded your starting points!},1,{@force/inline get(u(wxc)/VAR`FORCECHAR)=+xp/award %#=%q<diff>/Starting %q<diff> Points}
@wipe u(cg)/CMD`+POINTS

&CMD`INFOSET u(cg)=$^\+(flawedvirtue|virtueflaw|doom|liege|lure|totem|urge|house|yozi|nation)(?\: +(.+))?$:th setq(type,switch(%1,flawedvirtue,Flawed Virtue,virtueflaw,Virtue Flaw,doom,Doom,liege,Liege,lure,Lure,totem,Totem Animal,urge,Urge,house,House,yozi,Favored Yozi,nation,Nation));@assert strlen(%2)=@nspemit %#=msghead(CHARGEN) What will your %q<type> be?;@nspemit %#=msghead(CHARGEN) Your %q<type> is now [ansi(h,%2)]. Make sure this is appropriate for your Race!;th setstat(%#/D`INFO,%q<type>,%2)
@set here/CMD`INFOSET=regexp

&2 u(cg)=%tTo set an attribute:%r%r[ansi(h,%t+attr <attribute>=<value>)] (<attribute> may be a partial match. <value> must be 0 or greater.)%R%TYour choices are: [itemize(get(u(ex-cdo)/VAR`ATTRIBUTES),|,and,\,)]%R%R%TAll Attributes begin with 1 dot free, unless otherwise stated for your Splat.%r%r%tIn order of Primary/Secondary/Tertiary...[udefault(2`[getsplat(%#)],Your Splat appears unset or has no data entered.)]

&2`ABYSSAL u(cg)=Abyssals receive 8/6/4 Attribute points.
&2`ALCHEMICAL u(cg)=Alchemicals receive 8 Attribute Points for their Caste Attributes, minimum 2 each, 6 Attribute points in their Favored Attributes, and 4 Attribute points to be used freely.%r%tIn addition they receive 1 extra point for a Caste or Favored Attribute.%r%r%t[ansi(h,+castefavattr)] to set your Caste Favored Attributes%r%t[ansi(h,+favattr <attribute>)] to set your Favored Attribute(s).%r%t[ansi(h,+unfavattr <attribute>)] to unset your Favored Attribute(s).
&2`DRAGON-BLOODED u(cg)=Dragon-Blooded receive 8/6/4 Attribute Points.
&2`DRAGON-KING u(cg)=Modern Dragon-Kings receive 7/5/4.%r%tAncient Dragon-Kings receive 8/6/5.
&2`GHOST u(cg)=Ghosts receive 6/4/3 Attribute points. If converting from a living character, they retain existing ratings, and lose any Favored Attributes if they posessed them.
&2`GOD-BLOODED u(cg)=God-Blooded receive 6/5/3 Attribute points. Half-Castes add 1 additional point wherever they wish.
&2`INFERNAL u(cg)=Infernals receive 8/6/4 Attribute points.
&2`JADEBORN u(cg)=Enlightened Jadeborn receive 16/13/10, mininum 3 in each Attribute, maximum 7.%r%tUnenlightened Jadeborn receive 8/4/3 Attribute Points.
&2`LUNAR u(cg)=Lunars receive 8/6/4 Attribute points, and may add 1 additional point to any Caste or Favored Attribute.%r%tTattooed Lunars receive 3 Caste Attributes and 1 Favored Attribute. Casteless Receive only 2 Favored Attributes.%r%r%t[ansi(h,+castefavattr)] to set your Caste Favored Attributes%r%t[ansi(h,+favattr <attribute>)] to set your Favored Attribute(s).%r%t[ansi(h,+unfavattr <attribute>)] to unset your Favored Attribute(s).
&2`MORTAL u(cg)=Mortals receive 8/6/4 Attribute points.
&2`RAKSHA u(cg)=Noble Raksha receive 16/13/10, mininum 3 in each Attribute, maximum 7.%r%tCommoner Raksha begin with 0 in each Attribute, receive 10/7/4, and may not raise them above 4.
&2`SIDEREAL u(cg)=Sidereals receive 8/6/4 Attribute points.
&2`SOLAR u(cg)=Solars receive 8/6/4 Attribute points.
&2`SPIRIT u(cg)=Spirits usually receive (Essence x3) + (Cult x3) + (Virtues x2) Attribute points. Please consult with staff if designing a Spirit!

&CMD`+ATTR u(cg)=$^\+attr(?\: +(.+?))?(?\:=(.+))?$:@assert strlen(%1)=@nspemit %#=msghead(CHARGEN) What Attribute will you change?;@include u(ccs)/INC`PARTIAL=%1,get(u(ex-cdo)/VAR`ATTRIBUTES),|,CHARGEN,choice,Attribute;@assert and(not(lt(%2,0)),isint(%2))=@nspemit %#=msghead(CHARGEN) ERROR: Attributes must be whole numbers 0 or greater!;@nspemit %#=msghead(CHARGEN) You set [ansi(h,capnames(%q<choice>))] to %2.;th setstat(%#/D`ATTRIBUTES,capnames(%q<choice>),%2)
@set here/CMD`+ATTR=regexp

&CMD`+CASTEFAVATTR u(cg)=$+castefavattr:@assert strlen(getstat(%#/D`INFO,Caste))=@nspemit %#=msghead(CHARGEN) You do not have a Caste set!;@assert match(ALCHEMICAL|LUNAR,getsplat(%#),|)=@nspemit %#=msghead(CHARGEN) Only Alchemical and Lunar Exalted may favor Attributes.;@assert strlen(setr(newfavored,v(DAT`2`FAVORED`[edit(getstat(%#/D`INFO,Caste),%B,_)])))=@nspemit %#=msghead(CHARGEN) Your Caste does not have any listed Favored Attributes.;@nspemit %#=msghead(CHARGEN) You set your Favored Attributes to those of the [ansi(h,getstat(%#/D`INFO,Caste))] Caste.;th attrib_set(%#/D`ATTRIBUTES`FAVORED,setunion(get(%#/D`ATTRIBUTES`FAVORED),%q<newfavored>,|,|))

&CMD`+FAVATTR u(cg)=$^\+(un)?favattr(?\: +(.+))?$:@assert strln(%2)=@nspemit %#=msghead(CHARGEN) Which Attribute will you [if(strlen(%1),un-Favor,Favor)]?;@include u(CCS)/INC`PARTIAL=%2,get(u(ex-cdo)/VAR`ATTRIBUTES),|,CHARGEN,choice,Attribute;@switch/inline t(strlen(%1))=1,{@assert match(get(%#/D`ATTRIBUTES`FAVORED),%q<choice>,|)=@nspemit %#=msghead(CHARGEN) That Attribute is not Favored!};th attrib_set(%#/D`ATTRIBUTES`FAVORED,setr(new,set[if(strlen(%1),diff,union)](get(%#/D`ATTRIBUTES`FAVORED),capnames(%q<choice>),|,|)));@nspemit %#=msghead(CHARGEN) You now have [ansi(h,if(words(%q<new>,|),words(%q<new>,|),No))] Favored Attributes[if(words(%q<new>,|),:%B[ansi(h,itemize(%q<new>,|,and,\,))])]
@set here/CMD`+FAVATTR=regexp

&DAT`2`FAVORED`ADAMANT u(cg)=Strength|Appearance|Perception
&DAT`2`FAVORED`CHANGING_MOON u(cg)=Charisma|Manipulation|Appearance
&DAT`2`FAVORED`FULL_MOON u(cg)=Strength|Dexterity|Stamina
&DAT`2`FAVORED`HALF_MOON u(cg)=Charisma|Perception|Intelligence
&DAT`2`FAVORED`JADE u(cg)=Stamina|Charisma|Wits
&DAT`2`FAVORED`MOONSILVER u(cg)=Dexterity|Appearance|Wits
&DAT`2`FAVORED`NO_MOON u(cg)=Perception|Intelligence|Wits
&DAT`2`FAVORED`ORICHALCUM u(cg)=Strength|Charisma|Intelligence
&DAT`2`FAVORED`SOULSTEEL u(cg)=Stamina|Manipulation|Perception
&DAT`2`FAVORED`STARMETAL u(cg)=Dexterity|Manipulation|Intelligence
&DAT`2`FAVORED`WANING_MOON u(cg)=Dexterity|Manipulation|Wits
&DAT`2`FAVORED`WAXING_MOON u(cg)=Charisma|Manipulation|Appearance

&3 u(cg)=%t[ansi(h,+abil <ability>=<value>)] to set an Ability (<Ability> may be a partial match, <value> must be a number 0 or higher.)%r%t[ansi(h,+spec <ability or attribute>/<specialization>=<points>)] to set a Specialty.%R%TYour choices are: [itemize(get(u(ex-cdo)/VAR`ABILITIES),|,and,\,)]%r%r%tSetting an ability or specialization to 0 will remove it. Setting "Craft" Favored automatically favors all Crafts.[if(and(strlen(getsplat(%#)),hasattr(me/3`[getsplat(%#)])),%R%R%T[u(3`[getsplat(%#)])])]

&3`ABYSSAL u(cg)=Abyssals note their 5 Caste Abilities, and choose 5 Favored Abilities.%r%tAbyssals receive 28 ability points, minimum 10 among caste and favored, and receive 4 Specialty points.%r%r%t[ansi(h,+castefavored)] to set your Caste Abilities.%r%t[ansi(h,+favabil <Ability>)] to set an Ability as Favored.%r%t[ansi(h,+unfavabil <ability>)] to unset a Favored Ability.
&3`ALCHEMICAL u(cg)=Alchemicals receive 25 ability points and 4 Specialty points. In our House Rules they may also favor 3 Abilities.%r%t[ansi(h,+favabil <Ability>)] to set an Ability as Favored.%r%t[ansi(h,+unfavabil <ability>)] to unset a Favored Ability.
&3`DRAGON-BLOODED u(cg)=Dragon-Blooded note their 5 Aspect Abilities, and choose 3 Favored Abilities.%r%tOutcastes receive 25 Ability Points, while Lookshy and Dynasts receive 35. All Dragon-Blooded receive 4 Specialty points.%r%t[ansi(h,+heritage <Dynast/Lookshy/Outcaste>)] to set additional Ability info. A Dragon-Blood must have this many points in each heritage Ability.%r%r%t[ansi(h,+castefavored)] to set your Aspect Abilities.%r%t[ansi(h,+favabil <Ability>)] to set an Ability as Favored.%r%t[ansi(h,+unfavabil <ability>)] to unset a Favored Ability.
&3`DRAGON-KING u(cg)=Dragon-Kings receive 3 Breed Abilities, and choose 5 Favored Abilities.%r%tModern Dragon-Kings receive 25 and Ancient Dragon-Kings receive 35 Ability Points, minimum 10 among Breed or Favored Abilities.%r%r%t[ansi(h,+castefavored)] to set your Breed Abilities.%r%r%t[ansi(h,+favabil <Ability>)] to set an Ability as Favored.%r%t[ansi(h,+unfavabil <ability>)] to unset a Favored Ability.
&3`GHOST u(cg)=Ghosts receive 25 Ability points, and 1 Favored Ability. No Ability may exceed their Favored Ability.%r%tPlayer characters converting to Ghosts retain their old ratings, but not Caste/Favored/Auspicious Abilities, and must pick just 1.%r%t[ansi(h,+favabil <Ability>)] to set an Ability as Favored.%r%t[ansi(h,+unfavabil <ability>)] to unset a Favored Ability.
&3`GOD-BLOODED u(cg)=God-Blooded receive 25 Ability points, and 1 Favored Ability. No Ability may exceed their Favored Ability.%r%t[ansi(h,+favabil <Ability>)] to set an Ability as Favored.%r%t[ansi(h,+unfavabil <ability>)] to unset a Favored Ability.
&3`INFERNAL u(cg)=Infernals receive their 5 Caste Abilities, and choose 5 Favored Abilities.%r%tInfernals receive 28 ability points, minimum 10 among caste and favored, and receive 4 Specialty points.%r%r%t[ansi(h,+castefavored)] to set your Caste Abilities.%r%t[ansi(h,+favabil <Ability>)] to set an Ability as Favored.%r%t[ansi(h,+unfavabil <ability>)] to unset a Favored Ability.
&3`JADEBORN u(cg)=All Jadeborn receive Craft as Favored, and choose 5 other Favored Abilities.%r%tEnlightened Jadeborn receive 35 Ability points, minimum 10 among Favored.%r%tEnlightened Jadeborn may use Bonus XP to raise Abilities as high as 6 at Chargen.%r%tArtisans must have Awareness 1, Bureaucracy 2, Linguistics 2 (High Realm, Old Realm), Lore 2, Martial Arts or Melee 1, Occult 2, Performance or Presence 1, Socialize 2, and 2 points divided amongst Craft abilities.%r%r%tUnenlightened Jadeborn receive 22 Ability Points, minimum 14 among Favored and minimum 1 each.%r%r%t[ansi(h,+favabil <Ability>)] to set an Ability as Favored.%r%t[ansi(h,+unfavabil <ability>)] to unset a Favored Ability.
&3`LUNAR u(cg)=Lunars receive 25 ability points, and receive 4 Specialty points.%r%tLunars always Favor Survival, and may choose one additional Favored Ability.%r%tSilver Pact Lunars must have Survival 2, and at least point in Archery, Melee, Martial_Arts, or Thrown.%r%t[ansi(h,+favabil <Ability>)] to set an Ability as Favored.%r%t[ansi(h,+unfavabil <ability>)] to unset a Favored Ability.
&3`MAIN u(cg)=Please return to the first room and set your race!
&3`MORTAL u(cg)=Mortals receive 25 Ability points, and 1 Favored Ability. No Ability may exceed their Favored Ability.%r%t[ansi(h,+favabil <Ability>)] to set an Ability as Favored.%r%t[ansi(h,+unfavabil <ability>)] to unset a Favored Ability.
&3`RAKSHA u(cg)=Noble Raksha choose 5 Caste Abilities from the 10 associated with their Ascendant and Shadowed Castes, and may select 3 Additional Favored Abilities.%r%tThey receive 1 point in all Caste Abilities, and 2 point in all Favored Abilities free. They also receive 25 Ability points and 3 Specialty points to be used as they see fit.%r%t(Cannot use Chargen Ability points or Bonus XP to raise Favored Abilities above 5, Caste abilities above 4, or other Abilities above 3.)%r%r%tIn addition to Caste Abilities, Commoner Raksha may select 1 Favored Ability or convert a Caste Ability into a Favored Ability.%r%tThey receive 1 free point in each Caste Ability and 2 free points in their Favored Ability, non-cumulative if upgrading a Caste Ability. They also receive 25 Ability points and 5 Specialty points to be used as they see fit.%r%t(Cannot use Chargen Ability points or Bonus XP to raise Favored Abilities above 5, Caste abilities above 4, or other Abilities above 3.)%r%r%t[ljust(Diplomat:,14)] Linguistics, Occult, Ride, Socialize, Thrown%r%t[ljust(Entertainer:,14)] Investigation, Larceny, Medicine, Performance, Stealth%r%t[ljust(Warrior:,14)] Archery, Martial Arts, Melee, Presence, War%r%t[ljust(Worker:,14)] Bureaucracy, Craft, Integrity, Lore, Resistance%r%t[ljust(Guide:,14)] Athletics, Awareness, Dodge, Sail, Survival%r%r%tRaksha must set all Caste and Favored Abilities manually.%r%r%t[ansi(h,+favabil <Ability>)] to set an Ability as Favored.%r%t[ansi(h,+unfavabil <ability>)] to unset a Favored Ability.
&3`SIDEREAL u(cg)=Sidereals receive 5 Auspicious Abilities and 4 Favored Abilities.%r%tRonin Sidereals receive 25 Ability Points, minimum 10 among Auspicious and Favored Abilities, and 4 Specialty points.%r%tMembers of the Bureau of Destiny receive 35 Ability Points, minimum 15 among Auspicious and Favored Abilities, and 4 Specialty points..%r%r%tBureau of Destiny members require minimum ratings of the following by Caste due to training:%r%tALL must have Awareness 2, Bureaucracy 2, Linguistics 1 (Old Realm), Lore 3, Martial Arts 2, Occult 2, Socialize 1, Stealth 1, and at least 1 in Archery, Melee, or Thrown.%r%tChosen of Journeys must have Athletics 2, Linguistics 2 (Old Realm + an appropriate additional language), Resistance 2, Survival 2, and Ride or Sail 2%r%tChosen of Serenity must have Craft or Performance 2, Linguistics 2 (eloquence), Medicine 2, Presence 2, and Socialize 3%r%tChosen of Battles must have Archery or Melee 3, Athletics 2, Dodge 2, Presence 2, and War 2%r%tChosen of Secrets must have Awareness 2, Investigation 3, Larceny 2, Socialize 2, and Stealth 2%r%tChosen of Endings must have Awareness 2, Dodge 2, Integrity 2, Martial Arts 3, and Stealth 2%r%r%t[ansi(h,+castefavored)] to set your Auspicious Abilities.%r%t[ansi(h,+favabil <Ability>)] to set an Ability as Favored.%r%t[ansi(h,+unfavabil <ability>)] to unset a Favored Ability.
&3`SOLAR u(cg)=Solars receive 5 Caste Abilities and 5 Favored Abilities.%r%tSolars receive 28 ability points, minimum 10 among Caste and Favored Abilities, and receive 4 Specialty points.%r%r%t[ansi(h,+castefavored)] to set your Caste Abilities.%r%t[ansi(h,+favabil <Ability>)] to set an Ability as Favored.%r%t[ansi(h,+unfavabil <ability>)] to unset a Favored Ability.
&3`SPIRIT u(cg)=Please consult with staff if designing a Spirit!

&CMD`+ABIL u(cg)=$^\+abil(?\: +(.+?))?(?\:=(.+))?$:@assert strlen(%1)=@nspemit %#=msghead(CHARGEN) What Ability will you set? You have spent [ansi(h,lmath(add,iter(get(%#/D`ABILITIES),last(%i0,~),|)))] Ability dots!;@include u(ccs)/INC`PARTIAL=%1,get(u(ex-cdo)/VAR`ABILITIES),|,CHARGEN,choice,Ability;@assert and(not(lt(%2,0)),isint(%2))=@nspemit %#=msghead(CHARGEN) Abilities must be whole numbers 0 or greater!;th [if(%2,set,del)]stat(%#/D`ABILITIES,capnames(%q<choice>),%2);@nspemit %#=msghead(CHARGEN) [if(%2,You set [ansi(h,capnames(%q<choice>))] to %2,You remove [ansi(h,capnames(%q<choice>))])]. You have spent [ansi(h,lmath(add,iter(get(%#/D`ABILITIES),last(%i0,~),|)))] Ability dots!
@set here/CMD`+ABIL=regexp

&CMD`+CASTEFAVORED u(cg)=$+castefavored:@assert strlen(getstat(%#/D`INFO,Caste))=@nspemit %#=You do not have a Caste set!;@assert strlen(setr(newfavored,unique(iter(v(DAT`3`FAVORED`[edit(getstat(%#/D`INFO,Caste),%B,_)]),capnames(%i0),|,|),i,|,|)))=@nspemit %#=Your Caste does not have any listed Favored Abilities.;@nspemit %#=You set your Favored Abilities to those of the [ansi(h,getstat(%#/D`INFO,Caste))] Caste.;th attrib_set(%#/D`ABILITIES`FAVORED,setunion(get(%#/D`ABILITIES`FAVORED),%q<newfavored>,|,|))

&CMD`+FAVABIL u(cg)=$^\+(un)?favabil(?\: +(.+))?$:@assert strlen(%2)=@nspemit %#=msghead(CHARGEN) Which Ability will you [if(strlen(%1),un-Favor,Favor)]?;@include u(ccs)/INC`PARTIAL=%2,get(u(ex-cdo)/VAR`ABILITIES),|,CHARGEN,choice,Ability;@switch/inline t(strlen(%1))=1,{@assert match(get(%#/D`ABILITIES`FAVORED),%q<choice>,|)=@nspemit %#=msghead(CHARGEN) That Ability is not Favored!};th attrib_set(%#/D`ABILITIES`FAVORED,setr(new,set[if(strlen(%1),diff,union)](get(%#/D`ABILITIES`FAVORED),capnames(%q<choice>),|,|)));@nspemit %#=msghead(CHARGEN) You now have [ansi(h,if(words(%q<new>,|),words(%q<new>,|),No))] Favored Abilities[if(words(%q<new>,|),:%B[ansi(h,itemize(%q<new>,|,and,\,))])]
@set here/CMD`+FAVABIL=regexp

&CMD`+HERITAGE u(cg)=$^\+heritage(?\: +(.+))?$:@assert strlen(%1)=@nspemit %#=msghead(CHARGEN) Are you a Dynast, Lookshy, or Outcaste Dragon-Blooded?;@include u(ccs)/INC`PARTIAL=%1,DYNAST|LOOKSHY|OUTCASTE,|,CHARGEN,choice,Heritage;@switch/inline %q<choice>=dynast,{@nspemit %#=msghead(CHARGEN) You set your base skills as a Dynast-raised Dragon-blood. You have 35 points in total to spend, but you cannot lower these base skills below their current level, and you must spend at least 13 points in Favored abilities. If you don't have at least 2 dots in each of these baseline skills, you're likely considered embarassingly inept by your family.;@cpattr me/DAT`HERITAGE_DYNAST=%#/D`ABILITIES;th setstat(%#/D`INFO,Heritage,capnames(%q<choice>))},lookshy,{@nspemit %#=msghead(CHARGEN) You set your base skills as a Lookshy-raised Dragon-blood. You have 35 points in total to spend, but you must spend at least 13 on favored skills and you cannot lower the current skills below their present level. What "+sheet" gives now is a minimally-competent Lookshy Dragonblood.;@cpattr me/DAT`HERITAGE_LOOKSHY=%#/D`ABILITIES;th setstat(%#/D`INFO,Heritage,capnames(%1))},outcaste,{@nspemit %#=msghead(CHARGEN) You mark yourself as a Dragon-blood raised outside of the conventional structures of the Realm or Lookshy. While this gives you more freedom in how to spend your ability points, it also gives you fewer points to spend. You have 25 ability points and must spend 13 of them on Favored abilities.;&d`abilities %#;th setstat(%#/D`INFO,Heritage,capnames(%q<choice>))},{@nspemit %#=msghead(CHARGEN) Your choices for Dragon-Blooded heritage are Dynast, Lookshy, and Outcaste.}
@set here/CMD`+HERITAGE=regexp

&CMD`+SPEC u(cg)=$^\+spec(?\: +(.+?))?(?\:/(.+?))?(?\:=(.+))?$:@assert strlen(%1)=@nspemit %#=msghead(CHARGEN) What Ability or Attribute will you take a specialty in?;@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(ex-cdo)/VAR`ABILITIES),get(u(ex-cdo)/VAR`ATTRIBUTES),|,|),|,CHARGEN,abilchoice,Choice;@assert strlen(setr(specchoice,%2))=@nspemit %#=msghead(CHARGEN) What will the specialty be in?;@assert and(not(lt(%3,0)),isint(%3))=@nspemit %#=msghead(CHARGEN) Specialties must be whole numbers 0 or greater!;@nspemit %#=msghead(CHARGEN) [if(%3,You set [ansi(h,capnames(%q<abilchoice>)/%q<specchoice>)] to %3,You remove [ansi(h,capnames(%q<abilchoice>)/%q<specchoice>)])].;th [if(%3,set,del)]stat(%#/D`SPECIALTIES,capnames(%q<abilchoice>)/[trim(%q<specchoice>)],%3)
@set here/CMD`+SPEC=regexp

&DAT`3`FAVORED`AIR u(cg)=Linguistics|Lore|Occult|Stealth|Thrown
&DAT`3`FAVORED`ANKLOK u(cg)=Resistance|Survival|War
&DAT`3`FAVORED`BATTLES u(cg)=Archery|Athletics|Melee|Presence|War
&DAT`3`FAVORED`DAWN u(cg)=Archery|Martial_Arts|Melee|Thrown|War
&DAT`3`FAVORED`DAY u(cg)=Athletics|Awareness|Dodge|Larceny|Stealth
&DAT`3`FAVORED`DAYBREAK u(cg)=Craft|Investigation|Lore|Medicine|Occult
&DAT`3`FAVORED`DEFILER u(cg)=Craft|Investigation|Lore|Medicine|Occult
&DAT`3`FAVORED`DUSK u(cg)=Archery|Martial_Arts|Melee|Thrown|War
&DAT`3`FAVORED`EARTH u(cg)=Craft|Integrity|Resistance|War|Awareness
&DAT`3`FAVORED`ECLIPSE u(cg)=Bureaucracy|Linguistics|Ride|Sail|Socialize
&DAT`3`FAVORED`ENDINGS u(cg)=Awareness|Bureaucracy|Integrity|Martial_Arts|Medicine
&DAT`3`FAVORED`FIEND u(cg)=Bureaucracy|Linguistics|Ride|Sail|Socialize
&DAT`3`FAVORED`FIRE u(cg)=Athletics|Dodge|Melee|Presence|Socialize
&DAT`3`FAVORED`JADE u(cg)=Stamina|Charisma|Wits
&DAT`3`FAVORED`JOURNEYS u(cg)=Resistance|Ride|Sail|Survival|Thrown
&DAT`3`FAVORED`MALEFACTOR u(cg)=Integrity|Performance|Presence|Resistance|Survival
&DAT`3`FAVORED`MIDNIGHT u(cg)=Integrity|Performance|Presence|Resistance|Survival
&DAT`3`FAVORED`MOONSHADOW u(cg)=Bureaucracy|Linguistics|Ride|Sail|Socialize
&DAT`3`FAVORED`MOSOK u(cg)=Brawl|Larceny|Survival
&DAT`3`FAVORED`NIGHT u(cg)=Athletics|Awareness|Dodge|Larceny|Stealth
&DAT`3`FAVORED`PTEROK u(cg)=Athletics|Lore|Occult
&DAT`3`FAVORED`RAPTOK u(cg)=Craft|Socialize|Linguistics
&DAT`3`FAVORED`SCOURGE u(cg)=Athletics|Awareness|Dodge|Larceny|Stealth
&DAT`3`FAVORED`SECRETS u(cg)=Investigation|Larceny|Lore|Occult|Stealth
&DAT`3`FAVORED`SERENITY u(cg)=Craft|Dodge|Linguistics|Performance|Socialize
&DAT`3`FAVORED`SLAYER u(cg)=Archery|Martial_Arts|Melee|Thrown|War
&DAT`3`FAVORED`SOVEREIGN u(cg)=Integrity|Awareness|Bureaucracy|Presence|War
&DAT`3`FAVORED`TWILIGHT u(cg)=Craft|Investigation|Lore|Medicine|Occult
&DAT`3`FAVORED`WATER u(cg)=Bureaucracy|Investigation|Larceny|Martial_Arts|Sail
&DAT`3`FAVORED`WOOD u(cg)=Archery|Medicine|Performance|Ride|Survival
&DAT`3`FAVORED`ZENITH u(cg)=Integrity|Performance|Presence|Resistance|Survival
&DAT`HERITAGE_DYNAST u(cg)=Archery~1|Lore~2|Martial_Arts~1|Melee~1|Performance~1|Presence~1|Ride~1|Socialize~2|War~1
&DAT`HERITAGE_LOOKSHY u(cg)=Archery~2|Linguistics~3|Lore~2|Martial_Arts~2|Melee~2|Performance~1|Presence~1|Ride~1|Stealth~1|War~2

&DAT`3`FAVORED`QAESTOR u(cg)=Integrity|Investigation|Socialize|Larceny|Bureaucracy
&DAT`3`FAVORED`ECSTATIC u(cg)=Athletics|Craft|Dodge|Performance|Socialize
&DAT`3`FAVORED`MONOLITH u(cg)=Presence|Survival|Investigation|Athletics|Resistance
&DAT`3`FAVORED`JUGGERNAUT u(cg)=Athletics|Presence|Resistance|Integrity|Martial_Arts
&DAT`3`FAVORED`DELUGE u(cg)=Athletics|Survival|Sail|Investigation|Occult

&4 u(cg)=ansi(h,+addcharm <category>=<charm1>|<charm2>|<charm3>|etc) to add native Charms.%R[ansi(h,+delcharm <category>=<charm1|<charm2>|<charm3>|etc)] to delete native Charms.%RCategories for your character type's Native Charms: [itemize(get(u(ex-cdo)/VAR`CHARMS`[charmtype(%#)]),|,and,\,)]%R%R[ansi(h,+addocharm <splat>/<category>=<charm1>|<charm2>|<charm3>|etc)] to add Other Splat Charms.%R[ansi(h,+delocharm <splat>/<category>=<charm1|<charm2>|<charm3>|etc)] to delete Other Splat Charms.%R%R[ansi(h,+addmacharm <tier>/<slot number>=<charm1>|<charm2>|<charm3>|etc)] to add Martial Arts Charms.%R[ansi(h,+delmacharm <tier>/<slot number>=<charm1|<charm2>|<charm3>|etc)] to delete Martial Arts Charms.%R[ansi(h,+setma <tier>/<slot number>=<name>)] to set the name of the style in slot <number>.%R%R[ansi(h,+addspell <circle>=<spell1>|<spell2>|<spell3>|etc)] to add Sorcery or Necromancy spells.%R[ansi(h,+delspell <circle>=<spell1|<spell2>|<spell3>|etc)] to delete Sorcery or Necromancy spells.[if(and(strlen(getsplat(%#)),hasattr(me/4`[getsplat(%#)])),%R%R[u(4`[getsplat(%#)])])]%R%R[ansi(h,+addthaum <art or science>/<degree or procedure>=<name of procedure or degree>)] to take up a Thaumaturgical Degree or Procedure.%R[ansi(h,+delthaum <art or science>/<degree or procedure>=<name of procedure or degree>)] to remove a Thaumaturgical Degree or Procedure.%r(Please list all Degrees for accounting purposes!)

&4`ALCHEMICAL u(cg)=ansi(h,+addproto <tier>=<protocol1>|<protocol2>|<protocol3>|etc) to add Weaving Protocols.%R[ansi(h,+delproto <tier>=<protocol1>|<protocol2>|<protocol3>|etc)] to delete Weaving Protocols.%R%R[ansi(h,+slots <type>=<number>)] to set Charm Slots.

&4`DRAGON-KING u(cg)=ansi(h,+breedfavpath) to set your Breed Favored Paths%r[ansi(h,+path <path>=<value>)] to set how your Path points.%r%tYour choices are: [itemize(get(u(ex-cdo)/VAR`PATHS),|,and,\,)]%r%R[ansi(h,+favpath <path>)] to set a Favored Path%r[ansi(h,+unfavpath <path>)] to remove a Favored Path

&4`SIDEREAL u(cg)=ansi(h,+maidenfavcoll) to set your Caste Colleges.%R%T[ansi(h,+college <college>=<value>)] to set Astrology.%r%tYour choices are: [itemize(get(u(ex-cdo)/VAR`COLLEGES),|,and,\,)]

&4`SPIRIT u(cg)=Please consult with staff if designing a Spirit!

&CMD`+CASTEFAVOREDBREEDMAIDEN u(cg)=$^\+(breedfavpath|maidenfavcoll)$:@assert strlen(getstat(%#/D`INFO,Caste))=@nspemit %#=msghead(CHARGEN) You do not have a Caste set!;@assert strlen(setr(newfavored,v(DAT`4`FAVORED`[if(strmatch(%1,breedfavpath),PATHS,COLLEGES)]`[edit(getstat(%#/D`INFO,Caste),%B,_)])))=@nspemit %#=msghead(CHARGEN) Your Caste does not have any listed Favored [if(strmatch(%1,breedfavpath),Paths,Colleges)].;@nspemit %#=msghead(CHARGEN) You set your Favored [if(strmatch(%1,breedfavpath),Paths,Colleges)] to those of the [ansi(h,getstat(%#/D`INFO,Caste))] Caste.;@cpattr me/D`[if(strmatch(%1,breedfavpath),PATHS,COLLEGES)]_FAVORED=%#/D`[if(strmatch(%1,breedfavpath),PATHS,COLLEGES)]_FAVORED

&CMD`+CHARM u(cg)=$^\+(add|del)charm(?\: +(.+?))?(?\:=(.+?))?$:@include me/INC`%1CHARM=%2,%3,charmtype(%#)
@set here/CMD`+CHARM=regexp
&CMD`+OCHARM u(cg)=$^\+(add|del)ocharm(?\: +(.+?))?(?\:/(.+?))?(?\:=(.+?))?$:@include u(ccs)/INC`PARTIAL=%2,get(u(ex-cdo)/VAR`SPLATS),|,EDITCHAR,splat,Splat;@include me/INC`%1CHARM=%3,%4,%q<splat>
@set here/CMD`+OCHARM=regexp

&INC`BEGCHARM u(cg)=@assert strlen(get(u(ex-cdo)/VAR`CHARMS`%2))=@nspemit %#=msghead(CHARGEN) ERROR: That Splat does not have Charms!;@assert strlen(%0)=@nspemit %#=msghead(CHARGEN) ERROR: Charm Category field empty.;@include u(ccs)/INC`PARTIAL=%0,get(u(ex-cdo)/VAR`CHARMS`%2),|,EDITCHAR,cat,category;@assert strlen(%1)=@nspemit %#=msghead(CHARGEN) ERROR: No Charms entered to Add.;th setq(charms,iter(%1,squish(trim(%i0)),|,|))

&INC`ADDCHARM u(cg)=@include u(cg)/INC`BEGCHARM;@break words(setinter(%q<charms>,get(%#/D`CHARMS`%2`%q<cat>),|,i,|),|)=@nspemit %#=msghead(CHARGEN) ERROR: You already have %2 %q<cat> Charm(s): [itemize(setinter(%q<charms>,get(%#/D`CHARMS`%2`%q<cat>),|,i,|),|,and,\,)];&D`CHARMS`%2`%q<cat> %#=iter(setunion(%q<charms>,get(%#/D`CHARMS`%2`%q<cat>),|,i,|),capnames(%i0),|,|);@nspemit %#=msghead(CHARGEN) You now have the [capnames(%2)] [capnames(%q<cat>)] Charm(s): [ansi(h,itemize(iter(%q<charms>,capnames(%i0),|,|),|,and,\,))]

&INC`DELCHARM u(cg)=@include u(cg)/INC`BEGCHARM;@assert eq(words(%q<charms>,|),words(setinter(%q<charms>,get(%#/D`CHARMS`%2`%q<cat>),|,i,|),|))=@nspemit %#=msghead(CHARGEN) ERROR: You do not have %2 %q<cat> Charm(s): [itemize(setdiff(%q<charms>,get(%#/D`CHARMS`%2`%q<cat>),|,i,|),|,and,\,)];&D`CHARMS`%2`%q<cat> %#=iter(setdiff(get(%#/D`CHARMS`%2`%q<cat>),%q<charms>,|,i,|),capnames(%i0),|,|);@nspemit %#=msghead(CHARGEN) You no longer have the [capnames(%2)] [capnames(%q<cat>)] Charm(s): [ansi(h,itemize(iter(%q<charms>,capnames(%i0),|,|),|,and,\,))]

&CMD`+FAVPATHCOLLEGE u(cg)=$^\+(un)?fav(path|college)(?\: +(.+))?$:@assert strlen(%3)=@nspemit %#=msghead(CHARGEN) Which [capnames(%2)] will you [if(strlen(%2),un-Favor,Favor)]?;@include u(ccs)/INC`PARTIAL=%3,get(u(ex-cdo)/VAR`[if(strmatch(%1,path),PATHS,COLLEGES)]),|,CHARGEN,choice,%2;@switch/inline gt(strlen(%1),0)=1,{@assert match(get(%#/D`[if(strmatch(%2,path),PATHS,COLLEGES)]_FAVORED),%q<choice>,|)=@nspemit %#=msghead(CHARGEN) That [capnames(%2)] is not Favored!};th attrib_set(%#/D`[if(strmatch(%2,path),PATHS,COLLEGES)]_FAVORED,setr(new,set[if(strlen(%1),diff,union)](get(%#/D`[if(strmatch(%2,path),PATHS,COLLEGES)]_FAVORED),%q<choice>,|,|)));@nspemit %#=msghead(CHARGEN) You now have [ansi(h,if(words(%q<new>,|),words(%q<new>,|),No))] Favored [capnames(%2)]s[if(words(%q<new>,|),:%B[ansi(h,itemize(%q<new>,|,and,\,))])]
@set here/CMD`+FAVPATHCOLLEGE=regexp

&CMD`+MAGIC u(cg)=$^\+(add|del)(spell|proto)(?\: +(.+?))?(?\:=(.+?))?$:@include me/INC`%1MAGIC=%2,%3,%4
@set here/CMD`+MAGIC=regexp

&INC`BEGMAGIC u(cg)=@nspemit %#=msghead(CHARGEN) ERROR: Spell Category field empty.;@include u(ccs)/INC`PARTIAL=%1,get(u(ex-cdo)/VAR`[switch(%0,proto,Protocols,spell,Spells)]),|,EDITCHAR,cat,category;@assert strlen(%2)=@nspemit %#=msghead(CHARGEN) ERROR: No [switch(%0,proto,Protocols,spell,Spells)] entered to Add.;th setq(spells,iter(%2,squish(%i0),|,|))

&INC`ADDMAGIC u(cg)=@include u(cg)/INC`BEGMAGIC;@break words(setinter(%q<spells>,get(%#/D`[switch(%0,proto,Protocols,spell,Spells)]`%q<cat>),|,i,|),|)=@nspemit %#=msghead(CHARGEN) ERROR: You already have %q<cat> [switch(%0,proto,Protocols,spell,Spells)]: [itemize(setinter(%q<spells>,get(%#/D`[switch(%0,proto,Protocols,spell,Spells)]`%q<cat>),|,i,|),|,and,\,)];&D`[switch(%0,proto,Protocols,spell,Spells)]`%q<cat> %#=iter(setunion(%q<spells>,get(%#/D`[switch(%0,proto,Protocols,spell,Spells)]`%q<cat>),|,i,|),capnames(%i0),|,|);@nspemit %#=msghead(CHARGEN) You now have the [capnames(%q<cat>)] [switch(%0,proto,Protocols,spell,Spells)]: [ansi(h,itemize(iter(%q<spells>,capnames(%i0),|,|),|,and,\,))]

&INC`DELMAGIC u(cg)=@include u(cg)/INC`BEGMAGIC;@assert eq(words(%q<spells>,|),words(setinter(%q<spells>,get(%#/D`[switch(%0,proto,Protocols,spell,Spells)]`%q<cat>),|,i,|),|))=@nspemit %#=msghead(CHARGEN) ERROR: You do not have %q<cat> [switch(%0,proto,Protocols,spell,Spells)]: [itemize(setdiff(%q<spells>,get(%#/D`[switch(%0,proto,Protocols,spell,Spells)]`%q<cat>),|,i,|),|,and,\,)];&D`SPELLS`%q<cat> %#=iter(setdiff(get(%#/D`[switch(%0,proto,Protocols,spell,Spells)]`%q<cat>),%q<spells>,|,i,|),capnames(%i0),|,|);@nspemit %#=msghead(CHARGEN) You no longer have the [capnames(%q<cat>)] [switch(%0,proto,Protocols,spell,Spells)]: [ansi(h,itemize(iter(%q<spells>,capnames(%i0),|,|),|,and,\,))]

&CMD`+MACHARM u(cg)=$^\+(add|del)macharm(?\: +(.+?))?(?\:/(.+?))?(?\:=(.+?))?$:@include me/INC`%1MACHARM=%2,%3,%4
@set here/CMD`+MACHARM=regexp

&INC`BEGMACHARM u(cg)=@assert strlen(%0)=@nspemit %#=msghead(CHARGEN) ERROR: No Tier entered.;@include u(ccs)/INC`PARTIAL=%0,TERRESTRIAL|CELESTIAL|SIDEREAL,|,EDITCHAR,tier,tier;@assert strlen(%1)=@nspemit %#=msghead(CHARGEN) ERROR: No Style ID entered.;@assert valnum(%1)=@nspemit %#=msghead(CHARGEN) ERROR: Martial Arts Styles must be addressed by their slot IDs.;@assert strlen(%2)=@nspemit %#=msghead(CHARGEN) ERROR: Charms field empty.;th setq(charms,iter(%2,squish(trim(%i0)),|,|))

&INC`ADDMACHARM u(cg)=@include u(cg)/INC`BEGMACHARM;@break words(setinter(%q<charms>,get(%#/D`CHARMS`%q<tier>_MARTIAL_ARTS`%1),|,i,|),|)=@nspemit %#=msghead(CHARGEN) ERROR: You already have [default(%#/D`CHARMS`%q<tier>_MARTIAL_ARTS`%1`NAME,Unnamed Style)] [capnames(%q<tier>)] Charm(s): [itemize(setinter(%q<charms>,get(%#/D`CHARMS`%q<tier>_MARTIAL_ARTS`%1),|,i,|),|,and,\,)];&D`CHARMS`%q<tier>_MARTIAL_ARTS`%1 %#=iter(setunion(%q<charms>,get(%#/D`CHARMS`%q<tier>_MARTIAL_ARTS`%1),|,i,|),capnames(%i0),|,|);@nspemit %#=msghead(CHARGEN) You now have the [default(%0/D`CHARMS`%q<tier>_MARTIAL_ARTS`%1`NAME,Unnamed Style)] [capnames(%q<tier>)] Charm(s): [ansi(h,itemize(iter(%q<charms>,capnames(%i0),|,|),|,and,\,))]

&INC`DELMACHARM u(cg)=@include u(cg)/INC`BEGMACHARM;@assert eq(words(%q<charms>,|),words(setinter(%q<charms>,get(%#/D`CHARMS`%q<tier>_MARTIAL_ARTS`%1),|,i,|),|))=@nspemit %#=msghead(CHARGEN) ERROR: You already have [default(%#/D`CHARMS`%q<tier>_MARTIAL_ARTS`%1`NAME,Unnamed Style)] [capnames(%q<tier>)] Charm(s): [itemize(setdiff(%q<charms>,get(%#/D`CHARMS`%q<tier>_MARTIAL_ARTS`%1),|,i,|),|,and,\,)];&D`CHARMS`%q<tier>_MARTIAL_ARTS`%1 %#=iter(setdiff(get(%#/D`CHARMS`%q<tier>_MARTIAL_ARTS`%1),%q<charms>,|,i,|),capnames(%i0),|,|);@nspemit %#=msghead(CHARGEN) You no longer have the [default(%0/D`CHARMS`%q<tier>_MARTIAL_ARTS`%1`NAME,Unnamed Style)] [capnames(%q<tier>)] Charm(s): [ansi(h,itemize(iter(%q<charms>,capnames(%i0),|,|),|,and,\,))]

&CMD`+SETMA u(cg)=$^\+setma(?\: +(.+?))?(?\:/(.+?))?(?\:=(.+?))?$:@assert strlen(%1)=@nspemit %#=msghead(CHARGEN) ERROR: No Tier entered.;@include u(ccs)/INC`PARTIAL=%1,TERRESTRIAL|CELESTIAL|SIDEREAL,|,EDITCHAR,tier,tier;@assert strlen(%2)=@nspemit %#=msghead(CHARGEN) ERROR: No Style ID entered.;@assert valnum(%2)=@nspemit %#=msghead(CHARGEN) ERROR: Martial Arts Styles must be addressed by their slot IDs.;@assert strlen(get(%#/D`CHARMS`%q<tier>_MARTIAL_ARTS`%2))=@nspemit %#=msghead(CHARGEN) ERROR: You have no style by that ID.;@assert strlen(%3)=@nspemit %#=msghead(CHARGEN) ERROR: Style name field empty.;@nspemit %#=msghead(CHARGEN) Your [numth(%2)] [ansi(h,%q<tier>)] Martial Arts Style is now called [ansi(h,%3)];&D`CHARMS`%q<tier>_MARTIAL_ARTS`%2`NAME %#=%3
@set here/CMD`+SETMA=regexp

&CMD`+PATHCOLLEGE u(cg)=$^\+(path|college)(?\: +(.+?))?(?\:=(.+?))?$:@assert strlen(%2)=@nspemit %#=msghead(CHARGEN) What [capnames(%1)] will you set?;@include u(ccs)/INC`PARTIAL=%2,get(u(ex-cdo)/VAR`%1S),|,CHARGEN,choice,capnames(%1);@assert and(not(lt(%3,0)),isint(%3))=@nspemit %#=msghead(CHARGEN) [capnames(%1)]s must be whole numbers 0 or greater!;@nspemit %#=msghead(CHARGEN) [if(%3,You set [ansi(h,capnames(%q<choice>))] to %3,You remove [ansi(h,capnames(%q<choice>))])].;th [if(%3,set,del)]stat(%#/D`%1S,capnames(%q<choice>),%3)
@set here/CMD`+PATHCOLLEGE=regexp

&CMD`+SLOTS u(cg)=$^\+slots(?\: +(.+?))?(?\:=(.+?))?$:@assert strlen(%1)=@nspemit %#=msghead(CHARGEN) What Slot will you set?;@include u(ccs)/INC`PARTIAL=%1,get(u(ex-cdo)/VAR`SLOTS),|,CHARGEN,choice,Slot;@assert and(not(lt(%2,0)),isint(%2))=@nspemit %#=msghead(CHARGEN) Slots must be whole numbers 0 or greater!;@nspemit %#=msghead(CHARGEN) [if(%2,You set [ansi(h,capnames(%q<choice>))] to %2,You remove [ansi(h,capnames(%q<choice>))])];th [if(%2,set,del)]stat(%#/D`CHARM_SLOTS,capnames(%q<choice>),%2)
@set here/CMD`+SLOTS=regexp

&CMD`+THAUM u(cg)=$^\+(add|del)thaum(?\: +(.+?))?(?\:/(.+?))?(?\:=(.+?))?$:@include me/INC`%1THAUM=%2,%3,%4
@set here/CMD`+THAUM=regexp

&INC`BEGTHAUM u(cg)=@assert strlen(%0)=@nspemit %#=msghead(CHARGEN) ERROR: Practice (Arts or Sciences) field empty.;@include u(ccs)/INC`PARTIAL=%0,ARTS|SCIENCES,|,EDITCHAR,practice,Practice;@assert strlen(%1)=@nspemit %#=msghead(CHARGEN) ERROR: Type (Degrees or Procedures) Field empty.;@include u(ccs)/INC`PARTIAL=%1,PROCEDURES|DEGREES,|,EDITCHAR,type,Type;th setq(thaum,iter(%2,squish(trim(%i0)),|,|))

&INC`ADDTHAUM u(cg)=@include u(Cg)/INC`BEGTHAUM;@assert strlen(%2)=@nspemit %#=msghead(CHARGEN) ERROR: What Thaumaturgy will you add?;@break words(setinter(%2,get(%#/D`%q<practice>`%q<type>),|,i,|),|)=@nspemit %#=msghead(CHARGEN) ERROR: You already have [capnames(%q<practice> %q<type>)](s): [itemize(setinter(%2,get(%#/D`%q<practice>`%q<type>),|,i,|),|,and,\,)];&D`%q<practice>`%q<type> %#=iter(setunion(%2,get(%#/D`%q<practice>`%q<type>),|,i,|),capnames(%i0),|,|);@nspemit %#=msghead(CHARGEN) You now have the [capnames(%q<practice> %q<type>)](s): [ansi(h,itemize(iter(%2,capnames(%i0),|,|),|,and,\,))]

&INC`DELTHAUM u(cg)=@include u(Cg)/INC`BEGTHAUM;@assert strlen(%2)=@nspemit %#=msghead(CHARGEN) ERROR: What Thaumaturgy will you remove?;@assert eq(words(%2,|),words(setinter(%2,get(%#/D`%q<practice>`%q<type>),|,i,|),|))=@nspemit %#=msghead(CHARGEN) ERROR: You do not have [capnames(%q<practice> %q<type>)](s): [itemize(setdiff(%2,get(%#/D`%q<practice>`%q<type>),|,i,|),|,and,\,)];&D`%q<practice>`%q<type> %#=iter(setdiff(get(%#/D`%q<practice>`%q<type>),%2,|,i,|),capnames(%i0),|,|);@nspemit %#=msghead(CHARGEN) You no longer have the [capnames(%q<practice> %q<type>)](s): [ansi(h,itemize(iter(%2,capnames(%i0),|,|),|,and,\,))]

&CMD`+MAIDENFAVCOLL u(cg)=$+maidenfavcoll:@assert hasattr(u(cg)/DAT`4`FAVORED`COLLEGES`[setr(caste,getstat(%#/D`INFO,Caste))])=@nspemit %#=msghead(CHARGEN) ERROR: Your Caste has no In-House Colleges!;@nspemit %#=msghead(CHARGEN) You set your In-House Colleges to those of the Chosen of %q<caste>.;@cpattr u(cg)/DAT`4`FAVORED`COLLEGES`%q<caste>=%#/D`COLLEGES`FAVORED

&DAT`4`FAVORED`COLLEGES`BATTLES u(cg)=The_Banner|The_Gauntlet|The_Quiver|The_Shield|The_Spear
&DAT`4`FAVORED`COLLEGES`ENDINGS u(cg)=The_Corpse|The_Crow|The_Haywain|The_Rising_Smoke|The_Sword
&DAT`4`FAVORED`COLLEGES`JOURNEYS u(cg)=The_Captain|The_Gull|The_Mast|The_Messenger|The_Ship's_Wheel
&DAT`4`FAVORED`COLLEGES`SECRETS u(cg)=The_Guardians|The_Key|The_Mask|The_Sorcerer|The_Treasure_Trove
&DAT`4`FAVORED`COLLEGES`SERENITY u(cg)=The_Ewer|The_Lovers|The_Musician|The_Peacok|The_Pillar

&DAT`4`FAVORED`PATHS`ANKLOK u(cg)=Blazing_Fire|Flickering_Fire
&DAT`4`FAVORED`PATHS`MOSOK u(cg)=Flowing_Water|Shimmering_Water
&DAT`4`FAVORED`PATHS`PTEROK u(cg)=Celestial_Air|Clear_Air
&DAT`4`FAVORED`PATHS`RAPTOK u(cg)=Yielding_Earth|Growing_Wood

&5 u(cg)=You're almost done! Commands for this room are:%r%r[ansi(h,+background <background>=<value>)] to set a background%r%r[ansi(h,+virtue <Valor/Compassion/etc.>=<value>)] to set a virtue.%r[ansi(h,+grace <Cup/Way/Heart/etc.>=<value>)] to set a Grace.%r%r[ansi(h,+merit <merit>=<value>)] to set a merit%r[ansi(h,+flaw <flaw>=<value>)] to set a flaw%r%r[ansi(h,+posmutation <mutation>=<value>)] to set a Positive Mutation.%r[ansi(h,+neumutation <mutation>=<value>)] to set a Neutral mutation.%r[ansi(h,+negmutation <mutation>=<value>)] to set a Negative Mutation.[if(match(get(%#/D`CHARMS`LUNAR`KNACKS),Deadly Beastman Transformation,|),%r[ansi(h,+warmutation <mutation>=<value>)] to set a Warform Mutation)][if(match(get(%#/D`CHARMS`INFERNAL`MALFEAS),By Rage Recast,|),%R[ansi(h,+ragemutation <mutation>=<value>)] to set a By Rage Recast Mutation)]%r%r[ansi(h,+wp <value>)] to set your willpower.%r[ansi(h,+ess <value>)] to set your permanent essence.%r%r[ansi(h,+addlang <language>)] to add a known language.%r[ansi(h,+dellang <language>)] to remove a known language.%r%r[ansi(h,+hl <Health level list>)] to set your health levels to match what you've selected for Ox-Body Technique and equivalents, Mutations, etc. Lacking such, one would enter "+hl -0 -1 -1 -2 -2 -4 I" with a number of additional D health levels on the end equal to stamina. Those with it would add extra -0, -1, and -2 levels as they had chosen.%r%r[ansi(h,+help +intimacy)] for instructions on setting your Intimacies, Motivation, and Urge if applicable.%r%rTo remove a background, merit, flaw, or similar, set it to zero.%R%RPlease use the BP-based Mutation values (XP cost divided by 2 - Wings would cost 6BP as an Abomination, and are 12xp) for Mutations as sometimes the code must use them. For instance, Essence Abundance, or tallying up Warform mutations.%R%RCheck [ansi(h,+help +info)] to see how to add additional information - you should make a file for each relevant Background such as Artifacts and Resources, a suitable BGstory, an XP file detailing your chargen spending, and so on.%R%RWhen complete, paste the following command to your client: %r%t[ansi(h,+request/add apps/Finished with chargen!=Look me over please!)]


&CMD`+BACKGROUNDS u(cg)=$^\+(background|merit|flaw|posmutation|neumutation|negmutation|warmutation|ragemutation)(?\: (.+?))?(?\:=(.+?))?$:th setq(0,squish(%2),1,%3,2,switch(%1,background,backgrounds,merit,merits,flaw,flaws,posmutation,positive_mutations,neumutation,neutral_mutations,negmutation,negative_mutations,warmutation,warform_mutations,ragemutation,rage_mutations,ERROR),3,switch(%1,background,Background,merit,Merit,flaw,Flaw,posmutation,Positive Mutation,neumutation,Neutral Mutation,negmutation,Negative Mutation,warmutation,Warform Mutation,ragemutation,By Rage Recast Mutation,ERROR));@assert strlen(%q0)=@nspemit %#=msghead(CHARGEN) What %3 will you set?;@assert and(not(lt(%q1,0)),isint(%q1))=@nspemit %#=msghead(CHARGEN) %3 must be whole numbers 0 or greater!;@nspemit %#=msghead(CHARGEN) [if(%q1,You set your %q0 %q3 to %q1,You remove your %q0 %q3)].;th [if(%q1,set,del)]stat(%#/D`%q2,%q0,%q1)
@set here/CMD`+BACKGROUNDS=regexp

&CMD`+ESS u(cg)=$+ess *:@assert valnum(%0)=@nspemit %#=msghead(CHARGEN) That's not a valid value for Essence. Try something greater than 0.;th setstat(%#/D`INFO,Essence,%0);@nspemit %#=msghead(CHARGEN) You set your permanent Essence to %0.

&CMD`+HL u(cg)=$+hl *:@assert lmath(min,iter(%0,or(and(isint(%i0),not(gt(%i0,0))),regmatchi(%i0,^(I|D)$))))=@nspemit %#=msghead(CHARGEN) %0 contains an invalid value for health. Valid values are 0, negative whole numbers, I, and D.;&D`HEALTH %#=setr(sorthealth,sortby(u(ex-cdo)/FUN`HEALTHSORT,ucstr(%0)));@nspemit %#=msghead(CHARGEN) You set your health level progression to '[get(%#/D`HEALTH)]'. Don't forget your (stamina) dying Health Levels!

&CMD`+LANG u(cg)=$^\+(add|del)lang(?\: (.*))$:th setq(0,iter(%2,capnames(trim(%i0)),|,|));@switch strmatch(%1,add)=1,{@nspemit %#=msghead(CHARGEN) You add '%q0' to your list of known languages.;&D`LANGUAGES %#=setunion(get(%#/D`LANGUAGES),squish(trim(%q0)),|,|)},0,{@switch/inline t(match(get(%#/D`LANGUAGES),%q0,|))=1,{@nspemit %#=msghead(CHARGEN) You remove '%q0' from your list of known languages.;&D`LANGUAGES %#=setdiff(get(%#/D`LANGUAGES),%q0,|,|)},{@nspemit %#=msghead(CHARGEN) You don't seem to know that language. "+sheet" to check which languages you're presently listed as understanding.}}
@set here/CMD`+LANG=regexp



&CMD`+VIRTUES u(cg)=$^\+(virtue|Grace)(?\: (.+?))?(?\:=(.+?))?$:@assert strlen(%2)=@nspemit %#=msghead(CHARGEN) What [capnames(%1)] will you set?;@include u(ccs)/INC`PARTIAL=%2,get(u(ex-cdo)/VAR`[if(strmatch(%1,virtue),VIRTUES,GRACES)]),|,CHARGEN,choice,capnames(%1);@assert and(not(lt(%3,0)),isint(%3))=@nspemit %#=msghead(CHARGEN) [capnames(%1)]s must be whole numbers 0 or greater!;@nspemit %#=msghead(CHARGEN) [if(%3,You set [ansi(h,capnames(%q<choice>))] to %3,You remove [ansi(h,capnames(%q<choice>))])].;th [if(%3,set,del)]stat(%#/D`[capnames(%1)]S,capnames(%q<choice>),%3)
@set here/CMD`+VIRTUES=regexp

&CMD`+WP u(cg)=$+wp *:@assert valnum(%0)=@nspemit %#=msghead(CHARGEN) That's not a valid value for Willpower. Try something between 1 and 10.;th setstat(%#/D`INFO,Willpower,%0);@nspemit %#=msghead(CHARGEN) You set your Willpower to %0.

&CMD`+RESET u(cg)=$^\+reset(?\: +(.*))?$:@assert strlen(%1)=@nspemit %#=msghead(CHARGEN) What will you reset? Your options are: [itemize(ALL|SPLAT|ATTRIBUTES|ABILITIES|THAUMATURGY|PATHS|COLLEGES|CHARMS|SPELLS|BACKGROUNDS|MUTATIONS|VIRTUES|GRACES,|,and,\,)];@include u(ccs)/INC`PARTIAL=%1,ALL|SPLAT|ATTRIBUTES|ABILITIES|THAUMATURGY|PATHS|COLLEGES|CHARMS|SPELLS|BACKGROUNDS|MUTATIONS|VIRTUES|GRACES,|,CHARGEN,choice,choice;@switch/inline %q<choice>=ALL,{@include u(ccs)/INC`VERIFY={[ansi(hr,WARNING:)] This will remove ALL data entered on your character. You will have to start over entirely from scratch. This will even remove your +info files! Are you sure? enter +reset all again to verify!},RESET ALL,CHARGEN;@dolist/inline setdiff(lattr(%#/D`*),D`XP D`INFOFILE D`ACCOUNT D`IPLOG)=@wipe %#/%i0;@nspemit %#=msghead(CHARGEN) You have reset back to square one!;@break 1},SPLAT,{@wipe %#/D`INFO},THAUMATURGY,{@wipe %#/D`ARTS;@wipe %#/D`SCIENCES},MUTATIONS,{@wipe %#/D`*_MUTATIONS},{@wipe %#/D`%q<choice>};@nspemit %#=msghead(CHARGEN) You have reset your %q<choice>!
@set u(cg)/CMD`+RESET=regexp




