@@ DEPENDENCIES - Core
@switch/inline isdbref(u(ar))=0,{th attrib_set(u(coi)/AR,dig(Ancestor Room))}

&RFN`HEADER u(ar)=header(%0,%1,ROOM`BORDER,ROOM`BORDERDOT,ROOM`BORDERTEXT,%2)
&RFN`SEPARATOR u(ar)=separator(%0,%1,ROOM`BORDER,ROOM`BORDERDOT,ROOM`BORDERTEXT,%2)
&RFN`SUBHEADER u(ar)=subheader(%0,%1,ROOM`BORDER,ROOM`BORDERDOT,ROOM`BORDERTEXT,%2)

&NAMEFORMAT u(ar)=endtag(a)[u(RFN`HEADER,pueblize(moniker(%0)[if(cor(controls(%#,me),isadmin(%#)),%([num(%0)][flags(%0)]%))],look),1)]

&CONFORMAT u(ar)=u(FUN`CONFORMAT`EXTRA)[trim(if(setr(ply,words(lvplayers(me))),u(FUN`CONFORMAT`PLAYERS`[if(cand(strlen(setr(mode,v(VAR`MODE))),hasattrp(me/FUN`CONFORMAT`PLAYERS`%q<mode>)),%q<mode>,DEFAULT)])),%r)][if(words(lvthings(me)),if(words(%q<ply>),%r)[u(FUN`CONFORMAT`THINGS`[if(cand(strlen(setr(mode,v(VAR`MODE))),hasattrp(me/FUN`CONFORMAT`THINGS`%q<mode>)),%q<mode>,DEFAULT)])])][switch(type(me),THING,%r[u(RFN`HEADER)])]

&FUN`CONFORMAT`PLAYERS`DEFAULT u(ar)=u(RFN`SEPARATOR,Players)%R[align(19,Name)]%R[trim(iter(sort(lvplayers(me),namei),align(19,pueblize(ansi(custcolor(%#,ROOM`PLAYERNAME),name(%i0)),look [name(%i0)])),%b,%R),%r)]

&FUN`CONFORMAT`THINGS`DEFAULT u(ar)=u(RFN`SEPARATOR,Things)%R[align(40 28 8,Name,Owner,if(isadmin(%#),DBref,Enter))]%R[trim(iter(sort(lvthings(me),namei),align(40 28 8,pueblize(name(%i0),look [name(%i0)]),pueblize(name(owner(%i0)),+finger [name(owner(%i0))]),switch(isadmin(%#)[hasflag(%i0,ENTER_OK)],11,pueblize(ansi(hg,%i0),enter %i0),1*,ansi(hr,%i0),01,pueblize(ansi(hg,enter_ok),enter %i0),ansi(hr,can't))),%b,%R),%r)]

&FUN`CONFORMAT`EXTRA u(ar)=if(cor(nattr(me/VIEW`*),nattr(me/MUSIC`*)),separator(Extra)%R[iter(filterbool(#lambda/nattr(me/\%0`*),VIEW MUSIC),%T[u(FUN`EXTRA`%i0)],%b,%R)]%R)
&FUN`EXTRA`VIEW u(ar)=This room has views! Type [pueblize(ansi(h,views),views)] to list!
&FUN`EXTRA`MUSIC u(ar)=[ansi(hc,This room has a soundtrack! Type)] [pueblize(ansi(h,soundtrack),soundtrack)] [ansi(hc,to list!)]

&EXITFORMAT u(ar)=if(words(lvexits(me)),u(FUN`EXITFORMAT`[if(cand(strlen(setr(mode,v(VAR`MODE))),hasattrp(me/FUN`EXITFORMAT`%q<mode>)),%q<mode>,DEFAULT)]),u(RFN`HEADER))

&FUN`EXITFORMAT`DEFAULT u(ar)=u(RFN`SEPARATOR,Exits)%R[table(iter(sortkey(#lambda/default(\%0/ORDER,0),sort(lvexits(me),namei)),ljust(if(strlen(alias(%i0)),<[ansi(custcolor(%#,ROOM`EXITALIAS),pueblize(ucstr(alias(%i0)),name(%i0)))]>),6)[ansi(custcolor(%#,ROOM`EXITNAME),pueblize(name(%i0),name(%i0)))] to [ansi(custcolor(%#,ROOM`EXITDEST),left(name(loc(%i0)),sub(37,6,4,strlen(name(%i0)))))],%b,|),37,width(%#),|)]%R[u(RFN`HEADER)]

th attrib_set(u(cco-db)/VAR`CATEGORIES,setunion(get(u(cco-db)/VAR`CATEGORIES),ROOM,|,|))
&VAR`CATEGORIES`ROOM u(cco-db)=PLAYERNAME|THINGNAME|EXITNAME|EXITALIAS|EXITDEST|COLUMNNAME|BORDER
&COLOR`ROOM`EXITALIAS u(pco)=hx

@switch/inline isdbref(u(ae))=0,{th attrib_set(u(coi)/AE,open(Ancestor Exit,#0,u(ar)))}
&ODROP u(ae)=heads over from [fullname(home(me))]
&OSUCCESS u(ae)=heads over to [fullname(loc(me))]
&SUCCESS u(ae)=You head over to [fullname(loc(me))]

@switch/inline isdbref(u(ap))=0,{th attrib_set(u(coi)/AP,pcreate(Ancestor Player,digest(md5,secs())))}
@tel u(ap)=u(ar)
@pageformat u(ap)=ansi(custcolor(%@,PAGE`PAGE),colornames(%4,%!,lwho(%!),PAGES))
@outpageformat u(ap)=ansi(custcolor(%@,PAGE`OUTPAGE),colornames(%4,%!,lwho(%!),PAGES))
@chatformat u(ap)=if(strlen(setr(color,chancolor(%1))),<[ansi(%q<color>,stripansi(%1))]>[colornames(after(%5,>),%!,lwho(%!),CHANNELS)],<%1>[colornames(after(%5,>),%!,lwho(%!),CHANNELS)])

@switch/inline isdbref(u(at))=0,{th attrib_set(u(coi)/AT,create(Ancestor Thing <AT>))}
@tel u(at)=u(ar)