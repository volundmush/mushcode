@@ DEPENDENCIES - CORE

th u(NEWCOBJ,Radio Management System <RADIO>,radio,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)
th u(NEWCOBJ,Radio Frequency Parent <FREQ>,freq,u(cobj,radio),,1,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

&SEE_LOCK [u(cobj,freq)]=#TRUE
&JOIN_LOCK [u(cobj,freq)]=#TRUE
&SPEAK_LOCK [u(cobj,freq)]=#TRUE
&MODERATE_LOCK [u(cobj,freq)]=#FALSE
&ADMIN_LOCK [u(cobj,freq)]=#FALSE

&CMD`+RADIO`PENNMUSH [u(cobj,radio)]=$^(?s)(?\:\+)?(radio|freq|tune)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+RADIO`MAIN
@set [u(cobj,radio)]/CMD`+RADIO`PENNMUSH=regexp
&CMD`+RADIO`RHOSTMUSH [u(cobj,radio)]=$^(?s)(?\:\+)?(radio|freq|tune)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+RADIO`MAIN
@set [u(cobj,radio)]/CMD`+RADIO`RHOSTMUSH=regexp
&CMD`+RADIO`MAIN [u(cobj,radio)]=@attach %!/INC`GETSWITCH=%2;@include %!/INC`[u(strfirstof,%q<switch>,MAIN)]=%3,%4,%1
@set [u(cobj,radio)]/CMD`+RADIO`[u(choosegame,RHOSTMUSH,PENNMUSH)]=no_command

&CMD`+RADIOSHORT`PENNMUSH [u(cobj,radio)]=$^(?s)\.(\S+?)(?\:/(\S+)?)?(?\: +(.*))?$:@attach %!/CMD`+RADIOSHORT`MAIN
@set [u(cobj,radio)]/CMD`+RADIOSHORT`PENNMUSH=regexp
&CMD`+RADIOSHORT`RHOSTMUSH [u(cobj,radio)]=$^(?s)\.(\\S+?)(?\:/(\\S+)?)?(?\: +(.*))?$:@attach %!/CMD`+RADIOSHORT`MAIN
@set [u(cobj,radio)]/CMD`+RADIOSHORT`RHOSTMUSH=regexp
&CMD`+RADIOSHORT`MAIN [u(cobj,radio)]=@attach %!/INC`GETSWITCH=%2;@include %!/INC`[u(strfirstof,%q<switch>,MAIN)]=%1,%3
@set [u(cobj,radio)]/CMD`+RADIOSHORT`[u(choosegame,RHOSTMUSH,PENNMUSH)]=no_command

&SYSTEM`NAME [u(cobj,radio)]=RADIO

&SWITCHES`PLAYER [u(cobj,radio)]=CREATE|JOIN|LEAVE|ON|OFF|GAG|UNGAG|ALIAS|RENAME|RECALL|CODENAME|NOSPOOF|TITLE|NSRECALL|WHO|COLOR|CHOWN|CONFIG|DIRECTORY|LIST|ADDMOD|DELMOD|BAN|MUZZLE|UNMUZZLE|EMIT|DESCRIBE|INFO|SUMMARY|LOCK|GROUP|DELETE
&SWITCHES`ADMIN [u(cobj,radio)]=MONITOR|ACCMUZZLE|UNACCMUZZLE

&INC`CREATE [u(cobj,radio)]=@attach %!/INC`NAMECHECK=u(setr,chosename,trim(before(%0,/)));@stop isdbref(u(FUN`FINDFREQ,%q<chosename>))=@attach %!/INC`MSG=ERROR: A radio channel of that name already exists.;@tel u(setr,freq,create(%q<chosename>))=u(cobj,freq);@parent %q<freq>=u(cobj,freq);&OWNER %q<freq>=%:;@attach %!/INC`MSG=Radio channel '%0' created.;@attach %!/INC`MSG`CHAN=Radio channel '%0' created.;@attach %!/INC`JOIN
&FUN`FINDFREQ [u(cobj,radio)]=u(find_in,u(cobj,freq),%0)

&INC`FINDFREQ [u(cobj,radio)]=@attach %!/INC`NAMECHECK;@select/inline isdbref(u(setr,freq,u(FUN`FINDFREQ,%0)))=0,{@stop gt(words(u(setr,freq,namegraball(u(filter,CANSEE,children(u(cobj,freq)),%b,%b,%:),%0))),1)=@attach %!/INC`MSG=ERROR: That matched [u(itemize,iter(%q<freq>,name(%i0),%b,|),|,and,\,)]. Please be more specific.;@check words(%q<freq>)=@attach %!/INC`MSG=ERROR: Radio channel '%0' not found.};th u(setq,freq,objid(%q<freq>))

&FIL`PUBLIC [u(Cobj,radio)]=cor(strmatch(#TRUE,get(%0/SEE)),not(strlen(get(%0/SEE))))
&FIL`PRIVATE [u(cobj,radio)]=not(u(FIL`PUBLIC,%0))
&FIL`CANSEE [u(Cobj,radio)]=u(FUN`PERMISSION,%0,SEE,%1)

&INC`JOIN [u(cobj,radio)]=@attach %!/INC`FINDFREQ=trim(before(%0,/));@check u(FUN`PERMISSION,%q<freq>,JOIN,%#)=@attach %!/INC`MSG=ERROR: Permission denied.;th u(setq,slotname,u(strfirstof,trim(after(%0,/)),name(%q<freq>)));@stop hasattr(%#/D`RADIO`%q<slotname>)=@attach %!/INC`MSG=ERROR: That slot name is already in use. You must choose an alias that is not yet used.;th u(attrib_set,%#,u(setr,slot,D`RADIO`%q<slotname>),%q<freq>);&%q<slot>`NAME %#=%q<slotname>;@attach %!/INC`MSG=Slot '%q<slotname>' initialized and tuned to Radio Channel '[u(moniker,%q<freq>)]'.;th u(setq,mem,match(get(%q<freq>/MEMBERS),%:));@attach %!/INC`CONNECT=%:,%q<freq>;@attach %!/INC`UPDATESLOTS=%#,%q<freq>;@select/inline if(u(isadmin,%#),1,%q<mem>)=0,{@attach %!/INC`SYSRADIO=[ansi(hw,%n)] has joined the Radio Channel.}

&INC`UPDATESLOTS [u(cobj,radio)]=th u(attrib_set,%0,D`RADIO_META`[num(%1)],u(wildgrepi,%0,D`RADIO`*,%1))

&INC`CHECKSLOT [u(cobj,radio)]=@select/inline %2=TUNE,{@attach %!/INC`FINDFREQ},{@check strlen(%0)=@attach %!/INC`MSG=ERROR: No Slot entered to use.;@attach %!/INC`PARTIAL=%0,iter(lattr(%#/D`RADIO`*),last(%i0,`),%b,|),|,found,Radio Slot;@check hasattr(%#/D`RADIO`%q<found>)=@attach %!/INC`MSG=ERROR: No slot by that name.;th u(setq,slot,D`RADIO`%q<found>);th u(setq,slotname,default(%#/D`RADIO`%q<found>`NAME,ucstr(%q<found>)));th u(setq,freq,get(%#/%q<slot>))}

&INC`LEAVE [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@attach %!/WIPE=%#,%q<slot>;@attach %!/WIPE=%#,RADIO_[last(%q<slot>,`)];@attach %!/INC`UPDATESLOTS=%#,%q<freq>;@attach %!/INC`MSG=You have left Radio Channel '[u(moniker,%q<freq>)]'.;@select/inline words(get(%#/D`RADIO_META`[num(%q<freq>)]))=0,{@attach %!/INC`DISCONNECT=%:,%q<freq>;@select/inline u(isadmin,%#)=0,{@attach %!/INC`SYSRADIO=[ansi(hw,%n)] has left the Radio Channel.}}

&INC`SYSRADIO [u(cobj,radio)]=@attach %!/INC`BROADCAST=,%0,,1

&INC`CONNECT [u(cobj,radio)]=&MEMBERS %1=u(filter,ISOBJID,setunion(get(%1/MEMBERS),%0));&GAGGED %1=u(filter,ISOBJID,setdiff(get(%1/GAGGED),%0));&ENABLED %1=u(filter,ISOBJID,setunion(get(%1/ENABLED),%0))
&INC`DISCONNECT [u(cobj,radio)]=&MEMBERS %1=u(filter,ISOBJID,setdiff(get(%1/MEMBERS),%0));&GAGGED %1=u(filter,ISOBJID,setdiff(get(%1/GAGGED),%0));&ENABLED %1=u(filter,ISOBJID,setdiff(get(%1/ENABLED),%0))

&INC`NAMECHECK [u(cobj,radio)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: No frequency entered for the new Radio slot!;@check strlen(%0)=@attach %!/INC`MSG=ERROR: Frequency Field empty.;@stop gt(words(%0),1)=@attach %!/INC`MSG=ERROR: Radio names must not contain spaces.;@check valid(name,%0)=@attach %!/INC`MSG=ERROR: '%0' cannot be used as an object name.;@check valid(attrname,%0)=@attach %!/INC`MSG=ERROR: '%0' cannot be used as an attribute name.;@stop u(charsearch,%0,u(badchars) / `)=@attach %!/INC`MSG=ERROR: Radio names must not contain: [v(badchars)] / `

&INC`ON [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@attach %!/INC`CONNECT=%:,%q<freq>;@attach %!/INC`MSG=Radio Slot enabled.
&INC`OFF [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;&ENABLED %q<freq>=setdiff(get(%q<freq>/ENABLED),%:);@attach %!/INC`MSG=Radio Slot disabled.

&INC`MAIN [u(cobj,radio)]=@select/inline strlen(%0)=0,{@attach %!/INC`LIST},{@attach %!/INC`JAILCHECK;@attach %!/INC`CHECKSLOT;@check isobjid(%q<freq>)=@attach %!/INC`MSG=ERROR: Slot is no longer valid. Delete it!;@check match(get(%q<freq>/ENABLED),%:)=@attach %!/INC`MSG=That Slot is turned off.;@stop match(u(getstat,%#,D`RADIO,GAG),%q<freq>)=@attach %!/INC`MSG=ERROR: Slot is gagged.;@attach %!/INC`BROADCAST=%0,%1}

&INC`LIST [u(cobj,radio)]=@pemit %#=u(header,Your Radio Config);@pemit %#=ansi(u(color,%#,COLUMN_NAMES),align(3 12 15 22 22,Sta,Alias,FreqName,Codename,Title));@pemit %#=u(separator);@dolist/inline u(sortattr,u(lattr,%#/D`RADIO`*))={th u(setq,freq,get(%#/##));@pemit %#=align(3 12 15 22 22,if(match(get(%q<freq>/GAGGED),%:),Gag,if(match(get(%q<freq>/ENABLED),%:),ansi(hg,On),ansi(hx,Off))),default(%#/##`NAME,last(##,`)),if(u(FIL`PRIVATE,%q<freq>),ansi(hx,name(%q<freq>)),u(moniker,%q<freq>)),get(%#/##`CODENAME),get(%#/##`TITLE))};@pemit %#=u(footer)

&INC`DIRECTORY [u(cobj,radio)]=@pemit %#=u(header,Radio Directory);@pemit %#=ansi(u(color,%#,COLUMN_NAMES),align(3 15 18 41,Sta,Name,Owner,Description));@pemit %#=u(Publically Visible);@dolist/inline u(setr,public,u(sortname,u(filter,PUBLIC,children(u(cobj,freq)),%b,%b,%:)))={@pemit %#=u(FUN`RADIOLINE,##)};@pemit %#=u(separator,Hidden Channels);@dolist/inline u(sortname,setdiff(u(filter,CANSEE,children(u(cobj,freq)),%b,%b,%:),%q<public>))={@pemit %#=u(FUN`RADIOLINE,##)};@pemit %#=u(footer)

&FUN`RADIOLINE [u(cobj,radio)]=align(3 15 18 41,if(match(get(##/GAGGED),%:),Gag,if(match(get(##/MEMBERS),%:),if(match(get(##/ENABLED),%:),ansi(hg,On),ansi(hx,Off)),ansi(hx,N/A))),if(u(FIL`PRIVATE,%q<freq>),ansi(hx,name(##)),u(moniker,##)),if(u(setr,owner,get(##/OWNER)),u(moniker,%q<owner>)),left(get(##/SUMMARY),41))

&INC`INFO [u(Cobj,radio)]=@attach %!/INC`FINDFREQ;@check u(FUN`PERMISSION,%q<freq>,USE)=@attach %!/INC`MSG=ERROR: Permission denied.;@pemit %#=u(header,Radio Frequency: [u(moniker,%q<freq>)]);@pemit %#=get(%q<freq>/DESCRIBE);@pemit %#=u(separator);@pemit %#=[ansi(hw,OWNER:)] [if(get(%q<freq>/OWNER),u(moniker,get(%q<freq>/OWNER)),N/A)];@pemit %#=[ansi(hw,MODERATORS:)] [iter(u(sortname,u(filter,ISOBJID,get(%q<freq>/MODERATE))),name(%i0),%B,\,%b)];@pemit %#=[ansi(hw,MEMBERS:)] [iter(u(sortname,u(filter,ISOBJID,get(%q<freq>/MEMBERS))),name(%i0),%B,\,%b)];@pemit %#=u(footer)

&INC`COLOR [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@select/inline t(strlen(%1))=1,{@attach %!/INC`VALID`COLOR=%1;@attach %!/INC`MSG=You color Radio Slot [ucstr(%0)] to [ansi(%q<value>,ucstr(%0))];&%q<slot>`COLOR %#=%q<value>},0,{@attach %!/INC`MSG=You clear Radio Slot [ucstr(%0)]'s Color setting.;&%q<slot>`COLOR %#}

&INC`ALIAS [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@attach %!/INC`NAMECHECK=%1;@select/inline strmatch(%0,%1)=0,{@stop hasattr(%#/D`RADIO`%1)=@attach %!/INC`MSG=ERROR: That alias is already in use.;@attach %!/INC`MVTREE=%#,%q<slot>,%#,D`RADIO`%1};&D`RADIO`%1`NAME %#=%1;@attach %!/INC`MSG=You rename Slot '%0' to '%1';@mvattr %#/RADIO_%0=%#/RADIO_%1;@attach %!/INC`UPDATESLOTS=%#,%q<freq>

&INC`CODENAME [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@select/inline strlen(%1)=0,{@attach %!/INC`MSG=Radio Slot %q<slotname>'s Codename has been Cleared!;&%q<slot>`CODENAME %#},{th u(setq,codename,trim(%1));@stop u(charsearch,%q<codename>,%r %t)=@attach %!/INC`MSG=ERROR: Codenames may not contain linebreaks or tabs.;@stop gt(strlen(%q<codename>),u(PLAYER_NAME_LEN))=@attach %!/INC`MSG=ERROR: Codename is beyond the allowed [u(PLAYER_NAME_LEN)] characters.;@stop words(setdiff(u(choosegame,lsearch(all,eplayer,\[strlen(wildgrepi(##,D`RADIO`*`CODENAME,%q<codename>))\]),EPLAYER=\[strlen(u(wildgrepi,##,D`RADIO`*`CODENAME,%q<codename>))\]),%#))=@attach %!/INC`MSG=ERROR: Another player is already using that Codename.;@check valid(playername,%q<codename>)=@attach %!/INC`MSG=ERROR: Codenames may not match any player's name or alias.;@attach %!/INC`MSG=Your Codename on Radio Slot %q<slotname> is now: %q<codename>;&%q<slot>`CODENAME %#=%q<codename>}

&INC`TITLE [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@select/inline strlen(%1)=0,{@attach %!/INC`MSG=Radio Slot %q<slotname>'s Title has been Cleared!;@attach %!/WIPE=%#,%q<slot>`TITLE},{@stop u(charsearch,%1,%r %t)=@attach %!/INC`MSG=ERROR: Titles may not contain linebreaks or tabs.;@stop gt(strlen(%1),u(PLAYER_NAME_LEN))=@attach %!/INC`MSG=That radio title is too long!;@attach %!/INC`MSG=Your Title on Radio Slot %q<slotname> is now: %1;&%q<slot>`TITLE %#=%1}

&INC`MONITOR [u(cobj,radio)]=@select/inline t(u(getstat,%#,D`RADIO,Monitor))=1,{@attach %!/INC`MSG=You switch off Monitoring.;th u(setstat,%#,D`RADIO,Monitor,0)},0,{@attach %!/INC`MSG=You switch on Monitoring.;th u(setstat,%#,D`RADIO,Monitor,1)}

&INC`NOSPOOF [u(cobj,radio)]=@select/inline t(u(getstat,%#,D`RADIO,Nospoof))=1,{@attach %!/INC`MSG=You switch off Nospoof mode.;th u(setstat,%#,D`RADIO,Nospoof,0)},0,{@attach %!/INC`MSG=You switch on Nospoof.;th u(setstat,%#,D`RADIO,Nospoof,1)}

&INC`EMIT [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@attach %!/INC`BROADCAST=%0,%1,1

&INC`GAG [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@stop match(u(getstat,%#,D`RADIO,GAG),%q<freq>)=@attach %!/INC`MSG=Already gagging %q<slotname>.;@attach %!/INC`MSG=Frequency gagged.;@attach %!/INC`DISABLE`GAG=%:,%q<freq>
&INC`UNGAG [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@check match(u(getstat,%#,D`RADIO,GAG),%q<freq>)=@attach %!/INC`MSG=Not gagging %q<slotname>.;@attach %!/INC`MSG=Frequency ungagged.;@attach %!/INC`ENABLE`GAG=%:,%q<freq>

&INC`ENABLE`GAG [u(cobj,radio)]=&GAGGED %1=setdiff(get(%1/GAGGED),%0);th u(setstat,%0,D`RADIO,GAG,setdiff(u(getstat,%0,D`RADIO,Gag),%1))
&INC`DISABLE`GAG [u(cobj,radio)]=&GAGGED %1=setunion(get(%1/GAGGED),%0);th u(setstat,%0,D`RADIO,GAG,setunion(u(getstat,%0,D`RADIO,Gag),%1))

&INC`CHOWN [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@check u(FUN`PERMISSION,%q<freq>,OWNER)=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`CHECKPC=%1,1;@attach %!/INC`VERIFY={WARNING: This will change ownership of Frequency %q<slotname>. Are you sure? Enter command again in ten seconds to confirm.},RADIO CHOWN %q<freq> %q<t1>;&OWNER %q<freq>=%q<t1objid>;@attach %!/INC`MSG=Radio [u(moniker,%q<freq>)] ownership changed to: %q<t1name>\, by %n.,setunion(%# %q<t1>,);@attach %!/INC`MSG`CHAN=Radio [u(moniker,%q<freq>)] ownership changed to: %q<t1name>.

&INC`DELETE [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@check u(FUN`PERMISSION,%q<freq>,OWNER)=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`VERIFY={WARNING: This will delete Frequency %q<slotname>. Are you sure? Enter command again in ten seconds to confirm.},RADIO DELETE %q<freq>;@attach %!/INC`MSG=Radio [u(moniker,%q<freq>)] deleted.;@attach %!/INC`SYSRADIO=Frequency has been [ansi(hr,DELETED!)];@dolist/inline u(filter,ISOBJID,get(%q<freq>/MEMBERS))={@dolist/inline get(%i0/D`RADIO_META`[num(%q<freq>)])=@attach %!/WIPE=%i1,%i0;@attach %!/WIPE=%i0,D`RADIO_META`[num(%q<freq>)]};@attach %!/DELETE=%q<freq>

&INC`DESCRIBE [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@check u(FUN`PERMISSION,%q<freq>,ADMIN)=@attach %!/INC`MSG=ERROR: Permission denied.;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Nothing entered to describe the radio as!;@DESCRIBE %q<freq>=%1;@attach %!/INC`MSG=Description set!

&INC`SUMMARY [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@check u(FUN`PERMISSION,%q<freq>,ADMIN)=@attach %!/INC`MSG=ERROR: Permission denied.;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Nothing entered to describe the radio as!;&SUMMARY %q<freq>=%1;@attach %!/INC`MSG=Summary set!

&INC`RENAME [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@check u(FUN`PERMISSION,%q<freq>,OWNER)=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`NAMECHECK=%1;@break cand(isdbref(u(setr,found,u(FUN`FINDFREQ,%1))),not(strmatch(%q<freq>,%q<found>)))=@attach %!/INC`MSG=ERROR: Name conflict.;@attach %!/INC`MSG=Radio channel renamed!;@attach %!/INC`MSG`CHAN=Radio Channel '[u(moniker,%q<freq>)]' renamed to: %1;@name %q<freq>=%1

&INC`CONFIG [u(cobj,radio)]=@attach %!/INC`CHECKSLOT=before(%0,/);@check u(FUN`PERMISSION,%q<freq>,ADMIN)=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`PARTIAL=after(%0,/),CODENAME|TITLE|BANNER,|,config,Config Option;@attach %!/INC`VALID`BOOL=%1;&CONFIG`%q<config> %q<freq>=%q<value>;@attach %!/INC`MSG=Radio Config option %q<Config> set to %q<value>;@attach %!/INC`MSG`CHAN=Radio Channel '[u(moniker,%q<freq>)]' Config option %q<config> set to: %q<value>

&FUN`CONFIG [u(cobj,radio)]=default(%0/CONFIG`%1,switch(%1,CODENAME,1,TITLE,1,BANNER,0))

&FUN`PERMISSION [u(cobj,radio)]=localize(if(cor(u(isadmin,u(setr,target,objid(u(strfirstof,%2,%#)))),match(get(%0/OWNER),%q<target>)),1,switch(%1,OWNER,match(get(%0/OWNER),%q<target>),MODERATE,cor(match(get(%0/MODERATE),%q<target>),testlock(get(%0/MODERATE_LOCK),%q<target>)),SEE,cor(testlock(get(%0/SEE_LOCK),%q<target>)),SPEAK,cand(testlock(get(%0/SPEAK_LOCK),%q<target>),not(match(get(%0/BANNED),%q<target>))),0)))
@@ %0 - freq object, %1 - permission type, %2 - target if not enactor.

&INC`ADDMOD [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@check u(FUN`PERMISSION,%q<freq>,ADMIN)=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`CHECKPC=%1,1;@stop match(get(%q<freq>/MODERATE),%q<t1objid>)=@attach %!/INC`MSG=ERROR: %q<t1name> is already a moderator.;&MODERATE %q<freq>=setunion(get(%q<freq>/MODERATE),%q<t1objid>);@attach %!/INC`MSG=%q<t1name> is now a moderator for [u(moniker,%q<freq>)].;@attach %!/INC`MSG`NOTICE=You are now a moderator for [u(moniker,%q<freq>)].,%q<t1>;@attach %!/INC`MSG`CHAN=%q<t1name> added to [u(moniker,%q<freq>)] moderators.

&INC`DELMOD [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@check u(FUN`PERMISSION,%q<freq>,ADMIN)=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`CHECKPC=%1,1;@check match(get(%q<freq>/MODERATE),%q<t1objid>)=@attach %!/INC`MSG=ERROR: %q<t1name> is not a moderator.;&MODERATE %q<freq>=setdiff(get(%q<freq>/MODERATE),%q<t1objid>);@attach %!/INC`MSG=%q<t1name> is no longer a moderator for [u(moniker,%q<freq>)].;@attach %!/INC`MSG`NOTICE=You are no longer a moderator for [u(moniker,%q<freq>)].,%q<t1>;@attach %!/INC`MSG`CHAN=%q<t1name> removed from [u(moniker,%q<freq>)] moderators.

&INC`BAN [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@check u(FUN`PERMISSION,%q<freq>,MODERATE)=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`CHECKPC=%1,1;@stop u(FUN`PERMISSION,%q<freq>,MODERATE,%q<t1>)=@attach %!/INC`MSG=ERROR: Cannot ban privileged players.;@stop match(get(%q<freq>/BANNED),%q<t1objid>)=@attach %!/INC`MSG=ERROR: %q<t1name> is already banned.;@attach %!/INC`MSG=You have banned %q<t1objid> from Radio Channel [u(moniker,%q<freq>)].;@attach %!/INC`MSG`NOTICE=You were banned from Radio Channel [u(moniker,%q<freq>)],%q<t1>.;@attach %!/INC`MSG`CHAN=%q<t1name> was banned from Radio Channel [u(moniker,%q<freq>)].;&BANNED %q<freq>=u(filter,ISOBJID,setunion(get(%q<freq>/BANNED),%q<t1objid>))
&INC`UNBAN [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@check u(FUN`PERMISSION,%q<freq>,MODERATE)=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`CHECKPC=%1,1;@stop u(FUN`PERMISSION,%q<freq>,MODERATE,%q<t1>)=@attach %!/INC`MSG=ERROR: Cannot ban privileged players.;@check match(get(%q<freq>/BANNED),%q<t1objid>)=@attach %!/INC`MSG=ERROR: %q<t1name> is not banned.;@attach %!/INC`MSG=You have unbanned %q<t1objid> from Radio Channel [u(moniker,%q<freq>)].;@attach %!/INC`MSG`NOTICE=You were unbanned from Radio Channel [u(moniker,%q<freq>)],%q<t1>.;@attach %!/INC`MSG`CHAN=%q<t1name> was unbanned from Radio Channel [u(moniker,%q<freq>)].;&BANNED %q<freq>=u(filter,ISOBJID,setdiff(get(%q<freq>/BANNED),%q<t1objid>))

&INC`MUZZLE [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@check u(FUN`PERMISSION,%q<freq>,MODERATE)=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`CHECKPC=before(%1,/),1;@stop u(FUN`PERMISSION,%q<freq>,MODERATE,%q<t1>)=@attach %!/INC`MSG=ERROR: Cannot muzzle privileged players.;@stop gt(get(%q<freq>/MUZZLE`%q<t1>),secs())=@attach %!/INC`MSG=%q<t1name> is already muzzled.;@check strlen(after(%1,/))=@attach %!/INC`MSG=ERROR: Muzzle duration empty.;@check gt(u(setr,duration,u(stringsecs,after(%1,/))),0)=@attach %!/INC`MSG=ERROR: Duration was not accepted by stringsecs. Please see [u(pueblize,help stringsecs(),help stringsecs())].;th u(attrib_set,%q<freq>,MUZZLE`%q<t1>,u(setr,until,add(secs(),%q<duration>)));&MUZZLE`%q<t1>`BY %q<freq>=%:;&MUZZLE`%q<t1>`ON %q<freq>=secs();@attach %!/INC`MSG=Muzzled %q<t1name> from Radio Channel [u(moniker,%q<freq>)] until [u(fancytime,%q<until>)].;@attach %!/INC`MSG`NOTICE=You were Muzzled from Radio Channel '[u(moniker,%q<freq>)]' until [u(fancytime,%q<duration>,%q<t1>)].,%q<t1>;@attach %!/INC`MSG`CHAN=%q<t1name> Muzzled from Radio Channel '[u(moniker,%q<freq>)]' until [u(fancytime,%q<duration>,,UTC)].
&INC`UNMUZZLE [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@check u(FUN`PERMISSION,%q<freq>,MODERATE)=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`CHECKPC=before(%1,/),1;@stop u(FUN`PERMISSION,%q<freq>,MODERATE,%q<t1>)=@attach %!/INC`MSG=ERROR: Cannot muzzle privileged players.;@check gt(get(%q<freq>/MUZZLE`%q<t1>),secs())=@attach %!/INC`MSG=%q<t1name> is not muzzled.;@attach %!/WIPE=%q<freq>,MUZZLE`%q<t1>;@attach %!/INC`MSG=un-Muzzled %q<t1name> from Radio Channel [u(moniker,%q<freq>)].;@attach %!/INC`MSG`NOTICE=You were un-Muzzled from Radio Channel '[u(moniker,%q<freq>)]'.,%q<t1>;@attach %!/INC`MSG`CHAN=%q<t1name> un-Muzzled from Radio Channel '[u(moniker,%q<freq>)]'.

&INC`ACCMUZZLE [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@check u(FUN`PERMISSION,%q<freq>,MODERATE)=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`CHECKPC=before(%1,/),1;@check isdbref(u(setr,accid,u(accid,%q<t1>,1)))=@attach %!/INC`MSG=ERROR: %q<t1name> does not have an Account.;@stop gt(get(%q<freq>/ACCMUZZLE`%q<accid>),secs())=@attach %!/INC`MSG=%q<t1name> is already Account Muzzled.;@check strlen(after(%1,/))=@attach %!/INC`MSG=ERROR: Muzzle duration empty.;@check gt(u(setr,duration,u(stringsecs,after(%1,/))),0)=@attach %!/INC`MSG=ERROR: Duration was not accepted by stringsecs. Please see [u(pueblize,help stringsecs(),help stringsecs())].;th u(attrib_set,%q<freq>,ACCMUZZLE`%q<accid>,u(setr,until,add(secs(),%q<duration>)));&ACCMUZZLE`%q<accid>`BY %q<freq>=%:;&ACCMUZZLE`%q<accid>`ON %q<freq>=secs();@attach %!/INC`MSG=Account Muzzled %q<t1name> from Radio Channel [u(moniker,%q<freq>)] until [u(fancytime,%q<until>)].;@attach %!/INC`MSG`NOTICE=You were Account Muzzled from Radio Channel '[u(moniker,%q<freq>)]' until [u(fancytime,%q<duration>,%q<t1>)].,%q<t1>;@attach %!/INC`MSG`CHAN=%q<t1name> Account Muzzled from Radio Channel '[u(moniker,%q<freq>)]' until [u(fancytime,%q<duration>,,UTC)].
&INC`UNACCMUZZLE [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@check u(FUN`PERMISSION,%q<freq>,MODERATE)=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`CHECKPC=before(%1,/),1;@check isdbref(u(setr,accid,u(accid,%q<t1>,1)))=@attach %!/INC`MSG=ERROR: %q<t1name> does not have an Account.;@check gt(get(%q<freq>/ACCMUZZLE`%q<accid>),secs())=@attach %!/INC`MSG=%q<t1name> is not Account Muzzled.;@attach %!/WIPE=%q<freq>,ACCMUZZLE`%q<accid>;@attach %!/INC`MSG=Account un-Muzzled %q<t1name> from Radio Channel [u(moniker,%q<freq>)].;@attach %!/INC`MSG`NOTICE=You were Account un-Muzzled from Radio Channel '[u(moniker,%q<freq>)]'.,%q<t1>;@attach %!/INC`MSG`CHAN=%q<t1name> Account un-Muzzled from Radio Channel '[u(moniker,%q<freq>)]'.

&INC`BROADCAST [u(cobj,radio)]=@select/inline t(%3)=0,{@check strlen(%1)=@attach %!/INC`MSG=ERROR: Nothing entered to say!;@@ @stop strmatch(left(%1,1),|)=@attach %!/INC`MSG=ERROR: The Emit feature is disabled.;@stop match(get(%q<freq>/BANNED),%:)=@attach %!/INC`MSG=ERROR: You are banned from Radio Channel '[u(moniker,%q<freq>)]'.;@check u(FUN`PERMISSION,%q<freq>,USE)=@attach %!/INC`MSG=ERROR: Permission denied.;@stop gt(u(setr,muz,get(%q<freq>/MUZZLE`%#)),secs())=@attach %!/INC`MSG=ERROR: You are Muzzled from Radio Channel '[u(moniker,%q<freq>)]' until [u(fancytime,%q<muz>)].;@select/inline isdbref(u(setr,accid,num(u(accid,%#,1))))=1,{@stop gt(u(setr,muz,get(%q<freq>/ACCMUZZLE`%q<accid>)),secs())=@attach %!/INC`MSG=ERROR: You are Account Muzzled from Radio Channel '[u(moniker,%q<freq>)]' until [u(fancytime,%q<muz>)].}};th u(setq,targets,setdiff(setinter(u(setr,who,u(lwhoid)),setdiff(get(%q<freq>/ENABLED),get(%q<freq>/GAGGED))),u(setr,monitor,u(filter,MONITOR,%q<who>))));th u(setq,markup,u(markup,%1,setunion(u(lwhoid),get(%q<freq>/ENABLED))));th u(setq,banner,u(FUN`CONFIG,%q<freq>,BANNER));@select/inline %va=PennMUSH,{@message %q<targets>=<unset>,%!/FUN`FORMATBROADCAST,%#,##,%q<freq>,%q<markup>,%3,get(%#/%q<slot>`CODENAME),get(%#/%q<slot>`TITLE),%2,%q<banner>;@message setunion(%q<monitor>,if(%2,%#))=<unset>,%!/FUN`FORMATMONITOR,%#,##,%q<freq>,%q<markup>,%3,get(%#/%q<slot>`CODENAME),get(%#/%q<slot>`TITLE),%2,%q<banner>},RhostMUSH,{@pemit/list %q<targets>=udefault(%!/FUN`FORMATBROADCAST,<unset>,%#,##,%q<freq>,%q<markup>,,get(%#/%q<slot>`CODENAME),get(%#/%q<slot>`TITLE),%2);@pemit/list setunion(%q<monitor>,if(%2,%#))=udefault(%!/FUN`FORMATBROADCAST,<unset>,%#,##,%q<freq>,%q<markup>,,get(%#/%q<slot>`CODENAME),get(%#/%q<slot>`TITLE),%2)};@attach %!/INC`LOGRADIO=%#,%q<freq>,%q<markup>,%3,get(%#/%q<slot>`CODENAME),get(%#/%q<slot>`TITLE),%2;@select/inline cand(%2,not(match(%q<monitor>,%:)))=1,{@attach %!/INC`MSG=WARNING: Your Emit was sent to %q<freq>, but you must either set a slot to that frequency or toggle [pueblize(+radio/monitor,+radio/monitor)] to hear replies!}
@@ %0 - Enactor, %1 - recipient, %2 - frequency, %3 - message, %4 - System msg mode, %5 - codename, %6 - title, %7 -  emit mode. %8 - Banner.

&FIL`MONITOR [u(cobj,radio)]=u(getstat,%0,D`RADIO,Monitor)

&INC`WHO [u(cobj,radio)]=@attach %!/INC`CHECKSLOT;@check words(u(setr,listen,u(sortname,setdiff(setinter(u(setr,who,u(lwhoid,%#)),get(%q<freq>/ENABLED)),u(setr,monitor,u(filter,MONITOR,%q<who>)),))))=@attach %!/INC`MSG=Nobody is listening to %q<freq>.;@attach %!/INC`MSG=Listeners for %q<slotname> are: [u(itemize,iter(%q<listen>,u(moniker,%i0),,|),|,and,\,)]

&FUN`FORMATBROADCAST [u(cobj,radio)]=u(setq,recpslot,first(get(%1/D`RADIO_META`[num(%2)])))[u(setq,default,ansi(u(color,%1,RADIO_EDGE),-<)[ansi(u(color,%1,RADIO_START),RADIO:)] [ansi(u(strfirstof,get(%1/%q<recpslot>`COLOR),u(color,%1,RADIO_TITLE)),u(%!/FUN`FREQNAME,%1,%2))][if(%4,ansi(c,-SYS))][ansi(u(color,%1,RADIO_EDGE),>-)]%B[u(setr,fullmsg,[if(cor(%7,%4),u(colormarkup,%1,%3,%0),if(strlen(%6),trim(%6)%B)[u(speech,%0,u(setr,mesg,u(colormarkup,%1,%3,%0)),%1,if(cand(strlen(%5),cor(u(getstat,%1,D`RADIO,NoSpoof),%8)),\([u(moniker,%0,%1)]\) %5,if(strlen(%5),%5)),RADIO,lwho(),%8)])])])][u(strfirstof,ulocal(%1/RADIO_[last(%q<recpslot>,`)],switch(1,t(%7),|,strmatch(%3,:*),:,strmatch(%3,;*),;,"),u(%!/FUN`FREQNAME,%1,%2),switch(1,strmatch(%q<mesg>,:*),after(%q<mesg>,:),strmatch(%q<mesg>,;*),after(%q<mesg>,;),%q<mesg>),if(%7,Someone,u(moniker,%0,%1)),%6,%q<default>,%q<fullmsg>,%5,%2),%q<default>)]

@@ [if(%q<banner>,u(getproperty,%0,banner),u(moniker,%0,%1))]

&FUN`FORMATMONITOR [u(cobj,radio)]=ansi(u(color,%1,RADIO_EDGE),-<)[ansi(hw,RADIO MONITOR: [u(moniker,%2)])][if(%4,ansi(c,-SYS))][ansi(u(color,%1,RADIO_EDGE),>-)]%B[if(cor(%4,%7),ansi(h,name(%0))[if(%7,%BEMIT:%b)] [u(colormarkup,%1,%3,%0)],if(strlen(%6),trim(%6)%B)[u(speech,%0,u(colormarkup,%1,%3,%0),%1,if(strlen(%5),\([u(moniker,%0,%1)]\) %5,u(moniker,%0,%1)),RADIO,lwho(),%8)])]

&INC`LOGRADIO [u(cobj,radio)]=&LASTMSG %1=secs();@attach %!/INC`DOSQL=INSERT`RADIO_LOG/log_id,%6,objid(%1),name(%1),objid(%0),name(%0),%5,%4,%2;@attach %!/INC`DOSQL=RENDER`RADIO_LOG,u(render,%2),%q<log_id>;@attach %!/INC`DOSQL=DELETE`OLD_RADIO;

&Q`INSERT`RADIO_LOG [u(cobj,radio)]=INSERT INTO vol_messages (message_date_created,message_type,message_emit,source_objid,source_name,speaker_objid,speaker_name,speaker_title,speaker_codename,message_contents) VALUES (UTC_TIMESTAMP(),3,?,?,?,?,?,?,?,?)
&Q`RENDER`RADIO_LOG [u(Cobj,radio)]=UPDATE vol_messages SET message_contents_render=? WHERE message_id=?
&Q`DELETE`OLD_RADIO [u(cobj,radio)]=DELETE FROM vol_messages WHERE message_type=3 AND message_date_created<UTC_TIMESTAMP() - INTERVAL 3 DAY

&FIL`LASTRADIO [u(cobj,radio)]=lt(sub(secs(),get(%0/LASTMSG)),u(stringsecs,20d))

&SORTLAST [u(cobj,mco)]=u(SORTLAST,%0,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))
&SORTLAST`PENNMUSH [u(cobj,mco)]=sortkey(#lambda/get(\%0/LASTMSG),%0,n,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B),)
&SORTLAST`RHOSTMUSH [u(cobj,mco)]=sortby(#lambda/[lit([ncomp(get(%0/LASTMSG),get(%1/LASTMSG))])],%0,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))

&FUN`FREQNAME [u(cobj,radio)]=u(strfirstof,get(%0/[first(u(wildgrepi,%0,D`RADIO`*,%1))]`NAME),%1)

&INC`NSRECALL [u(cobj,radio)]=@attach %!/INC`RECALL=%0,%1,1

&INC`RECALL [u(cobj,radio)]=@select/inline regmatchi(%0,^\\d+\\.\\d+$)=1,{th u(setq,freq,%0)},0,{@attach %!/INC`CHECKSLOT;@check strlen(%q<freq>)=@attach %!/INC`MSG=ERROR: That Radio Slot has no Frequency set.};@select/inline %1=0,{th u(setq,max,u(mysql,COUNT`RADIO,objid(%q<freq>)))},{@select/inline strlen(%1)=0,{th u(setq,max,100)},{@check cand(gt(%1,-1),isint(u(setr,max,%1)))=@attach %!/INC`MSG=ERROR: Line Recall must be an integer 0 or greater.}};th u(setq,banner,u(FUN`CONFIG,%q<freq>,BANNER));@check words(u(setr,list,u(mysql,RECALL`RADIO_IDS,objid(%q<freq>),%q<max>)))=@attach %!/INC`MSG=ERROR: Nothing to recall!;@pemit %#=u(header,Frequency: %q<slotname> - %q<freq>);@dolist %q<list>={th u(setq,row,u(mysql3,RECALL`RADIO,##));@pemit %#=(On [u(fancytime,elements(%q<row>,4,chr(177)),%#)])%B[u(FUN`FORMAT[if(u(getstat,%#,D`RADIO,Monitor),MONITOR,BROADCAST)],elements(%q<row>,1,chr(177)),%#,%q<freq>,elements(%q<row>,5,chr(177)),elements(%q<row>,6,chr(177)),elements(%q<row>,3,chr(177)),elements(%q<row>,2,chr(177)),elements(%q<row>,6,chr(177)),%2)];@select/inline #@=words(%q<list>),{@pemit %#=u(footer)}}

@@ %0 - Enactor, %1 - recipient, %2 - frequency, %3 - message, %4 - sys message flag, %5 - alias

&Q`COUNT`RADIO [u(cobj,radio)]=SELECT COUNT(*) FROM vol_messages WHERE source_objid=?
&Q`RECALL`RADIO_IDS [u(cobj,radio)]=SELECT r.message_id FROM (SELECT * FROM vol_messages WHERE message_type=3 AND source_objid=? ORDER BY message_date_created DESC LIMIT ?) AS r ORDER BY r.message_date_created ASC

&Q`RECALL`RADIO [u(cobj,radio)]=SELECT speaker_objid,speaker_title,speaker_codename,message_date_created,message_contents,message_emit FROM vol_messages WHERE message_id=?

&PLAYER`DISCONNECT [u(cobj,radio)]=@stop %1;@dolist/inline u(getstat,%0,D`RADIO,GAG)={@attach %!/INC`DISABLE`GAG=objid(%0),objid(%i0)};th u(delstat,%0,D`RADIO,GAG)

@@ COMMUNICATIONS - +RADIO
&HLP`RADIO [u(cobj,radio)]=The Radio System is a customizable series of soft-channels meant to represent in-character radio frequencies, IC chat rooms, and other venues of instant communication.%R[ansi(hc,Aliases:)] radio%R%R%R[ansi(hc,Concepts)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,Channel)] - A Radio Channel behaves like a hardcoded channel\, distributing messages to connected players.%R[ansi(h,Alias)] - Players link/connect to channels by making ALIASES\, which can also be considered 'slots.' You configure the alias and send messages over it.%R[ansi(h,Names)] - Channel/Alias names obey the following rules: no spaces\, no / characters\, no ` characters\, must be a valid attribute name and object name. The simpler the better. Recommend only alphanumeric characters\, - and _.%R[ansi(h,codename)] - An alternate name you appear as. Codenames must be unique and can't match player names.%R[ansi(h,password)] - Frequencies with passwords do not appear on the public directory and can only be /join'd by providing their exact name (case insensitive) and password (case sensitive.) Private channels show in [ansi(hx,dark text)] in listings. Obligatory Disclaimer: These are stored in plain text and encrypt nothing. Do not use anything you wouldn't want admin or a hacker to see.)]%R%R[ansi(hc,Listing Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+radio/create <name>\[/<alias>\]\[=<password>\])] - Create a new Radio Channel owned by you. Also joins you to it. Alternate alias name and password are optional.%R[ansi(h,+radio/directory)] - Lists the public directory of Radio channels. Channels with passwords set are not public and do not show.%R[ansi(h,+radio/info <name>)] - View details about a specific Radio Channel.)]%R%R[ansi(hc,Alias Configuration)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+radio)] - Display your configured channels.%R[ansi(h,+radio/join <name>\[/<alias>\]\[=<password>\])] - Creates a new Radio Alias and sets it to <name>\, or an optional included alternate name. Passworded frequencies must be targeted by their EXACT names (case insensitive) and the (case sensitive) password provided.%R[ansi(h,+radio/leave <alias>)] - Erase the alias and leave the channel if it was your only one.%R[ansi(h,+radio/title <alias>=<title>)] - Set a title to appear before your name. Keep it short and sweet!%R[ansi(h,+radio/codename <alias>=<new codename>)] - Sets your Codename on an Alias. Set to nothing to clear it.%R[ansi(h,+radio/alias <alias>=<new alias>)] - Renames a slot/alias.%R[ansi(h,+radio/color <slot>=<colorcode>)] - Set a color for a radio frequency. Enter nothing to clear color settings.%R[ansi(h,+radio/nospoof)] - Toggle whether real names are shown next to codenames if a codename's in use.%R[ansi(h,.<slot>/<switch> <entry>)] - Equivalent to +radio/<switch> <slot>=<entry> - useful for .<slot>/gag\, etc)]%R%R[ansi(hc,Messaging Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+radio <slot>=<speech>)] - Send a message over <slot>'s Frequency.%R[ansi(h,.<slot> <speech>)] - Shortcut for sending messages. Inspired by hardcoded channels. Example: [ansi(h,.broadband hello!)]%R[ansi(h,+radio/off <alias>)] - Turns a slot off. Disabled slots can't send or receive messages. The opposite is /on.%R[ansi(h,+radio/gag <alias>)] - Temporarily stop receiving messages from a given channel. Cleared upon logoff or use of /ungag.%R[ansi(h,+radio/emit <alias>=<message>)] - Send an unformatted message if you have Moderate permission.)]%R%R[ansi(hc,Misc)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+radio/recall <alias>=<#>)] - recalls <#> of lines from <slot>'s frequency. If <#> is blank\, defaults to 10 lines. If 0\, shows ALL recorded messages.%R[ansi(h,+radio/nsrecall <alias>=<#>)] - Like /recall\, but as though nospoof were turned on.%R[ansi(h,+radio/who <alias>)] - See who's listening to a frequency.)]
+help/add Communications/+radio=[u(cobj,radio)]/HLP`RADIO

&HLP`FORMATTER [u(cobj,radio)]=[ansi(hc,Formatters)]%R%TJust like @chatformat, @pageformat, and similar hardcoded commands, +radio supports the ability to set custom formatters to change how you receive the message. This is an advanced topic and should not be used unless you're familiar with setting and clearing attributes.%R%TEach Radio slot will check your character for a corresponding RADIO_<slotname> attribute. If you created a slot called Police with +radio/init Police=11.17, you'd need to set an attribute called RADIO_POLICE. example: &RADIO_POLICE me=\%5.%R%TThis attribute is called via a ulocal(). It is passed the following arguments for substitution purposes, just like @chatformat:%R\%0 - The type of the message. " for say, : for pose, ; for semipose, | for an emit.%R\%1 - The frequency name.%R\%2 - The message contents.%R\%3 - The name of the speaker. Will be 'Someone' if it's an Emit.%R\%4 - The speaker's title.%R\%5 - The default message as if no formatter was set.%R\%6 - like \%5 but minus the default header.%R\%7 - The speaker's codename.%R\%8 - The frequency number.%R%TThis attribute WILL be moved if you rename a slot.%R%RTO DELETE A FORMATTER, SIMPLY CLEAR THE ATTRIBUTE. Example:%R%T&RADIO_POLICE me%R%T@wipe me/RADIO_POLICE%R(either will work.)%R%RDeleting the +radio slot will also delete the associated formatter.
+help/addsub +radio/Formatter=[u(cobj,hlp)]/HLP`FORMATTER


&HLP`MODERATION [u(Cobj,radio)]=[ansi(hc,Concepts)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,Owner)] - The person who created a Radio Channel 'owns' it. They count as a Moderator and can change the Radio Channel's settings\, name\, etc.%R[ansi(h,Moderator)] - Moderators are players chosen by the Owner to have the power to restrict access to a Radio Channel for rules infractions.%R[ansi(h,Durations)] - For the Muzzle commands\, <duration> must be a stringsecs-compatible entry. A safe bet is a number of days\, for example: 5d or 30d for 5 days or 30 days.)]%R%R[ansi(hc,Owner Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+radio/chown <alias>=<new owner>)] - Transfer ownership of a Channel.%R[ansi(h,+radio/rename <alias>=<name>)] - Change the Radio Channel's global name.%R[ansi(h,+radio/addmod <alias>=<player>)] - Assign a moderator to a Radio Channel. Can be revoked with /delmod.%R[ansi(h,+radio/config <alias>/<option>=<value>)] - Change settings. Value must be BOOL: 1 or 0 for Enable or Disable.%R%T[ansi(h,codename)] - channel allows codenames.%R%T[ansi(h,title)] - channel allows titles.%R[ansi(h,+radio/describe <alias>=<description>)] - Set the radio's description. This is visible in +radio/info. Useful for rules and guidelines\, etc.)]%R[ansi(h,+radio/summary <alias>=<text>)] - Use up to 41 characters to describe the channel for +radio/directory.%R%R[ansi(hc,Moderator Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+radio/ban <alias>=<player>)] - Prevent <player> from speaking on a channel.%R[ansi(h,+radio/muzzle <alias>=<player>/<duration>)] - Muzzle a player until <duration> has passed. They cannot speak on the channel. Use /unmuzzle to undo.%R[ansi(h,+radio/emit <alias>=<message>)] - Send an anonymous message over a Radio Channel.)]
+help/addsub +radio/Moderation=[u(cobj,radio)]/HLP`MODERATION


&SHLP`RADIO [u(cobj,radio)]=Staff count as owners and moderators of all Radio Channels.%R[ansi(hc,Staff Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+radio/monitor)] - toggles Monitor mode. While in Monitor mode\, you will receive ALL messages from ALL frequencies.%R[ansi(h,+radio/ban <alias>=<player>)] - Prevent <player> from speaking on a channel.%R[ansi(h,+radio/muzzle <alias>=<player>/<duration>)] - Muzzle a player until <duration> has passed. They cannot speak on the channel. Use /unmuzzle to undo.%R[ansi(h,+radio/emit <alias>=<message>)] - Send an anonymous message over a Radio Channel.%R[ansi(h,+radio/accmuzzle <alias>=<player>/<duration>)] - As with /muzzle but restricts whole account. undone with /unaccmuzzle.%R%RMonitor mode will apply when using +radio/recall%R%R/muzzle reprinted here for simplicity. Moderators have it too.)]
+shelp/add Communications/+radio=[u(cobj,radio)]/SHLP`RADIO