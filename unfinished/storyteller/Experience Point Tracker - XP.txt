@@ DEPENDENCIES - CORE

@select/inline isdbref(u(xp))=0,{@teleport create(Experience Point Tracker <XP>)=config(master_room)}
&xp u(coi)=locate(config(master_room),Experience Point Tracker <XP>,TXxi)
@parent u(xp)=u(coi)
@set u(xp)=WIZARD SAFE !NO_COMMAND

&CMD`+XP u(xp)=$^(?s)(?\:\+)?XP(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.+?))?(?\:/(.*))?$:@attach u(ccs)/INC`PARTIAL=%1,setunion(get(u(xp)/VAR`PLAYFLAGS),if(isadmin(%#),get(u(xp)/VAR`ADMINFLAGS)),|,|),|,XP,switch,switch;@attach u(xp)/INC`[strfirstof(%q<switch>,MAIN)]=%2,%3,%4
@set u(xp)/CMD`+XP=regexp

&VAR`ADMINFLAGS u(xp)=DELGAINED|DELSPENT|TOTALS|AWARD|SPEND
&VAR`PLAYFLAGS u(xp)=GAINED|SPENT

&INC`MAIN u(xp)=@select/inline cand(strlen(%0),isadmin(%#))=1,{@attach u(ccs)/INC`CHECKPC=%0,1,XP},0,{@attach u(ccs)/INC`CHECKPC=%#,1,XP};@attach u(xp)/INC`LIST=%q<t1>

&INC`LIST u(xp)=@pemit %#=header(name(%0) XP)%R[ljust(ansi(h,rjust(Current:,10)),10)][rjust(sub(u(setr,gained,u(u(xp)/FUN`COUNT,%0,G)),u(setr,spent,u(u(xp)/FUN`COUNT,%0,S))),4)]%r[ljust(ansi(h,rjust(Total:,10)),10)][rjust(%q<gained>,4)]%r[ljust(ansi(h,rjust(Spent:,10)),10)][rjust(%q<spent>,4)];@pemit %#=header()

&INC`LIST u(xp)=@pemit %#=header(name(%0) XP)%R[ljust(ansi(h,rjust(Current:,10)),10)][rjust(sub(u(setr,gained,u(u(xp)/FUN`COUNT,%0,G)),u(setr,spent,u(u(xp)/FUN`COUNT,%0,S))),4)]%r[ljust(ansi(h,rjust(Total:,10)),10)][rjust(%q<gained>,4)]%r[ljust(ansi(h,rjust(Spent:,10)),10)][rjust(%q<spent>,4)];@pemit %#=header()

&FUN`COUNT u(xp)=add(lmath(add,iter(u(lattr,%0/D`XP`%1`*),get(%0/%i0))),if(not(%2),switch(%1,G,u(FUN`EXTRA`GAINED,%0),S,u(FUN`EXTRA`SPENT,%0),0),0))
&FUN`TOTAL u(xp)=sub(u(FUN`COUNT,%0,G,%1),u(FUN`COUNT,%0,S,%2))

&INC`GAINED u(xp)=@select/inline cand(strlen(%0),isadmin(%#))=0,{@attach u(ccs)/INC`CHECKPC=%#,1,XP},1,{@attach u(ccs)/INC`CHECKPC=%0,1,XP};@attach u(xp)/INC`LISTBY=%q<t1>,G
&INC`SPENT u(xp)=@select/inline cand(strlen(%0),isadmin(%#))=0,{@attach u(ccs)/INC`CHECKPC=%#,1,XP},1,{@attach u(ccs)/INC`CHECKPC=%0,1,XP};@attach u(xp)/INC`LISTBY=%q<t1>,S

&INC`LISTBY u(xp)=@pemit %#=header(name(%0)'s [if(strmatch(%1,G),GAINED,SPENT)] XP);@dolist/inline sortkey(#lambda/last(\%0,`),u(lattr,%0/D`XP`%1`*))=@pemit %#=#[ljust(last(%i0,`),4)][ansi(h,default(%0/%i0,N/A))] for [ansi(h,default(%0/%i0`REASON,Unknown Reason))]%R%B%B%B%B%BHandled By%B[ansi(h,name(get(%0/%i0`BY)))] On [convsecs(get(%0/%i0`ON))];@pemit %#=header()

&INC`DELGAINED u(xp)=@attach u(xp)/INC`DELETE=%0,G,%1
&INC`DELSPENT u(xp)=@attach u(xp)/INC`DELETE=%0,S,%1

&INC`DELETE u(xp)=@attach u(ccs)/INC`CHECKPC=%0,1,XP;@check valnum(%2)=@pemit %#=XP entries must be addressed via whole and positive numbers!;@check hasattr(%q<t1>/D`XP`%1`%2)=@pemit %#=ansi(h,%q<t1name>) does not have that entry!;@select/inline v(VAR`ALERTSMODE)=1,{@nscemit/noisy u(CMO`STAFFREP)=ansi(h,%n) just removed [ansi(h,%q<t1name>)]'s XP [if(strmatch(%1,G),GAINED,SPENT)] Entry #%2: [get(%q<t1>/D`XP`%1`%2)] - [get(%q<t1>/D`XP`%1`%2`REASON)]};@pemit %#=announce(XP) You just removed [ansi(h,%q<t1name>)]'s XP [if(strmatch(%1,G),GAINED,SPENT)] Entry #%2: [get(%q<t1>/D`XP`%1`%2)] - [get(%q<t1>/D`XP`%1`%2`REASON)];@select/inline 1=not(strmatch(%q<t1>,%#)),{@pemit %q<t1>=announce(XP) [ansi(h,%n)] just removed your XP [if(strmatch(%1,G),GAINED,SPENT)] Entry #%2: [get(%q<t1>/D`XP`%1`%2)] - [get(%q<t1>/D`XP`%1`%2`REASON)]};@wipe %q<t1>/D`XP`%1`%2

&INC`TOTALS u(xp)=@pemit %#=header(mudname() XP Totals);@pemit %#=ansi(hc,Name[space(29)]Gained XP[space(3)]Splat);@dolist/inline revwords(sortkey(#lambda/u(u(xp)/FUN`COUNT,\%0,G),lsearch(all,eplayer,\[cand(u(u(xp)/FUN`COUNT,##,G),not(isadmin(##)),approved(##))\]),n))=@pemit %#=ljust(left(name(%i0),32),33)[ljust(rjust(u(u(xp)/FUN`COUNT,%i0,G),6),12)][getsplat(%i0)];@pemit %#=header()

&INC`AWARD u(xp)=@attach u(xp)/INC`MARKXP=%0,%1,%2,AWARD
&INC`SPEND u(xp)=@attach u(xp)/INC`MARKXP=%0,%1,%2,SPEND

&INC`MARKXP u(xp)=@check strlen(%0)=@pemit %#=announce(XP) ERROR: No targets entered!;@stop words(filterbool(#lambda/strmatch(\%0,#-*),u(setr,list,namelist(%0,#lambda/nspemit(%#,announce(XP) Target Not Found or Ambiguous: \%0)))))=@pemit %#=announce(XP) ERROR: One or more entered names did not resolve. Please check your entry and try again.;@check strlen(%1)=@pemit %#=announce(XP) ERROR: No XP entered to %3!;@check gt(%1,0)=@pemit %#=announce(XP) ERROR: XP amounts must be whole, positive numbers!;@pemit/list %q<list>=announce(XP) You have [switch(%3,AWARD,been awarded,spent)] %1 XP for: [strfirstof(%2,No reason given.)];@pemit %#=announce(XP) You have [switch(%3,AWARD,awarded [ansi(h,itemize(iter(%q<list>,name(%i0),%b,|),|,and,\,))] %1 XP,deducted %1 XP from [ansi(h,itemize(iter(%q<list>,name(%i0),%b,|),|,and,\,))])] for: [strfirstof(%2,No reason given.)];@dolist/inline %q<list>=@attach u(xp)/INC`APPLY=%i0,%1,[strfirstof(%2,No reason given.)],%:,switch(%3,AWARD,G,S);@select/inline v(VAR`ALERTSMODE)=1,{@nscemit/noisy u(CMO`STAFFREP)=ansi(h,%n) just [switch(%3,AWARD,awarded [ansi(h,itemize(iter(%q<list>,name(%i0),%b,|),|,and,\,))] %1 XP,deducted %1 XP from [ansi(h,itemize(iter(%q<list>,name(%i0),%b,|),|,and,\,))])] for: [strfirstof(%2,No reason given.)]}

&VAR`ALERTSMODE u(xp)=1

&INC`APPLY u(xp)=&[u(setr,attr,D`XP`%4`[nextslot(%0,D`XP`%4)])] %0=%1;&%q<attr>`ON %0=secs();&%q<attr>`BY %0=%3;&%q<attr>`REASON %0=%2

@@ ROLEPLAYING - +XP
+help/addmain Roleplaying/+xp=[u(xp)]/HLP`+XP
&HLP`+XP u(xp)=[ansi(hc,Aliases:)] xp%R%R[ansi(hc,Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+xp)] - Show how much XP you have.%R[ansi(h,+xp/gained)] - Show details on your gained XP.%R[ansi(h,+xp/spent)] - Show details on your XP spends.})]

+shelp/addmain Character/+xp=[u(xp)]/SHLP`+XP
&SHLP`+XP u(xp)=[ansi(hc,Viewing Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+xp <name>)] - check a player's XP totals.%R[ansi(h,+xp/gained <name>)] - Check a player's gained XP details.%R[ansi(h,+xp/spent <name>)] - Check a player's XP spend details.})]%R%R[ansi(hc,Managing Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+xp/delgained <name>=<#>)] - Remove a player's gained entry.%R[ansi(h,+xp/delspent <name>=<#>)] - Remove a player's spent entry.%R[ansi(h,+xp/award <list of player>=<amount>/<reason>)] - Awards XP to one or more players. Reason is optional.%R[ansi(h,+xp/spend <list of players>=<amount>/<reason>)] - Spend one or more players' XP. Reason is optional.%R%RNOTE: 'Current XP' is defined as 'gained - spent' while 'Total' is 'total gained ever.' To 'refund' XP, don't award it - delete the relevant spend records then make new ones.})]