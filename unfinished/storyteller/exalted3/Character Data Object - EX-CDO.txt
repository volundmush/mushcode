@@ Advantages

&INC`SET`SPLAT [u(cobj,data)]=@attach [u(cobj,data)]/INC`SET`SPHERE
&SPHERES [u(cobj,data)]=SOLAR|MORTAL

&INC`SET`CASTE [u(cobj,data)]=@attach [u(cobj,data)]/DO`SET`CASTE;@attach %!/DO`ANNOUNCE=%0,{ansi(h,name(%0))'s [u(setr,msg,Caste is now set to: [ansi(h,u(capnames,%q<find>))])]},{Your %q<msg>},{ansi(h,name(%0))'s %q<msg>}

&DO`VAL`CASTE [u(cobj,data)]=th u(setq,splat,u(getsphere,%0));@check words(u(setr,castes,get(u(cobj,data)/CASTE`%q<splat>)),|)=@attach %!/INC`MSG=[u(capnames,%q<splat>)]s do not have Castes.;@check strlen(%2)=@attach %!/INC`MSG=ERROR: No Caste name entered.;@attach u(ccs)/INC`PARTIAL=%2,%q<castes>,|,find,caste
&DO`SET`CASTE [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`CASTE;th u(setstat,%0/D`INFO,Caste,u(capnames,%q<find>))

&CASTE`SOLAR [u(cobj,data)]=DAWN|ZENITH|TWILIGHT|NIGHT|ECLIPSE
&CASTE`LUNAR [u(cobj,data)]=FULL MOON|CHANGING MOON|NO MOON|CASTELESS
&CASTE`TERRESTRIAL [u(cobj,data)]=AIR|EARTH|FIRE|WATER|WOOD
&CASTE`ABYSSAL [u(cobj,data)]=DUSK|MIDNIGHT|DAYBREAK|DAY|MOONSHADOW
&CASTE`LIMINAL [u(cobj,data)]=BLOOD|BREATH|FLESH|MARROW|SOIL
&CASTE`SIDEREAL [u(cobj,data)]=JOURNEYS|ENDINGS|BATTLES|SECRETS|SERENITY

&CASTE`ALCHEMICAL [u(cobj,data)]=ORICHALCUM|MOONSILVER|JADE|SOULSTEEL|STARMETAL|ADAMANT
&CASTE`DRAGON-KING [u(cobj,data)]=ANKLOK|MOSOK|PTEROK|RAPTOK
&CASTE`GOD-BLOODED [u(cobj,data)]=DIVINE|GHOST|DEMON|FAE|SOLAR|LUNAR|SIDEREAL|ABYSSAL|INFERNAL
&CASTE`INFERNAL [u(cobj,data)]=SLAYER|FIEND|SCOURGE|MALEFACTOR|DEFILER
&CASTE`JADEBORN [u(cobj,data)]=ARTISAN|WORKER|WARRIOR
&CASTE`RAKSHA [u(cobj,data)]=DIPLOMAT|COURTIER|IMPERIAL RAKSHA|SCRIBE|ENTERTAINER|LUMINARY|ESHU|ORNAMENTAL RAKSHA|WARRIOR|ANARCH|XIA|CATAPHRACT|WORKER|PANJANDRUM|ARTISAN|STRATEGOS|GUIDE|HARBINGER|VAGABOND|NOMAD|FERRYMAN|HERALD|SKALD|DRAGOON|ATTENDANT
&CASTE`SPIRIT [u(cobj,data)]=GOD|DEMON

@@ Attributes

&INC`SET`ATTRIBUTE [u(cobj,data)]=@attach [u(cobj,data)]/DO`SET`ATTRIBUTE=%0,elements(%1,2,|),%2;@select/inline t(strlen(%q<entry>))=0,{@attach %!/DO`ANNOUNCE=%0,{ansi(h,name(%0))'s [u(setr,msg,'[ansi(h,u(capnames,%q<find>))]' Attribute was cleared.)]},{Your %q<msg>},{ansi(h,name(%0))'s %q<msg>}},1,{@attach %!/DO`ANNOUNCE=%0,{ansi(h,name(%0))'s [u(setr,msg,'[ansi(h,u(capnames,%q<find>))]' Attribute was set to: [ansi(h,%q<entry>)].)]},{Your %q<msg>},{ansi(h,name(%0))'s %q<msg>}}

&DO`VAL`ATTRIBUTE [u(cobj,data)]=@check strlen(%1)=@attach %!/INC`MSG=ERROR: No Attribute name entered.;@attach %!/INC`PARTIAL=%1,u(u(cobj,data)/FUN`GET`ATTRIBUTES),|,find,attribute;@select/inline strlen(u(setr,entry,%2))=>0,{@check u(valnum,%2)=@attach %!/INC`MSG=ERROR: Attributes must be positive integers.}
&DO`SET`ATTRIBUTE [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`ATTRIBUTE;@attach [u(cobj,data)]/KEYVALUE`SET=%0,D`ATTRIBUTES,%q<find>,%q<entry>

&FUN`GET`ATTRIBUTES [u(cobj,data)]=v(ATTRIBUTES)
&FUN`RET`ATTRIBUTES [u(cobj,data)]=iter(ucstr(get(%0/D`ATTRIBUTES)),before(%i0,~),|,|)

&ATTRIBUTES [u(cobj,data)]=STRENGTH|CHARISMA|PERCEPTION|DEXTERITY|MANIPULATION|INTELLIGENCE|STAMINA|APPEARANCE|WITS

@@ Abilities

&INC`SET`ABILITY [u(cobj,data)]=@attach [u(cobj,data)]/DO`SET`ABILITY=%0,elements(%1,2,|),%2;@select/inline t(strlen(%q<entry>))=0,{@attach %!/DO`ANNOUNCE=%0,{ansi(h,name(%0))'s [u(setr,msg,'[ansi(h,u(capnames,%q<find>))]' Ability was cleared.)]},{Your %q<msg>},{ansi(h,name(%0))'s %q<msg>}},1,{@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))]'s [u(setr,msg,'[ansi(h,u(capnames,%q<find>))]' Ability was set to: [ansi(h,%q<entry>)].)]},{Your %q<msg>},{[ansi(h,name(%0))]'s %q<msg>}}

&DO`VAL`ABILITY [u(cobj,data)]=@check strlen(%1)=@attach %!/INC`MSG=ERROR: No Ability name entered.;@attach u(ccs)/INC`PARTIAL=%1,u([u(cobj,data)]/FUN`GET`ABILITIES),|,find,ability;@select/inline strlen(u(setr,entry,%2))=>0,{@check u(valnum,%2)=@attach %!/INC`MSG=ERROR: Abilities must be positive integers.}
&DO`SET`ABILITY [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`ABILITY;@select/inline strlen(%q<entry>)=0,{th u(delstat,%0/D`ABILITIES,ucstr(%q<find>))},{th u(setstat,%0/D`ABILITIES,ucstr(%q<find>),%q<entry>)}

&FUN`GET`ABILITIES [u(cobj,data)]=get(u(cobj,data)/VAR`ABILITIES)
&FUN`RET`ABILITIES [u(cobj,data)]=iter(ucstr(get(%0/D`ABILITIES)),before(%i0,~),|,|)

&FUN`GET`ABILITIES`EXTRA [u(cobj,data)]=ARCHERY|BRAWL|MARTIAL ARTS|MELEE|THROWN|WAR|BUREAUCRACY|LINGUISTICS|RIDE|SAIL|SOCIALIZE|ATHLETICS|AWARENESS|DODGE|LARCENY|STEALTH|CRAFT|INVESTIGATION|LORE|MEDICINE|OCCULT|INTEGRITY|PERFORMANCE|PRESENCE|RESISTANCE|SURVIVAL

&VAR`ABILITIES [u(cobj,data)]=ARCHERY|BRAWL|MELEE|THROWN|WAR|BUREAUCRACY|LINGUISTICS|RIDE|SAIL|SOCIALIZE|ATHLETICS|AWARENESS|DODGE|LARCENY|STEALTH|CRAFT|INVESTIGATION|LORE|MEDICINE|OCCULT|INTEGRITY|PERFORMANCE|PRESENCE|RESISTANCE|SURVIVAL

&INC`SET`STYLES [u(cobj,data)]=@attach [u(cobj,data)]/DO`SET`STYLES=%0,elements(%1,2,|),%2;@select/inline t(strlen(%q<entry>))=0,{@attach %!/DO`ANNOUNCE=%0,{ansi(h,name(%0))'s [u(setr,msg,'[ansi(h,u(capnames,%q<find>))]' Style was cleared.)]},{Your %q<msg>},{ansi(h,name(%0))'s %q<msg>}},1,{@attach %!/DO`ANNOUNCE=%0,{ansi(h,name(%0))'s [u(setr,msg,'[ansi(h,u(capnames,%q<find>))]' Style was set to: [ansi(h,%q<entry>)].)]},{Your %q<msg>},{ansi(h,name(%0))'s %q<msg>}}

&DO`VAL`STYLES [u(cobj,data)]=@check strlen(%1)=@attach %!/INC`MSG=ERROR: No Style name entered.;@attach u(ccs)/INC`PARTIAL=%1,u([u(cobj,data)]/FUN`GET`STYLES),|,EDITCHAR,find,style;@select/inline t(strlen(u(setr,entry,%2)))=1,{@check u(valnum,%2)=@attach %!/INC`MSG=ERROR: Styles must be positive integers.}
&DO`SET`STYLES [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`STYLES;@select/inline strlen(%q<entry>)=0,{th u(delstat,%0/D`STYLES,ucstr(%q<find>))},{th u(setstat,%0/D`STYLES,ucstr(%q<find>),%q<entry>)}

&INC`SET`CRAFTS [u(cobj,data)]=@attach [u(cobj,data)]/DO`SET`CRAFTS=%0,elements(%1,2,|),%2;@select/inline t(strlen(%q<entry>))=0,{@attach %!/DO`ANNOUNCE=%0,{ansi(h,name(%0))'s [u(setr,msg,'[ansi(h,u(capnames,%q<find>))]' Craft was cleared.)]},{Your %q<msg>},{ansi(h,name(%0))'s %q<msg>}},1,{@attach %!/DO`ANNOUNCE=%0,{ansi(h,name(%0))'s [u(setr,msg,'[ansi(h,u(capnames,%q<find>))]' Craft was set to: [ansi(h,%q<entry>)].)]},{Your %q<msg>},{ansi(h,name(%0))'s %q<msg>}}

&DO`VAL`CRAFTS [u(cobj,data)]=@check strlen(%1)=@attach %!/INC`MSG=ERROR: No Craft name entered.;@attach u(ccs)/INC`PARTIAL=%1,u([u(cobj,data)]/FUN`GET`CRAFTS),|,EDITCHAR,find,Craft;@select/inline t(strlen(u(setr,entry,%2)))=1,{@check u(valnum,%2)=@attach %!/INC`MSG=ERROR: Crafts must be positive integers.}
&DO`SET`CRAFTS [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`CRAFTS;@select/inline strlen(%q<entry>)=0,{th u(delstat,%0/D`CRAFTS,ucstr(%q<find>))},{th u(setstat,%0/D`CRAFTS,ucstr(%q<find>),%q<entry>)}

&FUN`RET`CRAFTS [u(cobj,data)]=iter(ucstr(get(%0/D`CRAFTS)),before(%i0,~),|,|)

&FUN`GET`CRAFTS [u(cobj,data)]=v(VAL`CRAFTS)
&VAL`CRAFTS [u(cobj,data)]=ARTIFACTS|GEOMANCY|FIRST AGE ARTIFICE

@@ Specialties

&INC`SET`SPECIALTY [u(cobj,data)]=@attach [u(cobj,data)]/Do`SET`SPECIALTY=%0,elements(%1,2,|),%2;@select/inline t(%q<entry>)=1,{@attach %!/DO`ANNOUNCE=%0,{u(setr,msg,ansi(h,name(%0)) now has the [u(setr,have,ansi(h,u(capnames,%q<find>/%q<spec>)) Specialty at [ansi(h,%q<entry>)])] )},{You now have the %q<have>},{%q<msg>}},0,{@attach %!/DO`ANNOUNCE=%0,{u(setr,msg,ansi(h,name(%0)) has lost the [u(setr,have,ansi(h,u(capnames,%q<find>/%q<spec>)) Specialty)] )},{You have lost the %q<have>},{%q<msg>}}

&DO`VAL`SPECIALTY [u(cobj,data)]=@check strlen(u(setr,enstat,before(%1,/)))=@attach %!/INC`MSG=ERROR: No stat entered to specialize in.;@attach u(ccs)/INC`PARTIAL=%q<enstat>,setunion(setunion(u([u(cobj,data)]/FUN`GET`ABILITIES),MARTIAL ARTS,|,i,|),u([u(cobj,data)]/FUN`GET`ATTRIBUTES),|,i,|),|,EDITCHAR,find,stat;@check strlen(u(setr,spec,after(%1,/)))=@attach %!/INC`MSG=ERROR: No Specialty name entered.;@select/inline t(strlen(u(setr,entry,%2)))=1,{@check u(valnum,%q<entry>)=@attach %!/INC`MSG=ERROR: Specialty values must be integers.}
&DO`SET`SPECIALTY [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`SPECIALTY;@select/inline t(%q<entry>)=0,{th u(delstat,%0/D`SPECIALTIES,ucstr(%q<find>/%q<spec>))},1,{th u(setstat,%0/D`SPECIALTIES,ucstr(%q<find>/%q<spec>),%q<entry>)}

&FUN`RET`SPECIALTIES [u(cobj,data)]=iter(ucstr(get(%0/D`SPECIALTIES)),before(%i0,~),|,|)

@@ Merits and Flaws


&VAR`MERITS [u(cobj,data)]=ALLIES|AMBIDEXTROUS|ARTIFACT|BACKING|BOUNDLESS ENDURANCE|COMMAND|CONTACTS|CULT|DANGER SENSE|DEMESNE|DIRECTION SENSE|EIDETIC MEMORY|FAMILIAR|FAST REFLEXES|FLEET OF FOOT|FOLLOWERS|GIANT|HEARTHSTONE|HIDEOUS|INFLUENCE|IRON STOMACH|LANGUAGE|MANSE|MENTOR|MARTIAL ARTIST|MIGHTY THEW|NATURAL IMMUNITY|PAIN TOLERANCE|QUICK DRAW|RETAINERS|RESOURCES|RESOURCES|SELECTIVE CONTRACEPTION|STRONG LUNGS|TEMPERED BY THE ELEMENTS|TOXIN RESISTANCE|CHAMELEON|CLAWS|FANGS|HOOVES|HORNS|ENHANCED SENSE|EXALTED HEALING|EXTRA LIMBS|GILLS|POISONED BODY|QUILLS|SUBTLETY|TAIL|THAUMATURGIST|UNUSUAL HIDE|VENOMOUS|WALL WALKING|WINGS
&VAR`FLAWS [u(cobj,data)]=ADDICTION|AMPUTEE|BLIND|DEAF|DERANGEMENT|MUTE|STERILE|WYLD MUTANT

@@ Native Charms

&GFN`CHARMTYPE [u(cobj,data)]=reswitchi(u(getsphere,%0),^(SOLAR|LUNAR|ABYSSAL|ALCHEMICAL|INFERNAL|TERRESTIAL|RAKSHA|SIDEREAL|GHOST|SPIRIT|MORTAL|JADEBORN)$,u(getsphere,%0),^(GOD-BLOODED)$,reswitchi(u(getstat,%0/D`INFO,Caste),^(Divine)$,Spirit,^(Demon)$,Spirit,^(Fae)$,Raksha,^(SOLAR|LUNAR|ABYSSAL|INFERNAL|SIDEREAL)$,u(getstat,%0/D`INFO,Caste),Mortal),MORTAL)

&INC`ADD`CHARM [u(cobj,data)]=@attach [u(cobj,data)]/DO`ADD`CHARM=%0,charmtype(%0)/[elements(%1,2,|)],%2;@attach [u(cobj,data)]/INC`ADD`CHARM`REPORT
&INC`REM`CHARM [u(cobj,data)]=@attach [u(cobj,data)]/DO`REM`CHARM=%0,charmtype(%0)/[elements(%1,2,|)],%2;@attach [u(cobj,data)]/INC`REM`CHARM`REPORT

&INC`ADD`CHARM`REPORT [u(cobj,data)]=@attach %!/DO`ANNOUNCE=%0,{u(setr,msg,ansi(h,name(%0)) now has the [u(setr,have,ansi(h,u(capnames,%q<find> %q<find2>)) Charms [itemize(iter(%q<entry>,ansi(h,u(capnames,%i0)),|,|),|,and,\,)])])},{You now have the %q<have>},{%q<msg>}
&INC`REM`CHARM`REPORT [u(cobj,data)]=@attach %!/DO`ANNOUNCE=%0,{u(setr,msg,ansi(h,name(%0)) no longer have the [u(setr,have,ansi(h,u(capnames,%q<find> %q<find2>)) Charms: [itemize(iter(%q<entry>,ansi(h,u(capnames,%i0)),|,|),|,and,\,)])])},{You no longer have the %q<have>},{%q<msg>}

&INC`ADD`OCHARM [u(cobj,data)]=@attach [u(cobj,data)]/DO`ADD`CHARM=%0,elements(%1,2,|)/[elements(%1,3,|)],%2;@attach [u(cobj,data)]/INC`ADD`CHARM`REPORT
&INC`REM`OCHARM [u(cobj,data)]=@attach [u(cobj,data)]/DO`REM`CHARM=%0,elements(%1,2,|)/[elements(%1,3,|)],%2;@attach [u(cobj,data)]/INC`REM`CHARM`REPORT

&DO`VAL`CHARM [u(cobj,data)]=@check strlen(before(%1,/))=@attach %!/INC`MSG=ERROR: No Charmtype entered.;@attach u(ccs)/INC`PARTIAL=before(%1,/),u([u(cobj,data)]/FUN`GET`CHARMTYPES),|,v(VAR`MSGHEAD),find,charmtype;@check strlen(after(%1,/))=@attach %!/INC`MSG=ERROR: No Charm Category entered.;@attach u(ccs)/INC`PARTIAL=after(%1,/),u([u(cobj,data)]/FUN`GET`CHARMTYPES`%q<find>),|,v(VAR`MSGHEAD),find2,Charm Category;@check strlen(%2)=@attach %!/INC`MSG=ERROR: No Charms entered.;@check strlen(u(setr,entry,filterbool(#lambda/strlen(\%0),%2,|,|)))=@attach %!/INC`MSG=ERROR: Charm entries invalid.
&DO`ADD`CHARM [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`CHARM;@stop words(u(setr,conflict,setinter(u(setr,oldcharms,ucstr(get(%0/D`CHARMS`%q<find>`%q<find2>))),ucstr(%q<entry>),|,i,|)),|)=@attach %!/INC`MSG=[switch(%0,%#,You already have,name(%0) already has)] [ansi(h,u(capnames,%q<find> %q<find2>))] Charms [itemize(u(capnames,%q<conflict>),|,and,\,)].;&D`CHARMS`%q<find>`%q<find2> %0=u(setr,newcharms,setunion(%q<oldcharms>,ucstr(%q<entry>),|,i,|))
&DO`REM`CHARM [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`CHARM;@stop words(u(setr,conflict,setdiff(ucstr(%q<entry>),u(setr,oldcharms,ucstr(get(%0/D`CHARMS`%q<find>`%q<find2>))),|,i,|)),|)=@attach %!/INC`MSG=[switch(%0,%#,You don't have have,name(%0) doesn't have)] [ansi(h,u(capnames,%q<find> %q<find2>))] Charms [itemize(u(capnames,%q<conflict>),|,and,\,)].;&D`CHARMS`%q<find>`%q<find2> %0=u(setr,newcharms,setdiff(get(%0/D`CHARMS`%q<find>`%q<find2>),ucstr(%q<entry>),|,i,|));@select/inline strlen(%q<newcharms>)=0,{@wipe %0/D`CHARMS`%q<find>`%q<find2>}

&FUN`GET`CHARMTYPES [u(cobj,data)]=SOLAR|ABYSSAL|TERRESTRIAL|LUNAR|SIDEREAL|INFERNAL|SPIRIT

&FUN`GET`CHARMTYPES`SOLAR [u(cobj,data)]=u(FUN`GET`ABILITIES)
&FUN`GET`CHARMTYPES`ABYSSAL [u(cobj,data)]=u(FUN`GET`ABILITIES)
&FUN`GET`CHARMTYPES`TERRESTRIAL [u(cobj,data)]=u(FUN`GET`ABILITIES)
&FUN`GET`CHARMTYPES`LUNAR [u(cobj,data)]=u(FUN`GET`ATTRIBUTES)
&FUN`GET`CHARMTYPES`SIDEREAL [u(cobj,data)]=u(FUN`GET`ABILITIES)
&FUN`GET`CHARMTYPES`INFERNAL [u(cobj,data)]=u(FUN`GET`ABILITIES)
&FUN`GET`CHARMTYPES`SPIRIT [u(cobj,data)]=UNIVERSAL

@@ &VAR`CHARMS`ALCHEMICAL [u(cobj,data)]=COMBAT|SURVIVAL|SPEED_AND_MOBILITY|SOCIAL|STEALTH_AND_DISGUISE|ANALYTIC_AND_COGNITIVE|LABOR_AND_UTILITY|SUBMODULES|GENERAL|MASS_COMBAT|SPIRITUAL
@@ &VAR`CHARMS`RAKSHA [u(cobj,data)]=MASK|HEART|CUP|RING|STAFF|SWORD|WAY
@@ &VAR`CHARMS`SPIRIT [u(cobj,data)]=GENERAL|UNIVERSAL|AEGIS|BLESSINGS|CURSES|DIVINATIONS|DIVINE_WORKS|EDGES|EIDOLA|ENCHANTMENTS|INHABITINGS|RELOCATIONS|SENDINGS|TANTRA
@@ &VAR`CHARMS`MORTAL [u(cobj,data)]=UNIVERSAL
@@ &VAR`CHARMS`GHOST [u(cobj,data)]=UNIVERSAL
@@ &VAR`CHARMS`JADEBORN [u(cobj,data)]=FOUNDATION|WORKER|WARRIOR|ARTISAN|ENLIGHTENED|CHAOS

@@ Martial Arts Charms

&INC`ADD`MACHARM [u(cobj,data)]=@attach [u(cobj,data)]/DO`ADD`MACHARM;@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))] [u(setr,have,gained [ansi(h,u([u(cobj,data)]/FUN`GET`STYLENAME,%0,%q<slot>))] Charms: [itemize(iter(%q<entry>,ansi(h,u(capnames,%i0)),|,|),|,and,\,)])]},{You %q<have>},{ansi(h,name(%0)) gained %q<have>}

&INC`REM`MACHARM [u(cobj,data)]=@attach [u(cobj,data)]/DO`REM`MACHARM;@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))] [u(setr,have,lost [ansi(h,u([u(cobj,data)]/FUN`GET`STYLENAME,%0,%q<slot>))] Charms: [itemize(iter(%q<entry>,ansi(h,u(capnames,%i0)),|,|),|,and,\,)])]},{You %q<have>},{ansi(h,name(%0)) lost %q<have>}

&DO`VAL`MACHARM [u(cobj,data)]=@check strlen(%1)=@attach %!/INC`MSG=ERROR: No Style entered.;@check words(u(setr,styles,u([u(cobj,data)]/FUN`RET`STYLES,%0)),|)=@attach %!/INC`MSG=ERROR: [switch(%0,%#,You don't,name(%0) doesn't)] know any styles.;@attach u(ccs)/INC`PARTIAL=%1,%q<styles>,|,v(VAR`MSGHEAD),style,style;th u(setq,slot,strfirstof(elements(first(wildgrepi(%0,D`CHARMS`MACHARMS`*`NAME,%q<style>)),4,`,`),nextslot(%0,D`CHARMS`MACHARMS)));@check strlen(%2)=@attach %!/INC`MSG=ERROR: No Charms entered.;@check strlen(u(setr,entry,filterbool(#lambda/strlen(\%0),%2,|,|)))=@attach %!/INC`MSG=ERROR: Charm entries invalid.

&DO`ADD`MACHARM [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`MACHARM;@stop words(u(setr,conflict,setinter(u(setr,oldcharms,ucstr(get(%0/D`CHARMS`MACHARMS`%q<slot>))),ucstr(%q<entry>),|,i,|)),|)=@attach %!/INC`MSG=[switch(%0,%#,You already have,name(%0) already has)] [u([u(cobj,data)]/FUN`RET`STYLENAME,%0,%q<slot>)] Charms [itemize(u(capnames,%q<conflict>),|,and,\,)].;&D`CHARMS`MACHARMS`%q<slot> %0=u(setr,newcharms,setunion(%q<oldcharms>,ucstr(%q<entry>),|,i,|));&D`CHARMS`MACHARMS`%q<slot>`NAME %0=ucstr(%q<style>)

&DO`REM`MACHARM [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`MACHARM;@stop words(u(setr,conflict,setdiff(ucstr(%q<entry>),u(setr,oldcharms,ucstr(get(%0/D`CHARMS`MACHARMS`%q<slot>))),|,i,|)),|)=@attach %!/INC`MSG=[switch(%0,%#,You don't have have,name(%0) doesn't have)] [u([u(cobj,data)]/FUN`RET`STYLENAME,%0,%q<slot>)] Charms [itemize(u(capnames,%q<conflict>),|,and,\,)].;&D`CHARMS`MACHARMS`%q<slot> %0=u(setr,newcharms,setdiff(get(%0/D`CHARMS`MACHARMS`%q<slot>),ucstr(%q<entry>),|,i,|));@select/inline strlen(%q<newcharms>)=0,{@wipe %0/D`CHARMS`MACHARMS`%q<slot>}

&FUN`GET`STYLES [u(cobj,data)]=setdiff(v(VAR`STYLES),v(VAR`STYLES`BANNED),|,i,|)

&VAR`STYLES [u(cobj,data)]=SNAKE STYLE|TIGER STYLE|SINGLE POINT SHINING INTO THE VOID STYLE|WHITE REAPER STYLE|EBON SHADOW STYLE|CRANE STYLE|SILVER-VOICED NIGHTINGALE STYLE|RIGHTEOUS DEVIL STYLE|BLACK CLAW STYLE|DREAMING PEARL COURTESAN STYLE|STEEL DEVIL STYLE
&VAR`STYLES`BANNED [u(cobj,data)]=

&FUN`RET`STYLES [u(cobj,data)]=iter(ucstr(get(%0/D`STYLES)),before(%i0,~),|,|)
&FUN`RET`STYLENAME [u(cobj,data)]=u(capnames,default(%0/D`CHARMS`MACHARMS`%1`NAME,Unnamed Style))

@@ Evocations
&INC`ADD`EVOCATION [u(cobj,data)]=@attach [u(cobj,data)]/DO`ADD`EVOCATION=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))] [u(setr,have,gained [ansi(h,u([u(cobj,data)]/FUN`RET`EVOCNAME,%0,%q<slot>))] Evocations: [itemize(iter(%q<entry>,ansi(h,u(capnames,%i0)),|,|),|,and,\,)])]},{You %q<have>},{ansi(h,name(%0)) gained %q<have>}

&INC`REM`EVOCATION [u(cobj,data)]=@attach [u(cobj,data)]/DO`REM`EVOCATION=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{[ansi(h,name(%0))] [u(setr,have,lost [ansi(h,u([u(cobj,data)]/FUN`RET`EVOCNAME,%0,%q<slot>))] Evocations: [itemize(iter(%q<entry>,ansi(h,u(capnames,%i0)),|,|),|,and,\,)])]},{You %q<have>},{ansi(h,name(%0)) lost %q<have>}

&DO`VAL`EVOCATION [u(cobj,data)]=@check strlen(u(setr,style,%1))=@attach %!/INC`MSG=ERROR: No Style entered.;th u(setq,slot,strfirstof(elements(first(wildgrepi(%0,D`CHARMS`EVOCATIONS`*`NAME,%q<style>)),4,`,`),nextslot(%0,D`CHARMS`EVOCATIONS)));@check strlen(%2)=@attach %!/INC`MSG=ERROR: No Charms entered.;@check strlen(u(setr,entry,filterbool(#lambda/strlen(\%0),%2,|,|)))=@attach %!/INC`MSG=ERROR: Charm entries invalid.

&DO`ADD`EVOCATION [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`EVOCATION;@stop words(u(setr,conflict,setinter(u(setr,oldcharms,ucstr(get(%0/D`CHARMS`EVOCATIONS`%q<slot>))),ucstr(%q<entry>),|,i,|)),|)=@attach %!/INC`MSG=[switch(%0,%#,You already have,name(%0) already has)] [u([u(cobj,data)]/FUN`RET`EVOCATIONS,%0,%q<slot>)] Charms [itemize(u(capnames,%q<conflict>),|,and,\,)].;&D`CHARMS`EVOCATIONS`%q<slot> %0=u(setr,newcharms,setunion(%q<oldcharms>,ucstr(%q<entry>),|,i,|));&D`CHARMS`EVOCATIONS`%q<slot>`NAME %0=ucstr(%q<style>)

&DO`REM`EVOCATION [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`EVOCATION;@stop words(u(setr,conflict,setdiff(ucstr(%q<entry>),u(setr,oldcharms,ucstr(get(%0/D`CHARMS`EVOCATIONS`%q<slot>))),|,i,|)),|)=@attach %!/INC`MSG=[switch(%0,%#,You don't have have,name(%0) doesn't have)] [u([u(cobj,data)]/FUN`RET`EVOCNAME,%0,%q<slot>)] Charms [itemize(u(capnames,%q<conflict>),|,and,\,)].;&D`CHARMS`EVOCATIONS`%q<slot> %0=u(setr,newcharms,setdiff(get(%0/D`CHARMS`EVOCATIONS`%q<slot>),ucstr(%q<entry>),|,i,|));@select/inline strlen(%q<newcharms>)=0,{@wipe %0/D`CHARMS`EVOCATIONS`%q<slot>}

&FUN`RET`EVOCNAME [u(cobj,data)]=u(capnames,default(%0/D`CHARMS`EVOCATIONS`%1`NAME,Unnamed Artifact))

@@ Health

&INC`ADD`HEALTH [u(cobj,data)]=@attach [u(cobj,data)]/DO`ADD`HEALTH;th u(setq,have,additional Health Levels: [sortby([u(cobj,data)]/FUN`HEALTHSORT,ucstr(%2))]);@attach %!/DO`ANNOUNCE=%0,{ansi(h,name(%0)) has gained %q<have>},{You have gained %q<have>},{[ansi(h,name(%0))] has gained %q<have>}

&INC`REM`HEALTH [u(cobj,data)]=@attach [u(cobj,data)]/DO`REM`HEALTH;th u(setq,have,Health Levels: [sortby([u(cobj,data)]/FUN`HEALTHSORT,ucstr(%2))]);@attach %!/DO`ANNOUNCE=%0,{ansi(h,name(%0)) has lost %q<have>},{You have lost %q<have>},{[ansi(h,name(%0))] has lost %q<have>}

&DO`VAL`HEALTH [u(cobj,data)]=@check strlen(%2)=@attach %!/INC`MSG=ERROR: Health levels field empty.;@check lmath(min,iter(ucstr(%2),or(isnum(%i0),regmatchi(%i0,^(I|D)$))))=@attach %!/INC`MSG=ERROR: %q<newhealth> contains an invalid value. Health values must be a negative number, I, or D.
&DO`ADD`HEALTH [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`HEALTH;&D`HEALTH %0=sortby([u(cobj,data)]/FUN`HEALTHSORT,cat(get(%0/D`HEALTH),ucstr(%2)))
&DO`REM`HEALTh [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`HEALTH;&D`HEALTH %0=sortby([u(cobj,data)]/FUN`HEALTHSORT,remove(get(%0/D`HEALTH),ucstr(%2)))

&FUN`HEALTHSORT [u(cobj,data)]=comp(u(FUN`HEALTHSORT`GET,%0),u(FUN`HEALTHSORT`GET,%1),N)

&FUN`HEALTHSORT`GET [u(cobj,data)]=switch(%0,-0,1,-1,2,-2,3,-4,4,I,5,D,6)

@@ Spells

&INC`ADD`SPELL [u(cobj,data)]=@attach [u(cobj,data)]/DO`ADD`SPELL=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{u(setr,msg,ansi(h,name(%0)) now has the [u(setr,have,ansi(h,u(capnames,%q<find>)) Spells [itemize(iter(%q<entry>,ansi(h,u(capnames,%i0)),|,|),|,and,\,)])])},{You now have the %q<have>},{%q<msg>}
&INC`REM`SPELL [u(cobj,data)]=@attach [u(cobj,data)]/DO`REM`SPELL=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{u(setr,msg,ansi(h,name(%0)) no longer have the [u(setr,have,ansi(h,u(capnames,%q<find>)) Spells: [itemize(iter(%q<entry>,ansi(h,u(capnames,%i0)),|,|),|,and,\,)])])},{You no longer have the %q<have>},{%q<msg>}

&DO`VAL`SPELL [u(cobj,data)]=@check strlen(%1)=@attach %!/INC`MSG=ERROR: No Spell Category entered.;@attach u(ccs)/INC`PARTIAL=%1,u([u(cobj,data)]/FUN`GET`SPELLS),|,v(VAR`MSGHEAD),find,Spell Category;@check strlen(%2)=@attach %!/INC`MSG=ERROR: No Spells entered.;@check strlen(u(setr,entry,filterbool(#lambda/strlen(\%0),%2,|,|)))=@attach %!/INC`MSG=ERROR: Spell entries invalid.

&DO`ADD`SPELL [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`SPELL;@stop words(u(setr,conflict,setinter(u(setr,oldcharms,ucstr(get(%0/D`SPELLS`%q<find>))),ucstr(%q<entry>),|,i,|)),|)=@attach %!/INC`MSG=[switch(%0,%#,You already have,name(%0) already has)] [ansi(h,u(capnames,%q<find>))] Spells [itemize(u(capnames,%q<conflict>),|,and,\,)].;&D`SPELLS`%q<find> %0=u(setr,newcharms,setunion(%q<oldcharms>,ucstr(%q<entry>),|,i,|))
&DO`REM`SPELL [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`SPELL;@stop words(u(setr,conflict,setdiff(ucstr(%q<entry>),u(setr,oldcharms,ucstr(get(%0/D`SPELLS`%q<find>))),|,i,|)),|)=@attach %!/INC`MSG=[switch(%0,%#,You don't have have,name(%0) doesn't have)] [ansi(h,u(capnames,%q<find>))] Spells [itemize(u(capnames,%q<conflict>),|,and,\,)].;&D`SPELLS`%q<find> %0=u(setr,newcharms,setdiff(get(%0/D`SPELLS`%q<find>),ucstr(%q<entry>),|,i,|));@select/inline strlen(%q<newcharms>)=0,{@wipe %0/D`SPELLS`%q<find>}

&FUN`GET`SPELLS [u(cobj,data)]=setdiff(v(VAR`SPELLS),v(VAR`SPELLS`BANNED),|,i,|)

&VAR`SPELLS [u(cobj,data)]=TERRESTRIAL|CELESTIAL|SOLAR|SHADOWLANDS|LABYRINTH|VOID
&VAR`SPELLS`BANNED [u(cobj,data)]=

@@ Favored

&INC`ADD`FAVORED [u(cobj,data)]=@attach [u(cobj,data)]/DO`ADD`FAVORED=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{u(setr,msg,ansi(h,name(%0)) now [u(setr,have,[ansi(h,u(capnames,%q<find>))] Favors: [itemize(iter(%q<entry>,ansi(h,u(capnames,%i0)),|,|),|,and,\,)])])},{You now %q<have>},{%q<msg>}
&INC`REM`FAVORED [u(cobj,data)]=@attach [u(cobj,data)]/DO`REM`FAVORED=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{u(setr,msg,ansi(h,name(%0)) no longer [u(setr,have,[ansi(h,u(capnames,%q<find>))] Favor: [itemize(iter(%q<entry>,ansi(h,u(capnames,%i0)),|,|),|,and,\,)])])},{You no longer %q<have>},{%q<msg>}

&DO`VAL`FAVORED [u(cobj,data)]=@check strlen(before(%1,/))=@attach %!/INC`MSG=ERROR: No Favored Type entered.;@attach u(ccs)/INC`PARTIAL=before(%1,/),u([u(cobj,data)]/FUN`GET`FAVORED),|,v(VAR`MSGHEAD),find,Favored Type;@check strlen(%2)=@attach %!/INC`MSG=ERROR: No [u(capnames,%q<find>)] entered.;th u(setq,find2,u([u(cobj,data)]/FUN`GET`FAVORED`%q<find>));th u(setq,entry,iter(ucstr(%2),grab(%q<find2>,if(strlen(%i0),%i0*),|),|,|));@check lmath(min,iter(%q<entry>,strlen(%i0),|,%b))=@attach %!/INC`MSG=ERROR: One or more [u(capnames,%q<find>)] didn't resolve. Please check your entry.

&DO`ADD`FAVORED [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`FAVORED;@stop words(u(setr,conflict,setinter(u(setr,oldcharms,ucstr(get(%0/D`FAVORED`%q<find>))),ucstr(%q<entry>),|,i,|)),|)=@attach %!/INC`MSG=[switch(%0,%#,You already have,name(%0) already has)] the [itemize(u(capnames,%q<conflict>),|,and,\,)] [ansi(h,u(capnames,%q<find>))] Favored.;&D`FAVORED`%q<find> %0=u(setr,newcharms,setunion(%q<oldcharms>,ucstr(%q<entry>),|,i,|))
&DO`REM`FAVORED [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`FAVORED;@stop words(u(setr,conflict,setdiff(ucstr(%q<entry>),u(setr,oldcharms,ucstr(get(%0/D`FAVORED`%q<find>))),|,i,|)),|)=@attach %!/INC`MSG=[switch(%0,%#,You don't have have,name(%0) doesn't have)] the [itemize(u(capnames,%q<conflict>),|,and,\,)] [ansi(h,u(capnames,%q<find>))] Favored.;&D`FAVORED`%q<find> %0=u(setr,newcharms,setdiff(get(%0/D`FAVORED`%q<find>),ucstr(%q<entry>),|,i,|));@select/inline strlen(%q<newcharms>)=0,{@wipe %0/D`FAVORED`%q<find>}

&FUN`GET`FAVORED [u(cobj,data)]=ATTRIBUTES|ABILITIES
&FUN`GET`FAVORED`ATTRIBUTES [u(cobj,data)]=u(FUN`GET`ATTRIBUTES)
&FUN`GET`FAVORED`ABILITIES [u(cobj,data)]=u(FUN`GET`ABILITIES)

@@ Supernal

&INC`ADD`SUPERNAL [u(cobj,data)]=@attach [u(cobj,data)]/DO`ADD`SUPERNAL=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{u(setr,msg,ansi(h,name(%0)) now [u(setr,have,[ansi(h,u(capnames,%q<find>))] Supernals: [itemize(iter(%q<entry>,ansi(h,u(capnames,%i0)),|,|),|,and,\,)])])},{You now %q<have>},{%q<msg>}
&INC`REM`SUPERNAL [u(cobj,data)]=@attach [u(cobj,data)]/DO`REM`SUPERNAL=%0,elements(%1,2,|),%2;@attach %!/DO`ANNOUNCE=%0,{u(setr,msg,ansi(h,name(%0)) no longer [u(setr,have,[ansi(h,u(capnames,%q<find>))] Supernals: [itemize(iter(%q<entry>,ansi(h,u(capnames,%i0)),|,|),|,and,\,)])])},{You no longer %q<have>},{%q<msg>}

&DO`VAL`SUPERNAL [u(cobj,data)]=@check strlen(before(%1,/))=@attach %!/INC`MSG=ERROR: No Favored Type entered.;@attach u(ccs)/INC`PARTIAL=before(%1,/),u([u(cobj,data)]/FUN`GET`SUPERNAL),|,v(VAR`MSGHEAD),find,Favored Type;@check strlen(%2)=@attach %!/INC`MSG=ERROR: No [u(capnames,%q<find>)] entered.;th u(setq,find2,u([u(cobj,data)]/FUN`GET`SUPERNAL`%q<find>));th u(setq,entry,iter(ucstr(%2),grab(%q<find2>,if(strlen(%i0),%i0*),|),|,|));@check lmath(min,iter(%q<entry>,strlen(%i0),|,%b))=@attach %!/INC`MSG=ERROR: One or more [u(capnames,%q<find>)] didn't resolve. Please check your entry.

&DO`ADD`SUPERNAL [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`SUPERNAL;@stop words(u(setr,conflict,setinter(u(setr,oldcharms,ucstr(get(%0/D`SUPERNAL`%q<find>))),ucstr(%q<entry>),|,i,|)),|)=@attach %!/INC`MSG=[switch(%0,%#,You already have,name(%0) already has)] the [itemize(u(capnames,%q<conflict>),|,and,\,)] [ansi(h,u(capnames,%q<find>))] as Supernal.;&D`SUPERNAL`%q<find> %0=u(setr,newcharms,setunion(%q<oldcharms>,ucstr(%q<entry>),|,i,|))
&DO`REM`SUPERNAL [u(cobj,data)]=@attach [u(cobj,data)]/DO`VAL`SUPERNAL;@stop words(u(setr,conflict,setdiff(ucstr(%q<entry>),u(setr,oldcharms,ucstr(get(%0/D`SUPERNAL`%q<find>))),|,i,|)),|)=@attach %!/INC`MSG=[switch(%0,%#,You don't have have,name(%0) doesn't have)] the [itemize(u(capnames,%q<conflict>),|,and,\,)] [ansi(h,u(capnames,%q<find>))] as Supernal.;&D`SUPERNAL`%q<find> %0=u(setr,newcharms,setdiff(get(%0/D`SUPERNAL`%q<find>),ucstr(%q<entry>),|,i,|));@select/inline strlen(%q<newcharms>)=0,{@wipe %0/D`SUPERNAL`%q<find>}

&FUN`GET`SUPERNAL [u(cobj,data)]=ATTRIBUTES|ABILITIES
&FUN`GET`SUPERNAL`ATTRIBUTES [u(cobj,data)]=u(FUN`GET`ATTRIBUTES)
&FUN`GET`SUPERNAL`ABILITIES [u(cobj,data)]=setunion(u(FUN`GET`ABILITIES),MARTIAL ARTS,|,i,|)

+shelp/addmain Characters/+editchar=+editchar is used to modify all parts of a character's +sheet!%R%R[ansi(h,+editchar/<flag> <player>|<categories|and|objects>\[=<modification>\])]%r%r[ansi(h,FLAG: SET)]%r[ansi(h,Splat)]%t%t=<splat>%r[ansi(h,Caste)]%T%T=<caste>%R[ansi(h,Essence)]%t%t=<value>%R[ansi(h,Willpower)]%t=<value>%R[ansi(h,Attribute)]%t<attribute>=<value>%r[ansi(h,Ability)]%t%t<ability>=<value>%r[ansi(h,Specialty)]%t<ability or attribute>/<specialty>=<value>%R[ansi(h,Style)]%t%t<style>=<value>%r[ansi(h,Craft)]%t%t<field>=<value> (Weaponsmithing, etc)%r%r[ansi(h,FLAG: ADD & REM)]%r[ansi(h,Charm)]%t%t<category>=<charm1>|<charm2>|etc (Native Charms)%r[ansi(h,MACharm)]%t%t<style>=<charm1>|<charm2>|etc (Martial Arts Charms)%r[ansi(h,Evocation)]%t<artifact>=<charm1>|<charm2>|etc (Evocations)%r[ansi(h,OCharm)]%t%t<splat>|<category>=<charm1>|<charm2>|etc (Cross-Splat Charms)%r[ansi(h,Spell)]%t%t<circle>=<spell1>|<spell2>|etc%r[ansi(h,Favored)]%t%t<Attribute/Ability>=<Item1>|<Item2>|etc%r[ansi(h,Supernal)]%t<Attribute/Ability>=<Item1>|<Item2>|etc%r[ansi(h,Health)]%t%t=<level1> <level2> etc (must be -0, -1, D, etc)%r%r[ansi(h,EXAMPLES)]%r%r[ansi(h,+editchar/set Isidoros|Attribute|Dexterity=4)]%r%tSets Isidoros' Dexterity to 4%r[ansi(h,+editchar/rem Isidoros|Health=-0 -1)]%r%tRemoves one -0 and one -1 from Isidoros' permanent Health Pool%R%R[ansi(h,CLEARING VALUES)]%RLeaves Values absolutely clear to remove them entirely! Example:%R+editchar/set Isidoros|Attribute|Dexterity=