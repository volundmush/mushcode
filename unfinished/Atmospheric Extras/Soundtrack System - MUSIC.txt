@@ DEPENDENCIES - CORE

@select/inline isdbref(u(music))=0,{@teleport create(Soundtrack System <MUSIC>)=config(master_room)}
&music u(coi)=locate(config(master_room),Soundtrack System <MUSIC>,TXxi)
@parent u(music)=u(coi)
@set u(music)=WIZARD SAFE !NO_COMMAND

&CMD`+SOUNDTRACK u(music)=$^(?s)(?\:\+)?(?\:soundtrack|music)(?\:/(\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@attach u(ccs)/INC`PARTIAL=%1,setunion(get(u(music)/VAR`PLAYFLAGS),if(isadmin(%#),get(u(music)/VAR`ADMINFLAGS)),|,|),|,music,switch,switch;@attach u(music)/INC`[strfirstof(%q<switch>,MAIN)]=%2,%3,%4
@set u(music)/CMD`+SOUNDTRACK=regexp

&VAR`ADMINFLAGS u(music)=SET|DEL
&VAR`PLAYFLAGS u(music)=

&INC`MAIN u(music)=@check u(setr,total,nattr(%l/MUSIC`*))=@pemit %#=announce(SOUNDTRACK) Sorry, nothing here to hear!;@pemit %#=header(Soundtrack for [strfirstof(u(%l/COLORNAME),name(%l))]);@dolist/inline sortkey(#lambda/last(\%0,`),u(lattr,%l/MUSIC`*))={@pemit %#=align(4 19 55,rjust(last(%i0,`),3,0).,ansi(h,default(%l/%i0,Unnamed)),weblink(get(%l/%i0`URL),get(%l/%i0`URL)))};@pemit %#=header(Total Songs: %q<total>)

&INC`SET u(music)=@check strlen(%0)=@pemit %#=announce(SOUNDTRACK) ERROR: No track number entered.;@check valnum(%0)=@pemit %#=announce(SOUNDTRACK) ERROR: Track Numbers must be whole, positive numbers.;@check strlen(%1)=@pemit %#=announce(SOUNDTRACK) ERROR: No track name entered.;@check strlen(%2)=@pemit %#=announce(SOUNDTRACK) ERROR: URL for the song empty!;&MUSIC`%0 %l=%1;&MUSIC`%0`URL %l=%2;@pemit %#=announce(SOUNDTRACK) Soundtrack Entry Set: Track %0: - %1 - [weblink(%2,%2)]

&INC`DEL u(music)=@check u(setr,total,nattr(%l/MUSIC`*))=@pemit %#=announce(SOUNDTRACK) Sorry, nothing here to hear!;@check strlen(%0)=@pemit %#=announce(SOUNDTRACK) ERROR: No track number entered.;@check valnum(%0)=@pemit %#=announce(SOUNDTRACK) ERROR: Track Numbers must be whole, positive numbers.;@check hasattr(%l/MUSIC`%0)=@pemit %#=announce(SOUNDTRACK) ERROR: No Track here by that number.;@wipe %l/MUSIC`%0;@pemit %#=announce(SOUNDTRACK) Track deleted!