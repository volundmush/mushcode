th u(NEWCOBJ,Places Management System <PLACES>,places,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

&CMD`+PLACES`PENNMUSH [u(cobj,places)]=$^(?s)(?\:\+)?places(?\:/(\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@attach %!/CMD`+PLACES`MAIN
@set [u(cobj,places)]/CMD`+PLACES`PENNMUSH=regexp
&CMD`+PLACES`RHOSTMUSH [u(cobj,places)]=$^(?s)(?\:\+)?places(?\:/(\\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@attach %!/CMD`+PLACES`MAIN
@set [u(cobj,places)]/CMD`+PLACES`RHOSTMUSH=regexp
&CMD`+PLACES`MAIN [u(cobj,places)]=th u(setq,sysname,%1);@attach %!/INC`GETSWITCH=%2;@attach %!/INC`[u(strfirstof,%q<switch>,MAIN)]=%2,%3,%4,%2[if(strlen(%3),/%3)]
@set [u(cobj,places)]/CMD`+PLACES`[switch(v(game),PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SWITCHES`PLAYER [u(cobj,places)]=
&SWITCHES`ADMIN [u(cobj,places)]=CREATE|DELETE|CONFIG|RENAME|DESC|SLOT
&SYSTEM`NAME [u(cobj,places)]=PLACES

&INC`CREATE [u(cobj,places)]=@check hastype(%l,ROOM)=@attach %!/INC`MSG=ERROR: Places can only be created in Rooms.;@check strlen(%3)=@attach %!/INC`MSG=ERROR: Must include a name for the location.;@stop u(valnum,%3)=@attach %!/INC`MSG=ERROR: Names cannot be integers.;@stop strmatch(%3,*/*)=@attach %!/INC`MSG=ERROR: Places cannot have a / in their name.;@stop strlen(u(FUN`FINDEXACT,%l,%3))=@attach %!/INC`MSG=ERROR: A Place with that name already exists.;th u(attrib_set,%l,u(setr,attr,PLACE`[u(setr,id,u(nextslot,%l,PLACE))]),%0);@select/inline t(strlen(%2))=1,{&%q<attr>`DESC %l=%2};@attach %!/INC`MSG=Place '%0' Created in Slot %q<id>.

&FUN`FINDEXACT [u(cobj,places)]=if(u(valnum,%1),if(hasattr(%0/PLACE`%1),%1),after(first(u(wildgrepi,%0,PLACE`*,%1)),`))

&FUN`FINDWILD [u(cobj,places)]=u(firstof,u(FUN`FINDEXACT,%0,%1),after(first(u(wildgrepi,%0,PLACE`*,%1*)),`))

&INC`FIND [u(cobj,places)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Place name field empty.;@check u(setr,id,u(FUN`FIND[if(%4,EXACT,WILD)],%l,%0))=@attach %!/INC`MSG=ERROR: Place '%0' not found.[if(%4,%bThis lookup requires exact spelling.)];th u(setq,attr,PLACE`%q<id>)[u(setq,pname,get(%l/PLACE`%q<id>))]

&INC`DESC [u(cobj,places)]=@attach %!/INC`FIND;@check strlen(%2)=@attach %!/INC`MSG=ERROR: New desc is empty!;&%q<attr>`DESC %l=%2;@attach %!/INC`MSG=Desc set for '%q<id>: %q<pname>'

&INC`DELETE [u(cobj,places)]=@attach %!/INC`FIND;@attach %!/INC`VERIFY={ansi(hr,WARNING:) This will Delete Place '%q<id>: %q<pname>'. Are you sure? Enter the same command again to verify.},%l/%q<attr>;@trigger u(cobj,places)/TRG`DELETE=%#,%l,%q<attr>;@attach %!/INC`MSG=Place '%q<id>: %q<pname>' deleted!;@wipe %l/%q<attr>

&INC`CONFIG [u(cobj,places)]=@attach %!/INC`FIND;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Config option empty!;@attach %!/INC`PARTIAL=%1,v(VAR`CONFIG),|,PLACES,choice,choice;@select/inline t(strlen(%2))=1,{@attach u(cobj,places)/VALID`%q<choice>=%2;&%q<attr>`CONFIG`%q<choice> %l=%q<entry>;@attach %!/INC`MSG=Place '%q<id>: %q<pname>' %q<choice> configured to: %q<entry>},0,{&%q<attr>`CONFIG`%q<choice> %l;@attach %!/INC`MSG=Place '%q<id>: %q<pname>' %q<choice> cleared. Set to defaults.}

&VAR`CONFIG [u(cobj,places)]=MAX|FULL|JOIN|OJOIN|DEPART|ODEPART|PREFIX|MUFFLE

&VALID`MAX [u(cobj,places)]=@check u(valnum,u(setr,entry,%0))=@attach %!/INC`MSG=ERROR: Entry must be a whole positive number.
&VALID`FULL [u(cobj,places)]=@check if(hasflag(%l,WIZARD),u(iswizard,%#),1)=@attach %!/INC`MSG=ERROR: For security reasons\, only Wizards may set messages on this room.;th u(setq,entry,%0)
&VALID`JOIN [u(cobj,places)]=@check if(hasflag(%l,WIZARD),u(iswizard,%#),1)=@attach %!/INC`MSG=ERROR: For security reasons\, only Wizards may set messages on this room.;th u(setq,entry,%0)
&VALID`OJOIN [u(cobj,places)]=@check if(hasflag(%l,WIZARD),u(iswizard,%#),1)=@attach %!/INC`MSG=ERROR: For security reasons\, only Wizards may set messages on this room.;th u(setq,entry,%0)
&VALID`DEPART [u(cobj,places)]=@check if(hasflag(%l,WIZARD),u(iswizard,%#),1)=@attach %!/INC`MSG=ERROR: For security reasons\, only Wizards may set messages on this room.;th u(setq,entry,%0)
&VALID`ODEPART [u(cobj,places)]=@check if(hasflag(%l,WIZARD),u(iswizard,%#),1)=@attach %!/INC`MSG=ERROR: For security reasons\, only Wizards may set messages on this room.;th u(setq,entry,%0)
&VALID`PREFIX [u(cobj,places)]=@check if(hasflag(%l,WIZARD),u(iswizard,%#),1)=@attach %!/INC`MSG=ERROR: For security reasons\, only Wizards may set messages on this room.;th u(setq,entry,%0)
&VALID`MUFFLE [u(cobj,places)]=@check match(lnum(0,100),u(setr,entry,%0))=@attach %!/INC`MSG=ERROR: Entry must be a number 0-100.

&INC`RENAME [u(cobj,places)]=@attach %!/INC`FIND;@check strlen(%2)=@attach %!/INC`MSG=ERROR: New name field is empty.;@stop strmatch(%2,*/*)=@attach %!/INC`MSG=ERROR: Place names may not include a /.;@stop u(FUN`FINDEXACT,%l,%2)=@attach %!/INC`MSG=ERROR: That name is already used here!;&%q<attr> %l=%2;@attach %!/INC`MSG=Place '%q<id>: %q<pname>' renamed to %2.

&INC`SLOT [u(cobj,places)]=@attach %!/INC`FIND;@check strlen(%2)=@attach %!/INC`MSG=ERROR: New slot number empty.;@check u(valnum,%2)=@attach %!/INC`MSG=ERROR: New slot must be a whole, positive number.;@stop hasattr(%l/PLACE`%2)=@attach %!/INC`MSG=ERROR: Slot is already occupied.;@attach %!/INC`MVTREE=%l,%q<attr>,%l,PLACE`%2;@attach %!/INC`MSG=Place '%q<id>: %q<pname>' Relocated to slot %2.

&INC`MAIN [u(cobj,places)]=@check words(u(setr,list,u(sortattr,u(lattr,%l/PLACE`*))))=@attach %!/INC`MSG=There are no Places here!;@pemit %#=u(HEADER,Places at [u(moniker,%l)]);@pemit %#=align(5 25 10 34,ID,Name,Capacity,Occupants);@pemit %#=u(SEPARATOR);@dolist/inline %q<list>={@pemit %#=align(5 25 10 34,last(%i0,`),get(%l/%i0),words(u(setr,occup,u(filter,ISOBJID,get(%l/%i0`OCCUPANTS))))/[u(FUN`CONF,%l,last(%i0,`),MAX)],itemize(iter(%q<occup>,name(%i0),%b,|),|,and,\,))};@pemit %#=u(SUBHEADER)

@ALEAVE [u(cobj,ancestor_room)]=@trigger me/CLEAN_PLACES=u(objid,%#)

&CLEAN_PLACES [u(cobj,places)]=@dolist/inline u(lattr,%0/PLACE`*`OCCUPANTS)={&%i0 me=u(sortname,setdiff(u(filter,ISOBJID,v(%i0)),%1));@wipe me/PLACE_[num(%1)]};@dolist/inline u(lattr,%1/PLACE_*)={@select/inline isdbref(after(%i0,_))=0,{@wipe %1/%i0}}

&PLAYER`DISCONNECT [u(cobj,places)]=@dolist/inline u(lattr,%!/PLAYER`DISCONNECT`*)={@trigger %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
&PLAYER`DISCONNECT`PLACES [u(cobj,places)]=@stop %1;@trigger %!/CLEAN_PLACES=loc(%0),u(objid,%0)

&CMD`+PLACES2 [u(cobj,places)]=$^(?s)(?\:\+)?(depart|place|mutter|plook)(?\: +(.*))?$:@attach u(cobj,places)/PLACES`%1=%2
@set u(cobj,places)/CMD`+PLACES2=regexp

&PLACES`PLOOK [u(cobj,places)]=@attach %!/INC`FIND;@check hasattrval(%l/%q<attr>`DESC)=@attach %!/INC`MSG=Place %q<pname> has no Description!;@pemit %#=u(HEADER,u(moniker,%l) - [get(%l/%q<attr>)]);@pemit %#=get(%l/%q<attr>`DESC);@select/inline u(isadmin,%#)=1,{@pemit %#=u(SEPARATOR,Admin Section);@pemit %#=ansi(u(color,%#,COLOR,COLUMN_NAMES),align(10 40 10,Option,Value,Default?));@pemit %#=u(SEPARATOR);@dolist/inline MAX MUFFLE FULL JOIN OJOIN DEPART ODEPART PREFIX={@pemit %#=align(10 40 10,%i0,u(FUN`CONF,%l,%q<id>,%i0),if(hasattrval(%l/%q<attr>`CONFIG`%i0),No,Yes))}};@pemit %#=u(SUBHEADER)

&PLACES`DEPART [u(cobj,places)]=@check cand(u(setr,id,get(%l/PLACE_%#)),hasattr(%l/PLACE`%q<id>))=@attach %!/INC`MSG=You are not occupying any places!;@trigger %l/CLEAN_PLACES=u(objid,%#),PLACE`%q<id>`OCCUPANTS;@attach u(cobj,places)/MSG`DEPART;@attach u(cobj,places)/MSG`ODEPART

&PLACES`PLACE [u(cobj,places)]=@stop cand(u(setr,id,get(%l/PLACE_%#)),hasattr(%l/PLACE`%q<id>))=@attach %!/INC`MSG=You are already occupying Place %q<id>: [get(%l/PLACE`%q<id>)]! Depart first!;@attach %!/INC`FIND;@stop cand(gte(words(get(%l/%q<attr>`OCCUPANTS)),u(FUN`CONF,%l,%q<id>,MAX)),not(u(isadmin,%#)))=@attach u(cobj,places)/MSG`FULL;&%q<attr>`OCCUPANTS %l=u(sortname,setunion(u(filter,ISOBJID,get(%l/%q<attr>`OCCUPANTS)),u(objid,%#)));&PLACE_%# %l=%q<id>;@attach u(cobj,places)/MSG`JOIN;@attach u(cobj,places)/MSG`OJOIN

&PLACES`MUTTER [u(cobj,places)]=@check cand(u(setr,id,get(%l/PLACE_%#)),hasattr(%l/PLACE`%q<id>))=@attach %!/INC`MSG=You are not occupying any places!;@check strlen(%0)=@attach %!/INC`MSG=What will you say?;@stop match(| ; :,left(%0,1))=@attach %!/INC`MSG=Sorry, no posing in Mutter mode.;@pemit/list u(setr,occup,get(%l/PLACE`%q<id>`OCCUPANTS))=u(FUN`CONF,%l,%q<id>,PREFIX)%b[speak(%#,%0,mutters\,)];@select/inline t(u(FUN`CONF,%l,%q<id>,MUFFLE))=1,{@nsoemit %l/%q<occup>=From [get(%l/PLACE`%q<id>)], [speak(%#,%0,mutters\,,%l/MUTTER)]}

&MUTTER [u(cobj,ancestor_room)]=[u(setq,id,v(PLACE_[num(%1)]))][u(setq,chance,default(PLACE`%q<id>`CONFIG`MUFFLE,30))]"[if(eq(%q<chance>,100),%0,iter(%0,if(lte(rand(0,100),%q<chance>),%i0,...)))]"

&FUN`CONF [u(cobj,places)]=default(%0/PLACE`%1`CONFIG`%2,v(DEF`%2))

&FUN`MSG [u(cobj,places)]=udefault(%0/PLACE`%1`CONFIG`%2,u(DEF`%2,name(%3),subj(%3),obj(%3),poss(%3),aposs(%3),%1,get(%0/PLACE`%1)),name(%3),subj(%3),obj(%3),poss(%3),aposs(%3),%1,get(%0/PLACE`%1))

&MSG`FULL [u(cobj,places)]=@pemit %#=u(FUN`MSG,%l,%q<id>,FULL,u(objid,%#))
&MSG`JOIN [u(cobj,places)]=@pemit %#=u(FUN`MSG,%l,%q<id>,JOIN,u(objid,%#))
&MSG`OJOIN [u(cobj,places)]=@oemit %#=u(FUN`MSG,%l,%q<id>,OJOIN,u(objid,%#))
&MSG`DEPART [u(cobj,places)]=@pemit %#=u(FUN`MSG,%l,%q<id>,DEPART,u(objid,%#))
&MSG`ODEPART [u(cobj,places)]=@oemit %#=u(FUN`MSG,%l,%q<id>,ODEPART,u(objid,%#))

&DEF`FULL [u(cobj,places)]=That location is full!
&DEF`MAX [u(cobj,places)]=5
&DEF`JOIN [u(cobj,places)]=You sit down at %6.
&DEF`OJOIN [u(cobj,places)]=%0 sits down at %6.
&DEF`DEPART [u(cobj,places)]=You leave %6.
&DEF`ODEPART [u(cobj,places)]=%0 leaves %6.
&DEF`PREFIX [u(cobj,places)]=At your location,
&DEF`MUFFLE [u(cobj,places)]=30

@@ COMMUNICATIONS - +PLACES
+help/add +places=[u(cobj,places)]/HLP`+PLACES
+help/category +places=Communications
&HLP`+PLACES [u(cobj,places)]=The Places System adds special 'locations' within a room, such as a dinner table or bench, where characters can communicate with less risk of being overheard. Fair warning, it does not affect +pot or +scene logging!%R%R[ansi(hc,Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,places)] - Display a list of all Places in the room.%R[ansi(h,plook <place>)] - Display a place's description if it has one.%R[ansi(h,place <place>)] - Join a place if there's room.%R[ansi(h,depart)] - Leave your current place. Leaving the room or fully disconnecting also vacates your seat.%R[ansi(h,mutter <message>)] - talk to others at your place. Similar to Say. Mutters CAN be overheard by others in the room but many words will be muffled. Some places may muffle more effectively than others.)]

+shelp/add +places=[u(cobj,places)]/SHLP`+PLACES
+shelp/category +places=Communications
&SHLP`+PLACES [u(cobj,places)]=[ansi(hc,Staff Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+places/create <name>\[=<description>\])] - Creates a new place. Description is optional.%R[ansi(h,+places/desc <place>=<description>)] - Changes a Place's description.%R[ansi(h,+places/rename <place>=<new name>)] - Rename a place.%R[ansi(h,+places/slot <place>=<new slot id>)] - Move a place to a different slot. Useful for changing orders around.%R[ansi(h,+places/delete <place>)] - Deletes a Place.%R[ansi(h,+places/config <place>/<option>=<value>)] - Sets a configuration option \(options listed below\). Setting to nothing clears that option. \(returned to defaults.\)%R%T[ansi(h,MAX)] - A whole\, positive number. How many people fit there at most.%R%T[ansi(h,MUFFLE)] - A percentage\, 0 to 100. How much of the speech will be heard. \(Dice roll per word\, if roll is under Chance it will show the word.\)%R%T[ansi(h,JOIN)] - Message shown to player when they enter a Place.%R%T[ansi(h,OJOIN)] - Message shown to others when someone enters a place.%R%T[ansi(h,DEPART)] - Message shown to player when they leave a Place.%R%T[ansi(h,ODEPART)] - Message shown to others when someone leaves a place.%R%T[ansi(h,PREFIX)] - Message shown to people mutter'ing at a place.%R%T[ansi(h,FULL)] - Message shown to people trying to join a full place.%R%RABOUT MESSAGES: This should be a plain text message. Several substitutions are provided for customization:%R%T\%0 - The player's name.%R%T\%1 - Equal to \%s\, subjective pronoun.%R%T\%2 - Equal to \%o\, objective pronoun.%R%T\%3 - Equal to \%p\, possessive pronoun.%R%T\%4 - Equal to \%a\, absolute possessive.%R%T\%5 - The SLOT of the place.%R%T\%6 - the NAME of the place.%R%RThe substitutions will be evaluated when typed\, so must be escaped with a \\\, such as \\\%0. Alternatively\, use \]+places/config instead of +places to make the whole command no-eval.)]
