@@ DEPENDENCIES - CORE

@select/inline isdbref(u(rumor))=0,{@teleport create(Rumor System <RUMOR>)=config(master_room)}
&rumor u(coi)=locate(config(master_room),Rumor System <RUMOR>,TXxi)
@parent u(rumor)=u(coi)
@set u(rumor)=WIZARD SAFE !NO_COMMAND

@select/inline isdbref(u(rumordb))=0,{@teleport create(Rumor System Database <RUMORDB>)=u(rumor)}
&rumordb u(coi)=locate(u(rumor),Rumor System Database <RUMORDB>,TXxi)
@set u(rumordb)=WIZARD SAFE
@lock/user:READ u(rumordb)=#TRUE
@lset u(rumordb)/READ=!no_inherit
@lock/user:POST u(rumordb)=#TRUE
@lset u(rumordb)/POST=!no_inherit
@lock/user:ADMIN u(rumordb)=@[u(sgo)]/Command
@lset u(rumordb)/ADMIN=!no_inherit

&CMD`+RUMOR u(rumor)=$^(?s)(?\:\+)?(?\:rumor|rumour)(?\:/(\S+))?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@attach %!/INC`PARTIAL=%1,setunion(get(u(rumor)/VAR`PLAYFLAGS),if(u(isadmin,%#),get(u(rumor)/VAR`ADMINFLAGS)),|,|),|,music,switch,switch;@attach %!/INC`[u(strfirstof,%q<switch>,MAIN)]=trim(%2),trim(%3),trim(%4)
@set u(rumor)/CMD`+RUMOR=regexp

&SWITCHES`ADMIN u(rumor)=MAKECIRCLE|DELCIRCLE|RENCIRCLE|LOCKCIRCLE|UNLOCKCIRCLE|DESCCIRCLE
&SWITCHES`PLAYER u(rumor)=NEW|QUASH|PROMOTE|REMRUMOR|CIRCLES

&HEADER u(RUMOR)=header(%0,,RUMOR`BORDER,RUMOR`BORDERDOT,RUMOR`BORDERTEXT)
&SUBHEADER u(RUMOR)=subheader(%0,,RUMOR`BORDER,RUMOR`BORDERDOT,RUMOR`BORDERTEXT)
&SEPARATOR u(RUMOR)=separator(%0,,RUMOR`BORDER,RUMOR`BORDERDOT,RUMOR`BORDERTEXT)
&MSGHEAD u(RUMOR)=msghead(v(VAR`MSGHEAD))
&ROOMMSGHEAD u(RUMOR)=msgheadroom(v(VAR`MSGHEAD))
&SYSTEM`NAME u(RUMOR)=RUMOR

&INC`CIRCLENAMECHECK u(rumor)=@check strlen(%0)=@attach %!/INC`MSG=You must enter the new category name!;@check cand(valid(name,%0),lte(strlen(%0),14))=@attach %!/INC`MSG='%0' is not a valid category name. Categories must be valid object names and be 14 characters or less.;@stop locate(u(rumor),%0,TXxi)=@attach %!/INC`MSG=ERROR: Circle already exists.

&INC`MAKECIRCLE u(rumor)=@check hasflag(%#,WIZARD)=@attach %!/INC`MSG=Permission Denied: Wizard only.;@attach %!/INC`CIRCLENAMECHECK=%0;@attach %!/INC`CATEXIST=%0;@tel u(setr,cat,create(%0))=u(rumordb);@set %q<cat>=WIZARD SAFE;@parent %q<cat>=u(rumordb);@attach %!/INC`MSG=Circle '%0' created! Make sure to use +rumor/lockcircle to restrict it!;@attach %!/INC`CMSG=Circle '%0' created!

&INC`DELCIRCLE u(rumor)=@check hasflag(%#,WIZARD)=@attach %!/INC`MSG=Permission Denied: Wizard only.;@attach %!/INC`FINDCIRCLE=%0;@attach %!/INC`VERIFY={ansi(hr,WARNING:) This will delete Rumor Circle '%q<catname>'. All Rumors in that category will be deleted. Are you sure you want to do this? Enter the same command again within ten seconds to verify!},DELETE %q<cat>;@attach %!/INC`MSG=Circle '%0' Deleted!;@attach %!/INC`CMSG={Deleted Rumor Circle '%q<catname>'!};@set %q<cat>=!SAFE;@nuke %q<cat>;@nuke %q<cat>;@dolist/inline iter(u(wildgrepi,u(rumordb),*`CIRCLE,%q<cat>),first(%i0,`))={@trigger u(rumor)/TRG`DELRUMOR=%i0}

&INC`DESCCIRCLE u(rumor)=@check hasflag(%#,WIZARD)=@attach %!/INC`MSG=Permission Denied: Wizard only.;@attach %!/INC`FINDCIRCLE=%0;@check strlen(%2)=@attach %!/INC`MSG=ERROR: No description entered!;@attach %!/INC`MSG=Desc set.;@describe %q<cat>=%2

&INC`CIRCLES u(rumor)=@pemit %#=u(HEADER,Rumor Circles);@pemit %#=align(10 9 57,Name,Access,Description);@pemit %#=u(SEPARATOR);@dolist/inline u(FUN`VALIDCIRCLES,%#,Post)={@pemit %#=align(10 9 57,name(%i0),u(strfirstof,if(u(FUN`PERMCHECK,%i0,ADMIN,%#),ADMIN),if(u(FUN`PERMCHECK,%i0,POST,%#),POST),if(u(FUN`PERMCHECK,%i0,READ,%#),READ)),get(%i0/DESCRIBE))};@pemit %#=u(HEADER)

&INC`FINDCIRCLE u(rumor)=@check strlen(%0)=@attach %!/INC`MSG=No Category Name entered!;@check words(u(setr,cats,if(strlen(%1),u(FUN`VALIDCIRCLES,%#,%1),u(FUN`LISTCIRCLES))))=@attach %!/INC`MSG=There are no Categories.;@check isdbref(u(setr,cat,objid(namegrab(%q<cats>,%0))))=@attach %!/INC`MSG=Category '%0' not found.;th u(setq,catname,name(%q<cat>))

&FUN`PERMCHECK u(rumor)=switch(%1,READ,cor(u(FUN`PERMCHECK,%0,POST,%2),elock(%0/%1,%2)),POST,cor(u(FUN`PERMCHECK,%0,Admin,%2),elock(%0/%1,%2)),ADMIN,elock(%0/%1,%2))

&FUN`LISTCIRCLES u(rumor)=iter(setinter(children(u(rumordb)),lthings(u(rumordb)),%b,namei),objid(%i0))
&FUN`LISTCIRCLES`NAMES u(rumor)=iter(u(strfirstof,%0,u(FUN`LISTCIRCLES)),name(%i0),%b,|)

&FUN`VALIDCIRCLES u(rumor)=filterbool(#lambda/u(FUN`PERMCHECK,\%0,%1,%0),u(FUN`LISTCIRCLES))

&INC`RENCIRCLE u(rumor)=@check hasflag(%#,WIZARD)=@attach %!/INC`MSG=Permission Denied: Wizard only.;@attach %!/INC`FINDCIRCLE=%0;@attach %!/INC`CIRCLENAMECHECK=%2;@name %q<cat>=%2;@attach %!/INC`CMSG=Renamed Rumor Circle %q<catname> to %2.;@attach %!/INC`MSG=Rename successful.

&INC`LOCKCIRCLE u(rumor)=@attach %!/INC`LOCK;@check strlen(%2)=@attach %!/INC`MSG=ERROR: No lock string entered. See [u(pueblize,+help +key)] for more information.;@attach u(kls)/INC`DOLOCK=%q<cat>,user:%q<lock>,%2
&INC`UNLOCKCIRCLE u(rumor)=@attach %!/INC`LOCK;@unlock/user:%q<lock> %q<cat>;@attach %!/INC`MSG=You release the %q<lock> lock on %q<catname>;@attach %!/INC`CMSG={Released the %q<lock> Lock on Rumor Circle %q<catname>}

&INC`LOCK u(rumor)=@check hasflag(%#,WIZARD)=@attach %!/INC`MSG=ERROR: Permission Denied. This command is Wizard-only.;@attach %!/INC`FINDCIRCLE=%0;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Lock Type field empty.;@attach %!/INC`PARTIAL=%1,READ|POST|ADMIN,|,lock,lock

&INC`NEW u(rumor)=@check approved(%#)=@attach %!/INC`MSG=ERROR: Approved only.;@attach %!/INC`FINDCIRCLE=%0,POST;@select/inline t(strlen(%1))=1,{@check lmath(min,iter(u(setr,subjdbs,namelist(%1,FUN`NAMEERROR)),isdbref(%i0)))=@attach %!/INC`MSG=ERROR: One or more names did not resolve, cannot continue.;th u(setq,subjids,iter(%q<subjdbs>,objid(%i0)))};@check strlen(%2)=@attach %!/INC`MSG=ERROR: Rumor text field is empty.;&[u(setr,attr,u(nextslot,u(rumordb)))]`BYDB u(rumordb)=u(objid,%#);&%q<attr>`BY u(rumordb)=%n;&%q<attr>`ON u(rumordb)=secs();&%q<attr>`SUBJECT u(rumordb)=%q<subjids>;&%q<attr>`CIRCLE u(rumordb)=%q<cat>;&%q<attr>`TEXT u(rumordb)=%2;&%q<attr>`DURATION u(rumordb)=0;&%q<attr>`ID u(rumordb)=u(setr,newid,baseconv(v(VAR`CURRENT`ID),10,64));&VAR`CURRENT`ID u(rumor)=add(1,v(VAR`CURRENT`ID));&VAR`RUMORIDS u(rumor)=setunion(%q<newid>,v(VAR`RUMORIDS));&%q<attr>`COMMID u(rumordb)=0;@attach %!/INC`CMSG={Posted new %q<catname> Rumor[if(words(%q<subjids>),%babout [itemize(iter(%q<subjids>,name(%i0),%b,|),|,and,\,)])]: [u(pueblize,+rumor %q<attr>,+rumor %q<attr>)]};@attach %!/INC`MSG=Rumor posted!;@attach %!/INC`RMSG=v(VAR`MSGHEAD),filterbool(#lambda/u(FUN`PERMCHECK,%q<cat>,READ,\%0),unique(lwho())),{New %q<catname> Rumor Posted[if(words(%q<subjids>),%babout [itemize(iter(%q<subjids>,name(%i0),%b,|),|,and,\,)])]: [u(pueblize,+rumor %q<attr>,+rumor %q<attr>)]}

&FUN`GETIDS u(rumor)=iter(u(lattr,u(rumordb)/*`ID),get(u(rumordb)/%i0))

&VAR`DEFAULT`TIMEOUT u(rumor)=mul(60,60,24,7)
&VAR`FINALCOUNTDOWN u(rumor)=mul(60,60,24,3)
&VAR`TIMEMOD`PROMOTE u(rumor)=mul(60,60,24,-1)
&VAR`TIMEMOD`QUASH u(rumor)=mul(60,60,24)

&INC`QUASH u(rumor)=@attach %!/INC`COMMENT=%0,%1,%2,-
&INC`PROMOTE u(rumor)=@attach %!/INC`COMMENT=%0,%1,%2,+

&INC`COMMENT u(rumor)=@check approved(%#)=@attach %!/INC`MSG=ERROR: Approved only.;@check strlen(%0)=@attach %!/INC`MSG=ERROR: No ID entered to check.;@check u(valnum,%0)=@attach %!/INC`MSG=ERROR: IDs must be whole, positive numbers.;@check match(u(FUN`VALIDRUMORS,%#),%0)=@attach %!/INC`MSG=ERROR: Rumor '%0' not found.;@check u(FUN`PERMCHECK,get(u(rumordb)/%0`CIRCLE),POST,%#)=@attach %!/INC`MSG=Permission denied.;@stop cand(match(get(u(rumordb)/%0`COMMENTERS),u(objid,%#)),not(u(FUN`PERMCHECK,get(u(rumordb)/%0`CIRCLE),ADMIN,%#)))=@attach %!/INC`MSG=You have already influenced this rumor.;@check strlen(%2)=@attach %!/INC`MSG=ERROR: Nothing entered to [switch(%3,-,quash,promote)] with.;@select/inline %3=+,{@wipe u(rumordb)/%0`DAYSLEFT};&%0`DURATION u(rumordb)=add(get(u(rumordb)/%0`DURATION),u(VAR`TIMEMOD`[switch(%3,+,PROMOTE,-,QUASH)]));&%0`COMMENTERS u(rumordb)=setunion(u(objid,%#),get(u(rumordb)/%0`COMMENTERS));@attach %!/INC`MSG=Rumor [switch(%3,+,promoted,-,quashed)]!;&[u(setr,attr,%0`COMM`[u(nextslot,u(rumordb),%0`COMM)])] u(rumordb)=%3;&%q<attr>`ON u(rumordb)=secs();&%q<attr>`TEXT u(rumordb)=%2;&%q<attr>`BY u(rumordb)=%n;&%q<attr>`BYDB u(rumordb)=u(objid,%#);&%q<attr>`ID u(rumordb)=u(setr,newid,baseconv(get(u(rumordb)/%0`COMMID),10,64));&%0`COMMID u(rumordb)=add(1,get(u(rumordb)/%0`COMMID));&%0`POSTIDS u(rumordb)=setunion(%q<newid>,get(u(rumordb)/%0`POSTIDS));&D`RUMOR`READ`u(setr,id,get(u(rumordb)/%0`ID))] %#=setunion(get(%#/D`RUMOR`READ`%q<id>),%q<newid>);@attach %!/INC`CMSG={switch(%3,+,Promoted,-,Quashed) [u(pueblize,name(get(u(rumordb)/%0`CIRCLE)) Rumor %0,+rumor %0)]};th u(setq,cat,objid(get(u(rumordb)/%0`CIRCLE)));@attach %!/INC`RMSG=v(VAR`MSGHEAD),setdiff(filterbool(#lambda/u(FUN`PERMCHECK,%q<cat>,READ,\%0),unique(lwho())),%#),{[u(pueblize,name(get(u(rumordb)/%0`CIRCLE)) Rumor %0,+rumor %0)] has been [switch(%3,+,promoted,-,quashed)].}

&INC`REMRUMOR u(rumor)=@check strlen(%0)=@attach %!/INC`MSG=ERROR: No ID entered to check.;@check u(valnum,%0)=@attach %!/INC`MSG=ERROR: IDs must be whole, positive numbers.;@check match(u(FUN`VALIDRUMORS,%#),%0)=@attach %!/INC`MSG=ERROR: Rumor '%0' not found.;@check u(FUN`PERMCHECK,get(u(rumordb)/%0`CIRCLE),ADMIN,%#)=@attach %!/INC`MSG=Permission denied.;@attach %!/INC`VERIFY={ansi(hr,WARNING:) This will delete Rumor %0. Are you sure? Enter the command again to verify.},DELETE RUMOR %0;@trigger u(rumor)/TRG`DELRUMOR=%0;@attach %!/INC`MSG=Rumor Deleted!

&INC`MAIN u(rumor)=@select/inline t(strlen(%0))=1,{@attach %!/INC`SHOWRUMOR},0,{@attach %!/INC`RUMORLIST}

&INC`SHOWRUMOR u(rumor)=@check strlen(%0)=@attach %!/INC`MSG=ERROR: No ID entered to check.;@check u(valnum,%0)=@attach %!/INC`MSG=ERROR: IDs must be whole, positive numbers.;@check match(u(FUN`VALIDRUMORS,%#),%0)=@attach %!/INC`MSG=ERROR: Rumor '%0' not found.;th u(setq,admin,u(FUN`PERMCHECK,get(u(rumordb)/%0`CIRCLE),ADMIN,%#));@pemit %#=u(HEADER,Rumor %0);@pemit %#=get(u(rumordb)/%0`TEXT);@select/inline %q<admin>=1,{@pemit %#=(By [if(isdbref(get(u(rumordb)/%0`BYDB)),name(get(u(rumordb)/%0`BYDB)),get(u(rumordb)/%0`BY))] on [u(fancytime,get(u(rumordb)/%0`ON),%#)])};@select/inline t(nattr(u(rumordb)/%0`COMM`*))=1,{@pemit %#=u(SEPARATOR);@dolist/inline u(sortattr,u(lattr,u(rumordb)/%0`COMM`*))={@pemit %#=align(>5 72,switch(get(u(rumordb)/%i0),+,ansi(hg,+),-,ansi(hr,-)),get(u(rumordb)/%i0`TEXT)[if(%q<admin>,%b(By [if(isdbref(get(u(rumordb)/%i0`BYDB)),name(get(u(rumordb)/%i0`BYDB)),get(u(rumordb)/%i0`BY))] on [u(fancytime,get(u(rumordb)/%i0`ON),%#)]))])}};@pemit %#=u(HEADER);&D`RUMOR`READ %#=setinter(setunion(get(%#/D`RUMOR`READ),u(setr,id,get(u(rumordb)/%0`ID))),v(VAR`RUMORIDS));@cpattr u(rumordb)/%0`POSTIDS=%#/D`RUMOR`READ`%q<id>

&INC`RUMORLIST u(rumor)=@pemit %#=u(HEADER,Rumors);@pemit %#=align(4 6 16 20 14 10,#,Known,Circle,Subject,Rating,Remain);@pemit %#=u(SEPARATOR);@dolist/inline u(FUN`VALIDRUMORS,%#)={@pemit %#=align(4 6 16 20 14 10,u(pueblize,%i0,+rumor %i0),if(match(get(%#/D`RUMOR`READ),u(setr,id,get(u(rumordb)/%i0`ID))),if(not(words(setdiff(get(u(rumordb)/%i0`POSTIDS),get(%#/D`RUMOR`READ`%q<id>)))),Yes,Upd),No),if(isobjid(get(u(rumordb)/%i0`CIRCLE)),name(get(u(rumordb)/%i0`CIRCLE)),???),if(words(u(setr,subj,u(filter,ISOBJID,get(u(rumordb)/%i0`SUBJECT)))),itemize(iter(%q<subj>,name(%i0),%b,|),|,and,\,),General),switch(u(setr,rating,sub(u(setr,prom,words(u(wildgrepi,u(rumordb),%i0`COMM`*,+))),u(setr,quash,words(u(wildgrepi,u(rumordb),%i0`COMM`*,-))))),-*,-[rjust(abs(%q<rating>),2,0)],%b[rjust(%q<rating>,2,0)])%B(+[rjust(%q<prom>,2,0)]/-[rjust(%q<quash>,2,0)]),if(hasattr(u(rumordb)/%i0`DAYSLEFT),ansi(hr,squish(etime(sub(u(VAR`FINALCOUNTDOWN),get(u(rumordb)/%i0`DAYSLEFT)),6))),squish(etime(sub(u(VAR`DEFAULT`TIMEOUT),get(u(rumordb)/%i0`DURATION)),6))))};@pemit %#=u(HEADER)

&FUN`VALIDRUMORS u(rumor)=filterbool(#lambda/u(FUN`CANSEE,\%0,%0),sort(reglattr(u(rumordb)/^\\d+$)))

&FUN`CANSEE u(rumor)=localize(if(isobjid(u(setr,circ,get(u(rumordb)/%0`CIRCLE))),elock(%q<circ>/READ,%1),1))

&STARTUP u(rumor)=@trigger me/TRG`RUMORMILL

&TRG`RUMORMILL u(rumor)=th unu(setq,);th u(setq,tick,mul(60,5));th u(setq,rumors,filterbool(#lambda/not(match(get(u(rumordb)/\%0`FLAGS),FREEZE)),reglattr(u(rumordb)/^\\d+$)));th u(setq,infinal,filterbool(#lambda/hasattr(u(rumordb)/\%0`DAYSLEFT),%q<rumors>));th u(setq,normals,setdiff(%q<rumors>,%q<infinal>));@dolist/inline/nobreak %q<infinal>={&%i0`DAYSLEFT u(rumordb)=add(%q<tick>,get(u(rumordb)/%i0`DAYSLEFT));@check gte(get(u(rumordb)/%i0`DAYSLEFT),u(VAR`FINALCOUNTDOWN));th u(setq,timedout,setunion(%q<timedout>,%i0))};@dolist/inline/nobreak %q<normals>={&%i0`DURATION u(rumordb)=add(%q<tick>,get(u(rumordb)/%i0`DURATION));@check gte(get(u(rumordb)/%i0`DURATION),u(VAR`DEFAULT`TIMEOUT));th u(setq,finals,setunion(%q<finals>,%i0));&%i0`DAYSLEFT u(rumordb)=0};@select/inline t(words(setdiff(%q<finals>,%q<infinal>)))=1,{@attach %!/INC`SCMSG={Rumors [itemize(sort(setdiff(%q<finals>,%q<infinal>)),%b,and,\,)] have been put on Final Countdown.}};@select/inline t(words(%q<timedout>))=1,{@attach %!/INC`SCMSG={Rumors [itemize(sort(%q<timedout>),%b,and,\,)] have TIMED OUT.}};@dolist/inline %q<timedout>={@trigger u(rumor)/TRG`DELRUMOR=%i0};@wait %q<tick>=@trigger u(rumor)/TRG`RUMORMILL

&TRG`DELRUMOR u(rumor)=th u(setq,id,get(u(rumordb)/%0`ID));@wipe u(rumordb)/%0;th u(setq,ids,u(FUN`GETIDS));&VAR`RUMORIDS u(rumor)=%q<ids>;@dolist/inline lsearch(all,type,player)={&D`RUMOR`READ %i0=setinter(get(%i0/D`RUMOR`READ),%q<ids>);@wipe %i0/D`RUMOR`READ`%q<id>}

@@ RUMOR SYSTEM
+help/addmain Communications/+rumor=[u(rumor)]/HLP`+RUMOR
&HLP`+RUMOR u(rumor)=The Rumor system is designed to represent in-character rumors spreading around town - or various social circles. Rumor posters remain anonymous to all but staff members, and rumors can be anonymously promoted or quashed by players - only one or the other per each rumor, so choose wisely. Over time they are removed, but more pervasively supported rumors take longer to time out.%R%R[ansi(hc,Commands)]%R[align(5 [sub(u(width,%#),6)],,{[ansi(h,+rumor)] - Show the list of rumors.%R[ansi(h,+rumor <id>)] - Show a specific rumor's details.%R[ansi(h,+rumor/circles)] - Shows circles available to you and what permissions you hold.%R[ansi(h,+rumor/new <circle>\[/<subject>\]=<text>)] - Create a new rumor. Subject takes a player-name-list the same way that page does (names are separated by spaces, multi-word names are in ""). Subject is optional.%R[ansi(h,+rumor/promote <id>=<text>)] - Adds a promotional anonymous comment to a Rumor. Example would be more details, related events, etc. This keeps the Rumor around longer.%R[ansi(h,+rumor/quash <id>=<text>)] - Like /promote, but is negative. Makes Rumors time out quicker.})]%R%R[ansi(hc,Admin Commands)]%R(listed for players granted Admin priveleges to a Circle)%R[align(5 [sub(u(width,%#),6)],,{[ansi(h,+rumor/delrumor <id>)] - Deletes a rumor. Dangerous!})]%R%RA NOTE on Rumor timeouts:%R%RWhen a Rumor has been around for the max duration, it will be marked for Final Countdown - a 3 day period. If the Rumor receives a Promote, then the countdown is reset and the Rumor will be pushed back into the normal timeout. Quashes during the Final Countdown have no effect but are still registered.

+shelp/addmain Communications/+rumor=[u(rumor)]/SHLP`+RUMOR
&SHLP`+RUMOR u(rumor)=[ansi(hc,Wizard Commands)]%R[align(5 [sub(u(width,%#),6)],,{[ansi(h,+rumor/makecircle <name>)] - Create a new Circle. They're locked READ:<everyone>, POST:<everyone>, ADMIN:<staff> by default. Name must be something simple and short, like a race name or a topic.%R[ansi(h,+rumor/lockcircle <circle>/<type>=<lockstring>)] - Lock a category using the +key system. <type> must be READ, POST, or ADMIN. READ: who can see the circle's rumors. POST: Who can post. (Very rarely would you want READ: Yes and POST: No. Remember to set both!) ADMIN: Who can see the posters' identities and remove rumors. POST implicitly grants READ. ADMIN implicitly grants ALL permissions.%R[ansi(h,+rumor/unlockcircle <circle>/<type>)] - Clears the lock back to its default.%R[ansi(h,+rumor/rencircle <circle>=<newname>)] - Renames a Circle.%R[ansi(h,+rumor/delcircle <circle>)] - Deletes a Circle. WARNING: DANGEROUS COMMAND.%R[ansi(h,+rumor/desccircle <circle>=<description>)] - Describe a circle for the /circles display.})]%R%RStaff and those with ADMIN can use /quash and /promote without limit for plot flexibility.
