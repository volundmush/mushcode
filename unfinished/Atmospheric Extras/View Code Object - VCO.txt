@select/inline isdbref(u(vco))=0,{@tel create(View Code Object <VCO>)=config(master_room)}
&vco u(coi)=locate(config(master_room),View Code Object <VCO>,TXxi)
@parent u(vco)=u(coi)
@set u(vco)=WIZARD SAFE !NO_COMMAND

&CMD`VIEW u(vco)=$^(?s)(?\:\+)?(view|lore)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach u(ccs)/INC`PARTIAL=%2,setunion(get(u(vco)/VAR`VIEW`PLAYFLAGS),if(isadmin(%#),get(u(vco)/VAR`VIEW`ADMINFLAGS)),|,|),|,VIEW,switch,switch;@attach u(vco)/INC`VIEW`[strfirstof(%q<switch>,MAIN)]=%3,%4
@set u(vco)/CMD`VIEW=regexp

&VAR`VIEW`ADMINFLAGS u(vco)=ADD|SET|DEL|REN

&GFN`VIEWS u(vco)=iter(u(lattr,%0/VIEW`*),capnames(get(%0/%i0)),%b,|)

&FUN`FINDVIEW u(vco)=sortkey(#lambda/get(%l/\%0),wildgrepi(%0,VIEW`*,%1*),i)

&INC`VIEW`MAIN u(vco)=@check strlen(%0)=@pemit %#=You can view the following: [itemize(iter(views(%l),pueblize(%i0,view %i0),|,|),|,and,\,)];@check words(u(setr,views,u(FUN`FINDVIEW,%l,%0)))=@pemit %#=There's no view named %0 here.;@stop gt(words(%q<views>),1)=@pemit %#=That view name is ambiguous. Please be more specific in the view name.;@pemit %#=header(View: [get(%l/%q<views>)]);@pemit %#=u(%l/%q<views>`DESC);@pemit %#=header()

&INC`VIEW`ADD u(vco)=@check strlen(%0)=@pemit %#=msghead(VIEW) ERROR: View name empty.;@stop words(grepi(%l,VIEW`*,%0))=@pemit %#=msghead(VIEW) ERROR: There is already a View here by that name. Use /set to change its contents, /ren to rename it, or /del to remove it.;@check strlen(%1)=@pemit %#=msghead(VIEW) ERROR: View text empty.;&[u(setr,attr,VIEW`[nextslot(%l,VIEW)])] %l=%0;&%q<attr>`DESC %l=%1;@pemit %#=msghead(VIEW) View set!

&INC`VIEW`REN u(vco)=@check strlen(%0)=@pemit %#=What will you rename?;@check words(u(setr,views,u(FUN`FINDVIEW,%l,%0)))=@pemit %#=There's no view named %0 here.;@stop gt(words(%q<views>),1)=@pemit %#=That view name is ambiguous. Please be more specific in the view name.;@check strlen(%1)=@pemit %#=msghead(VIEW) ERROR: No new name entered.;@stop words(grepi(%l,VIEW`*,%1))=@pemit %#=msghead(VIEW) ERROR: There is already a View here by that name.;&%q<views> %l=%0;@pemit %#=msghead(VIEW) Rename complete.

&INC`VIEW`DEL u(vco)=@check strlen(%0)=@pemit %#=What will you rename?;@check words(u(setr,views,u(FUN`FINDVIEW,%l,%0)))=@pemit %#=There's no view named %0 here.;@stop gt(words(%q<views>),1)=@pemit %#=That view name is ambiguous. Please be more specific in the view name.;@wipe %l/%q<views>;@pemit %#=msghead(VIEW) Deleted!