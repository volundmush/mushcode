

@@ RUN THIS ON CODE WIZARD OBJECT!
@cmdquota me=99999
@switch/first/inline hasflag(%#,UNREGISTERED)=1,{@set me=!UNREGISTERED};@power me=no_pay

think if(isdbref(u(cobj,ccp)),,setq(0,create(Core Code Parent <CCP>))[tel(%q0,config(master_room))][parent(%#,%q0)][set(%q0,COBJ`CCP:[objid(%q0)])][set(%q0,WIZARD SAFE !NO_COMMAND)])
@@ Yeah, it's an awful hodgepodge of side effect functions. For a good cause.

&COBJ [parent(%#)]=u(COBJ`%0)
&VA [u(cobj,ccp)]=PennMUSH
&GAME [u(cobj,ccp)]=PennMUSH

&IS_ANCESTOR [u(cobj,ccp)]=cand(hasattr(u(cobj,ccp),COBJ`ANCESTOR_PLAYER),strmatch(num(%0),num(u(cobj,ancestor_player))))

&ATTRIB_SET [u(cobj,ccp)]=if(u(IS_ANCESTOR,%0),,attrib_set(%0/%1,%2))
&VATTRIB_SET [u(cobj,ccp)]=u(attrib_set,%0,%1,%2)[set(%0/%1,visual)]

&LATTR [u(cobj,ccp)]=lattr(%0,%1)
&LATTRP [u(cobj,ccp)]=lattrp(%0,%1)
&REGLATTR [u(cobj,ccp)]=reglattr(%0,%1)
&STRINGSECS [u(cobj,ccp)]=stringsecs(%0)
&SETQ [u(cobj,ccp)]=setq(%0,%1)
&SETR [u(cobj,ccp)]=setr(%0,%1)

&ELEMENTS [u(cobj,ccp)]=elementsmux(%0,%1,%2)

&STARTUP [u(cobj,ccp)]=@dolist/inline lattr(%!/START`*)={@include/nobreak %!/##}

&START`FUNCTIONS [u(cobj,ccp)]=@dolist/inline setunion(header subheader footer separator isadmin isapproved approved sortname color mxpmenu pueblize my_options opother my_names speech markup colormarkup chancolor colornames choosegame sortorder namelink namecolor weblink codename getproperty hideidle lastidle chanhash getmoniker,v(FUNCTIONLIST`EXTRA))={@function vol_%i0=u(cobj,ccp),%i0;@function/restrict vol_%i0=localize};@dolist/inline v(FUNCTIONLIST`ADMIN)={@function/restrict %i0=admin}

@@ &FUNCTIONLIST`EXTRA [u(cobj,ccp)]=
@@ &FUNCTIONLIST`ADMIN [u(cobj,ccp)]=
@@ These are meant to be added to by game packages.

&START`MAIN [u(cobj,ccp)]=@command/disable kill;@dolist/inline/nobreak @dig @open @link={@command/restrict %i0=!POWER^GUEST&!FLAG^GAGGED&(POWER^BUILDER|FLAG^WIZARD|FLAG^ROYALTY)};@dolist/inline short-desc playstatus={@attribute/access/retroactive %i0=visual};@dolist/inline D={@attribute/access/retroactive %i0=mortal_dark WIZARD};@dolist/inline V={@attribute/access/retroactive %i0=WIZARD visual veiled}

@DESCRIBE [u(cobj,ccp)]=This needs a desc!

@@ FILTER SECTION

&FIL`ISDBREF [u(cobj,ccp)]=isdbref(%0)
&FIL`ISOBJID [u(cobj,ccp)]=isobjid(%0)
&FIL`ISADMIN [u(cobj,ccp)]=u(isadmin,%0)
&FIL`OLDOBJID [u(cobj,ccp)]=not(isobjid(before(%0,~)))
&FIL`ISSLOT [u(cobj,ccp)]=isint(last(%0,`))
&FIL`ALIASMATCH [u(cobj,ccp)]=strmatch(alias(%0),%1*)
&FIL`NOTGUEST [u(cobj,ccp)]=not(u(isguest,%0))
&FIL`FINDACCOUNT [u(cobj,ccp)]=match(get(%0/CHARACTERS),%q<finddb>)
&FIL`STRLEN [u(cobj,ccp)]=t(strlen(%0))
&FIL`ISINT [u(cobj,ccp)]=isint(%0)
&FIL`LISTENRHOST [u(cobj,ccp)]=orflags(%0,pc)
&FIL`HASATTR [u(cobj,ccp)]=hasattr(%0/%1)
&FIL`TESTLOCK [u(cobj,ccp)]=testlock(%1,%0)

@@ LOCK MANAGER
&INC`VALID`GROUPLOCK [u(cobj,ccp)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: No lock entered!;@check u(lmin,iter(%0,cand(isdbref(u(setr,fgrp,u(u(cobj,group)/FUN`FINDGROUP,before(%i0,/),%#))),cor(not(strlen(after(%i0,/))),if(isint(after(%i0,/)),gte(after(%i0,/),0),match(u(u(cobj,group)/FUN`PERMS,%q<fgrp>,1),after(%i0,/),|)))),|,%B))=@attach %!/INC`MSG=ERROR: One or more group entries did not validate. Group names must be typed correctly and the /specification must be a Rank integer or a full Permission - sorry\, no partial match.;th setq(value,setr(lockstr,u(GROUP_LOCK,%0))[if(%1,|V`ADMIN:>0|FLAG^ROYALTY|FLAG^WIZARD)]);th u(setq,valueformat,iter(%0,[setq(fgrp,u(u(cobj,group)/FUN`FINDGROUP,before(%i0,/),%#))][if(get(%q<fgrp>/SET`PRIVATE),*PRIVATE*,u(%q<fgrp>/GET`NAME))]%B\([if(not(strlen(after(%i0,/))),Member,if(isint(setr(parm,after(%i0,/))),Rank %q<parm>+,ucstr(%q<parm>) Permission))]\),|,%BOR%B))

&GROUP_LOCK [u(cobj,ccp)]=localize(iter(%0,[setq(fgrp,u(u(cobj,group)/FUN`FINDGROUP,before(%i0,/),%1))][if(not(strlen(after(%i0,/))),setq(parm,9001),setq(parm,after(%i0,/)))][if(isint(%q<parm>),V`GROUP`[get(%q<fgrp>/D`ID)]:<=[strfirstof(after(%i0,/),9001)],V`GROUP`[get(%q<fgrp>/D`ID)]`PERMISSION`[ucstr(%q<parm>)]:>0)],|,|))

@@ U-FUNCTION SECTION

&NEWCOBJ [u(cobj,ccp)]=localize(if(not(isobjid(u(cobj,%1))),setr(newobj,objid(create(%0)))[if(not(%4),parent(%q<newobj>,u(firstof,%3,u(cobj,ccp))))][tel(%q<newobj>,u(firstof,%2,u(MASTER_ROOM)))][u(attrib_set,u(cobj,ccp),COBJ`%1,%q<newobj>)][set(%q<newobj>,%5)]))
@@ %0 - @name. %1 - alias. %2 - location to store. %3 - parent, defaults to MCO. %4 - No parent. %5 - PennMUSH flags. %6 - RhostMUSH flags.

&CANHEAR [u(cobj,ccp)]=lockfilter(FLAG^CONNECTED|FLAG^PUPPET|FLAG^MONITOR,%0)

&FIND_IN [u(cobj,ccp)]=locate(%0,%1,TXxi)

&ACCID [u(cobj,ccp)]=default(%0/D`ACCOUNT,if(%1,,%0))
&FALLBACK [u(cobj,ccp)]=u(strfirstof,get(%0/D`ACCOUNT`ID),get(%0/D`ID))

&ALTS [u(cobj,ccp)]=u(filter,ISOBJID,strfirstof(if(hasattrp(%0/D`ACCOUNT),get(get(%0/D`ACCOUNT)/CHARACTERS)),objid(%0)))
&PALTS [u(cobj,ccp)]=alts(%@)

&ISGUEST [u(cobj,ccp)]=haspower(%0,GUEST)

&ISADMIN [u(cobj,ccp)]=cor(orflags(%0,Wr),match(get(u(conf,STAFF_GROUP)/MEMBERS),objid(%0)))

&ISADMIN2 [u(cobj,ccp)]=u(isadmin,%#)

&ISWIZARD2 [u(cobj,ccp)]=u(iswizard,%#)

&ISWIZARD [u(cobj,ccp)]=hasflag(%0,WIZARD)

&ISHIDDEN [u(cobj,ccp)]=hidden(%0)

&ISIC [u(cobj,ccp)]=default(room(%0)/D`IC,0)

&MASTER_ROOM [u(cobj,ccp)]=config(master_room)
&OPEN_CREATION [u(cobj,ccp)]=strmatch(config(player_creation),yes)

&NATTR [u(cobj,ccp)]=nattr(%0)

&NCHILDREN [u(cobj,ccp)]=nchildren(%0)

&FILTER [u(cobj,ccp)]=filter(FIL`%0,%1,if(not(strlen(%2)),%b,%2),if(not(strlen(%3)),%b,%3),%4,%5,%6,%7,%8,%9)

&CSECS [u(cobj,ccp)]=csecs(%0)

&CTIME [u(cobj,ccp)]=ctime(%0)

&FANCYBASE [u(cobj,ccp)]=if(%0,timefmt(%2,%0,if(strlen(%1),if(isdbref(%1),u(gettz,%1),%1),u(gettz,%#))),<invalid date>)

&FANCYTIME [u(cobj,ccp)]=u(fancybase,%0,%1,$b $d $I:$M$p $Z)

&FANCYTIME2 [u(cobj,ccp)]=u(fancybase,%0,%1,$a $b $d $I:$M$p $Z)

&FANCYTIME3 [u(cobj,ccp)]=u(fancybase,%0,%1,$b $d $I:$M$p $Y $Z)

&FANCYDAY [u(cobj,ccp)]=u(fancybase,%0,%1,$b $d $Y)

&FANCYDATE [u(cobj,ccp)]=u(fancybase,%0,%1,$m/$d/$Y)

&DATE [u(cobj,ccp)]=u(fancybase,%0,%1,$x)

&PLAYER_NAME_LEN [u(cobj,ccp)]=config(player_name_len)

&CHANCOLOR [u(cobj,ccp)]=localize(if(isdbref(setr(mog,cmogrifier(stripansi(%0)))),u(channelcolor,%@,%q<mog>)))

&GETTZ [u(cobj,ccp)]=strfirstof(get(%0/TZ),UTC)

&CHARSEARCH [u(cobj,ccp)]=lmath(max,iter(%1,strmatch(%0,*%i0*)))

&BADCHARS [u(cobj,ccp)]=%r %t | ~ [u(rsep)] [u(fsep)] ^

&NEWCONF [u(cobj,ccp)]=[u(attrib_set,u(cobj,%0),CATEGORIES,setunion(get(u(cobj,%0)/CATEGORIES),%1,|,|))][u(attrib_set,u(cobj,%0),CATEGORIES`%1,setunion(get(u(cobj,%0)/CATEGORIES`%1),%2,|,|))][u(attrib_set,u(cobj,%0),DEFAULT`%1`%2,%3)][u(attrib_set,u(cobj,%0),DESC`%1`%2,%4)][u(attrib_set,u(cobj,%0),TYPE`%1`%2,%5)]

&REMCONF [u(cobj,ccp)]=@attach %!/WIPE=u(cobj,%0),*`%1`%2;&CATEGORIES`%1 [u(cobj,%0)]=setdiff(get(u(cobj,%0)/CATEGORIES`%1),%2,|,|);@select/inline words(get(u(cobj,%0)/CATEGORIES`%1),|)=0,{&CATEGORIES [u(cobj,%0)]=setdiff(get(u(cobj,%0)/CATEGORIES),%1,|,|)}

&LMAX [u(cobj,ccp)]=lmath(max,%0,%1)
&LMIN [u(cobj,ccp)]=lmath(min,%0,%1)
&LADD [u(cobj,ccp)]=lmath(add,%0,%1)
&AGO [u(cobj,ccp)]=switch(1,lt(%0,60),%0s,lt(%0,3600),div(%0,60)m,lt(%0,86400),div(%0,3600)h,div(%0,86400)d)
&SMALLTIME [u(cobj,ccp)]=etime(%0,3)
&ETIME [u(cobj,ccp)]=etime(%0)

&CAPNAMES [u(cobj,ccp)]=regeditalli(lcstr(%0),v(REG`CAPNAMES),capstr($1),v(REG`CAPNAMES3),lcstr($0),v(REG`CAPNAMES2),$1[capstr($2)])

&REG`CAPNAMES [u(cobj,ccp)]=(?:^|(?<=[_\/\-\|\s()\+]))([a-z]+)
&REG`CAPNAMES2 [u(cobj,ccp)]=(^|(?<=[(\|\/]))(of|the|a|and|in)
&REG`CAPNAMES3 [u(cobj,ccp)]=\b(of|the|a|and|in)\b

&COMMAFY [u(cobj,ccp)]=squish(flip(foreach(#lambda/if(mod(\%1,3),\%0,\\,\%0),flip(%0))),\,)

@@ Maybe universal?
@@ [switch(flip(foreach(#lambda/[lit([if(mod(%1,3),%0,%,%0)])],flip(%0))),%,)]

&SETSTAT [u(cobj,ccp)]=localize(u(delstat,%0,%1,%2)[u(attrib_set,%0,%1,setunion(get(%0/%1),%2~%3,|))])
&DELSTAT [u(cobj,ccp)]=u(attrib_set,%0,%1,setdiff(get(%0/%1),grab(get(%0/%1),%2~*,|),|))
&GETSTAT [u(cobj,ccp)]=if(strlen(%3),add(rest(grab(get(%0/%1),%2~*,|),~),rest(grab(get(%0/D`%3),%2~*,|),~)),rest(grab(get(%0/%1),%2~*,|),~))
&GRABSTAT [u(cobj,ccp)]=rest(grab(get(%0/%1),%2~*,|),~)

&SLOTR [u(cobj,ccp)]=[u(setq,strfirstof(%2,u(slotreg)),setunion(r(strfirstof(%2,u(slotreg))),%0~%1,|))]%1
&SLOTQ [u(cobj,ccp)]=u(setq,strfirstof(%2,u(slotreg)),setunion(r(strfirstof(%2,u(slotreg))),%0~%1,|))
&UNSLOTQ [u(cobj,ccp)]=u(setq,strfirstof(%2,u(slotreg)),setdiff(r(strfirstof(%2,u(slotreg))),grab(r(strfirstof(%2,u(slotreg))),%0~*,|),|))
&GSLOT [u(cobj,ccp)]=rest(grab(r(strfirstof(%1,u(slotreg))),%0~*,|),~)

&SLOTREG [u(cobj,ccp)]=slotreq

&LISTMAX [u(cobj,ccp)]=lmath(max,iter(%0,strlen(%i0),|,|),|)

&NEXTSLOT [u(cobj,ccp)]=add(1,lmath(max,iter(u(filter,ISSLOT,lattr(%0/[if(strlen(%1),%1`*,*)])),last(%i0,`))))

&GETMONIKER [u(cobj,ccp)]=localize(if(%1,if(setr(col,strfirstof(u(color,%1,NAMES,%0,,1),switch(1,strmatch(num(%0),num(%1)),u(color,%1,v(SYSTEM`COLORS),ME),strmatch(num(%0),%#),u(color,%1,v(SYSTEM`COLORS),SPEAKER),u(color,%1,v(SYSTEM`COLORS),SPEAKER)))),ansi(%q<col>,name(%0)),moniker(%0)),moniker(%0)))

&NAMELINK [u(cobj,ccp)]=localize(u(mxpmenu,u(strfirstof,%2,u(getmoniker,%0)),iter(setr(com,strfirstof(%1,u(conf,NAMELINK))),%i0 [name(%0)],|,|),%q<com>))

&NAMEGRAB [u(cobj,ccp)]=namegrab(%0,%1)

&NAMEGRABALL [u(cobj,ccp)]=namegraball(%0,%1)

&NUMTH [u(cobj,ccp)]=%0[switch(%0,11,th,12,th,13,th,switch(right(%0,1),1,st,2,nd,3,rd,th))]

&PUEBLIZE [u(cobj,ccp)]=u(MXPMENU,%0,%1,%2,%3)

&MXPMENU [u(cobj,ccp)]=localize(if(u(CAN_MXP,strfirstof(%3,%#)),tagwrap(send,"[render(strfirstof(%1,%0),html)]" hint="[render(switch(words(setr(hint,strfirstof(%2,%1,%0)),|),>1,Right Click for Menu \(Default: [first(%q<hint>,|)]\)|[rest(%q<hint>,|)],%q<hint>),html)]",%0),if(u(CAN_PUEBLO,strfirstof(%3,%#)),tagwrap(a,XCH_CMD="[render(strfirstof(first(%1,|),first(%1,|)),html)]",%0))))

&CAN_MXP [u(cobj,ccp)]=1
&CAN_PUEBLO [u(cobj,ccp)]=1

&TIMESTAMP [u(cobj,ccp)]=timefmt($Y-$m-$d $H:$M:$S,u(firstof,%0,secs()))

&UNPRIV [u(cobj,ccp)]=localize(setq(unprivtext,%0)[objeval(u(cobj,ap),s(%q<unprivtext>))])

&VALNUM [u(cobj,ccp)]=cand(isint(%0),gte(%0,%1))

&WEBLINK [u(cobj,ccp)]=localize(if(strlen(%0),tagwrap(a,href="[render(setr(webfix,if(not(strmatch(%0,*://*)),http://%0,%0)),html)]",strfirstof(%1,%q<webfix>))))

&RYG [u(cobj,ccp)]=<[if(gt(%0,50),255,round(mul(255,fdiv(mul(%0,2),100)),0))] [if(gte(%0,50),sub(mul(255,2),round(mul(255,fdiv(mul(%0,2),100)),0)),255)] 0>

&ALIASGRAB [u(cobj,ccp)]=if(match(%0,%1),%1,first(filter(#lambda/strmatch(alias(\%0),%1*),u(sortalias,%0))))

&ALIASGRABALL [u(cobj,ccp)]=if(match(%0,%1),%1,filter(#lambda/strmatch(alias(\%0),%1*),sortkey(#lambda/alias(\%0),%0,i)))

&RANDWORD [u(cobj,ccp)]=randword(%0,%1)

&STRFIRSTOF [u(cobj,ccp)]=strfirstof(%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)

&FIRSTOF [u(cobj,ccp)]=firstof(%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)

&ITEMIZE [u(cobj,ccp)]=itemize(%0,%1,%2,%3)

&TABLE [u(cobj,ccp)]=table(%0,%1,%2,%3,%4)

&BASECONV [u(cobj,ccp)]=baseconv(%0,%1,%2)

&WIDTH [u(cobj,ccp)]=width(%0)

&CENTER [u(cobj,ccp)]=center(%0,%1,%2)

&HASATTRVAL [u(cobj,ccp)]=hasattrval(%0)

&NAMELIST [u(cobj,ccp)]=namelist(%0,%1)

&LWHOID [u(cobj,ccp)]=setunion(lwhoid(%0),)

&LWHO [u(cobj,ccp)]=lwho(%0)

&GETALIAS [u(cobj,ccp)]=alias(%0)

&PUEBLO [u(cobj,ccp)]=pueblo(%0)

&DECOMPOSE [u(cobj,ccp)]=decompose(%0)

&LVPLAYERS [u(cobj,ccp)]=lvplayers(%0)

&LPLAYERS [u(cobj,ccp)]=lplayers(%0)

&LTHINGS [u(cobj,ccp)]=lthings(%0)

&LVTHINGS [u(cobj,ccp)]=lvthings(%0)

&LVEXITS [u(cobj,ccp)]=lvexits(%0)

&TRIMLINES [u(cobj,ccp)]=trimpenn(%0,%R)

&TRIMTABS [u(cobj,ccp)]=trimpenn(%0,%T)

&TRIMCASH [u(cobj,ccp)]=trimpenn(%0,$)

&WILDGREPI [u(cobj,ccp)]=wildgrepi(%0,%1,%2)

&UMAIL [u(cobj,ccp)]=u(elements,mail(%0),2)

&FULLALIAS [u(cobj,ccp)]=fullalias(%0)

@@ PROPERTY SECTION
&GETPROPERTY [u(cobj,ccp)]=localize(if(isdbref(%0),u(GETPROPERTY`%1,%0,%2,%3,%4)))

&GETPROPERTY`SEX [u(cobj,ccp)]=if(%1,left(get(%0/SEX),%1),get(%0/SEX))
&GETPROPERTY`NAME [u(cobj,ccp)]=u(getmoniker,%0)
&GETPROPERTY`ALIAS [u(cobj,ccp)]=if(%1,u(fullalias,%0),u(getalias,%0))
&GETPROPERTY`ALIASNAME [u(cobj,ccp)]=u(getmoniker,%0)[if(strlen(u(getalias,%0)),%B\([u(getalias,%0)]\))]
&GETPROPERTY`NAMELINK [u(cobj,ccp)]=u(namelink,%0,edit(%1,^^^,|))
&GETPROPERTY`HIDEIDLE [u(cobj,ccp)]=u(hideidle,%0)
&GETPROPERTY`HIDECONN [u(cobj,ccp)]=u(hideconn,%0)
&GETPROPERTY`LASTIDLE [u(cobj,ccp)]=u(lastidle,%0)
&GETPROPERTY`LASTIDLE2 [u(cobj,ccp)]=u(lastidle2,%0)
&GETPROPERTY`LOCATION [u(cobj,ccp)]=if(findable(%#,%0),if(cand(elock(loc(%0)/Teleport,%#),%1),u(pueblize,u(getmoniker,loc(%0)),+port [loc(%0)]),u(getmoniker,loc(%0))),*UNFINDABLE*)
&GETPROPERTY`TIMEZONE [u(cobj,ccp)]=u(gettz,%0)
&GETPROPERTY`UMAIL [u(cobj,ccp)]=u(umail,%0)
&GETPROPERTY`ALTS [u(cobj,ccp)]=if(isdbref(u(cobj,alts)),u(u(cobj,alts)/FUN`ALTS`LIST,%0))

&GETPROPERTY`URL [u(cobj,ccp)]=u(weblink,get(%0/V`PROPERTY`%1))

&GETPROPERTY`THEME [u(cobj,ccp)]=localize(if(strlen(u(setr,themes,get(%0/V`THEME))),u(pueblize,setr(theme,first(u(elements,%q<themes>,3,u(fsep)),u(rsep))),+theme %q<theme>))
&GETPROPERTY`TYPE [u(cobj,ccp)]=first(u(elements,get(%0/V`THEME),2,u(fsep)),u(rsep))

&GETPROPERTY`SCENE [u(cobj,ccp)]=if(default(%0/D`OBSERVER,0),ansi(hw,OBS),default(%0/D`SCENE,???))

&GETPROPERTY`GROUPDB [u(cobj,ccp)]=if(%1,u(elements,get(%0/V`TIERS),%1),get(%0/V`TIERS))
&GETPROPERTY`TIERDB [u(cobj,ccp)]=if(%2,u(elements,get(%0/V`TIERS`%1),%2),get(%0/V`TIERS`%1))

&GETPROPERTY`FACTION [u(cobj,ccp)]=if(isdbref(setr(grp,u(getproperty`tierdb,%0,u(conf,FACTION_TIER),1))),u(%q<grp>/GET`NAME))
&GETPROPERTY`FACABBR [u(cobj,ccp)]=if(isdbref(setr(grp,u(getproperty`tierdb,%0,u(conf,FACTION_TIER),1))),u(%q<grp>/GET`ABBREVIATION))
&GETPROPERTY`FACLETTER [u(cobj,ccp)]=if(isdbref(setr(grp,u(getproperty`tierdb,%0,u(conf,FACTION_TIER),1))),u(%q<grp>/GET`LETTER))
&GETPROPERTY`FACRANK [u(cobj,ccp)]=if(isdbref(setr(grp,num(u(getproperty`tierdb,%0,u(conf,FACTION_TIER),1)))),setq(title,get(%0/D`GROUP`%q<grp>`TITLE))[setq(rankname,get(%q<grp>/RANK`[setr(rank,get(%0/D`GROUP`%q<grp>`RANK))]`NAME))][setq(disp,if(%1,u(strfirstof,%q<title>,%q<rankname>),%q<rankname>))][if(%2,%q<rank>-%q<disp>,%q<disp>)])

&GETPROPERTY`GROUPS [u(cobj,ccp)]=iter(rest(u(getproperty`groupdb,%0,%1)),u(%i0/GET`NAME),%b,if(%2,|,\,%b))
&GETPROPERTY`BANNER [u(cobj,ccp)]=if(isdbref(setr(grp,first(u(getproperty`[if(strlen(%1),tierdb,groupdb)],%0,u(strfirstof,%1,1),1)))),u(%q<grp>/GET`COLORIZE,name(%0)),u(getmoniker,%0))

&GETPROPERTY`GROUPNAMES [u(cobj,ccp)]=iter(u(getproperty`groupdb,%0,%1),u(%i0/GET`NAME),%b,if(%2,|,\,%b))
&GETPROPERTY`TIERNAMES [u(cobj,ccp)]=iter(u(getproperty`tierdb,%0,%1,%2),u(%i0/GET`NAME),%b,if(%3,|,\,%b))

&GETPROPERTY`GROUPABBRS [u(cobj,ccp)]=iter(u(getproperty`groupdb,%0,%1),u(%i0/GET`ABBREVIATION),%b,if(%2,|,\,%b))
&GETPROPERTY`TIERABBRS [u(cobj,ccp)]=iter(u(getproperty`tierdb,%0,%1,%2),u(%i0/GET`ABBREVIATION),%b,if(%3,|,\,%b))

&COLUMNLINE [u(cobj,ccp)]=lalign(iter(u(setr,hdr,%0),after(%i0,~),|,%b),iter(%q<hdr>,before(%i0,~),|,u(fsep)),u(fsep))
&PLAYERLINE [u(cobj,ccp)]=lalign(iter(u(setr,col,%1),after(%i0,~),|,%b),iter(%q<col>,u(getprop,%0,%i0),|,u(fsep)),u(fsep))

&GETPROP [u(cobj,ccp)]=u(strfirstof,u(getproperty,%0,setr(fget,u(elements,setr(get,before(%1,~)),1,/)),u(elements,%q<get>,2,/),u(elements,%q<get>,3,/),u(elements,%q<get>,4,/),u(elements,%q<get>,5,/)),if(strlen(%2),get(%0/%2`%q<fget>)))

&INC`VALID`COLUMNS [u(cobj,ccp)]=@check strlen(setr(value,stripansi(trim(%0))))=@attach %!/INC`MSG=ERROR: No Columns data given!;@stop strmatch(u(COLUMNLINE,%q<value>),*#-1*)=@attach %!/INC`MSG=ERROR: Column definition results in an Error. Check input and try again. +help DynamicData

&INC`VALID`GETTABLE [u(cobj,ccp)]=@check strlen(setr(value,stripansi(trim(%0))))=@attach %!/INC`MSG=ERROR: No Columns data given!;@stop strmatch(u(PLAYERLINE,%#,%q<value>),*#-1*)=@attach %!/INC`MSG=ERROR: GetTable definition results in an Error. Check input and try again. +help DynamicData

&chan_title_len [u(cobj,ccp)]=config(chan_title_len)

&CODENAME [u(cobj,ccp)]=if(match(get(%0/MOGUSE`PLAYERNAME),codename),get(%0/CODENAME`%#))

@@ Sort section.

&SORTATTR [u(cobj,ccp)]=sortkey(#lambda/last(\%0,`),%0,n,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))
&SORTIDLE [u(cobj,ccp)]=sort(%0,idle,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))
&SORTNAME [u(cobj,ccp)]=sort(%0,namei,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))
&SORTALIAS [u(cobj,ccp)]=sortkey(#lambda/lcstr(alias(\%0)),%0,i,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))
&SORTOLDID [u(cobj,ccp)]=sortkey(#lambda/after(\%0,~),%0,i,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))
&SORTPRIORITY [u(cobj,ccp)]=sortkey(#lambda/default(\%0/D`ALTS`PRIORITY,0),%0,n,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))
&SORTORDER [u(cobj,ccp)]=sortkey(#lambda/default(\%0/ORDER,99),%0,n,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))

&CHOOSEGAME [u(cobj,ccp)]=%0
&CHOOSEQ [u(cobj,ccp)]=u(setq,%0,%1)
&CHOOSER [u(cobj,ccp)]=u(setq,%0,%1)

@@ REGEX STORAGE
&REG`CAPNAMES [u(cobj,ccp)]=(?:^|(?<=[_\/\-\|\s()\+]))([a-z]+)
&REG`CAPNAMES2 [u(cobj,ccp)]=(^|(?<=[(]))(of|the|a|and)
&REG`CAPNAMES3 [u(cobj,ccp)]=\b(of|the|a|and)\b

&REG`SQL`ENTRY [u(cobj,ccp)]=\?|\!

@@ SQL WRAPPER

&RENDER [u(cobj,ccp)]=render(%0,ansi)

&RSEP [u(cobj,ccp)]=chr(176)
&FSEP [u(cobj,ccp)]=chr(177)

&MYSQL [u(cobj,ccp)]=if(hasattrp(%!/Q`%0),sql(u(setr,sqlformat,u(SQL`FORMAT,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9))),#-1 SQL QUERY DATA NOT FOUND)
&MYSQL2 [u(cobj,ccp)]=if(hasattrp(%!/Q`%0),sql(u(setr,sqlformat,u(SQL`FORMAT,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)),|,^),#-1 SQL QUERY DATA NOT FOUND)
&MYSQL3 [u(cobj,ccp)]=if(hasattrp(%!/Q`%0),sql(u(setr,sqlformat,u(SQL`FORMAT,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)),u(rsep),u(fsep)),#-1 SQL QUERY DATA NOT FOUND)



&CALL`0 [u(cobj,ccp)]=sql(CALL %0(),switch(%1,0,%b,2,|,3,u(rsep),%b),switch(%1,0,%b,2,^,3,u(fsep),%b))

&CALL`1 [u(cobj,ccp)]=sql(CALL %0(%2),switch(%1,0,%b,2,|,3,u(rsep),%b),switch(%1,0,%b,2,^,3,u(fsep),%b))

&CALL`2 [u(cobj,ccp)]=sql(CALL %0(%2,%3),switch(%1,0,%b,2,|,3,u(rsep),%b),switch(%1,0,%b,2,^,3,u(fsep),%b))

&CALL`3 [u(cobj,ccp)]=sql(CALL %0(%2,%3,%4),switch(%1,0,%b,2,|,3,u(rsep),%b),switch(%1,0,%b,2,^,3,u(fsep),%b))

&CALL`4 [u(cobj,ccp)]=sql(CALL %0(%2,%3,%4,%5),switch(%1,0,%b,2,|,3,u(rsep),%b),switch(%1,0,%b,2,^,3,u(fsep),%b))

&CALL`5 [u(cobj,ccp)]=sql(CALL %0(%2,%3,%4,%5,%6),switch(%1,0,%b,2,|,3,u(rsep),%b),switch(%1,0,%b,2,^,3,u(fsep),%b))

&CALL`6 [u(cobj,ccp)]=sql(CALL %0(%2,%3,%4,%5,%6,%7),switch(%1,0,%b,2,|,3,u(rsep),%b),switch(%1,0,%b,2,^,3,u(fsep),%b))

&CALL`7 [u(cobj,ccp)]=sql(CALL %0(%2,%3,%4,%5,%6,%7,%8),switch(%1,0,%b,2,|,3,u(rsep),%b),switch(%1,0,%b,2,^,3,u(fsep),%b))

&CALL`8 [u(cobj,ccp)]=sql(CALL %0(%2,%3,%4,%5,%6,%7,%8,%9),switch(%1,0,%b,2,|,3,u(rsep),%b),switch(%1,0,%b,2,^,3,u(fsep),%b))

&SQL`IN`NUMBER [u(cobj,ccp)]=iter(%0,%i0,%1,\,%b)
&SQL`IN`STRING [u(cobj,ccp)]=iter(%0,'[sqlescape(%i0)]',%1,\,%b)

&SQL`FORMAT [u(cobj,ccp)]=localize(edit(edit(regeditalli(v(Q`%0),v(REG`SQL`ENTRY),if(isint(setr(set,v(setr(num,add(%q<num>,1))))),%q<set>,switch($0,!,%q<set>,[switch(%q<set>,NULL,NULL,SET_NULL,SET_NULL,'[sqlescape(%q<set>)]')]))),=NULL,%BIS NULL),=SET_NULL,=NULL))

&INC`DOSQL [u(cobj,ccp)]=@stop strmatch(setr(errcheck,u(mysql,before(%0,/),%1,%2,%3,%4,%5,%6,%7,%8,%9)),#-*)[if(strlen(after(%0,/)),setq(after(%0,/),sql(SELECT last_insert_id\(\))))]={@attach %!/INC`MSG=SQL ERROR - [before(%0,/)]: %q<errcheck>;@attach %!/INC`MSG`CHAN=SQL ERROR - %!/[before(%0,/)]: %q<errcheck> - QUERY: %q<sqlformat>}

&INC`DOCALL`0 [u(cobj,ccp)]=@stop strmatch(setr(errcheck,u(call`0,before(%0,/),0,%1,%2,%3,%4,%5,%6,%7,%8,%9)),#-*)[if(strlen(after(%0,/)),setq(after(%0,/),first(%q<errcheck>)))]={@attach %!/INC`MSG=SQL ERROR - [before(%0,/)]: %q<errcheck>;@attach %!/INC`MSG`CHAN=SQL ERROR - %!/[before(%0,/)]: %q<errcheck>}

&INC`DOCALL`1 [u(cobj,ccp)]=@stop strmatch(setr(errcheck,u(call`1,before(%0,/),0,%1,%2,%3,%4,%5,%6,%7,%8,%9)),#-*)[if(strlen(after(%0,/)),setq(after(%0,/),first(%q<errcheck>)))]={@attach %!/INC`MSG=SQL ERROR - [before(%0,/)]: %q<errcheck>;@attach %!/INC`MSG`CHAN=SQL ERROR - %!/[before(%0,/)]: %q<errcheck>}

&INC`DOCALL`2 [u(cobj,ccp)]=@stop strmatch(setr(errcheck,u(call`2,before(%0,/),0,%1,%2,%3,%4,%5,%6,%7,%8,%9)),#-*)[if(strlen(after(%0,/)),setq(after(%0,/),first(%q<errcheck>)))]={@attach %!/INC`MSG=SQL ERROR - [before(%0,/)]: %q<errcheck>;@attach %!/INC`MSG`CHAN=SQL ERROR - %!/[before(%0,/)]: %q<errcheck>}

&INC`SEARCHSQL [u(cobj,ccp)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: No %1 name entered!;@select/inline words(u(setr,tempresults,u(strfirstof,u(setr,searchexact,u(mysql3,FIND`%2`EXACT[if(isint(%0),`ID)],%0,%4,%5,%6,%7,%8,%9)),if(not(strlen(%q<searchexact>)),u(mysql3,FIND`%2`WILD,sqlescape(%0),%4,%5,%6,%7,%8,%9)))),u(rsep))=>1,{@stop 1=@attach %!/INC`MSG=ERROR: Results are Ambiguous. That matches: [iter(%q<tempresults>,u(elements,%i0,2,u(fsep)),u(rsep),\,%b)]},0,{@stop 1=@attach %!/INC`MSG=ERROR: Could not find %1 '%0'. Choices are: [iter(u(mysql3,FIND`%2`LIST,%4,%5,%6,%7,%8,%9),%i0,u(rsep),\,%b)]},1,{@attach %!/INC`LOADSQL=%2,u(elements,%q<tempresults>,1,u(fsep)),%3;th unsetq(tempresults searchexact)}
@@ Searches database for id,name pairs and attempts partial matching and etc.
@@ %0 - Text or ID to search for. %1 - name to display it as. %2 - QUERY CATEGORY to search. What kind of OBJECT is this? %3 - What slot # to load data into? %4+ - Additional parameters needed by SQL queries.

&INC`LOADSQL [u(cobj,ccp)]=@check strlen(u(setr,datarow,u(mysql3,LOAD`%0,%1)))=@attach %!/INC`MSG=ERROR: %0 data not found.;@attach %!/INC`REGOBJ=capstr(lcstr(u(strfirstof,%3,%0))),v(FIELD`%0),%2,%q<datarow>;th unsetq(datarow)
@@ Retrieves FIELD`%0 names and loads them into structured named registers.
@@ %0 - The TYPE OF OBJECT this is going to be. This must match the queries. Q`LOAD`%0 will be called. %1 - The ID of the object to load. %2 - The # that this will be loaded into. %3 - Alternate SQL LOAD name to use.

&INC`REGOBJ [u(cobj,ccp)]=@dolist/inline %1={th u(setq,%0%2.%i0,u(elements,%3,inum(0),u(fsep)))}
@@ Takes arbitrary data (usually from SQL) and loads it into named registers following pattern of <thing>#.<parameter>.
@@ %0 - the Thing name. %1 - space-delimited list of parameters. %2 - The # for this object. %3 - The data, presented in mysql fashion (delimiter is character 177)

@@ The CONFIG Subsystem.
&CMD`+GAMECONFIG`PENNMUSH [u(cobj,ccp)]=$^\+gameconfig(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+GAMECONFIG`MAIN
@set [u(cobj,ccp)]/CMD`+GAMECONFIG`PENNMUSH=regexp no_inherit
&CMD`+GAMECONFIG`MAIN [u(cobj,ccp)]=@attach %!/INC`CONFIG=%2,%3

&INC`CONFIG [u(cobj,ccp)]=@select/inline strlen(%0)=0,{@attach %!/INC`CONFIG`DISPLAY},{@check u(iswizard,%#)=@attach %!/INC`MSG=ERROR: Permission denied. Changing Config requires Wizard permissions.;@attach %!/INC`CONFIG`CHANGE}
&INC`CONFIG`DISPLAY [u(cobj,ccp)]=@pemit %#=u(header,u(SYSTEM`NAME) Configuration);@dolist/inline/delimit | [v(CONFIG`OPTIONS)]={@pemit %#=u(separator,##);@pemit %#=align(10 10 12 40,rjust(ansi(hw,TYPE:),10),v(CONFIG`##`VALID),ansi(hw,DESCRIPTION:),v(CONFIG`##));@pemit %#=[rjust(ansi(hw,DEFAULT:),10)]%b[v(CONFIG`##`DEFAULT)];@pemit %#=[rjust(ansi(hw,CURRENT:),10)]%b[u(setr,cur,u(conf,##))];@select/inline cand(strlen(u(setr,form,get(%!/CONFIG`##`FORMATTED))),not(strmatch(%q<form>,%q<cur>)))=1,{@pemit %#=[rjust(ansi(hw,FORMATTED:),10)]%B%q<form>}};@pemit %#=u(footer)
&INC`CONFIG`CHANGE [u(cobj,ccp)]=@attach %!/INC`PARTIAL=%0,v(CONFIG`OPTIONS),|,op,Option;@select/inline strlen(%1)=>0,{@attach %!/INC`CONFIG`SET},{@attach %!/INC`CONFIG`CLEAR}
&INC`CONFIG`SET [u(cobj,ccp)]=@attach %!/INC`VALID`[v(CONFIG`%q<op>`VALID)]=%1;th u(attrib_set,%!,CONFIG`%q<op>`CUSTOM,%q<value>);th u(attrib_set,%!,CONFIG`%q<op>`FORMATTED,%q<valueformat>);@attach %!/INC`MSG=You set Config Option '%q<op>' to: %q<value>;@attach %!/INC`MSG`CHAN=Config Option '%q<op>' set to: %q<value>;@attach %!/CONFIG`%q<op>`FINISH
&INC`CONFIG`CLEAR [u(cobj,ccp)]=@attach %!/WIPE=%!,CONFIG`%q<op>`CUSTOM;@attach %!/INC`MSG=You set Config Option '%q<op>' to its Default.;@attach %!/INC`MSG`CHAN=Config Option '%q<op>' reset to Default.;@attach %!/CONFIG`%q<op>`FINISH

&CONFIG`OPTIONS [u(cobj,ccp)]=WIZTAG|ROYTAG|ANONYMOUS_NOTICES|PUBLIC_EMAIL|STAFF_ALERTS|REQUIRE_APPROVAL|NAMELINK|HEADER_FILL|SUBHEADER_FILL|SEPARATOR_FILL|FOOTER_FILL|BORDER_MODE|COLUMN_NAMES|HEADER_TEXT|HEADER_STAR|BORDER|MSG|MSGTEXT|OOC|OOCEDGE|STAFF_GROUP|ROOM_PLAYERS|ROOM_THINGS|ROOM_EXITS|EXIT_COLOR|EXIT_ALIAS|FACTION_TIER
@set [u(cobj,ccp)]/CONFIG`OPTIONS=no_inherit

&CONFIG`WIZTAG [u(cobj,ccp)]=Color of WIZ tag in systems.
&CONFIG`WIZTAG`DEFAULT [u(cobj,ccp)]=hr
&CONFIG`WIZTAG`VALID [u(cobj,ccp)]=COLOR

&CONFIG`ROYTAG [u(cobj,ccp)]=Color of ROY tag in systems.
&CONFIG`ROYTAG`DEFAULT [u(cobj,ccp)]=hr
&CONFIG`ROYTAG`VALID [u(cobj,ccp)]=COLOR

&CONFIG`STAFF_GROUP [u(cobj,ccp)]=Staff Group for use with wizlist, +admin, etc. Recommend usage of a Private Group called _Admin
&CONFIG`STAFF_GROUP`DEFAULT [u(cobj,ccp)]=
&CONFIG`STAFF_GROUP`VALID [u(cobj,ccp)]=GROUP

&CONFIG`ANONYMOUS_NOTICES [u(cobj,ccp)]=Anonymize administrative actions in messages to players?
&CONFIG`ANONYMOUS_NOTICES`DEFAULT [u(cobj,ccp)]=0
&CONFIG`ANONYMOUS_NOTICES`VALID [u(cobj,ccp)]=BOOL

&CONFIG`PUBLIC_EMAIL [u(cobj,ccp)]=Game's public email for purpose of various messages.
&CONFIG`PUBLIC_EMAIL`DEFAULT [u(cobj,ccp)]=
&CONFIG`PUBLIC_EMAIL`VALID [u(cobj,ccp)]=WORD

&CONFIG`STAFF_ALERTS [u(cobj,ccp)]=Channel(s) to send important staff-only reports to.
&CONFIG`STAFF_ALERTS`DEFAULT [u(cobj,ccp)]=
&CONFIG`STAFF_ALERTS`VALID [u(cobj,ccp)]=LIST

&CONFIG`REQUIRE_APPROVAL [u(cobj,ccp)]=Game requires use of the +approval system? Used for application-based games.
&CONFIG`REQUIRE_APPROVAL`DEFAULT [u(cobj,ccp)]=1
&CONFIG`REQUIRE_APPROVAL`VALID [u(cobj,ccp)]=BOOL

&CONFIG`FACTION_TIER [u(cobj,ccp)]=Group Tier that will be used by default for all 'Faction' GetProperty features. 0 or higher please!
&CONFIG`FACTION_TIER`DEFAULT [u(cobj,ccp)]=1
&CONFIG`FACTION_TIER`VALID [u(cobj,ccp)]=INT

&CONFIG`NAMELINK [u(cobj,ccp)]=List of commands targeting <character> to show in MXP menus for namelink displays. (PennMUSH only.)
&CONFIG`NAMELINK`DEFAULT [u(cobj,ccp)]=+finger|+join
&CONFIG`NAMELINK`VALID [u(cobj,ccp)]=LIST

&CONFIG`HEADER_FILL [u(cobj,ccp)]=Symbol to use for Header lines.
&CONFIG`HEADER_FILL`DEFAULT [u(cobj,ccp)]==
&CONFIG`HEADER_FILL`VALID [u(cobj,ccp)]=WORD

&CONFIG`SUBHEADER_FILL [u(cobj,ccp)]=Symbol to use for Sub-Header lines.
&CONFIG`SUBHEADER_FILL`DEFAULT [u(cobj,ccp)]==
&CONFIG`SUBHEADER_FILL`VALID [u(cobj,ccp)]=WORD

&CONFIG`SEPARATOR_FILL [u(cobj,ccp)]=Symbol to use for Separator lines.
&CONFIG`SEPARATOR_FILL`DEFAULT [u(cobj,ccp)]==
&CONFIG`SEPARATOR_FILL`VALID [u(cobj,ccp)]=WORD

&CONFIG`FOOTER_FILL [u(cobj,ccp)]=Symbol to use for Footer lines.
&CONFIG`FOOTER_FILL`DEFAULT [u(cobj,ccp)]=-
&CONFIG`FOOTER_FILL`VALID [u(cobj,ccp)]=WORD

&CONFIG`BORDER_MODE [u(cobj,ccp)]=Alters style of headers/footers. 0 = No edges. 1 = + edges. 2 = slash-edges
&CONFIG`BORDER_MODE`DEFAULT [u(cobj,ccp)]=0
&CONFIG`BORDER_MODE`VALID [u(cobj,ccp)]=WORD

&CONFIG`COLUMN_NAMES [u(cobj,ccp)]=Color for table column names.
&CONFIG`COLUMN_NAMES`DEFAULT [u(cobj,ccp)]=g
&CONFIG`COLUMN_NAMES`VALID [u(cobj,ccp)]=COLOR

&CONFIG`HEADER_TEXT [u(cobj,ccp)]=Color for text in System Header/Subheaders.
&CONFIG`HEADER_TEXT`DEFAULT [u(cobj,ccp)]=hw
&CONFIG`HEADER_TEXT`VALID [u(cobj,ccp)]=COLOR

&CONFIG`HEADER_STAR [u(cobj,ccp)]=Color for * in System Headers.
&CONFIG`HEADER_STAR`DEFAULT [u(cobj,ccp)]=hm
&CONFIG`HEADER_STAR`VALID [u(cobj,ccp)]=COLOR

&CONFIG`BORDER [u(cobj,ccp)]=Color for borders in enclosed tables.
&CONFIG`BORDER`DEFAULT [u(cobj,ccp)]=m
&CONFIG`BORDER`VALID [u(cobj,ccp)]=COLOR

&CONFIG`OOC [u(cobj,ccp)]=Used for the OOC tag prefixing +ooc messages.
&CONFIG`OOC`DEFAULT [u(cobj,ccp)]=hr
&CONFIG`OOC`VALID [u(cobj,ccp)]=COLOR

&CONFIG`OOCEDGE [u(cobj,ccp)]=Used for the symbols of the +ooc message prefix.
&CONFIG`OOCEDGE`DEFAULT [u(cobj,ccp)]=hw
&CONFIG`OOCEDGE`VALID [u(cobj,ccp)]=COLOR

&CONFIG`MSG [u(cobj,ccp)]=Color for the -=<>=- part of System Messages.
&CONFIG`MSG`DEFAULT [u(cobj,ccp)]=hm
&CONFIG`MSG`VALID [u(cobj,ccp)]=COLOR

&CONFIG`MSGTEXT [u(cobj,ccp)]=Color for the System Name in System Messages.
&CONFIG`MSGTEXT`DEFAULT [u(cobj,ccp)]=hw
&CONFIG`MSGTEXT`VALID [u(cobj,ccp)]=COLOR

&CONFIG`ROOM_PLAYERS [u(cobj,ccp)]=Rooms use attribute PLAYERS`<this> for displaying Players. Attributes go on the Ancestor Room.
&CONFIG`ROOM_PLAYERS`DEFAULT [u(cobj,ccp)]=DEFAULT
&CONFIG`ROOM_PLAYERS`VALID [u(cobj,ccp)]=WORD
&CONFIG`ROOM_PLAYERS`FINISH [u(cobj,ccp)]=th u(attrib_set,u(cobj,ancestor_room),SETTINGS`ROOM_PLAYERS,%q<value>)

&CONFIG`ROOM_THINGS [u(cobj,ccp)]=Rooms use attribute THINGS`<this> for displaying Things. Attributes go on the Ancestor Room.
&CONFIG`ROOM_THINGS`DEFAULT [u(cobj,ccp)]=DEFAULT
&CONFIG`ROOM_THINGS`VALID [u(cobj,ccp)]=WORD
&CONFIG`ROOM_THINGS`FINISH [u(cobj,ccp)]=th u(attrib_set,u(cobj,ancestor_room),SETTINGS`ROOM_THINGS,%q<value>)

&CONFIG`ROOM_EXITS [u(cobj,ccp)]=Rooms use attribute EXITS`<this> for displaying Exits. Attributes go on the Ancestor Room.
&CONFIG`ROOM_EXITS`DEFAULT [u(cobj,ccp)]=DEFAULT
&CONFIG`ROOM_EXITS`VALID [u(cobj,ccp)]=WORD
&CONFIG`ROOM_EXITS`FINISH [u(cobj,ccp)]=th u(attrib_set,u(cobj,ancestor_room),SETTINGS`ROOM_EXITS,%q<value>)

&CONFIG`EXIT_COLOR [u(cobj,ccp)]=Color to display Exits in.
&CONFIG`EXIT_COLOR`DEFAULT [u(cobj,ccp)]=n
&CONFIG`EXIT_COLOR`VALID [u(cobj,ccp)]=COLOR
&CONFIG`EXIT_COLOR`FINISH [u(cobj,ccp)]=th u(attrib_set,u(cobj,ancestor_room),SETTINGS`EXIT_COLOR,%q<value>)

&CONFIG`EXIT_ALIAS [u(cobj,ccp)]=Color to display Exit Aliases in.
&CONFIG`EXIT_ALIAS`DEFAULT [u(cobj,ccp)]=hw
&CONFIG`EXIT_ALIAS`VALID [u(cobj,ccp)]=COLOR
&CONFIG`EXIT_ALIAS`FINISH [u(cobj,ccp)]=th u(attrib_set,u(cobj,ancestor_room),SETTINGS`EXIT_ALIAS,%q<value>)

&CONF [u(cobj,ccp)]=strfirstof(get(%!/CONFIG`%0`CUSTOM),get(%!/CONFIG`%0`DEFAULT))
&PCONF [u(cobj,ccp)]=u(color,%@,%1)

@@ COLOR SYSTEM!
&CMD`+COLORS`PENNMUSH [u(cobj,ccp)]=$^\+color(?\:s)?(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+COLORS`MAIN
@set [u(cobj,ccp)]/CMD`+COLORS`PENNMUSH=regexp no_inherit
&CMD`+COLORS`MAIN [u(cobj,ccp)]=@select/inline strlen(%1)=>0,{@attach %!/INC`PARTIAL=%1,NAME,|,switch;@attach %!/INC`COLOR`NAME=%2,%3},{@attach %!/INC`COLOR=%2,%3,SYSTEM}
@set [u(cobj,ccp)]/CMD`+COLORS`MAIN=no_inherit

&INC`COLOR`NAME [u(cobj,ccp)]=@switch/inline strlen(%0)=0,{@attach %!/INC`COLOR`NAME`LIST},{@attach %!/INC`COLOR`NAME`SET}
@set [u(cobj,ccp)]/INC`COLOR`NAME=no_inherit

&INC`COLOR`NAME`LIST [u(cobj,ccp)]=@pemit %#=u(header,Name Color);@pemit %#=ansi(u(color,%#,v(SYSTEM`COLORS),COLUMN_NAMES),align(34 10,Name,Color));@pemit %#=u(separator);@dolist/inline u(filter,ISDBREF,edit(u(lattr,u(strfirstof,u(accid,%#),%#)/CUSTCOLOR`CHARACTER`#*),CUSTCOLOR`CHARACTER`,))={@pemit %#=align(34 10,u(getmoniker,##),get(%#/CUSTCOLOR`CHARACTER`##))};@pemit %#=u(footer)
@set [u(cobj,ccp)]/INC`COLOR`NAME`LIST=no_inherit

&INC`COLOR`NAME`SET [u(cobj,ccp)]=@attach %!/INC`CHECKPC=%0,1;@switch/inline strlen(%1)=0,{@attach %!/INC`MSG=Colorname for '%q<t1name>' restored to default.;@attach %!/WIPE=u(accid,%#),CUSTCOLOR`CHARACTER`%q<t1>},{@attach %!/INC`VALID`COLOR=%1;th u(attrib_set,u(accid,%#),CUSTCOLOR`CHARACTER`%q<t1>,%q<value>);@attach %!/INC`MSG=u(setr,msg,NAMES %q<t1name> Set to: %q<value>)}
@set [u(cobj,ccp)]/INC`COLOR`NAME`SET=no_inherit

&COLOR [u(cobj,ccp)]=switch(%+,2,u(strfirstof,get(%0/CUSTCOLOR`%1),u(conf,%1)),3,strfirstof(get(%0/CUSTCOLOR`%1`%2`CUSTOM),v(COLOR`%2`DEFAULT),get(%0/CUSTCOLOR`SYSTEM`%2`CUSTOM),u(conf,%2)))
&GROUPCOLOR [u(cobj,ccp)]=get(%0/CUSTCOLOR`GROUP`%1)
&NAMECOLOR [u(cobj,ccp)]=udefault(%0/COLOR_CHARACTER,switch(1,strmatch(num(%0),num(%1)),u(color,%0,v(SYSTEM`COLORS),ME),strmatch(num(%1),num(%2)),u(color,%0,v(SYSTEM`COLORS),SPEAKER),default(%0/CUSTCOLOR`CHARACTER`%1,u(color,%0,v(SYSTEM`COLORS),OTHER))),%1)

&CMD`COLORDEMO [u(cobj,ccp)]=$^(?s)(?\:\+)?colordemo(?\:/(\S+)?)?$:@attach %!/INC`PARTIAL=%1,LONG,|,switch;@select/inline %q<switch>=LONG,{@dolist/inline lnum(97,122)=@pemit %#=letq(c,colors(chr(%i0)*),letq(g,regeditall(%qc,\\d+\\b,),iter(unique(sort(%qg)),ljust(%i0,15): [iter(u(elements,%qc,matchall(%qg,%i0)),ansi(+%i0,+%i0))],%b,%r)))},{@pemit %#=iter(lnum(0,255),ansi(+xterm%i0,+xterm%i0))}
@set [u(cobj,ccp)]/CMD`COLORDEMO=regexp no_inherit

&CHANNELCOLOR [u(cobj,ccp)]=get(%0/CUSTCOLOR`CHANNEL`%1)

&GOODCOLORS [u(cobj,ccp)]=setdiff(lnum(1,231),7 8 [lnum(16,21)])

&INC`COLORS [u(cobj,ccp)]=@attach %!/INC`COLOR
&INC`COLOR [u(cobj,ccp)]=@select/inline strlen(%0)=0,{@attach %!/INC`COLOR`DISPLAY},{@attach %!/INC`COLOR`CHANGE}
&INC`COLOR`DISPLAY [u(cobj,ccp)]=@pemit %#=u(header,u(SYSTEM`NAME) Colors);@dolist/inline/delimit | [v(COLORS`OPTIONS)]={@pemit %#=u(separator,##);@pemit %#=align(10 10 12 40,rjust(ansi(hw,TYPE:),10)%R[rjust(ansi(hw,DEFAULT:),10)],default(COLOR`##`VALID,COLOR)%R[v(COLORS`##`DEFAULT)],ansi(hw,DESCRIPTION:),v(COLORS`##));@pemit %#=[rjust(ansi(hw,CURRENT:),10)]%b[u(setr,cur,u(color,%#,v(SYSTEM`COLORS),##))];@select/inline cand(strlen(u(setr,form,get(u(accid,%#)/CUSTCOLOR`[v(SYSTEM`COLORS)]`##`FORMATTED))),not(strmatch(%q<form>,%q<cur>)))=1,{@pemit %#=[rjust(ansi(hw,FORMATTED:),10)]%B%q<form>}};@pemit %#=u(footer,+colordemo for color list)
&INC`COLOR`CHANGE [u(cobj,ccp)]=@attach %!/INC`PARTIAL=%0,v(COLORS`OPTIONS),|,col,Color;@select/inline strlen(%1)=>0,{@attach %!/INC`COLOR`SET},{@attach %!/INC`COLOR`CLEAR}
&INC`COLOR`SET [u(cobj,ccp)]=@attach %!/INC`VALID`[default(COLORS`%q<col>`VALID,COLOR)]=%1;th u(attrib_set,u(accid,%#),CUSTCOLOR`[v(SYSTEM`COLORS)]`%q<col>`CUSTOM,%q<value>);th u(attrib_set,u(accid,%#),CUSTCOLOR`[v(SYSTEM`COLORS)]`%q<col>`FORMATTED,%q<valueformat>);@attach %!/INC`MSG=Color '%q<col>' set to: %q<valueformat>;@attach %!/COLOR`%q<col>`FINISH
&INC`COLOR`CLEAR [u(cobj,ccp)]=@attach %!/WIPE=u(accid,%#),CUSTCOLOR`[v(SYSTEM`COLORS)]`%q<col>`CUSTOM;@attach %!/INC`MSG=You set Color '%q<col>' to its Default.;@attach %!/INC`MSG`CHAN=Color '%q<col>' reset to Default.;@attach %!/COLORS`%q<col>`FINISH

&COLORS`OPTIONS [u(cobj,ccp)]=BORDER|COLUMN_NAMES|HEADER_STAR|HEADER_TEXT|MSG|MSGTEXT|QUOTES|SPEECH|ME|SPEAKER|OTHER|ROW_A|ROW_B
&SYSTEM`COLORS [u(cobj,ccp)]=SYSTEM

&COLORS`BORDER [u(cobj,ccp)]=Color of borders shown in all command systems.
&COLORS`BORDER`DEFAULT [u(cobj,ccp)]=
&COLORS`BORDER`VALID [u(cobj,ccp)]=COLOR

&COLORS`COLUMN_NAMES [u(cobj,ccp)]=Color for column names in tables. Like this one.
&COLORS`COLUMN_NAMES`DEFAULT [u(cobj,ccp)]=
&COLORS`COLUMN_NAMES`VALID [u(cobj,ccp)]=COLOR

&COLORS`HEADER_STAR [u(cobj,ccp)]=Used for the * in Headers of most system.
&COLORS`HEADER_STAR`DEFAULT [u(cobj,ccp)]=
&COLORS`HEADER_STAR`VALID [u(cobj,ccp)]=COLOR

&COLORS`HEADER_TEXT [u(cobj,ccp)]=Used for the text that shows in headers.
&COLORS`HEADER_TEXT`DEFAULT [u(cobj,ccp)]=
&COLORS`HEADER_TEXT`VALID [u(cobj,ccp)]=COLOR

&COLORS`MSG [u(cobj,ccp)]=Used for the -=<>=- message component of system messages.
&COLORS`MSG`DEFAULT [u(cobj,ccp)]=
&COLORS`MSG`VALID [u(cobj,ccp)]=COLOR

&COLORS`MSGTEXT [u(cobj,ccp)]=Used for the system name shown between -=<>=- in system messages.
&COLORS`MSGTEXT`DEFAULT [u(cobj,ccp)]=
&COLORS`MSGTEXT`VALID [u(cobj,ccp)]=COLOR

&COLORS`QUOTES [u(cobj,ccp)]=Color applied to the " " marks when they enclose text, in the context of player speech.
&COLORS`QUOTES`DEFAULT [u(cobj,ccp)]=
&COLORS`QUOTES`VALID [u(cobj,ccp)]=COLOR

&COLORS`SPEECH [u(cobj,ccp)]=Color applied to dialogue enclosed in " " when spoken by another player.
&COLORS`SPEECH`DEFAULT [u(cobj,ccp)]=
&COLORS`SPEECH`VALID [u(cobj,ccp)]=COLOR

&COLORS`ME [u(cobj,ccp)]=Color applied to YOUR name when spoken somewhere.
&COLORS`ME`DEFAULT [u(cobj,ccp)]=
&COLORS`ME`VALID [u(cobj,ccp)]=COLOR

&COLORS`SPEAKER [u(cobj,ccp)]=Color applied to the name of someone speaking. Overridden by +color/name and ME.
&COLORS`SPEAKER`DEFAULT [u(cobj,ccp)]=
&COLORS`SPEAKER`VALID [u(cobj,ccp)]=COLOR

&COLORS`OTHER [u(cobj,ccp)]=Color applied to names of people being referenced. Overriden by other +color/name options, ME, and SPEAKER.
&COLORS`OTHER`DEFAULT [u(cobj,ccp)]=
&COLORS`OTHER`VALID [u(cobj,ccp)]=COLOR

&COLORS`ROW_A [u(cobj,ccp)]=For table displays, first color for alternating colored rows.
&COLORS`ROW_A`DEFAULT [u(cobj,ccp)]=
&COLORS`ROW_A`VALID [u(cobj,ccp)]=COLOR

&COLORS`ROW_B [u(cobj,ccp)]=For table displays, second color for alternating colored rows.
&COLORS`ROW_B`DEFAULT [u(cobj,ccp)]=
&COLORS`ROW_B`VALID [u(cobj,ccp)]=COLOR

@@ Options System!
&CMD`+OPTIONS`PENNMUSH [u(cobj,ccp)]=$^\+options(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+OPTIONS`MAIN
@set [u(cobj,ccp)]/CMD`+OPTIONS`PENNMUSH=regexp no_inherit
&CMD`+OPTIONS`MAIN [u(cobj,ccp)]=@attach %!/INC`OPTION=%2,%3,SYSTEM

&INC`OPTIONS [u(cobj,ccp)]=@attach %!/INC`OPTION
&INC`OPTION [u(cobj,ccp)]=@select/inline strlen(%0)=0,{@attach %!/INC`OPTION`DISPLAY},{@attach %!/INC`OPTION`CHANGE}
&INC`OPTION`DISPLAY [u(cobj,ccp)]=@pemit %#=u(header,u(SYSTEM`NAME) Options);@dolist/inline/delimit | [v(OPTION`OPTIONS)]={@pemit %#=u(separator,##);@pemit %#=align(10 10 12 40,rjust(ansi(hw,TYPE:),10)%R[rjust(ansi(hw,DEFAULT:),10)],v(OPTION`##`VALID)%R[v(OPTION`##`DEFAULT)],ansi(hw,DESCRIPTION:),v(OPTION`##));@pemit %#=[rjust(ansi(hw,CURRENT:),10)]%b[u(setr,cur,u(op,%2,##))];@select/inline cand(strlen(u(setr,form,get(u(accid,%#)/OPTION`%2`##`FORMATTED))),not(strmatch(%q<form>,%q<cur>)))=1,{@pemit %#=[rjust(ansi(hw,FORMATTED:),10)]%B%q<form>}};@pemit %#=u(footer)
&INC`OPTION`CHANGE [u(cobj,ccp)]=@attach %!/INC`PARTIAL=%0,v(OPTION`OPTIONS),|,op,Option;@select/inline strlen(%1)=>0,{@attach %!/INC`OPTION`SET},{@attach %!/INC`OPTION`CLEAR}
&INC`OPTION`SET [u(cobj,ccp)]=@attach %!/INC`VALID`[v(OPTION`%q<op>`VALID)]=%1;th u(attrib_set,u(accid,%#),OPTION`%2`%q<op>`CUSTOM,%q<value>);th u(attrib_set,u(accid,%#),OPTION`%2`%q<op>`FORMATTED,%q<valueformat>);@attach %!/INC`MSG=OPTION Option '%q<op>' set to: %q<value>;@attach %!/OPTION`%q<op>`FINISH
&INC`OPTION`CLEAR [u(cobj,ccp)]=@attach %!/WIPE=u(accid,%#),OPTION`%2`%q<op>`CUSTOM;@attach %!/INC`MSG=You set OPTION Option '%q<op>' to its Default.;@attach %!/INC`MSG`CHAN=OPTION Option '%q<op>' reset to Default.;@attach %!/OPTION`%q<op>`FINISH

&OP [u(cobj,ccp)]=strfirstof(get(%#/OPTION`%0`%1`CUSTOM),v(OPTION`%1`DEFAULT))
&OPOTHER [u(cobj,ccp)]=strfirstof(get(%0/OPTION`%1`%2`CUSTOM),v(OPTION`%2`DEFAULT))
&MY_OPTIONS [u(cobj,ccp)]=u(opother,%@,%0,%1)

&OPTION`OPTIONS [u(cobj,ccp)]=TIMEZONE|IDLE|MAIL|SCREEN_READER
@set [u(cobj,ccp)]/OPTION`OPTIONS=no_inherit

&OPTION`TIMEZONE [u(cobj,ccp)]=Timezone used for all displays. See help timezones2 for a list. Is case-sensitive.
&OPTION`TIMEZONE`DEFAULT [u(cobj,ccp)]=UTC
&OPTION`TIMEZONE`VALID [u(cobj,ccp)]=TIMEZONE
&OPTION`TIMEZONE`FINISH [u(cobj,ccp)]=@tz [u(accid,%#)]=%q<value>

&OPTION`IDLE [u(cobj,ccp)]=Seconds until pagers receive idle message. Only works if you haven't set an @idle. You must use @idle if you want a custom message.
&OPTION`IDLE`DEFAULT [u(cobj,ccp)]=1800
&OPTION`IDLE`VALID [u(cobj,ccp)]=DURATION
&OPTION`IDLE`FINISH [u(cobj,ccp)]=&IDLE_SECS [u(accid,%#)]=%q<value>

&OPTION`MAIL [u(cobj,ccp)]=Show unread mail breakdown on login?
&OPTION`MAIL`DEFAULT [u(cobj,ccp)]=1
&OPTION`MAIL`VALID [u(cobj,ccp)]=BOOL

&OPTION`SCREEN_READER [u(Cobj,ccp)]=Strip excess formatting from headers/separators.
&OPTION`SCREEN_READER`DEFAULT [u(Cobj,ccp)]=0
&OPTION`SCREEN_READER`VALID [u(cobj,ccp)]=BOOL

@@ COMMON CODE SNIPPETS

&SYSTEM`NAME [u(cobj,ccp)]=SYSTEM

&INC`CHECKPC [u(cobj,ccp)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: You must enter a player name!;@check strlen(%1);@check isdbref(setr(t%1objid,objid(setr(t%1,pmatch(%0)))))=@attach %!/INC`MSG=ERROR: %0 [if(strmatch(%q<t%1>,#-2*),matches multiple players!,does not match a player!)];th setq(t%1name,name(r(t%1)));th setq(t%1id,get(r(t%1)/D`ID));@select/inline isdbref(u(cobj,account))=1,{th setq(t%1acc,get(r(t%1)/D`ACCOUNT));th setq(t%1accid,get(r(t%1)/D`ACCOUNT`ID));th setq(t%1idfallback,strfirstof(get(r(t%1)/D`ACCOUNT`ID),get(r(t%1)/D`ID)));th setq(t%1accdb,num(r(t%1acc)))},0,{th setq(t%1acc,r(t%1objid));th setq(t%1accid,r(t%1id))};th setq(t%1tz,get(r(t%1)/TIMEZONE))
@@ CHECK PC Arguments: %0 = player being checked, %1 = register number to load their data into.

&INC`VERIFY [u(cobj,ccp)]=@select/inline strmatch(get(%#/VERIFY_OK),%1)=0,{th u(attrib_set,%#,VERIFY_OK,%1);@wait 10=@wipe %#/VERIFY_OK;@stop 1={@attach %!/INC`MSG=%0,,,%2}},1,{@wipe %#/VERIFY_OK}
@@ VERIFY Arguments: %0 = message to display, %1 = code to set for verification.

&INC`CPTREE [u(cobj,ccp)]=@cpattr %0/%1=%2/%3;@dolist/inline lattr(%0/%1`**)={@cpattr %0/##=%2/%3`[u(elements,##,lnum(add(words(%1,`),1),add(words(%1,`),11)),`,`)]}

&AGFN`CPTREE [u(cobj,ccp)]=null(attrib_set(%2/%3,get(%0/%1))[iter(lattr(%0/%1`**),attrib_set(%2/%3`[u(elements,%i0,lnum(add(words(%1,`),1),add(words(%1,`),11)),`,`)],get(%0/%i0)))])

@@ CPTREE arguments: %0 = source DBREF, %1 = source root attribute, %2 = target dbref, %3 = new root attr

&INC`MVTREE [u(cobj,ccp)]=@cpattr %0/%1=%2/%3;@dolist/inline lattr(%0/%1`**)={@cpattr %0/##=%2/%3`[elements(##,lnum(add(1,words(%1,`)),add(words(%1,`),11)),`,`)]};@attach %!/WIPE=%0,%1
@@ MVTREE arguments: %0 = source DBREF, %1 = source root attribute, %2 = target dbref, %3 = new root attr

&INC`GETSWITCH [u(cobj,ccp)]=@attach %!/INC`PARTIAL=%0,u(SYSTEM`SWITCHES),|,switch,switch

&SYSTEM`SWITCHES [u(cobj,ccp)]=setunion(setunion(setunion(u(SWITCHES`PLAYER),u(SWITCHES`PLAYER`EXTRA),|,|),if(u(isadmin,%#),setunion(u(SWITCHES`ADMIN),u(SWITCHES`ADMIN`EXTRA),|,|)),|,|),if(u(iswizard,%#),setunion(v(SWITCHES`WIZARD),v(SWITCHES`WIZARD`EXTRA),|,|)),|,|)

&INC`PARTIAL [u(cobj,ccp)]=@select/inline or(not(strlen(%0)),setr(matched,match(%1,setr(strfirstof(%3,choice),%0),strfirstof(%2,%b))))=0,{@check words(setr(strfirstof(%3,choice),graball(%1,%0*,strfirstof(%2,%b),strfirstof(%2,%b))))=@attach %!/INC`MSG={ERROR: Invalid %4! Valid choices are: [u(itemize,%1,strfirstof(%2,%b),and,\,)]};@stop gt(words(r(strfirstof(%3,choice)),strfirstof(%2,%b)),1)=@attach %!/INC`MSG={ERROR: %0 matched [u(itemize,r(strfirstof(%3,choice)),strfirstof(%2,%b),and,\,)]. Please be more specific.}},1,{@select/inline cand(t(strlen(%0)),t(%q<matched>))=1,{th setq(strfirstof(%3,choice),u(elements,%1,%q<matched>,strfirstof(%2,%b)))}}
@@ PARTIAL %0 = entry, %1 = choices, %2 = delimiter, %3 = register name, %4 = topic name

&WIPE [u(cobj,ccp)]=@select/inline hasflag(%0,SAFE)=1,{@set %0=!SAFE;@wipe %0/%1;@set %0=SAFE},0,{@wipe %0/%1}

&DELETE [u(cobj,ccp)]=@set %0=!SAFE;@nuke %0;@nuke %0

&INC`VALID`EMAIL [u(cobj,ccp)]=th setq(value,trim(%0));@check strlen(%q<value>)=@attach %!/INC`MSG=ERROR: Nothing entered!;@check lte(strlen(%q<value>),255)=@attach %!/INC`MSG=ERROR: Emails must be 255 characters or less.;@stop strmatch(%q<value>,* *)=@attach %!/INC`MSG=ERROR: Email addresses may not contain spaces.;@check strmatch(%q<value>,*@*.*)=@attach %!/INC`MSG=ERROR: An Email must be in the format: *@*.* (example@game.com).;th u(setq,valueformat,%q<value>)
&INC`VALID`COLOR [u(cobj,ccp)]=@stop strmatch(ansi(setr(value,%0),Test),#-*)=@attach %!/INC`MSG=ERROR: '%0' is not a valid color.;th u(setq,valueformat,ansi(%q<value>,%q<value>))
&INC`VALID`WORD [u(cobj,ccp)]=th setq(value,trim(%0));th u(setq,valueformat,%q<value>)
&INC`VALID`LIST [u(cobj,ccp)]=th setq(value,trim(%0));th u(setq,valueformat,%q<value>)
&INC`VALID`BOOL [u(cobj,ccp)]=@check match(0 1,setr(value,%0))=@attach %!/INC`MSG={ERROR: BOOL options can only be 0 (false) or 1 (true).};th u(setq,valueformat,if(%q<value>,True,False))
&INC`VALID`DBREF [u(cobj,ccp)]=@check isdbref(%0)=@attach %!/INC`MSG={ERROR: DBREF not found.};th setq(value,objid(%0));th u(setq,valueformat,name(%q<value>))
&INC`VALID`TIMEZONE [u(cobj,ccp)]=@check valid(timezone,setr(value,%0))=@attach %!/INC`MSG={ERROR: That is not a valid timezone. Check help timezone};th u(setq,valueformat,%q<value>)
&INC`VALID`DURATION [u(cobj,ccp)]=@check setr(value,u(stringsecs,%0))=@attach %!/INC`MSG={ERROR: '%0' did not resolve into a time. Check help stringsecs()};th u(setq,valueformat,etime(%q<value>))
&INC`VALID`POSINT [u(cobj,ccp)]=@check u(valnum,%0,%2)=@attach %!/INC`MSG={ERROR: '[strfirstof(%1,%0)]' must be a whole number %2 or greater.};th setq(value,%0);th u(setq,valueformat,%q<value>)
&INC`VALID`POSNUM [u(cobj,ccp)]=@check gt(u(setr,value,%0),0)=@attach %!/INC`MSG=ERROR: '%0' must be a positive number.;th u(setq,valueformat,%q<value>)
&INC`VALID`NUMBER [u(cobj,ccp)]=@check isnum(u(setr,value,%0))=@attach %!/INC`MSG=ERROR: '%0' must be a number.;th u(setq,valueformat,%q<value>)
&INC`VALID`INT [u(cobj,ccp)]=@check isint(%0)=@attach %!/INC`MSG={ERROR: '%0' must be a whole number 0 or greater.};th setq(value,%0);th u(setq,valueformat,%q<value>)
&INC`VALID`LOCK [u(cobj,ccp)]=@check valid(lockkey,%0)=@attach %!/INC`MSG={ERROR: '%0' was not accepted by the @lock system.};th setq(value,%0);th u(setq,valueformat,%q<value>)
&INC`VALID`TIME [u(cobj,ccp)]=@check strlen(%0)=@attach %!/INC`MSG=You didn't enter a date!;@check gt(setr(value,convtime(%0,u(gettz,%#))),0)=@attach %!/INC`MSG=The entered date was not recognized. Did you typo? Dates should be in abbreviated 24-hour <month> <day> <hour>:<minute> format using YOUR timezone\, such as Jun 26 7:00 or Oct 31 13:00.;th u(setq,valueformat,convsecs(%q<value>,UTC))
&INC`VALID`FUTURE [u(cobj,ccp)]=@attach %!/INC`VALID`TIME;@check gt(%q<value>,secs())=@attach %!/INC`MSG=That would be in the past!
&INC`VALID`GROUP [u(cobj,ccp)]=@check isdbref(u(setr,value,objid(u(u(cobj,group)/FUN`FINDGROUP,%0,%:))))=@attach %!/INC`MSG=ERROR: Group '%0' not found.;th u(setq,valueformat,u(%q<value>/GET`NAME))

&INC`VALID`GROUPLIST [u(Cobj,ccp)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: No GroupList entered!;@dolist/inline/delimit | %0={@attach %!/INC`VALID`GROUP=before(u(choosegame,%i0,%d0),/);th u(setq,finalvalue,squish(cat(%q<finalvalue>,%q<value>)))};th u(setq,value,%q<finalvalue>);th u(setq,valueformat,iter(%q<value>,u(%i0/GET`NAME),%b,\,%b))

&INC`VALID`RADIO [u(Cobj,ccp)]=@check isdbref(u(setr,value,objid(u(u(cobj,radio)/FUN`FINDFREQ,%0,%:))))=@attach %!/INC`MSG=ERROR: Radio '%0' not found.;th u(setq,valueformat,u(getmoniker,%q<value>))

&INC`VALID`RADIOLIST [u(cobj,ccp)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: No RadioList entered!;@dolist/inline/delimit | %0={@attach %!/INC`VALID`RADIO=before(u(choosegame,%i0,%d0),/);th u(setq,finalvalue,squish(cat(%q<finalvalue>,%q<value>)))};th u(setq,value,%q<finalvalue>);th u(setq,valueformat,iter(%q<value>,u(getmoniker,%i0),%b,\,%b))

&INC`VALID`ENAME [u(cobj,ccp)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Must enter a name!;@attach %!/INC`VALID`WORD=%0;@stop isnum(%q<value>)=@attach %!/INC`MSG=ERROR: %1 names cannot be numbers alone!;@stop u(charsearch,%q<value>,/ [iter(u(setr,badchars,47 36 176 177 96 34 94 124),chr(%i0))])=@attach %!/INC`MSG=ERROR: %1 names cannot contain characters: [iter(%q<badchars>,chr(%i0))]
@@ Used to make sure that entered value is a good name for a database entity.

@@ MOVEMENT SECTION
&INC`CANRELOCATE [u(cobj,ccp)]=@select/inline cor(u(isadmin,%#),u(isadmin,%0))=0,{@check elock(loc(%0)/leave,%0)=@attach %!/INC`MSG=LEAVE @lock Failure.;@check elock(%1/Enter,%0)=@attach %!/INC`MSG=ENTER @lock failure.;@check elock(%1/Teleport,%0)=@attach %!/INC`MSG=TELEPORT @lock failure.}

&INC`RELOCATE [u(cobj,ccp)]=@select/inline u(ishidden,%0)=1,{@tel/silent %0=%1},{@tel %0=%1}

@@ MESSAGING SECTION
&INC`MSG [u(cobj,ccp)]=@message strfirstof(%1,%#)=%0,%!/MSG,##,%0,%1,strfirstof(%2,u(SYSTEM`NAME)),%3,%4,%5,%6
@@ %0 - Message. %1 - Recipients. %2 - System name override. %3 - Bool. Format dates?

&INC`MSG`ROOM [u(cobj,ccp)]=@message strfirstof(%1,lcon(%l))=%0,%!/RMSG,##,%0,%1,u(SYSTEM`NAME),strfirstof(%4,%#),%5,%6,%7,%8

&INC`MSG`NOTICE [u(cobj,ccp)]=@attach %!/INC`MSG=[ansi(h,\[[if(cand(u(isadmin,%#),cor(u(ishidden,%#),u(conf,ANONYMOUS_NOTICES))),SYSTEM,name(%#))]\])]%B%0,%1,%2,%3,%4,%5,%6,%7,%8,%9

&INC`MSG`CHAN [u(cobj,ccp)]=th setq(chanmsg,if(not(%5),ansi(h,\[[u(getmoniker,u(firstof,%4,%#))]\])%B)[if(cand(strlen(strfirstof(%2,u(SYSTEM`NAME))),not(%3)),strfirstof(%2,u(SYSTEM`NAME)):%b)]%0);@dolist/inline/delimit | [strfirstof(%1,u(conf,STAFF_ALERTS))]={@cemit/noisy %i0=%q<chanmsg>}
@@ %0 - Message. %1 - Channels (| delimit). %2 - System. %3 - Ignore system boolean. %4 - Enactor. %5 - Ignore enactor.

&INC`MSG`SYSCHAN [u(cobj,ccp)]=th setq(chanmsg,[if(not(%5),ansi(h,\[[strfirstof(%4,SYSTEM)]\]%B))][if(cand(strlen(strfirstof(%2,u(SYSTEM`NAME))),not(%3)),strfirstof(%2,u(SYSTEM`NAME)):%b)]%0);@dolist/inline/delimit | [u(strfirstof(%1,u(conf,STAFF_ALERTS)))]={@cemit/noisy %i0=%q<chanmsg>}

&MSG [u(cobj,ccp)]=if(not(%5),u(msghead,%3,%0)%B)[if(%4,regeditalli(%1,v(REG`ENCODETIME),u(fancytime,$<time>,u(gettz,%0))),%1)]
&RMSG [u(cobj,ccp)]=if(not(%5),u(msghead,%3,%0)%B\[[ansi(h,u(getmoniker,%4))]\]%B)%1

&REG`ENCODETIME [u(cobj,ccp)]=\^\^\^(?P<time>\d+)\^\^\^

&MSGHEAD [u(cobj,ccp)]=localize(udefault(%!/MSGHEAD`CUSTOM,if(u(opother,u(firstof,%1,%#),SYSTEM,SCREEN_READER),ansi(u(color,u(firstof,%1,%#),v(SYSTEM`COLORS),MSGTEXT),ucstr(%0)):,ansi(setr(msgcolor,u(color,u(firstof,%1,%#),v(SYSTEM`COLORS),MSG)),-=<)[ansi(u(color,u(firstof,%1,%#),v(SYSTEM`COLORS),MSGTEXT),ucstr(%0))][ansi(%q<msgcolor>,>=-)]),%0,%1,%2,%3,%4,%5,%6,%7,%8,%9))

&MSGROOM [u(cobj,ccp)]=localize(if(u(opother,u(firstof,%1,%#),SYSTEM,SCREEN_READER),ansi(u(color,u(firstof,%1,%#),v(SYSTEM`COLORS),MSGTEXT),ucstr(%0)):,ansi(setr(msgcolor,u(color,u(firstof,%1,%#),v(SYSTEM`COLORS),MSG)),-=<*)[ansi(u(color,u(firstof,%1,%#),v(SYSTEM`COLORS),MSGTEXT),ucstr(%0))][ansi(%q<msgcolor>,*>=-)]))

&HEADER [u(cobj,ccp)]=localize(u(HEADER`PREP,%1,strfirstof(%3,u(conf,BORDER_MODE)),HEADER,%4,%5,%6,%7)[setq(headertext,if(strlen(%0),[ansi(%q<bordercolor>,<)][if(u(pueblo,%q<target>),endtag(a))][setr(borderstar,ansi(%q<hstarcolor>,*))]%B[if(%2,%0,ansi(%q<htextcolor>,%0))]%B%q<borderstar>[ansi(%q<bordercolor>,>)]))][u(HEADER`RENDER,HEADER,%0)])

&SUBHEADER [u(cobj,ccp)]=localize(u(HEADER`PREP,%1,%3,SUBHEADER,%4,%5,%6,%7)[setq(headertext,if(strlen(%0),[ansi(%q<bordercolor>,<)][if(u(pueblo,%q<target>),endtag(a))]%B[if(%2,%0,ansi(%q<htextcolor>,%0))]%B[ansi(%q<bordercolor>,>)]))][u(HEADER`RENDER,SUBHEADER,%0)])

&SEPARATOR [u(cobj,ccp)]=localize(u(HEADER`PREP,%1,%3,SEPARATOR,%4,%5,%6,%7)[setq(headertext,if(strlen(%0),[ansi(%q<bordercolor>,<)][if(u(pueblo,%q<target>),endtag(a))]%B[if(%2,%0,ansi(%q<htextcolor>,%0))]%B[ansi(%q<bordercolor>,>)]))][u(HEADER`RENDER,SEPARATOR,%0)])

&FOOTER [u(cobj,ccp)]=localize(u(HEADER`PREP,%1,strfirstof(%3,u(conf,BORDER_MODE)),FOOTER,%4,%5,%6,%7)[setq(headertext,if(strlen(%0),[ansi(%q<bordercolor>,<)][if(u(pueblo,%q<target>),endtag(a))]%B[if(%2,%0,ansi(%q<htextcolor>,%0))]%B[ansi(%q<bordercolor>,>)]))][u(HEADER`RENDER,FOOTER,%0)])

&HEADER`PREP [u(cobj,ccp)]=setq(target,owner(u(strfirstof,%0,%#)))[setq(bordercolor,u(strfirstof,%4,u(color,%q<target>,v(SYSTEM`COLORS),BORDER)))][setq(bordermode,%1)][setq(width,sub(strfirstof(%3,u(width,%q<target>)),2))][setq(fill,u(conf,%2_FILL))][switch(%q<bordermode>,2,switch(%2,HEADER,setq(start,ansi(%q<bordercolor>,/))[setq(end,ansi(%q<bordercolor>,chr(92)))],FOOTER,setq(start,ansi(%q<bordercolor>,chr(92)))[setq(end,ansi(%q<bordercolor>,/))]),1,setq(start,setr(end,ansi(%q<bordercolor>,+))),setq(start,setr(end,ansi(%q<bordercolor>,%q<fill>))))][setq(htextcolor,u(strfirstof,%6,u(color,%q<target>,v(SYSTEM`COLORS),HEADER_TEXT)))][setq(hstarcolor,u(strfirstof,%5,u(color,%q<target>,v(SYSTEM`COLORS),HEADER_STAR)))]

&HEADER`RENDER [u(cobj,ccp)]=if(u(opother,%q<target>,SYSTEM,SCREEN_READER),if(strlen(%1),%1),%q<start>[u(center,%q<headertext>,%q<width>,ansi(%q<bordercolor>,%q<fill>))]%q<end>)

@@ HEADER family args: %0 - Title. %1 - Viewer dbref. %2 - True: Do not color title. %3 - Border mode bool. %4 - Width.

&MARKUP [u(cobj,ccp)]=regeditalli(%0,\([edit(edit(iter(%1,\\b[name(%i0)]\\b,%b,|),-,\\-),.,\\.)]\),if(neq(ord(left(setr(found,$0),1)),ord(lcstr(left($0,1)))),^^^[pmatch(%q<found>)]:%q<found>^^^,%q<found>))
@@ %0 - Text to search. %1 - Dbrefs to check.

&COLORMARKUP [u(cobj,ccp)]=localize(if(%3,regeditalli(%1,v(REG`MARKUP),$2),regeditalli(%1,v(REG`MARKUP),if(%4,u(namelink,$1,,ansi(u(namecolor,%0,$1,%2),$2)),ansi(u(namecolor,%0,$1,%2),$2)))))

&CHANHASH [u(cobj,ccp)]=default(cmogrifier(%0)/HASH`[digest(md5,%1)],%1)

@@ %0 - Viewer. %1 - Text to search. %2 - Speaker. %3 - Bool for 'just demarkup'. %4 - Also use namelink?

&REG`MARKUP [u(cobj,ccp)]=\^\^\^(#\d+)\:([^^]+)\^\^\^

&SPEECH [u(cobj,ccp)]=localize([setq(quote,ansi(u(color,%2,%4,QUOTES),"))][setq(speechcol,u(color,%2,%4,SPEECH))][setq(dispname,if(strlen(%3),%3,if(%6,u(getproperty,%0,banner),if(strlen(setr(namecol,u(namecolor,%2,%0,%0))),ansi(%q<namecol>,name(%0)),u(getmoniker,%0)))))][regeditalli(speakpenn(&[if(%7,u(namelink,%0,,%q<dispname>),%q<dispname>)],%1),v(REG`URL),u(weblink,$0,$0),v(REG`SPEECH),%q<quote>[ansi(%q<speechcol>,$1)]%q<quote>)])
@@ %0 - Speaker. %1 - Message. %2 - Viewer. %3 - Name display override. %4 - Color display mode. %5 - Dbref list for colored-names lookup. %6 - Banner override bool. %7 - Use Namelink?

&REG`SPEECH [u(cobj,ccp)]=(?s)"([^ "][^"]*[^ "])"
&REG`URL [u(cobj,ccp)]=\w+\://\S+[^"]

&COLORNAMES [u(cobj,ccp)]=localize(setq(names,sortkey(#lambda/strlen(\%0),iter(setr(dbs,setunion(%3,u(get_names,%0))),localize(setr(norname,name(%i0))[if(not(strmatch(%q<norname>,setr(accname,accname(%i0)))),|%q<accname>)]),%b,|),-n,|,|))[regeditalli(%1,\\b\(%q<names>\)\\b,if(match(name(%0)|[accname(%0)],setr(fname,$0),|),if(strlen(setr(fcolor,strfirstof(u(color,%0,NAMES,%0,,1),u(color,%0,NAMES,MINE,,1)))),ansi(%q<fcolor>,accname(%0)),u(getmoniker,%0)),if(isdbref(setr(fdb,namegrab(%q<dbs>,stripaccents(%q<fname>)))),if(strlen(setr(fcolor,strfirstof(u(color,%0,NAMES,%q<fdb>,,1),u(color,%0,NAMES,switch(%q<fdb>,%4,SPEAKER,OTHER),,1)))),ansi(%q<fcolor>,accname(%q<fdb>)),u(getmoniker,%q<fdb>)),%q<fname>)))])
@@ Old one, in case I need it again
@@ &COLORNAMES`PENNMUSH [u(cobj,ccp)]=setq(colstring,iter(%3,\\b[name(%i0)]\\b^[switch(%i0,%0,if(strlen(setr(colfound,strfirstof(u(color,%0,NAMES,MINE,,1),u(color,%0,NAMES,%i0,,1)))),ansi(%q<colfound>,name(%i0)),u(getmoniker,%i0)),%4,if(strlen(setr(colfound,strfirstof(u(color,%0,NAMES,%i0,,1),u(color,%0,NAMES,SPEAKER,,1)))),ansi(%q<colfound>,name(%i0)),u(getmoniker,%i0)),if(strlen(setr(colfound,u(color,%0,NAMES,%i0,,1))),ansi(%q<colfound>,name(%i0)),u(getmoniker,%i0)))],%b,beep()))[setq(final,fold(COLORNAME`PENN,%q<colstring>,%1,beep()))]%q<final>

&COLORNAME`PENN [u(cobj,ccp)]=regeditalli(%0,before(%1,^),after(%1,^))
@@ %0 - Viewer. %1 - Text to search. %2 - Display mode. %3 - Dbrefs to check. %4 - Speaker.

@@ Visibility and Connections
&LASTIDLE [u(cobj,ccp)]=switch(objeval(%#,conn(%0)),-1,ansi(hx,elements(get(%0/last),2 3)),u(hideidle,%0))

&LASTIDLE2 [u(cobj,ccp)]=switch(objeval(%#,conn(%0)),-1,ansi(hx,elements(get(%0/last),2 3 5)),u(hideidle,%0))

&HIDECONN [u(cobj,ccp)]=switch(objeval(%#,conn(%0)),-1,ansi(hx,Off),ansi(if(u(ishidden,%0),hx,hg),u(smalltime,conn(%0),3)))

&HIDEIDLE [u(cobj,ccp)]=switch(objeval(%#,idle(%0)),-1,ansi(hx,Off),ansi(if(u(ishidden,%0),hx,u(ryg,round(mul(fdiv(bound(idle(%0),0,3600),3600),100),0))),u(smalltime,idle(%0),3)))

&LASTCONN [u(cobj,ccp)]=switch(objeval(%#,conn(%0)),-1,ansi(hx,elements(get(%0/last),2 3)),u(hideconn,%0))

@@ Color and Configuration

&MY_NAMES [u(cobj,ccp)]=u(get_names,%@)
&GET_NAMES [u(cobj,ccp)]=localize(u(filter,ISDBREF,edit(lattr(setr(accid,u(firstof,u(accid,%0),%0))/D`CONFIG`NAMES`#*),D`CONFIG`NAMES`,)))

&APPROVED [u(cobj,ccp)]=u(isapproved,%0)
&APPROVED2 [u(cobj,ccp)]=u(isapproved,%#)

&ISAPPROVED [u(cobj,ccp)]=cor(u(isadmin,%0),if(u(conf,REQUIRE_APPROVAL),default(%0/D`APPROVED,0),1))

@@ EVENT HANDLING - PENN
&PLAYER`CONNECT [u(cobj,ccp)]=@dolist/inline/nobreak lattr(%!/PLAYER`CONNECT`*)={@trigger/spoof %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9};@dolist/inline/nobreak u(filter,HASATTR,children(%!),%b,%b,PLAYER`CONNECT)={@trigger/spoof ##/PLAYER`CONNECT=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,ccp)]/PLAYER`CONNECT=no_inherit

@@ Player-Approve is a pseudo-event triggered by the Account Management System.
&PLAYER`APPROVE [u(cobj,ccp)]=@dolist/inline/nobreak lattr(%!/PLAYER`APPROVE`*)={@trigger/spoof %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9};@dolist/inline/nobreak u(filter,HASATTR,children(%!),%b,%b,PLAYER`APPROVE)={@trigger/spoof ##/PLAYER`APPROVE=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,ccp)]/PLAYER`APPROVE=no_inherit

&PLAYER`CREATE [u(cobj,ccp)]=@dolist/inline/nobreak lattr(%!/PLAYER`CREATE`*)={@trigger/spoof %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9};@dolist/inline/nobreak u(filter,HASATTR,children(%!),%b,%b,PLAYER`CREATE)={@trigger/spoof ##/PLAYER`CREATE=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,ccp)]/PLAYER`CREATE=no_inherit

&PLAYER`DISCONNECT [u(cobj,ccp)]=@dolist/inline/nobreak lattr(%!/PLAYER`DISCONNECT`*)={@trigger/spoof %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9};@dolist/inline/nobreak u(filter,HASATTR,children(%!),%b,%b,PLAYER`DISCONNECT)={@trigger/spoof ##/PLAYER`DISCONNECT=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,ccp)]/PLAYER`DISCONNECT=no_inherit

&SOCKET`LOGINFAIL [u(cobj,ccp)]=@dolist/inline/nobreak lattr(%!/SOCKET`LOGINFAIL`*)={@trigger/spoof %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9};@dolist/inline/nobreak u(filter,HASATTR,children(%!),%b,%b,SOCKET`LOGINFAIL)={@trigger/spoof ##/SOCKET`LOGINFAIL=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,ccp)]/SOCKET`LOGINFAIL=no_inherit

&SOCKET`CONNECT [u(cobj,ccp)]=@dolist/inline/nobreak lattr(%!/SOCKET`CONNECT`*)={@trigger/spoof %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9};@dolist/inline/nobreak u(filter,HASATTR,children(%!),%b,%b,SOCKET`CONNECT)={@trigger/spoof ##/SOCKET`CONNECT=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,ccp)]/SOCKET`CONNECT=no_inherit

&OBJECT`RENAME [u(cobj,ccp)]=@dolist/inline/nobreak lattr(%!/OBJECT`RENAME`*)={@trigger/spoof %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9};@dolist/inline u(filter,HASATTR,children(%!),%b,%b,OBJECT`RENAME)={@trigger/spoof ##/OBJECT`RENAME=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,ccp)]/OBJECT`RENAME=no_inherit

&OBJECT`DESTROY [u(cobj,ccp)]=@dolist/inline/nobreak lattr(%!/OBJECT`DESTROY`*)={@trigger/spoof %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9};@dolist/inline u(filter,HASATTR,children(%!),%b,%b,OBJECT`DESTROY)={@trigger/spoof ##/OBJECT`DESTROY=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,ccp)]/OBJECT`DESTROY=no_inherit

&OBJECT`CREATE [u(cobj,ccp)]=@dolist/inline/nobreak lattr(%!/OBJECT`CREATE`*)={@trigger/spoof %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9};@dolist/inline u(filter,HASATTR,children(%!),%b,%b,OBJECT`CREATE)={@trigger/spoof ##/OBJECT`CREATE=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,ccp)]/OBJECT`CREATE=no_inherit

&OBJECT`MOVE [u(cobj,ccp)]=@dolist/inline/nobreak lattr(%!/OBJECT`MOVE`*)={@trigger/spoof %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9};@dolist/inline u(filter,HASATTR,children(%!),%b,%b,OBJECT`MOVE)={@trigger/spoof ##/OBJECT`MOVE=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,ccp)]/OBJECT`MOVE=no_inherit

&PLAYER`CREATE`ID [u(cobj,ccp)]=th u(attrib_set,%0,D`ID,u(call`4,volp_character,0,'%0','[sqlescape(%1)]',u(csecs,%0),u(isguest,%0)));
@set [u(cobj,ccp)]/PLAYER`CREATE`ID=no_inherit
&PLAYER`CONNECT`ID [u(cobj,ccp)]=th u(attrib_set,%0,D`ID,u(call`4,volp_character,0,'%0','[sqlescape(name(%0))]',u(csecs,%0),u(isguest,%0)));
@set [u(cobj,ccp)]/PLAYER`CONNECT`ID=no_inherit

&SQL`CONNECT [u(cobj,ccp)]=@sql SET time_zone = '+00:00';@nscemit/noisy u(cmo`staffrep)=SQL server connect: %0
&SQL`CONNECTFAIL [u(cobj,ccp)]=@attach %!/INC`MSG`SYSCHAN={SQL server connect fail: %0}
&SQL`DISCONNECT [u(cobj,ccp)]=@attach %!/INC`MSG`SYSCHAN={SQL server disconnect: %0}
&START`SQL [u(cobj,ccp)]=@check sql(SELECT DATABASE());@trigger %!/LOOP`SQL;@trigger %!/LOOP`REINDEX
&LOOP`SQL [u(cobj,ccp)]=@sql SELECT DATABASE();@wait 60=@trigger %!/LOOP`SQL
&LOOP`REINDEX [u(cobj,ccp)]=@dolist sql(SHOW TABLES)=@sql ANALYZE TABLE %i0;@wait mul(60,60,24)=@trigger %!/LOOP`REINDEX


@@ Json Section
&J`INSERT`ARRAY [u(cobj,ccp)]=json_mod(%0,insert,$\[[json_query(%0,size)]\],%1)
&J`CREATE`ARRAY [u(cobj,ccp)]=fold(J`INSERT`ARRAY,iter(%0,u(J`%1,%i0,%2,%3,%4,%5,%6,%7,%8,%9),u(rsep),u(rsep)),\[\],u(rsep))
&J`NUMBER [u(cobj,ccp)]=json(number,%0)
&J`BOOLEAN [u(cobj,ccp)]=json(boolean,%0)
&J`STRING [u(Cobj,ccp)]=json(string,%0)