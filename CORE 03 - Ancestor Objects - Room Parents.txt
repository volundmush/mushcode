@@ DEPENDENCIES: CORE

@@ ANCESTOR ROOM
@select/inline isdbref(u(oldcobj,ancestor_room))=1,{&COBJ`ANCESTOR_ROOM [u(cobj,ccp)]=[objid([u(oldcobj,ancestor_room)])]},0,{@select/inline isdbref(u(cobj,ancestor_room))=0,{th u(attrib_set,u(cobj,ccp),COBJ`ANCESTOR_ROOM,objid(dig(Ancestor Room)))}}

&HEADER [u(cobj,ancestor_room)]=vol_header(%0,,%1,,v(COLOR`BORDER),v(COLOR`HEADER_STAR),v(COLOR`HEADER_TEXT))
&SEPARATOR [u(cobj,ancestor_room)]=vol_separator(%0,,%1,,v(COLOR`BORDER),v(COLOR`HEADER_STAR),v(COLOR`HEADER_TEXT))
&SUBHEADER [u(cobj,ancestor_room)]=vol_subheader(%0,,%1,,v(COLOR`BORDER),v(COLOR`HEADER_STAR),v(COLOR`HEADER_TEXT))

&NAMEFORMAT [u(cobj,ancestor_room)]=[vol_choosegame(endtag(a),)][u(HEADER,moniker(%!)[if(cor(controls(%#,%!),vol_isadmin(%#)),%(%!%))],1)]

&CONFORMAT [u(cobj,ancestor_room)]=[u(CONFORMAT`EXTRA)][if(words(lvplayers(%!)),udefault(PLAYERS`[u(SETTINGS`ROOM_PLAYERS)],u(PLAYERS`DEFAULT)))][if(words(lvthings(%!)),%R[udefault(THINGS`[switch(num(vol_masterroom()),num(%!),MASTER_ROOM,u(SETTINGS`ROOM_THINGS))],u(THINGS`DEFAULT))])][switch(type(%!),THING,%R[u(SUBHEADER)])]

&PLAYERS`DEFAULT [u(cobj,ancestor_room)]=u(SEPARATOR,Players)%R[ansi(vol_color(%#,COLUMN_NAMES),align(19,Name))]%R[iter(vol_sortname(lvplayers(%!)),align(19,vol_mxpmenu(moniker(%i0),look [name(%i0)])),%b,%R)]

&THINGS`DEFAULT [u(cobj,ancestor_room)]=[u(SEPARATOR,Things)]%R[ansi(vol_color(%#,COLUMN_NAMES),align(40 28 8,Name,Owner,if(isadmin(%#),DBref,Enter)))]%R[iter(vol_sortname(lvthings(%!)),align(40 28 8,vol_pueblize(moniker(%i0),look [name(%i0)]),vol_pueblize(name(owner(%i0)),+finger [name(owner(%i0))]),switch(vol_isadmin(%#)[hasflag(%i0,ENTER_OK)],11,vol_pueblize(ansi(hg,%i0),enter %i0),1*,ansi(hr,%i0),01,vol_pueblize(ansi(hg,enter_ok),enter %i0),ansi(hr,can't))),%b,%R)]

&THINGS`MASTER_ROOM [u(cobj,ancestor_room)]=[u(SEPARATOR,Things)]%R[ansi(vol_color(%#,ROOM_COLUMNS),align(40 28 8,Name,Owner,if(vol_isadmin(%#),DBref,Enter)))]%R[iter(vol_sortname(lvthings(%!)),align(40 28 8,name(%i0),name(owner(%i0)),switch(vol_isadmin(%#)[hasflag(%i0,ENTER_OK)],11,ansi(hg,%i0),1*,ansi(hr,%i0),01,ansi(hg,enter_ok),ansi(hr,can't))),%b,%R)]

&CONFORMAT`EXTRA [u(cobj,ancestor_room)]=[if(cor(nattr(%!/VIEW`*),nattr(%!/MUSIC`*),nattr(%!/PLACE`*)),u(separator,Extra)%R[iter(filterbool(#lambda/nattr(%!/\%0`*),PLACE VIEW MUSIC),%T[u(FUN`EXTRA`%i0)],%b,%R)]%R)]
&FUN`EXTRA`VIEW [u(cobj,ancestor_room)]=This room has views! Type [vol_pueblize(ansi(h,+view),+view)] to list!
&FUN`EXTRA`MUSIC [u(cobj,ancestor_room)]=[ansi(hc,This room has a soundtrack! Type)] [vol_pueblize(ansi(h,soundtrack),soundtrack)] [ansi(hc,to list!)]
&FUN`EXTRA`PLACE [u(cobj,ancestor_room)]=This room has places! Type [vol_pueblize(ansi(h,places),places)] to list!

&EXITFORMAT [u(cobj,ancestor_room)]=[if(words(lvexits(%!)),udefault(EXITFORMAT`[u(SETTINGS`ROOM_EXITS)],u(EXITFORMAT`DEFAULT)),u(SUBHEADER))]

&EXITFORMAT`DEFAULT [u(cobj,ancestor_room)]=[u(SEPARATOR,Exits)]%R[table(iter(vol_sortorder(vol_sortname(lvexits(%!))),ljust(if(strlen(alias(%i0)),<[ansi(vol_color(%#,ROOM,EXITALIAS),vol_pueblize(ucstr(alias(%i0)),name(%i0)))]>),6)[ansi(vol_color(%#,ROOM,EXITNAME),vol_pueblize(moniker(%i0),name(%i0)))] to [ansi(vol_color(%#,ROOM,EXITDEST),left(name(loc(%i0)),sub(37,6,4,strlen(name(%i0)))))],%b,|),37,width(%#),|)]%R[u(SUBHEADER)]

&FIL`VISIBLE [u(cobj,ancestor_room)]=not(u(FIL`DARK,%0))
&FIL`DARK [u(cobj,ancestor_room)]=hasflag(%0,DARK)

@@ ANCESTOR EXIT

@select/inline isdbref(u(oldcobj,ancestor_exit))=1,{th u(attrib_set,u(cobj,ccp),COBJ`ANCESTOR_EXIT,objid(u(oldcobj,ancestor_exit)))},0,{@select/inline isdbref(u(cobj,ancestor_exit))=0,{th u(attrib_set,u(cobj,ccp),COBJ`ANCESTOR_EXIT,objid(open(Ancestor Exit,#0,u(cobj,ancestor_room))))}}

&ODROP [u(cobj,ancestor_exit)]=heads over from [fullname(home(me))]
&OSUCCESS [u(cobj,ancestor_exit)]=heads over to [fullname(loc(me))]
&SUCCESS [u(cobj,ancestor_exit)]=You head over to [fullname(loc(me))]

@@ ANCESTOR PLAYER

@select/inline isdbref(u(setr,ap,u(oldcobj,ancestor_player)))=1,{th u(attrib_set,u(cobj,ccp),COBJ`ANCESTOR_PLAYER,objid(%q<ap>));@newpass %q<ar>=scramble(digest(md5,secs()))},0,{@select/inline isdbref(u(cobj,ancestor_player))=0,{th u(attrib_set,u(cobj,ccp),COBJ`ANCESTOR_PLAYER,objid(pcreate(Ancestor Player,scramble(digest(md5,secs())))))}}
@tel [u(cobj,ancestor_player)]=[u(cobj,ancestor_room)]

@pageformat [u(cobj,ancestor_player)]=ansi(vol_color(%@,PAGE),vol_colormarkup(%!,vol_markup(%4,lwho(%!)),%#))
@outpageformat [u(cobj,ancestor_player)]=ansi(vol_color(%@,OUTPAGE),vol_colormarkup(%!,vol_markup(%4,lwho(%!)),%#))
@adescribe [u(cobj,ancestor_player)]=@check [vol_my_config(ALERTS,ADESC)];think GAME> %n just looked at you.
@idle [u(cobj,ancestor_player)]=if(cand(gte(idle(%!),v(IDLE_SECS)),v(IDLE_SECS)),Currently idle\, please try again later!)

@CHATFORMAT [u(cobj,ancestor_player)]=if(cor(vol_chanpriority(%1),strmatch(%#,%!)),if(strlen(setr(color,vol_chancolor(%1))),<[ansi(%q<color>,stripansi(%1))]>[if(strlen(%4),%B%4%B,%B)][vol_speech(%#,switch(%0,@,|,%0)%2,%!,if(vol_my_config(CHANNELS,NAMELINK),vol_namelink(%#)),CHANNELS,setunion(lwho(),))],<%1>[if(strlen(%4),%B%4%B,%B)][vol_speech(%#,switch(%0,@,|,%0)[vol_colormarkup(%!,vol_chanhash(%1,%2),%#)],%!,strfirstof(vol_codename(cmogrifier(%1)),if(vol_my_config(CHANNELS,NAMELINK),vol_namelink(%#,%!))),CHANNELS)]))

&ACONNECT`SUITE [u(cobj,ancestor_player)]=@dolist/inline/nobreak u(lattrp,%!/ACONNECT`SUITE`*)={@attach %!/##}

@@ ANCESTOR THING
@select/inline isdbref(u(oldcobj,ancestor_thing))=1,{th u(attrib_set,u(cobj,ccp),COBJ`ANCESTOR_THING,objid(u(oldcobj,ancestor_thing)))},0,{@select/inline isdbref(u(cobj,ancestor_thing))=0,{th u(attrib_set,u(cobj,ccp),COBJ`ANCESTOR_THING,objid(create(Ancestor Thing <AT>)))}}
@tel [u(cobj,ancestor_thing)]=[u(cobj,ancestor_room)]
