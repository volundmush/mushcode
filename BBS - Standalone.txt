@@ OBJECT CREATION
@switch/inline isdbref(u(bbs))=0,{th attrib_set(%#/bbs,setr(bbs,create(Bulletin Board System <BBS>)));&BBS %#=%q<bbs>;&BBS %q<bbs>=%q<bbs>}
@set u(bbs)=WIZARD !NO_COMMAND

@switch/inline isdbref(u(bbs))=1,{@switch/inline isdbref(u(bbs-db))=0,{@tel setr(bbs-db,create(BBS Database <BBS-DB>))=u(bbs);&BBS-DB u(bbs)=%q<bbs-db>;&BBS-DB %#=%q<bbs-db>}}
@set u(bbs-db)=WIZARD

@@ FUNCTIONS AND INCLUDES
&GFN`ANNOUNCE u(bbs)=ansi(hm,-=<)[ansi(h,ucstr(%0))][ansi(hm,>=-)]

&GFN`VALNUM u(bbs)=cand(isint(%0),gt(%0,0))

&GFN`SETSTAT u(bbs)=localize(attrib_set(%0,setunion(setdiff(setr(0,get(%0)),grab(%q0,%1~*,|),|),%1~%2,|)))

&GFN`GETSTAT u(bbs)=if(strlen(%2),add(rest(grab(get(%0),%1~*,|),~),rest(grab(get(before(%0,/)/D`%2),%1~*,|),~)),rest(grab(get(%0),%1~*,|),~))

&GFN`NEXTSLOT u(bbs)=add(1,lmath(max,iter(filterbool(#lambda/isint(last(\%0,`)),lattr(%0/[if(strlen(%1),%1`*,*)])),last(%i0,`))))

&GFN`HEADER u(bbs)=center(if(strlen(%0),ansi(m,<)[if(pueblo(%#),endtag(a))][ansi(hm,*%B)][if(%1,%0,ansi(hw,%0))][ansi(hm,%B*)][ansi(m,>)]),78,ansi(m,-=-))

&GFN`CPTREE u(bbs)=null(attrib_set(%2/%3,get(%0/%1))[iter(lattr(%0/%1`**),attrib_set(%2/%3`[elements(%i0,lnum(add(words(%1,`),1),add(words(%1,`),11)),`,`)],get(%0/%i0)))])

&GFN`SUBHEADER u(bbs)=center(if(strlen(%0),%b[if(%1,%0,ansi(hw,%0))]%b),78,ansi(m,-=-))

&INC`CHECKPC u(bbs)=@assert strlen(%0)=@nspemit %#=u(GFN`ANNOUNCE,%2) ERROR: You must enter a player name!;@assert strlen(%1);@assert strlen(%2);@assert isdbref(setr(t%1,pmatch(%0)))=@nspemit %#=u(GFN`ANNOUNCE,%2) ERROR: %0 [if(strmatch(%q<t%1>,#-2*),matches multiple players!,does not match a player!)];th setq(t%1objid,objid(pmatch(%0)));th setq(t%1name,name(pmatch(%0)))

&INC`VERIFY u(bbs)=@switch/inline strmatch(get(%#/VERIFY_OK),%1)=0,{th attrib_set(%#/VERIFY_OK,%1);@wait 10=@wipe %#/VERIFY_OK;@break 1={@nspemit %#=if(strlen(%2),u(GFN`ANNOUNCE,%2)%B)%0}},1,{@wipe %#/VERIFY_OK}

&INC`PARTIAL u(bbs)=@switch/inline cor(not(strlen(%0)),match(%1,setr(strfirstof(%4,choice),%0),strfirstof(%2,%b)))=0,{@assert words(setr(strfirstof(%4,choice),graball(%1,%0*,strfirstof(%2,%b),strfirstof(%2,%b))))=@nspemit %#=u(GFN`ANNOUNCE,%3) ERROR: Invalid %5! Valid choices are: [itemize(%1,strfirstof(%2,%b))];@break gt(words(r(strfirstof(%4,choice)),strfirstof(%2,%b)),1)=@nspemit %#=u(GFN`ANNOUNCE,%3) ERROR: %0 matched [itemize(r(strfirstof(%4,choice)),strfirstof(%2,%b),and,\,)]. Please be more specific.}
@@ PARTIAL %0 = entry, %1 = choices, %2 = delimiter, %3 = announce, %4 = register name, %5 = topic name

@@ BB CODE
&CMD`BB u(bbs)=$^(?s)(?\:\+)?(bb|gb)(\S+)(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@include u(bbs)/INC`INIT`%1;@include u(bbs)/INC`PARTIAL=%2,setunion(setunion(v(VAR`BB`PLAYCOMMS),if(setr(isadmin,switch(%1,bb,isadmin(%#),gb,u(u(gso)/FUN`GRPPERM,%:,%q<gid1>,BBADMIN))),v(VAR`BB`ADMINCOMMS)),|,|),if(hasflag(%#,WIZARD),v(VAR`BB`WIZCOMMS)),|,|),|,setr(ann,ucstr(%1S)),command,command;@include u(bbs)/INC`[strfirstof(%q<command>,WRITE)]=ucstr(%1),%3,%4,%5
@set u(bbs)/CMD`BB=regexp

&CMD`BBWRITE u(bbs)=$^(?s)\+(bb|gb)(?\: +(.*))?$:th setq(ann,ucstr(%1S));@include u(bbs)/INC`INIT`%1;@include u(bbs)/INC`WRITE=ucstr(%1),%2
@set u(bbs)/CMD`BBWRITE=regexp

&VAR`BB`PLAYCOMMS u(bbs)=READ|POST|REMOVE|MOVE|SCAN|NEXT|EDIT|WRITE|JOIN|LEAVE|LIST|PROOF|TOSS|CATCHUP|SEARCH|NEW|TIMEOUT|ALL|CHECK|COMMENT|SCANALL
&VAR`BB`ADMINCOMMS u(bbs)=NEWGROUP|CLEARGROUP|CONFIG|TIMEOUT|LOCK|UNLOCK|ORDER|RENAME
&VAR`BB`WIZCOMMS u(bbs)=IMPORT|CONVERT

&INC`INIT`GB u(bbs)=@assert isdbref(u(gso))=@nspemit %#=u(GFN`ANNOUNCE,GBS) ERROR: The Group System is not installed!;@include u(gso)/INC`CODE`VALGROUP=switch(%q<command>,read,strfirstof(%5,u(GFN`GETSTAT,%#/D`GROUP,Group),0),strfirstof(u(GFN`GETSTAT,%#/D`GROUP,Group),0)),1

&FUN`VALIDGROUPS`BB u(bbs)=filterbool(#lambda/u(FUN`CAN%1`BB,%0,\%0,%2),u(FUN`LISTGROUPS`BB))
&FUN`BBADMIN`BB u(bbs)=cor(isadmin(%0),if(strmatch(lock(%1/Admin),*UNLOCKED*),0,elock(%1/Admin,%0)))
&FUN`CANREAD`BB u(bbs)=cand(cor(elock(%1/Read,%0),u(FUN`BBADMIN`BB,%0,%1)),cor(%2,not(default(%0/D`BB`%1`OMIT,0))))
&FUN`CANWRITE`BB u(bbs)=cand(cor(elock(%1/Write,%0),u(FUN`BBADMIN`BB,%0,%1)),cor(%2,not(default(%0/D`BB`%1`OMIT,0))))

&FUN`VALIDGROUPS`GB u(bbs)=filterbool(#lambda/u(FUN`CAN%1`GB,%0,\%0,%2,strfirstof(%3,%q<gid1>)),u(FUN`LISTGROUPS`GB,strfirstof(%3,%q<gid1>)))
&FUN`BBADMIN`GB u(bbs)=u(u(gso)/FUN`GRPPERM,objid(%0),%1,MODERATE,GROUP)
&FUN`CANREAD`GB u(bbs)=cand(cor(u(FUN`BBADMIN`GB,%0,%3),if(strlen(get(u(bbs-db)/BB`%1`GREAD)),lte(u(u(gso)/FUN`GETRANK,%3,objid(%0)),get(u(bbs-db)/BB`%1`GREAD)),1)),cor(%2,not(default(%0/D`BB`%1`OMIT,0))))
&FUN`CANWRITE`GB u(bbs)=cand(cor(u(FUN`BBADMIN`GB,%0,%3),if(strlen(get(u(bbs-db)/BB`%1`GWRITE)),lte(u(u(gso)/FUN`GETRANK,%3,objid(%0)),get(u(bbs-db)/BB`%1`GWRITE)),1)),cor(%2,not(default(%0/D`BB`%1`OMIT,0))))

&INC`CHECK u(bbs)=@switch/inline t(default(%#/D`BB`CHECK,1))=1,{@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) You will no longer +bbscan on connect!;&D`BB`CHECK %#=0},0,{@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) You will now +bbscan on connect!;&D`BB`CHECK %#=1}

&PLAYER`CONNECT`CHECK u(bbs)=@assert t(default(%0/D`BB`CHECK,1));@force/inplace %0=+bbscanall

&FUN`LISTGROUPS`BB u(bbs)=filterbool(#lambda/isdbref(\%0),get(u(bbs-db)/groups))
&FUN`LISTGROUPS`GB u(bbs)=filterbool(#lambda/isdbref(\%0),get(%q<gid1>/BOARDS))

&INC`CHECK`FINDBB u(bbs)=@assert strlen(%1)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: You must enter a board to use!;th setq(listgroups,u(FUN`LISTGROUPS`%0),valgroups,u(FUN`VALIDGROUPS`%0,%#,%2,%3));th setq(try,if(u(GFN`VALNUM,%1),%1,match(%q<listgroups>,namegrab(%q<valgroups>,%1))));@assert isdbref(setr(bb,elements(%q<listgroups>,%q<try>)))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: You are not a [ucstr(%2)] member of that board.;th setq(bbnum,match(%q<listgroups>,%q<bb>))

&INC`CHECK`FINDPOST u(bbs)=@assert strlen(%1)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: No post entered!;@assert u(GFN`VALNUM,%1)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Posts must be addressed by their post #s!;@assert strlen(setr(post,elements(u(FUN`LISTPOSTS,%2),setr(postnum,%1))))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Post not found.

&FUN`LISTPOSTS u(bbs)=sortkey(#lambda/baseconv(\%0,36,10),lattr(%0/*))

&FUN`MESSLIST u(bbs)=setunion(iter(%2,switch(1,u(GFN`VALNUM,%i0),%i0,regmatchi(%i0,^\\d+-\\d+$),setq(num1,abs(before(%i0,-)),num2,abs(after(%i0,-)))[setq(ord,sort(%q<num1> %q<num2>))][lnum(first(%q<ord>),last(%q<ord>))],strmatch(%i0,u),iter(setunion(setdiff(lattr(%1/*),get(%0/D`BB`%1`READ)),filterbool(#lambda/strmatch(u(FUN`MSGFLAG,%0,%1,\%0),C),lattr(%1/*))),match(u(FUN`LISTPOSTS,%1),%i0)),0),\,,%B),,%B,n)

&FUN`CONFIG u(bbs)=if(cand(strlen(%0),isdbref(%0)),u(FUN`CONFIG`BB`%1,%0),if(strlen(%0),u(FUN`CONFIG`GLOBAL`%0)))

&FUN`CONFIG`GLOBAL`AUTOTIMEOUT u(bbs)=default(u(bbs-db)/BB`AUTOTIMEOUT,0)
&FUN`CONFIG`GLOBAL`TIMEOUT u(bbs)=default(u(bbs-db)/BB`TIMEOUT,mul(60,60,24,30))
&FUN`CONFIG`GLOBAL`INTERVAL u(bbs)=default(u(bbs-db)/BB`INTERVAL,mul(60,60,24))
&FUN`CONFIG`BB`TIMEOUT u(bbs)=strfirstof(get(u(bbs-db)/BB`%0`TIMEOUT),u(FUN`CONFIG,timeout))
&FUN`CONFIG`BB`ANONYMOUS u(bbs)=t(strlen(get(u(bbs-db)/BB`%0`ANONYMOUS)))

&INC`POST u(bbs)=@include u(bbs)/INC`JAILCHECK;@switch/inline strlen(%1)=0,{@assert hasattr(%#/D`BB`NEW)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: You don't have a post in progress.;th setq(gid1,get(%#/D`BB`NEW`GID));@include u(bbs)/INC`POST`MAKEMESS=%#,get(%#/D`BB`NEW`BBNUM),get(%#/D`BB`NEW`BB),get(%#/D`BB`NEW`TITLE),get(%#/D`BB`NEW`TEXT),get(%#/D`BB`NEW`TYPE);@wipe %#/D`BB`NEW},{@include u(bbs)/INC`CHECK`FINDBB=%0,%1,WRITE;@assert strlen(%2)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Title field empty!;@switch/inline strlen(%3)=0,{@include u(bbs)/INC`POST`BEGIN},{@include u(bbs)/INC`POST`MAKEMESS=%#,%q<bbnum>,%q<bb>,left(%2,34),%3,%0}}

&INC`POST`BEGIN u(bbs)=@break hasattr(%#/D`BB`NEW)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: You already have a post in progress. use +bb <text> to add to it, +bbproof to view it, or +bbpost to post it. Alternatively, +bbtoss to trash it.;&D`BB`NEW`BBNUM %#=%q<bbnum>;&D`BB`NEW`BB %#=%q<bb>;&D`BB`NEW`TITLE %#=%2;&D`BB`NEW`TYPE %#=%0;&D`BB`NEW`GID %#=%q<gid1>;@nspemit %#=You start your posting to Board #%q<bbnum> ([name(%q<bb>)])%RYou can now compose the body of the post by using '+bbwrite <text>'%Ror '+bb <text>'. When you are finished, type '+bbpost' to send it. See '+help bbs' for help!

&INC`WRITE u(bbs)=@assert hasattr(%#/D`BB`NEW)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: You don't have a post in progress.;@break eq(strlen(get(%#/D`BB`NEW`TEXT)),setr(max,strlen(lnum(1,9999))))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Your post has already reached %q<max> characters - all that will fit. You may not add more text.;@assert strlen(%1)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Nothing entered to write. Use +bbproof to see what you've written!;&D`BB`NEW`TEXT %#=trim(cat(trim(get(%#/D`BB`NEW`TEXT)),trim(%1)));@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) Text added to bbpost. Your post has reached [strlen(get(%#/D`BB`NEW`TEXT))] of [strlen(lnum(1,9999))] max characters!

&INC`TOSS u(bbs)=@assert hasattr(%#/D`BB`NEW)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: You don't have a post in progress.;@wipe %#/D`BB`NEW;@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) Post discarded.

&INC`PROOF u(bbs)=@assert hasattr(%#/D`BB`NEW)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: You don't have a post in progress.;@nspemit %#=u(GFN`HEADER,get(%#/D`BB`NEW`TITLE));@nspemit %#=get(%#/D`BB`NEW`TEXT);@nspemit %#=u(GFN`HEADER,Length: [strlen(get(%#/D`BB`NEW`TEXT))] Characters)

&INC`POST`MAKEMESS u(bbs)=th setq(next,baseconv(default(u(bbs-db)/BB`%2`NEXT,1),10,36));&BB`%2`NEXT u(bbs-db)=add(default(u(bbs-db)/BB`%2`NEXT,1),1);&%q<next> %2=%4;&%q<next>`BY %2=name(%0);&%q<next>`BYDB %2=objid(%0);&%q<next>`ON %2=secs();&%q<next>`HDR %2=%3;&%q<next>`TIMEOUT %2=firstof(u(FUN`CONFIG,%2,TIMEOUT),0);@switch/inline %5=BB,{@include u(bbs)/INC`ALERT=%5,%1,%2,{(New [ucstr(%5)] Message ([setr(messnum,%1/[words(lattr(%2/*))])]) posted to '[name(%2)]' by %n: %3)}},GB,{@include u(bbs)/INC`ALERT=%5,%1,%2,{ansi(hc,<GROUP BB>) New message posted on '%q<gname1>' '[name(%2)]' Board ([setr(messnum,%1/[words(lattr(%2/*))])]) by %n: %3}};&D`BB`%q<bb>`READ %#=setunion(get(%#/D`BB`%q<bb>`READ),%q<next>);&D`BB`LASTPOST %#=%q<bb> %q<next>

&INC`ALERT u(bbs)=@switch/inline %0=BB,{th setq(members,filterbool(#lambda/u(FUN`CANREAD`BB,\%0,%2,,),setunion(lwho(),)))},GB,{th setq(members,filterbool(#lambda/cand(isgroupmember(\%0,%q<gid1>),u(FUN`CANREAD`GB,\%0,%2,,%q<gid1>)),setunion(lwho(),)))};@nspemit/list %q<members>=%3
@@ %0 - Mode, GB or BB. %1 - Board number. %2 - Board DBREF. %3 - Message.

&INC`NEWGROUP u(bbs)=@assert strlen(%1%2)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: No name entered for the new board!;@assert valid(name,%1%2)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: That is not a suitable name for a board.;@break isdbref(locate(switch(%0,BB,u(bbs-db),GB,%q<gid1>),trim(%1%2),TXxi))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: A board already has that name.;@tel setr(bb,create(trim(%1%2)))=switch(%0,BB,u(bbs-db),GB,%q<gid1>);@power %q<bb>=many_attribs;@switch/inline %0=BB,{&groups u(bbs-db)=trim(cat(get(u(bbs-db)/groups),%q<bb>))},GB,{&BOARDS %q<gid1>=trim(cat(get(%q<gid1>/BOARDS),%q<bb>))};@set %q<bb>=SAFE;@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) Board '[trim(%1%2)]' created!;th setq(bbnum,match(u(FUN`LISTGROUPS`%0),%q<bb>))

&INC`CLEARGROUP u(bbs)=@include u(bbs)/INC`CHECK`FINDBB=%0,%1,WRITE;@include u(bbs)/INC`VERIFY={[ansi(hr,WARNING:)] Deleting %q<ann> Board %q<bbnum> will also delete all messages. This cannot be undone! Please enter the same command again within 10 seconds to verify.},CLEARGROUP %0 %1,%q<ann>;@set %q<bb>=!SAFE;@nuke %q<bb>;@nuke %q<bb>;@wipe u(bbs-db)/BB`%q<bb>;@switch/inline %0=BB,{&groups u(bbs-db)=ldelete(get(u(bbs-db)/groups),%q<bbnum>)},GB,{&BOARDS %q<gid1>=ldelete(get(%q<gid1>/`BOARDS),%q<bbnum>)}

&INC`REMOVE u(bbs)=@include u(bbs)/INC`CHECK`FINDBB=%0,%1,WRITE;@assert strlen(%2)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: No posts entered to remove.;th setq(listposts,u(FUN`LISTPOSTS,%q<bb>));@assert words(setr(processed,filterbool(#lambda/if(strlen(setr(post,elements(%q<listposts>,\%0))),cor(%q<isadmin>,strmatch(get(%q<bb>/\%q<post>`BYDB),%:))),setr(orig,u(FUN`MESSLIST,%#,%q<bb>,before(%2,/))))))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: No posts to remove.;@switch/inline gt(words(setr(cannot,setdiff(%q<processed>,%q<orig>,%b,n))),0)=1,{@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) Could not remove posts: [itemize(%q<cannot>,%b,and,\,)]};@break cand(gt(words(%q<processed>),1),strlen(after(%2,/)))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) Cannot remove comments from more than one post!;@switch/inline t(strlen(after(%2,/)))=1,{@include u(bbs)/INC`REMOVE`COMMENT},{@dolist %q<processed>={@wipe %q<bb>/[elements(%q<listposts>,%i0)];@nspemit %#=Message %i0 removed from group #%q<bbnum> ([name(%q<bb>)]).}}

&INC`REMOVE`COMMENT u(bbs)=@assert u(GFN`VALNUM,after(%2,/))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Comments must be addressed by their comment number!;@assert strlen(setr(comm,elements(sortkey(#lambda/last(\%0,`),lattr(%q<bb>/[elements(%q<listposts>,%q<processed>)]`COMM`*)),after(%2,/))))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Comment not found.;@assert cor(%q<isadmin>,strmatch(get(%q<bb>/%q<comm>`BYDB),%:))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: You don't have permission to remove that comment.;@wipe %q<bb>/%q<comm>;@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) Comment removed!

&INC`READ u(bbs)=@switch/inline strlen(%1%2)=0,{@include u(bbs)/INC`READ`LISTGROUPS},{@switch/inline cand(strlen(%1),strlen(%2))=1,{@include u(bbs)/INC`READ`CHECKPOST},{@switch/inline gt(strlen(%1),0)=1,{@include u(bbs)/INC`READ`LISTMESS},{@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: How the hell did we get here?}}}

&FUN`NAME u(bbs)=switch(%0,BB,mudname() BBS%B[if(isdbref(%1),-%b[name(%1)],Boards)],GB,name(%2) GBS[if(isdbref(%1),-%b[name(%1)],Boards)])

&INC`ALL u(bbs)=@assert words(setr(allunread,filterbool(#lambda/cor(strlen(setdiff(lattr(\%0/*),get(%#/D`BB`\%0`READ))),lmath(max,iter(lattr(\%0/*),strmatch(u(FUN`MSGFLAG,%#,\%0,\%i0),C)))),u(FUN`VALIDGROUPS`%0,%#,READ))))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) There are no Unread messages on the %q<ann>!;@dolist %q<allunread>={@switch/inline %0=BB,{@force/inplace %#=+bbread %i0/u},GB,{@force/inplace %#=+gbread %i0/u}}

&INC`READ`LISTGROUPS u(bbs)=@switch/inline %0=BB,{th setq(allgroups,get(u(bbs-db)/groups),groups,u(FUN`VALIDGROUPS`BB,%#,READ))},GB,{th setq(allgroups,get(%q<gid1>/BOARDS),groups,u(FUN`VALIDGROUPS`GB,%#,READ))};@nspemit %#=u(GFN`HEADER,u(fun`name,%0,%q<bb>,%q<gid1>))%r[space(7)]Group Name[space(20)]Last Post[space(6)]# of messages;@dolist %q<groups>={@nspemit %#=rjust(match(%q<allgroups>,%i0),2)[center(switch(switch(%0,BB,[if(strmatch(lock(%i0/READ),*UNLOCKED*),1,0)]:[if(strmatch(lock(%i0/WRITE),*UNLOCKED*),1,0)]:[u(FUN`CANWRITE`%0,%#,%i0)],GB,[not(hasattr(u(bbs-db)/BB`%i0`GREAD))]:[not(hasattr(u(bbs-db)/BB`%i0`GWRITE))]:[u(FUN`CANWRITE`%0,%#,%i0,,%q<gid1>)]),1:1:1,,1:*:1,{(-)},1:*:0,-,*),5)][ljust(name(%i0),30)][ljust(if(setr(time,lmath(max,iter(lattr(%i0/*),strfirstof(get(%i1/%i0`EDITED),get(%i1/%i0`ON))))),timefmt($b $d $Y,%q<time>),None),20)][rjust(nattr(%i0/*),3)]%b[ljust(if(words(setdiff(lattr(%i0/*),get(%#/D`BB`%i0`READ))),U,if(gt(get(u(bbs-db)/BB`%i0`COMMSET),get(%#/D`BB`%i0`COMMREAD)),C)),2)];@switch/inline eq(inum(0),words(%q<groups>))=1,{@nspemit %#=u(GFN`SUBHEADER)%r'*' = restricted[space(5)]'-' = read only[space(5)]'\(-\)' = read only\, but you can write%r[u(GFN`HEADER)]}};@switch/inline words(%q<groups>)=0,{@nspemit %#=u(GFN`SUBHEADER)%r'*' = restricted[space(5)]'-' = read only[space(5)]'\(-\)' = read only\, but you can write%r[u(GFN`HEADER)]}

&INC`READ`LISTMESS u(bbs)=@include u(bbs)/INC`CHECK`FINDBB=%0,%1,READ;@nspemit %#=u(GFN`HEADER,u(fun`name,%0,%q<bb>,%q<gid1>))%r[space(8)]Message[space(28)]Posted[space(8)]By;@dolist setr(posts,u(FUN`LISTPOSTS,%q<bb>))={@nspemit %#=ljust(%q<bbnum>/[match(%q<posts>,%i0)],6)[ljust(u(u(bbs)/FUN`MSGFLAG,%#,%q<bb>,%i0),2)][ljust(get(%q<bb>/%i0`HDR),35)][ljust(timefmt($b $d $Y,lmath(max,firstof(get(%q<bb>/%i0`EDITED),get(%q<bb>/%i0`ON)))),14)][left(if(get(u(bbs-db)/BB`%q<bb>`ANONYMOUS),if(%q<isadmin>,([ansi(hx,get(%q<bb>/%i0`BY))]),default(u(bbs-db)/BB`%q<bb>`ANONYMOUS,Anonymous)),get(%q<bb>/%i0`BY)),21)];@switch/inline eq(inum(0),words(%q<posts>))=1,{@nspemit %#=u(GFN`HEADER)}};@switch/inline words(%q<posts>)=0,{@nspemit %#=u(GFN`HEADER)}

&FUN`MSGFLAG u(bbs)=if(match(get(%0/D`BB`%1`READ),%2),if(nattr(%1/%2`COMM`*),if(gt(get(%1/%2`COMMSET),baseconv(u(GFN`GETSTAT,%1/%2`COMMREAD,%0),64,10)),C)),U)

@@ if(match(get(%0/D`BB`%1`READ),%2),,U)

&INC`READ`CHECKPOST u(bbs)=@include u(bbs)/INC`CHECK`FINDBB=%0,%1,READ;@assert strlen(%2)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: No posts entered to check.;th setq(listposts,u(FUN`LISTPOSTS,%q<bb>));@assert words(setr(processed,filterbool(#lambda/strlen(elements(%q<listposts>,\%0)),u(FUN`MESSLIST,%#,%q<bb>,%2))))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Posts not found.;@dolist %q<processed>=@include u(bbs)/INC`READ`SHOWPOST=%0,%i0,%q<bb>

&FUN`GETTIMEOUT u(bbs)=sub(add(strfirstof(get(%0/%1`TIMEOUT),u(FUN`CONFIG,%1,timeout)),strfirstof(get(%0/%1`EDITED),get(%0/%1`ON))),secs())

&INC`READ`SHOWPOST u(bbs)=@include u(bbs)/INC`CHECK`FINDPOST=%0,%1,%2;@nspemit %#=u(GFN`HEADER,u(fun`name,%0,%2,%q<gid1>));@nspemit %#=ljust(Message: %q<bbnum>/%q<postnum> \([if(not(u(FUN`CONFIG,autotimeout)),No System Timeout,if(not(u(FUN`CONFIG,%q<bb>,timeout)),No Board Timeout,if(eq(get(%q<bb>/%q<post>`TIMEOUT),0),No Post Timeout,if(gte(setr(showtimeout,u(FUN`GETTIMEOUT,%q<bb>,%q<post>)),0),timestring(%q<showtimeout>),Timed Out!))))]\),35)Posted[space(8)]Author%r[ljust(get(%2/%q<post>`HDR),35)][ljust(timefmt($b $d $Y,lmath(max,firstof(get(%2/%q<post>`EDITED),get(%2/%q<post>`ON)))),14)][left(if(get(u(bbs-db)/BB`%q<bb>`ANONYMOUS),if(%q<isadmin>,([ansi(hx,get(%q<bb>/%q<post>`BY))]),default(u(bbs-db)/BB`%q<bb>`ANONYMOUS,Anonymous)),get(%q<bb>/%q<post>`BY)),21)]%R[u(GFN`HEADER)];@nspemit %#=get(%q<bb>/%q<post>);@switch/inline t(nattr(%q<bb>/%q<post>`COMM`*))=1,{@include u(bbs)/INC`READ`SHOWCOMMS},0,{@nspemit %#=u(GFN`HEADER)};th setq(oldids,setdiff(get(%#/D`BB`%2`READ),lattr(%2/*)));&D`BB`%2`READ %#=setunion(setdiff(get(%#/D`BB`%2`READ),%q<oldids>),%q<post>);th u(GFN`SETSTAT,%q<bb>/%q<post>`COMMREAD,%#,baseconv(secs(),10,64));&D`BB`%q<bb>`COMMREAD %#=lmath(max,iter(lattr(%q<bb>/%q<post>`COMM`*),get(%q<bb>/%i0`ON)))

&INC`READ`SHOWCOMMS u(bbs)=@dolist setr(comms,sortkey(#lambda/last(\%0,`),lattr(%q<bb>/%q<post>`COMM`*)))={@nspemit %#=u(GFN`HEADER,Comment [inum(0)] - Added [timefmt($b $d $Y,get(%q<bb>/%i0`ON))]);@nspemit %#=ansi(h,get(%q<bb>/%i0`BY)) Commented:;@nspemit %#=get(%q<bb>/%i0);@switch/inline eq(words(%q<comms>),inum(0))=1,{@nspemit %#=u(GFN`HEADER)}}

&INC`COMMENT u(bbs)=@include u(bbs)/INC`CHECK`FINDBB=%0,%1,WRITE;@include u(bbs)/INC`CHECK`FINDPOST=%0,%2,%q<bb>;@assert strlen(%3)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Comment contents empty!;&[setr(attr,%q<post>`COMM`[u(GFN`NEXTSLOT,%q<bb>,%q<post>`COMM)])] %q<bb>=%3;&%q<attr>`BY %q<bb>=%n;&%q<attr>`BYDB %q<bb>=%:;&%q<attr>`ON %q<bb>=secs();&%q<post>`EDITED %q<bb>=secs();@switch/inline strmatch(get(u(job)/vb),%#)=0,{@include u(bbs)/INC`ALERT=%0,%q<bbnum>,%q<bb>,{(New Comment added to [ucstr(%0)][if(strmatch(%0,GB),%b\([name(%q<gid1>)\)])] Message ([setr(messnum,%q<bbnum>/%q<postnum>)]) '[get(%q<bb>/%q<post>`HDR)]' on '[name(%q<bb>)]' by %n)}};&BB`%q<bb>`COMMSET u(bbs-db)=secs()

&INC`NEXT u(bbs)=th iter(u(FUN`VALIDGROUPS`%0,%#,READ),iter(setr(listposts,u(u(bbs)/FUN`LISTPOSTS,%i0)),if(match(U C,u(u(bbs)/FUN`MSGFLAG,%#,%i1,%i0)),setq(msg,%i0,bb,%i1)[ibreak(2)])));@assert isdbref(%q<bb>)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) There are no unread postings on the %q<ann>.;th setq(bbnum,match(u(FUN`LISTGROUPS`%0),%q<bb>));@include u(bbs)/INC`READ`SHOWPOST=%0,match(%q<listposts>,%q<msg>),%q<bb>

&INC`NEW u(bbs)=@include u(bbs)/INC`NEXT

&INC`EDIT u(bbs)=@include u(bbs)/INC`JAILCHECK;@switch/inline %1=TEXT,{@include u(bbs)/INC`EDIT`POST},TITLE,{@include u(bbs)/INC`EDIT`POST},{@include u(bbs)/INC`CHECK`FINDBB=%0,%1,WRITE;@include u(bbs)/INC`CHECK`FINDPOST=%0,%2,%q<bb>;@assert cor(%q<isadmin>,isadmin(%#),strmatch(get(%q<bb>/%q<post>`BYDB),%:))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: You cannot edit that post.;@assert strlen(before(%3,/))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: No text entered to check for.;&%q<post> %q<bb>=edit(get(%q<bb>/%q<post>),before(%3,/),after(%3,/));&%q<post>`EDITED %q<bb>=secs();@nspemit %#=u(GFN`HEADER)%RMessage %q<bbnum>/%q<postnum> ([name(%q<bb>)]/%q<postnum>) now reads:%R[u(GFN`SUBHEADER)];@nspemit %#=get(%q<bb>/%q<post>);@nspemit %#=u(GFN`HEADER)}

&INC`EDIT`POST u(bbs)=@assert hasattr(%#/D`BB`NEW)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: You don't have a post in progress.;@assert strlen(before(%3,/))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: No text entered to check for.;&D`BB`NEW`%1 %#=edit(get(%#/D`BB`NEW`%1),before(%3,/),after(%3,/));@nspemit %#=u(GFN`HEADER)%RMessage now reads:%R[u(GFN`SUBHEADER,get(%#/D`BB`NEW`TITLE))];@nspemit %#=get(%#/D`BB`NEW`TEXT);@nspemit %#=u(GFN`HEADER)

&INC`LEAVE u(bbs)=@include u(bbs)/INC`CHECK`FINDBB=%0,%1,READ,1;@break default(%#/D`BB`%q<bb>`OMIT,0)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) You have already left that board.;&D`BB`%q<bb>`OMIT %#=1;@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) You have removed yourself from the [name(%q<bb>)] board.
&INC`JOIN u(bbs)=@include u(bbs)/INC`CHECK`FINDBB=%0,%1,READ,1;@assert default(%#/D`BB`%q<bb>`OMIT,0)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) You are already watching this board.;&D`BB`%q<bb>`OMIT %#=0;@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) You have joined the [name(%q<bb>)] board.

&INC`SCAN u(bbs)=@assert words(setr(groups,filterbool(#lambda/words(iter(lattr(\%0/*),u(u(bbs)/FUN`MSGFLAG,%#,\%0,\%i0))),u(FUN`VALIDGROUPS`%0,%#,READ))))=@switch/inline not(%2)=1,{@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) There are no unread postings on the %q<ann>.};@nspemit %#=u(GFN`HEADER,u(FUN`NAME,%0,,%q<gid1>) - Unread Postings);th setq(listgroups,u(FUN`LISTGROUPS`%0));@dolist[if(cand(%1,strmatch(%0,BB)),/notify)] %q<groups>={th setq(posts,u(FUN`LISTPOSTS,%i0));@nspemit %#=name(%i0) (#[setr(bbnum,match(%q<listgroups>,%i0))]): [setr(num,words(setr(unread,filterbool(#lambda/match(U C,u(u(bbs)/FUN`MSGFLAG,%#,%i0,\%0)),%q<posts>))))] unread ([itemize(iter(%q<unread>,setr(postnum,match(%q<posts>,%i0))),%b,and,\,)]);th setq(total,add(%q<total>,%q<num>));@switch/inline if(cand(strmatch(%0,BB),eq(words(%q<groups>),inum(0)),not(%1)),1,if(cand(%2,%3),1,0))=1,{@nspemit %#=u(GFN`HEADER,Total Unread: %q<total>)}}

&INC`SCANALL u(bbs)=@wait me={@dolist setr(allgroups,filterbool(#lambda/isgroupmember(%:,\%0),u(u(gso)/FUN`LISTGROUPS)))={th setq(gid1,%i0,gname1,name(%i0));@include/nobreak u(bbs)/INC`SCAN=GB,1,1,eq(inum(0),words(%q<allgroups>))};@switch/inline gt(%q<total>,0)=0,{@nspemit %#=u(GFN`HEADER);@nspemit %#=u(GFN`ANNOUNCE,GBS) There are no unread postings on the GBS.},1,{@nspemit %#=u(GFN`HEADER,Total Unread: %q<total>)}};@include/nobreak u(bbs)/INC`SCAN=BB,1

&INC`LIST u(bbs)=th setq(listgroups,u(FUN`LISTGROUPS`%0));@nspemit %#=u(GFN`HEADER,u(FUN`NAME,%0,,%q<gid1>))%r[align(36 8 20 8,Available Bulletin Board Groups,Member?,Timeout,Capacity)]%r[u(GFN`SUBHEADER)];@dolist setr(groups,u(FUN`VALIDGROUPS`%0,%#,READ,1))={@nspemit %#=align(5 31 -7 20 12,%b[match(%q<listgroups>,%i0)],name(%i0),switch(default(%#/D`BB`%i0`OMIT,0),0,Yes,No),if(u(FUN`CONFIG,%i0,timeout),timestring(u(FUN`CONFIG,%i0,timeout)),none),rjust(nattr(%i0/*),4,0)/[switch(strlen(default(u(bbs-db)/BB`%i0`NEXT,1)),1,1800?,2,1800?,3,1800?,4,1600?,5,1300?,6,1100?,7,1000?,8,900?,9,800?,10,700?,11,600?,12,500?,13,400?)]);@switch/inline eq(inum(0),words(%q<groups>))=1,{@nspemit %#=u(GFN`HEADER)%RTo join boards, type '+[lcstr(%0)]join <board number>'%RTo leave boards, type '+[lcstr(%0)]leave <board number>'%R[u(GFN`HEADER)]}}

&INC`CATCHUP u(bbs)=@assert strlen(%1)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: No board entered to catchup.;@switch/inline %1=ALL,{@dolist u(FUN`VALIDGROUPS`%0,%#,READ)={&D`BB`%i0`READ %#=lattr(%i0/*);@dolist lattr(%i0/*`COMM`*)={th u(GFN`SETSTAT,%i1/[before(%i0,`)]`COMMREAD,%#,baseconv(secs(),10,64))}};@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) All postings on all %q<ann> boards marked as read.},{@dolist/delimit , %1={@include u(bbs)/INC`CHECK`FINDBB=%0,%i0,READ;&D`BB`%q<bb>`READ %#=lattr(%q<bb>/*);@dolist lattr(%q<bb>/*`COMM`*)={th u(GFN`SETSTAT,%q<bb>/[before(%i0,`)]`COMMREAD,%#,baseconv(secs(),10,64))};@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) All postings on board [name(%q<bb>)] marked as read.}}

&INC`MOVE u(bbs)=@include u(bbs)/INC`CHECK`FINDBB=%0,%1,WRITE;@include u(bbs)/INC`CHECK`FINDPOST=%0,%2,%q<bb>;@assert cor(%q<isadmin>,isadmin(%#),strmatch(get(%q<bb>/%q<post>`BYDB),%:))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: You cannot move that post.;@assert strlen(%3)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: No destination board entered.;th setq(oldbb,%q<bb>);@include u(bbs)/INC`CHECK`FINDBB=%0,%3,WRITE;th setq(next,baseconv(default(u(bbs-db)/BB`%q<bb>`NEXT,1),10,36));&BB`%q<bb>`NEXT u(bbs-db)=add(default(u(bbs-db)/BB`%q<bb>`NEXT,1),1);th u(GFN`CPTREE,%q<oldbb>,%q<post>,%q<bb>,%q<next>);@wipe %q<oldbb>/%q<post>;@nspemit %#=Message '%q<postnum>' moved from Board '[name(%q<oldbb>)]' to Board '[name(%q<bb>)]'

&INC`CONFIG u(bbs)=@switch/inline cor(cand(strlen(%1),not(u(GFN`VALNUM,%1))),not(strlen(%1)))=1,{@include u(bbs)/INC`CONFIG`GLOBAL},{@switch/inline cand(and(strlen(%1),u(GFN`VALNUM,%1)),not(u(GFN`VALNUM,%2)),not(strlen(after(%2,/))))=1,{@include u(bbs)/INC`CONFIG`BOARD},{@include u(bbs)/INC`CONFIG`POST}}

&INC`CONFIG`GLOBAL u(bbs)=@break strmatch(%0,GB)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Global Options cannot be changed in Group mode.;@switch/inline strlen(%1)=0,{@include u(bbs)/INC`CONFIG`GLOBAL`SHOW},{@include u(bbs)/INC`PARTIAL=%1,timeout|autotimeout|interval,|,%q<ann>,parameter,parameter;@switch/inline strlen(%3)=0,{@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) %q<parameter> for the %q<ann> cleared.;&BB`%q<parameter> u(bbs-db)},{@include u(bbs)/INC`CONFIG`GLOBAL`%q<parameter>;@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) %q<parameter> for the %q<ann> set to: %3.;&BB`%q<parameter> u(bbs-db)=strfirstof(%q<entry>,%3)}}

&INC`CONFIG`GLOBAL`SHOW u(bbs)=@nspemit %#=u(GFN`HEADER,BBS - Global Settings);@nspemit %#=AUTOTIMEOUT: [u(FUN`CONFIG,autotimeout)] - [if(u(FUN`CONFIG,autotimeout),On,Off)];@nspemit %#=TIMEOUT: [u(FUN`CONFIG,timeout)] [if(u(FUN`CONFIG,timeout),- [timestring(u(FUN`CONFIG,timeout))],Off)];@nspemit %#=INTERVAL: [u(FUN`CONFIG,interval)] [if(u(FUN`CONFIG,interval),- [timestring(u(FUN`CONFIG,interval))],Unset! Dangerous!)];@nspemit %#=u(GFN`HEADER)

&INC`CONFIG`GLOBAL`TIMEOUT u(bbs)=@assert strlen(setr(entry,stringsecs(%3)))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Entry doesn't convert properly. Check help stringsecs for proper format.

&INC`CONFIG`GLOBAL`INTERVAL u(bbs)=@assert setr(entry,stringsecs(%3))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Entry doesn't convert properly. Check help stringsecs for proper format.

&INC`CONFIG`GLOBAL`AUTOTIMEOUT u(bbs)=@assert regmatchi(setr(entry,%3),^0|1$)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Entry must be binary truth - 0 (off) or 1 (on).

&INC`CONFIG`BOARD u(bbs)=@include u(bbs)/INC`CHECK`FINDBB=%0,%1,WRITE;@switch/inline strlen(%2)=0,{@include u(bbs)/INC`CONFIG`BOARD`SHOW},{@include u(bbs)/INC`PARTIAL=%2,anonymous|timeout,|,%q<ann>,parameter,parameter;@switch/inline strlen(%3)=0,{@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) %q<parameter> for Board [name(%q<bb>)] cleared.;&BB`%q<bb>`%q<parameter> u(bbs-db)},{@include u(bbs)/INC`CONFIG`GLOBAL`%q<parameter>;@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) %q<parameter> for Board [name(%q<bb>)] set to: %3.;&BB`%q<bb>`%q<parameter> u(bbs-db)=strfirstof(%q<entry>,%3)}}

&INC`CONFIG`BOARD`SHOW u(bbs)=@nspemit %#=u(GFN`HEADER,%q<ann> - [name(%q<bb>)] Board Settings);@nspemit %#=TIMEOUT: [strfirstof(u(FUN`CONFIG,%q<bb>,timeout),0)] [if(u(FUN`CONFIG,%q<bb>,timeout),- [timestring(u(FUN`CONFIG,%q<bb>,timeout))])] [if(strlen(get(u(bbs-db)/BB`%q<bb>`TIMEOUT)),- Using Board Setting,- Using Global Default)];@nspemit %#=ANONYMOUS: [u(FUN`CONFIG,%q<bb>,anonymous)] [if(u(FUN`CONFIG,%q<bb>,anonymous),- [get(u(bbs-db)/BB`%q<bb>`ANONYMOUS)])];@nspemit %#=u(GFN`HEADER)

&INC`CONFIG`BOARD`TIMEOUT u(bbs)=@assert strlen(setr(entry,stringsecs(%3)))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Entry doesn't convert properly. Check help stringsecs for proper format.

&INC`CONFIG`BOARD`ANONYMOUS u(bbs)=@assert strlen(%3)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Anonymous entry must be a string.

&INC`CONFIG`POST u(bbs)=@switch/inline strlen(after(%2,/))=0,{@include u(bbs)/INC`CONFIG`GLOBAL`POST},{@include u(bbs)/INC`CHECK`FINDBB=%0,%1,WRITE;@include u(bbs)/INC`CHECK`FINDPOST=%0,before(%2,/),%q<bb>;@include u(bbs)/INC`PARTIAL=after(%2,/),timeout,|,%q<ann>,parameter,parameter;@switch/inline strlen(%3)=0,{@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) %q<parameter> for the %q<ann> cleared.;&%q<post>`%q<parameter> %q<bb>;&%q<post>`EDITED %q<bb>=secs()},{@include u(bbs)/INC`CONFIG`GLOBAL`%q<parameter>;@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) %q<parameter> for Board [name(%q<bb>)]'s Post %q<postnum> set to: %3.;&%q<post>`%q<parameter> %q<bb>=strfirstof(%q<entry>,%3)};&%q<post>`EDITED %q<bb>=secs()}

&INC`CONFIG`POST`SHOW u(bbs)=@nspemit %#=u(GFN`HEADER,%q<ann> - [name(%q<bb>)] - Message %q<postnum> Settings);@nspemit %#=TIMEOUT: [strfistof(get(%q<bb>/%q<post>`TIMEOUT),0)] [if(get(%q<bb>/%q<post>`TIMEOUT),- [timestring(get(%q<bb>/%q<post>`TIMEOUT))])];@nspemit %#=u(GFN`HEADER)

&INC`CONFIG`POST`TIMEOUT u(bbs)=@assert strlen(setr(entry,stringsecs(%3)))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Entry doesn't convert properly. Check help stringsecs for proper format.

&INC`TIMEOUT u(bbs)=@assert u(FUn`CONFIG,Autotimeout)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) Timeouts are currently disabled.;@switch/inline strlen(%1)=0,{@include u(bbs)/INC`LIST},{@include u(bbs)/INC`CHECK`FINDBB=%0,%1,WRITE;@switch/inline strlen(%2)=0,{@assert words(setr(processed,filterbool(#lambda/if(strlen(setr(post,elements(u(FUN`LISTPOSTS,%q<bb>),\%0))),cor(%q<isadmin>,strmatch(get(%q<bb>/\%q<post>`BYDB),%:))),setr(orig,u(FUN`MESSLIST,%#,%q<bb>,%2)))))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: No posts to change timeouts.;@switch/inline gt(words(setr(cannot,setdiff(%q<processed>,%q<orig>,%b,n))),0)=1,{@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) Could not change timeout for posts: [itemize(%q<cannot>,%b,and,\,)]};@assert strlen(%3)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: No new timeout entered!;@switch/inline %3=0,{@assert %q<isadmin>=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) Only admin may set a timeout of 0.};@include u(bbs)/INC`CONFIG`POST`TIMEOUT;@switch/inline %q<isadmin>=0,{@break gt(%q<entry>,u(FUN`CONFIG,%q<bb>,Timeout))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Cannot set a post's timeout greater than a board's timeout.};@dolist %q<processed>={@switch/inline %q<isadmin>=0,{@break eq(get(%q<bb>/[elements(u(FUN`LISTPOSTS,%q<bb>),%i0)]),0)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Only admin may apply timeouts to static posts.};&[elements(u(FUN`LISTPOSTS,%q<bb>),%i0)]`TIMEOUT %q<bb>=%q<entry>;@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) Timeout for %q<bbnum>/%i0 set to [timestring(%q<entry>)]!}},{@assert u(FUN`CONFIG,%q<bb>,Timeout)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) That board has no timeouts to adjust.;@nspemit %#=u(GFN`HEADER,u(fun`name,%0,%q<bb>,%q<gid1>) - Timeouts)%r[space(8)]Message[space(28)]Timeout[space(9)]By;@dolist setr(posts,u(FUN`LISTPOSTS,%q<bb>))={@nspemit %#=ljust(%q<bbnum>/[match(%q<posts>,%i0)],6)[ljust(u(u(bbs)/FUN`MSGFLAG,%#,%q<bb>,%i0),2)][ljust(get(%q<bb>/%i0`HDR),35)][ljust(if(gte(setr(ptime,u(FUN`GETTIMEOUT,%q<bb>,%i0)),0),timestring(%q<ptime>),Timed Out),16)][left(if(get(u(bbs-db)/BB`%q<bb>`ANONYMOUS),if(%q<isadmin>,([ansi(hx,get(%q<bb>/%i0`BY))]),default(u(bbs-db)/BB`%q<bb>`ANONYMOUS,Anonymous)),get(%q<bb>/%i0`BY)),21)];@switch/inline eq(inum(0),words(%q<posts>))=1,{@nspemit %#=u(GFN`HEADER)}}}}

&STARTUP u(bbs)=@trigger u(bbs)/TRG`TIMEOUT

&TRG`TIMEOUT u(bbs)=@switch/inline u(FUN`CONFIG,autotimeout)=1,{@dolist lattr(u(bbs-db)/BB`#*)={@assert isdbref(last(%i0,`))=@wipe u(bbs-db)/%i0;@switch/inline gt(u(FUN`CONFIG,last(%i0,`),timeout),0)=1,{@dolist lattr(last(%i0,`)/*)={@switch/inline gt(setr(timeoutsecs,strfirstof(get(last(%i1,`)/%i0`TIMEOUT),u(FUN`CONFIG,last(%i1,`),timeout))),0)=1,{@switch/inline gt(secs(),add(%q<timeoutsecs>,strfirstof(get(last(%i1,`)/%i0`EDITED),get(last(%i1,`)/%i0`ON))))=1,{@wipe last(%i1,`)/%i0}}};@set last(%i0,`)=SAFE}};@set u(bbs)=SAFE};@wait u(FUN`CONFIG,interval)=@trigger u(bbs)/TRG`TIMEOUT

&INC`LOCK u(bbs)=@include u(bbs)/INC`CHECK`FINDBB=%0,%1,WRITE;@include u(bbs)/INC`PARTIAL=%2,read|write,|,%q<ann>,lock,lock;@switch/inline %0=BB,{@assert cand(u(kls),u(kdb))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Key System not installed, cannot continue.;@assert strlen(%3)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: No lock string entered. See +help +key for more information.;@include u(kls)/INC`DOLOCK=%q<bb>,user:%q<lock>,%3,%q<ann>},GB,{@assert u(GFN`VALNUM,%3)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: %q<lock> Lock must be a number!;@break lte(%3,u(u(gso)/FUN`GETRANK,%q<gid1>,%:))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Cannot raise the %q<lock> Lock beyond your own rank.;@assert hasattrp(%q<gid1>/RANK`[setr(entry,abs(%3))])=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Cannot find that rank.;&BB`%q<bb>`G%q<lock> u(bbs-db)=%q<entry>;@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) You set the %q<lock> Lock on Group Board [name(%q<bb>)] to: Rank %q<entry> or higher.}

&INC`UNLOCK u(bbs)=@include u(bbs)/INC`CHECK`FINDBB=%0,%1,WRITE;@include u(bbs)/INC`PARTIAL=%2,read|write,|,%q<ann>,lock,lock;@switch/inline %0=BB,{@unlock/user:%q<lock> %q<bb>;@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) You release the %q<lock> Lock on [name(%q<bb>)]},GB,{@assert lte(get(u(bbs-db)/BB`%q<bb>`G%q<lock>),u(u(gso)/FUN`GETRANK,%q<gid1>,%:))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Cannot release a %q<lock> Lock that's beyond your own rank.;&BB`%q<bb>`G%q<lock> u(bbs-db);@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) You release the %q<lock> Lock on [name(%q<bb>)]}

&INC`CONVERT u(bbs)=@assert strlen(%1)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: No BBPOCKET entered to convert.;@assert isdbref(%1)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: That is not an object.;@assert cand(hasattr(%1/groups),hasattr(%1/version))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) That is not a Myrddin BBS BBpocket!;@assert strlen(%3)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: No board number entered to convert from.;@assert isdbref(setr(oldbb,elements(get(%1/groups),%3)))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Board not found.;@include u(bbs)/INC`VERIFY={[ansi(hr,WARNING:)] This will attempt to import a board from the old Myrddin BBS. A new, unlocked board will be created and posts copied/converted. Please enter the same command again within ten seconds to verify.},%q<ann> CONVERT %1 %3,%q<ann>;@include u(bbs)/INC`NEWGROUP=%0,name(%q<oldbb>);@dolist setr(list,sortkey(#lambda/baseconv(\%0,36,10),get(%q<oldbb>/MESS_LST)))={@switch/inline %0=BB,{th setq(next,baseconv(default(u(bbs-db)/BB`%q<bb>`NEXT,1),10,36));&BB`%q<bb>`NEXT u(bbs-db)=add(default(u(bbs-db)/BB`%q<bb>`NEXT,1),1)},GB,{};th setq(head,get(%q<oldbb>/HDR_%i0));&%q<next> %q<bb>=get(%q<oldbb>/BDY_%i0);&%q<next>`BY %q<bb>=elements(%q<head>,3,|);&%q<next>`BYDB %q<bb>=objid(elements(%q<head>,4,|));&%q<next>`ON %q<bb>=convtime(elements(%q<head>,2,|));&%q<next>`HDR %q<bb>=elements(%q<head>,1,|);&%q<next>`TIMEOUT %q<bb>=if(u(FUN`CONFIG,%q<bb>,TIMEOUT),u(FUN`CONFIG,%q<bb>,TIMEOUT),0)};@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) Conversion of [words(%q<list>)] messages (hopefully) complete!

&INC`ORDER u(bbs)=@assert setr(num,words(setr(list,switch(%0,BB,get(u(bbs-db)/groups),GB,get(%q<gid1>/boards)))))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: No boards to re-order.;@assert strlen(%1)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: New order field empty.;@assert eq(%q<num>,words(%1))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: new order field should not contain more than %q<num> numbers.;@break words(setdiff(lnum(1,%q<num>),%1))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Some numbers in that list don't match with the original.;@assert strmatch(lnum(1,%q<num>),sort(%1))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: At least one number was duplicated in the new order field.;th setq(newlist,elements(%q<list>,%1));@switch/inline %0=BB,{&groups u(bbs-db)=%q<newlist>},GB,{&boards %q<gid1>=%q<newlist>};@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) The board order was changed!

&INC`RENAME u(bbs)=@include u(bbs)/INC`CHECK`FINDBB=%0,%1,WRITE;@assert strlen(%3)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: New name field empty.;@assert valid(name,%3)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: Not a suitable object name. Please try something simpler - perhaps without fancy characters?;@break isdbref(locate(switch(%0,BB,u(bbs-db),GB,%q<gid1>),trim(%3),TXxi))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: A board already has that name.;@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) You renamed Board [name(%q<bb>)] to: %3;@name %q<bb>=%3

&INC`IMPORT u(bbs)=@assert strlen(%1)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: No BBPOCKET entered to convert.;@assert isdbref(%1)=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) ERROR: That is not an object.;@assert cand(hasattr(%1/groups),hasattr(%1/version))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) That is not a Myrddin BBS BBpocket!;@include u(bbs)/INC`VERIFY={[ansi(hr,WARNING:)] This will attempt to import all boards in sequence from an old Myrddin BBS. The imported boards will be APPENDED to the board list. This could wreak havoc (on the new board list, not the old) if something goes wrong. Make sure you have a backup! Please enter the same command again within ten seconds to verify.},%q<ann> IMPORT %1,%q<ann>;@dolist filterbool(#lambda/isdbref(\%0),get(%1/groups))={@include u(bbs)/INC`NEWGROUP=%0,name(%i0);@include u(bbs)/INC`CONVERT=%0,%q<bbnum>,1,%i0}

&INC`SEARCH u(bbs)=@include u(bbs)/INC`CHECK`FINDBB=%0,%1,WRITE;@include u(bbs)/INC`CHECKPC=%3,1,%q<ann>;@assert words(setr(posts,iter(grepi(%q<bb>,*`BYDB,%q<t1objid>),first(%i0,`))))=@nspemit %#=u(GFN`ANNOUNCE,%q<ann>) No posts by %q<t1name> on [name(%q<bb>)].;@nspemit %#=u(GFN`HEADER,BBS - [name(%q<bb>)])%r[space(8)]Message[space(28)]Posted[space(8)]By;@dolist/inline %q<posts>={@nspemit %#=ljust(%q<bbnum>/[match(u(FUN`LISTPOSTS,%q<bb>),%i0)],6)[ljust(u(u(bbs)/FUN`MSGFLAG,%#,%q<bb>,%i0),2)][ljust(get(%q<bb>/%i0`HDR),35)][ljust(timefmt($b $d $Y,lmath(max,firstof(get(%q<bb>/%i0`EDITED),get(%q<bb>/%i0`ON)))),14)][left(if(get(u(bbs-db)/BB`%q<bb>`ANONYMOUS),if(%q<isadmin>,([ansi(hx,get(%q<bb>/%i0`BY))]),default(u(bbs-db)/BB`%q<bb>`ANONYMOUS,Anonymous)),get(%q<bb>/%i0`BY)),21)]};@nspemit %#=u(GFN`HEADER)