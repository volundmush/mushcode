@@ DEPENDENCIES: Core, MySQL, MediaWiki, PHP
@@ You can install SceneSys with only MySQL and enjoy 
@@ the scheduling features but the web elements are what
@@ make it really shine.

th u(NEWCOBJ,Scene Management System <SCENE>,scene,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

&CMD`+SCENE`PENNMUSH [u(cobj,scene)]=$^(?s)(?\:\+)?(scene|scenes|plot|schedule|pot|summary)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+SCENE`MAIN
@set [u(cobj,scene)]/CMD`+SCENE`PENNMUSH=regexp
&CMD`+SCENE`RHOSTMUSH [u(cobj,scene)]=$^(?s)(?\:\+)?(scene|scenes|plot|schedule|pot|summary)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+SCENE`MAIN
@set [u(cobj,scene)]/CMD`+SCENE`RHOSTMUSH=regexp
&CMD`+SCENE`MAIN [u(cobj,scene)]=th u(setq,mode,switch(%1,scene,scene,scenes,scenes,schedule,scenes,plot,plot,pot,pot,summary,summary));@select/inline t(match(scene scenes plot,%q<mode>))=1,{@check not(strmatch(sql(SELECT DATABASE()),#-*))=@attach %!/INC`MSG=ERROR: SQL is not ready.};@attach %!/INC`GETSWITCH=%2;@include %!/INC`%q<mode>`[u(strfirstof,%q<switch>,MAIN)]=trim(%3),trim(%4),%3[if(strlen(%4),=%4)]
@set [u(cobj,scene)]/CMD`+SCENE`[switch(v(game),PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SYSTEM`SWITCHES [u(cobj,scene)]=setunion(v(SWITCHES`%q<mode>`PLAYER),if(u(isadmin,%#),v(SWITCHES`%q<mode>`ADMIN)),|,|)

&SWITCHES`SCENE`PLAYER [u(cobj,scene)]=SETUP|CHARID|JOIN|LEAVE|TITLE|DESC|CREATE|LOUDCREATE|RECALL|FINISH|PAUSE|CONTINUE|UNDO|REDO|SPOOF|MOVE|PLOT|OLD|WHO
&SWITCHES`SCENE`ADMIN [u(cobj,scene)]=DELETE|MERGE

&SWITCHES`SCENES`PLAYER [u(cobj,scene)]=ADD|DELETE|RESCHEDULE|DESC|TITLE|PLOT|TAG|UNTAG|MAIL|MINE|INVITE
&SWITCHES`SCENES`ADMIN [u(cobj,scene)]=

&SWITCHES`PLOT`PLAYER [u(cobj,scene)]=ALL
&SWITCHES`PLOT`ADMIN [u(cobj,scene)]=ADD|DESC|TITLE|START|END|TYPE|RUNNER|STATUS|DELETE

&SWITCHES`POT`PLAYER [u(cobj,scene)]=LAST|MAX|LIST|BRIEF|MODE|SUMMARY|TOGGLE
&SWITCHES`SUMMARY`PLAYER [u(cobj,scene)]=SILENT

&DEFAULT`SCENESYS [u(cobj,config)]=@@ Emptry attrib for Rhost.
&DEFAULT`SCENESYS`URL [u(cobj,config)]=example.org
&DESC`SCENESYS`URL [u(cobj,config)]=Path to website.
&TYPE`SCENESYS`URL [u(cobj,config)]=WORD

&DEFAULT`SCENESYS`PATH [u(cobj,config)]=/scene/view.php?id=
&DESC`SCENESYS`PATH [u(cobj,config)]=Path to view.php.
&TYPE`SCENESYS`PATH [u(cobj,config)]=WORD

&DEFAULT`SCENESYS`IDLE [u(cobj,config)]=1800
&DESC`SCENESYS`IDLE [u(cobj,config)]=Duration between idle checks?
&TYPE`SCENESYS`IDLE [u(cobj,config)]=DURATION

&DEFAULT`SCENESYS`BOARD [u(cobj,config)]=
&DESC`SCENESYS`BOARD [u(cobj,config)]=Board for scene announcements?
&TYPE`SCENESYS`BOARD [u(cobj,config)]=DBREF

&DEFAULT`IC [u(cobj,pconf)]=@@ empty attrib for Rhost.
&DEFAULT`IC`QUOTES [u(cobj,pconf)]=hw
&DESC`IC`QUOTES [u(cobj,pconf)]=For IC quotations.
&TYPE`IC`QUOTES [u(cobj,pconf)]=COLOR

&DEFAULT`IC`SPEECH [u(cobj,pconf)]=h
&DESC`IC`SPEECH [u(cobj,pconf)]=For IC dialogue.
&TYPE`IC`SPEECH [u(cobj,pconf)]=COLOR

&DEFAULT`IC`BORDERS [u(cobj,pconf)]=0
&DESC`IC`BORDERS [u(cobj,pconf)]=Show borders around poses?
&TYPE`IC`BORDERS [u(cobj,pconf)]=BOOL

&SYSTEM`NAME [u(cobj,scene)]=SCENESYS

&OBJECT`DESTROY [u(cobj,scene)]=@dolist/inline u(lattr,%!/OBJECT`DESTROY`*)={@trigger %!/OBJECT`DESTROY`##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}

&OBJECT`DESTROY`PURGESPOOF [u(cobj,scene)]=@select/inline %2=PLAYER,{@dolist/inline u(choosegame,lsearch(all,elock,D`SCENE`SPOOF:%0),search(EVAL=\[strmatch(get(##/D`SCENE`SPOOF),%0)\]))=&D`SCENE`SPOOF %i0}

&STARTUP [u(cobj,scene)]=@attach/nobreak %!/STARTUP`OVERRIDE;@trigger %!/LOOP`IDLECHECK

&STARTUP`OVERRIDE [u(cobj,scene)]=@select/inline v(game)=PennMUSH,{@dolist/inline @emit pose say semipose=@hook/override/inline %i0=%!,override`ic},RhostMUSH,{@dolist/inline @emit pose say semipose P S=@hook/ignore %d0}

&LOOP`IDLECHECK [u(cobj,scene)]=@dolist u(MYSQL,SCENE`IDLECHECK)={@attach %!/INC`MSGSCENE=##,{Scene ## - '[u(mysql,get`scenetitle,##)]' has been automatically marked Unfinished for idleness. Use [u(pueblize,+scene/continue ##)] to resume it.};@attach %!/INC`DOSQL=SET`SCENESTATE,2,##};@wait u(game_config,scenesys,idle)=@trigger %!/LOOP`IDLECHECK

&Q`SCENE`IDLECHECK [u(cobj,scene)]=select scene.scene_id from $SCENES$ as scene natural join $POSES$ as final_pose where scene.scene_state='0' and final_pose.pose_id = (select pose.pose_id from $POSES$ as pose where pose.scene_id = scene.scene_id order by pose.pose_id DESC limit 1) and final_pose.pose_time < UTC_TIMESTAMP() - INTERVAL 3 HOUR

&OVERRIDE`IC [u(cobj,scene)]=$^(?s)(pose|semipose|say|@emit)(?\:/noeval)?(?\: +(.*))?$:@check strlen(%2)=@pemit %#=Huh? (Type "help" for help.);@attach %!/INC`USERCHECK=u(objid,owner(%#));th u(setq,locid,u(objid,%l));th u(setq,custloc,uldefault(%l/D`CUSTLOC,%l,%#));th u(setq,sceneactive,u(mysql,get`activehere,%q<locid>,%q<custloc>));th u(setq,custrecp,uldefault(%l/D`CUSTRECP,lcon(%l),%#));th u(setq,logged,u(FUN`ISLOGGED,u(objid,owner(%#)),%q<pid>,%2,get(%#/D`SCENE`ACTIVE),%q<sceneactive>));th u(setq,basic,u(basicspeak,if(%q<logged>,u(firstof,get(%#/D`SCENE`SPOOF),owner(%#)),%#),switch(%1,pose,:,semipose,;,say,",@emit,|)%2));@attach/nobreak %!/INC`HEADER=%q<logged>,%q<sceneactive>;@attach/nobreak %!/INC`POTRECORD=%1,%2;@select/inline v(game)=PennMUSH,{@message %q<custrecp>=%q<basic>,%!/FUN`FORMATPOSE,##,if(%q<logged>,u(firstof,get(%#/D`SCENE`SPOOF),owner(%#)),%#),switch(%1,pose,:,semipose,;,say,",@emit,|),%2,%q<custrecp>},RhostMUSH,{@pemit/list %q<custrecp>=udefault(%!/FUN`FORMATPOSE,%q<basic>,##,if(%q<logged>,u(firstof,get(%#/D`SCENE`SPOOF),owner(%#)),%#),switch(%1,pose,:,semipose,;,say,",@emit,|),%2,%q<custrecp>)};@attach %!/INC`FOOTER=%q<logged>,%q<sceneactive>;@attach %!/INC`POSEERROR=%q<logged>;@attach/nobreak %!/INC`RECPOSE=u(objid,owner(%#)),%q<logged>,u(trimlines,u(trimtabs,%q<basic>))
@set u(cobj,scene)/OVERRIDE`IC=regexp

&OVERRIDE`IC2 [u(cobj,scene)]=$"*:@attach/command %!/OVERRIDE`IC=,say,%0
&OVERRIDE`IC3 [u(cobj,scene)]=$\:*:@attach/command %!/OVERRIDE`IC=,pose,%0
&OVERRIDE`IC4 [u(cobj,scene)]=$\;*:@attach/command %!/OVERRIDE`IC=,semipose,%0
@select/inline v(game)=PennMUSH,{@dolist/inline IC2 IC3 IC4=@set u(cobj,scene)/OVERRIDE`%i0=no_command}

&BASICSPEAK [u(cobj,scene)]=u(BASICSPEAK`[v(game)],%0,%1)
&BASICSPEAK`PENNMUSH [u(cobj,scene)]=speak(%0,%1)
&BASICSPEAK`RHOSTMUSH [u(cobj,scene)]=parsestr(%1,%0,",",,owner(%#))

&FUN`FORMATPOSE [u(cobj,scene)]=u(speech,%1,%2%3,%0,,IC,%4)

&INC`RECPOSE [u(cobj,scene)]=@select/inline isint(%1)=1,{@check u(setr,pid,u(mysql,get`playerid,u(objid,u(firstof,get(%0/D`SCENE`SPOOF),%0))));@attach %!/INC`DOSQL=SET`NEWPOSE,%q<pid>,%1,switch(v(game),PennMUSH,%2,RhostMUSH,stripansi(%2))}
&Q`SET`NEWPOSE [u(cobj,scene)]=INSERT INTO $POSES$ (player_id,scene_id,pose,pose_time) VALUES (?,?,?,UTC_TIMESTAMP())

&INC`POSEERROR [u(cobj,scene)]=@check words(%q<sceneactive>);@select/inline strmatch(%0,#-*)=1,{@attach %!/INC`MSG=ERROR: [rest(%0)]}

&FUN`ISLOGGED [u(cobj,scene)]=if(cand(not(%3),words(%4)),#-1 SCENE NOT JOINED,if(not(match(%4,%3)),#-2 JOINED SCENE (%3) NOT PRESENT,if(u(mysql,get`scenestate,%3),#-3 SCENE (%3) IS NOT ACTIVE,if(lt(words(%2),10),#-4 INSUFFICIENT WORD COUNT,%3))))

&INC`HEADER [u(cobj,scene)]=@check u(isic,u(objid,owner(%#)));th u(setq,poser,ansi(hw,u(moniker,u(objid,%#))) has posed[if(u(setr,spoof,get(%#/D`SCENE`SPOOF)),%B\(as [u(moniker,%q<spoof>)]\))]%b);th u(setq,logheader,switch(1,not(words(%q<sceneactive>)),,cand(words(%q<sceneactive>),not(%q<logged>)),%[[ansi(hr,NO LOG: [u(itemize,%q<sceneactive>,%b,and,\,)])]%]%B,t(match(%q<sceneactive>,%q<logged>)),%[[ansi(hg,LOG: %0)]%]%B,));@dolist/inline u(setr,footrecp,u(filter,poseheader,%q<custrecp>,%b,%b,%0,%1))={@select/inline t(u(player_config,##,IC,BORDERS))=1,{@pemit ##=u(header,%q<logheader>%q<poser>,##)},{@pemit ##=u(center,%q<logheader>%q<poser>,u(width,##),%B)}}

&FIL`POSEHEADER [u(cobj,scene)]=cor(words(%2),t(u(player_config,owner(%0),IC,BORDERS)))
 
&INC`FOOTER [u(cobj,scene)]=@check isic(u(objid,owner(%#)));th u(setq,poser,End of [ansi(hw,u(moniker,u(objid,owner(%#))))]'s pose[if(u(setr,spoof,get(%#/D`SCENE`SPOOF)),%B\(as [u(moniker,%q<spoof>)]\))]%b);@dolist/inline %q<footrecp>={@select/inline t(u(player_config,##,IC,BORDERS))=1,{@pemit ##=u(subheader,%q<logheader>%q<poser>,##)}}

&INC`POTRECORD [u(cobj,scene)]=@check get(%l/D`POT);th u(attrib_set,%l,u(setr,potattr,D`POSES`[u(nextslot,%l,D`POSES)]),%1);&%q<potattr>`CUSTLOC %l=%2;&%q<potattr>`TYPE %l=switch(%0,@EMIT,|,SAY,",SEMIPOSE,;,POSE,:);&%q<potattr>`BY %l=owner(%#);&%q<potattr>`WHEN %l=secs();@switch/inline gt(u(setr,count,u(nattr,%l/D`POSES`*)),80)=1,{@dolist/inline elements(u(sortattr,u(lattr,%l/D`POSES`*)),lnum(1,sub(%q<count>,80)))=@attach %!/WIPE=%l,##}

&INC`POT`MAIN [u(cobj,scene)]=@check get(%l/D`POT)=@attach %!/INC`MSG=ERROR: +pot is disabled in this location.;@switch/inline default(%#/D`POT`MODE,0)=0,{@attach %!/INC`POT`LIST},1,{@attach %!/INC`POT`BRIEF}
&FIL`POSELOC [u(cobj,scene)]=strmatch(get(%1/%0`CUSTLOC),%2)

&FUN`POT`POSES [u(cobj,scene)]=u(filter,POSELOC,u(sortattr,u(lattr,%0/D`POSES`*)),%b,%b,%0,%2)

&FUN`POT`POSERS [u(cobj,scene)]=setunion(iter(u(FUN`POT`POSES,%0,%1,%2),get(%l/%i0`BY)),)

&FUN`POT`LASTPOSE [u(cobj,scene)]=localize(last(u(filter,poseby,u(FUN`POT`POSES,%0,%1),%b,%b,%2)))
&FIL`POSEBY [u(cobj,scene)]=strmatch(get(%1/%0`BY),%2)

&INC`POT`BRIEF [u(cobj,scene)]=@check get(%l/D`POT)=@attach %!/INC`MSG=ERROR: +pot is disabled in this location.;@select/inline u(strfirstof,%0,get(%#/D`POT`MAX))=0,{th u(setq,max,0)},>0,{th u(setq,max,%0)},{th u(setq,max,15)};@check words(u(setr,poses,if(%q<max>,revwords(elements(revwords(u(FUN`POT`POSES,%l,u(%l/D`CUSTLOC,%#))),lnum(1,%q<max>))),u(FUN`POT`POSES,%l,u(%l/D`CUSTLOC,%#)))))=@attach %!/INC`MSG=No active RP in the room!;@pemit %#=u(HEADER,RP Activity for [u(moniker,%l)]);@pemit %#=ansi(u(color,%#,POT,COLUMN_NAMES),align(20 4 4 [sub(u(width,%#),31)],Name,Pose,Idle,Summary));@pemit %#=u(separator);@dolist/inline %q<poses>=@pemit %#=align(20 4 4 [sub(u(width,%#),31)],u(pueblize,u(moniker,get(%l/##`BY)),+pot/last [name(get(%l/##`BY))]),ansi(u(ryg,round(mul(fdiv(bound(sub(secs(),get(%l/##`WHEN)),0,3600),3600),100),0)),u(smalltime,sub(secs(),get(%l/##`WHEN)))),ansi(u(ryg,round(mul(fdiv(bound(idle(get(%l/##`BY)),0,3600),3600),100),0)),u(smalltime,idle(get(%l/##`BY)))),get(%l/##`SUMMARY));@pemit %#=u(SUBHEADER)

&INC`POT`LAST [u(cobj,scene)]=@check get(%l/D`POT)=@attach %!/INC`MSG=ERROR: +pot is disabled in this location.;@attach %!/INC`CHECKPC=%0,1;@check strmatch(loc(%q<t1>),%l)=@attach %!/INC`MSG=%q<t1name> is not in the room!;@check strlen(u(setr,pose,u(FUN`POT`LASTPOSE,%l,u(%l/D`CUSTLOC,%#),%q<t1>)))=@attach %!/INC`MSG=%q<t1name> hasn't posed in this room.;@pemit %#=u(header,%q<t1name>'s Last Pose [u(smalltime,u(setr,ago,sub(secs(),get(%l/%q<pose>`WHEN))))] Ago);@pemit %#=u(FUN`FORMATPOSE,%#,%q<t1>,get(%l/%q<pose>`TYPE),u(trimlines,get(%l/%q<pose>)),IC,u(FUN`POT`POSERS,%l,u(%l/D`CUSTLOC,%#)));@pemit %#=u(subheader)

&INC`POT`MODE [u(cobj,scene)]=&D`POT`MODE %#=u(setr,mode,not(get(%#/D`POT`MODE)));@attach %!/INC`MSG=+pot will now default to +pot/[if(%q<mode>,summary,list)]

&INC`POT`LIST [u(cobj,scene)]=@check get(%l/D`POT)=@attach %!/INC`MSG=ERROR: +pot is disabled in this location.;@select/inline u(strfirstof,%0,get(%#/D`POT`MAX))=0,{th u(setq,max,0)},>0,{th u(setq,max,%0)},{th u(setq,max,15)};@check words(u(setr,poses,if(%q<max>,revwords(elements(revwords(u(FUN`POT`POSES,%l,u(%l/D`CUSTLOC,%#))),lnum(1,%q<max>))),u(FUN`POT`POSES,%l,u(%l/D`CUSTLOC,%#)))))=@attach %!/INC`MSG=No active RP in the room!;th u(setq,players,u(lplayers,%l));@pemit %#=u(header,Recent Poses);@dolist %q<poses>={@pemit %#=u(separator,u(moniker,get(%l/##`BY)) posed [ansi(c,trim(u(smalltime,sub(secs(),get(%l/##`WHEN)))))] Ago);@pemit %#=u(FUN`FORMATPOSE,%#,get(%l/##`BY),get(%l/##`TYPE),get(%l/##),u(lplayers,%l));@switch/inline #@=words(%q<poses>),{@pemit %#=u(subheader,End of Poses)}}

&INC`POT`MAX [u(cobj,scene)]=@check u(valnum,%0)=@attach %!/INC`MSG=ERROR: You must enter a whole number greater than 0 for your maximum poses!;&D`POT`MAX %#=%0;@attach %!/INC`MSG=You will now see only the last %0 poses in +pot.

&INC`POT`TOGGLE [u(cobj,scene)]=@check cor(controls(%#,%l),u(isadmin,%#))=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`MSG`ROOM=Pot will [if(u(setr,mode,not(get(%l/D`POT))),now,no longer)]%bfunction in this location.,lcon(%l);&D`POT %l=%q<mode>

&Q`GET`ACTIVEHERE [u(cobj,scene)]=SELECT scene_id from $SCENES$ WHERE room_objid=? AND room_custloc=? AND scene_state='0'

&Q`GET`SCENELOC [u(cobj,scene)]=SELECT room_objid FROM $SCENES$ WHERE scene_id=?
&Q`GET`SCENECUSTLOC [u(cobj,scene)]=SELECT room_custloc FROM $SCENES$ WHERE scene_id=?

&Q`GET`SCENESTATE [u(cobj,scene)]=SELECT scene_state FROM $SCENES$ WHERE scene_id=?

&INC`SUMMARY`SILENT [u(cobj,scene)]=@attach %!/INC`SUMMARY`MAIN=%0,%1,%2,1
&INC`SUMMARY`MAIN [u(cobj,scene)]=@stop gt(strlen(%2),1024)=@attach %!/INC`MSG=That summary is too long.;@select/inline %3=1,{@pemit %#=ansi(hx,>>> SILENT SUMMARY: [u(moniker,%#)] - %2)},{@pemit/list uldefault(%l/D`CUSTRECP,lcon(%l),%#)=ansi(h,>>> SUMMARY - [u(moniker,%#)] <<< - %2)};@select/inline strlen(u(setr,pose,u(FUN`POT`LASTPOSE,%l,u(%l/D`CUSTLOC,%#),owner(%#))))=>0,{&%q<pose>`SUMMARY %l=%2}

&INC`SCENE`SETUP [u(cobj,scene)]=@select/inline cand(u(isadmin,%#),strlen(%0))=1,{@attach %!/INC`CHECKPC=%0,1},0,{@attach %!/INC`CHECKPC=%#,1};@select/inline gt(u(setr,id,u(mysql,get`playerid,%q<t1objid>)),0)=1,{@attach %!/INC`DOSQL=RENAME`PLAYER,%q<t1name>,%q<t1objid>},0,{@attach %!/INC`DOSQL=INIT`PLAYER,%q<t1objid>,%q<t1name>};@attach %!/INC`MSG=[if(strmatch(u(objid,%#),%q<t1objid>),You set yourself up for SceneSys.,You have set %q<t1name> up for SceneSys.)] The unique Char_id is: [ansi(h,u(mysql,get`playerid,%q<t1objid>))]

&OBJECT`RENAME`CHARID [u(cobj,scene)]=@select/inline type(%0)=PLAYER,{@check u(setr,pid,u(mysql,get`playerid,%0));th u(mysql,rename`player,%1,%0)}

&INC`SCENE`CHARID [u(cobj,scene)]=@select/inline t(strlen(%0))=1,{@attach %!/INC`CHECKPC=%0,1},0,{@attach %!/INC`CHECKPC=%#,1};@check gt(u(setr,id,u(mysql,get`playerid,%q<t1objid>)),0)=@attach %!/INC`MSG=%q<t1name> does not have a Char_id.;@attach %!/INC`MSG=The Unique Char_id for %q<t1name> is: [ansi(h,%q<id>)]

&INC`DOSQL [u(cobj,scene)]=@stop strmatch(u(setr,errcheck,u(mysql,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)),#-*)=@attach %!/INC`MSG=SQL ERROR: %q<errcheck>

&Q`INIT`PLAYER [u(cobj,scene)]=INSERT INTO $PLAYERS$ (objid,player_name) VALUES (?,?)
&Q`GET`PLAYERID [u(cobj,scene)]=SELECT player_id FROM $PLAYERS$ WHERE objid=?
&Q`GET`PLAYEROBJID [u(cobj,scene)]=SELECT objid FROM $PLAYERS$ WHERE player_id=?
&Q`RENAME`PLAYER [u(cobj,scene)]=UPDATE $PLAYERS$ SET player_name=? WHERE objid=?

&INC`USERCHECK [u(cobj,scene)]=@select/inline t(u(setr,pid,u(mysql,get`playerid,u(objid,owner(%0)))))=0,{@attach %!/INC`DOSQL=INIT`PLAYER,u(objid,owner(%0)),name(%0);@attach %!/INC`USERCHECK}

&INC`SCENE`MERGE [u(cobj,scene)]=@check u(iswizard,%#)=@attach %!/INC`MSG=Sorry, wizard only.;@attach %!/INC`CHECKPC=%0,1;@check strlen(%1)=@attach %!/INC`MSG=No ID entered to merge to.;@check strlen(u(setr,oldobj,u(mysql,get`playerobjid,%1)))=@attach %!/INC`MSG=That ID does not exist.;@stop u(isobjid,%q<oldobj>)=@attach %!/INC`MSG=ID '%1' is currently assigned to %q<oldobj> - [name(%q<oldobj>)]. It cannot be merged with another.;@attach %!/INC`VERIFY={[ansi(h,WARNING:)] This will assign Char_Id %1 to %q<t1name>. All poses, scenes, plots, and other SceneSys data attached to their Current ID, if they have one, shall be converted to the old ID. This cannot be undone. Continue? Enter the same command again to verify.},MERGE %q<oldobj> %1;@select/inline gt(u(setr,pid,u(mysql,get`playerid,%q<t1objid>)),0)=1,{@attach %!/INC`DOSQL=SET`ALLPOSES,%1,%q<pid>;@attach %!/INC`DOSQL=SET`ALLSCENES,%1,%q<pid>;@attach %!/INC`DOSQL=SET`ALLSCHEDULE,%1,%q<pid>;@attach %!/INC`DOSQL=SET`ALLPLOTS,%1,%q<pid>;@attach %!/INC`DOSQL=DELETE`PLAYERID,%q<pid>};@attach %!/INC`DOSQL=SET`PLAYERID,%q<t1objid>,%q<t1name>,%1;@attach %!/INC`MSG=The merge was completed.

&Q`SET`ALLPOSES [u(cobj,scene)]=UPDATE $POSES$ SET player_id=? WHERE player_id=?
&Q`SET`ALLSCENES [u(cobj,scene)]=UPDATE $SCENES$ SET player_id=? WHERE player_id=?
&Q`SET`ALLSCHEDULE [u(cobj,scene)]=UPDATE $SCHEDULE$ SET player_id=? WHERE player_id=?
&Q`SET`ALLPLOTS [u(cobj,scene)]=UPDATE $PLOTS$ SET player_id=? WHERE player_id=?
&Q`DELETE`PLAYERID [u(cobj,scene)]=DELETE FROM $PLAYERS$ WHERE player_id=?
&Q`SET`PLAYERID [u(cobj,scene)]=UPDATE $PLAYERS$ SET objid=?, player_name=? WHERE player_id=?

&INC`SCENE`LOUDCREATE [u(cobj,scene)]=@attach %!/INC`SCENE`CREATE=%0,%1,1
&INC`SCENE`CREATE [u(cobj,scene)]=@check approved(%#)=@attach %!/INC`MSG=You are not approved!;th u(setq,title,%0[if(strlen(%1),=%1)]);@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`DOSQL=INIT`SCENE,%q<pid>,%q<title>,u(objid,%l),name(%l),uldefault(%l/D`CUSTLOC,%l,%#);@check gt(u(setr,scene,u(mysql,get`newsceneid,%q<pid>)),0)=@attach %!/INC`MSG=ERROR: Scene could not be created.;@attach %!/INC`MSG`ROOM=created a new scene here: Scene %q<scene>[if(strlen(%q<title>),%b-%b%q<title>)]. Use [u(pueblize,+scene/join %q<scene>,+scene/join %q<scene>)] to join it!,uldefault(%l/D`CUSTRECP,lcon(%l),%#);@select/inline t(%2)=1,{@attach %!/INC`MSG`CHAN=ansi(h,[u(moniker,%#)]) created a Scene at [if(match(u(u(navi)/FUN`LISTIC),%l),u(pueblize,%l - [name(%l)],+port %l),name(%l))]: Scene %q<scene>[if(strlen(%q<title>),%b-%b%q<title>)],u(game_config,channels,roleplay),,1,,1};@attach %!/INC`SCENE`JOIN=%q<scene>

&Q`INIT`SCENE [u(cobj,scene)]=INSERT INTO $SCENES$ (player_id,scene_title,room_objid,room_name,room_custloc,creation_date) VALUES (?,?,?,?,?,UTC_TIMESTAMP())
&Q`GET`NEWSCENEID [u(cobj,scene)]=SELECT max(scene_id) from $SCENES$ WHERE player_id=?

&INC`MESSAGE [u(cobj,scene)]=@attach %!/INC`MSG`ROOM=%1,setunion(setunion(lockfilter(D`SCENE`ACTIVE:%0,u(mysql,get`objidsinscene,%0)),u(objid,%#)),iter(uldefault(%l/D`CUSTRECP,lcon(%l),%#),u(objid,%i0)))

&INC`MSGSCENE [u(cobj,scene)]=@attach %!/INC`MSG`ROOM=%1,setunion(setunion(lockfilter(D`SCENE`ACTIVE:%0,setunion(u(objid,%#),u(mysql,get`objidsinscene,%0))),u(objid,%#)),iter(uldefault(%l/D`CUSTRECP,lcon(%l),%#),u(objid,%i0)))

&INC`SCENE`JOIN [u(cobj,scene)]=@check approved(%#)=@attach %!/INC`MSG=You are not approved!;@attach %!/INC`USERCHECK=u(objid,%#);@select/inline gt(strlen(u(setr,scene,%0)),0)=1,{@select/inline u(valnum,%q<scene>)=0,{@attach %!/INC`CHECKPC=%q<scene>,2;@check u(valnum,u(setr,scene,get(%q<t2>/D`SCENE`ACTIVE)))=@attach %!/INC`MSG=%q<t2name> is not joined to a scene.},1,{@check u(setr,scene,u(mysql,get`scenewhereidandstate,%q<scene>,0))=@attach %!/INC`MSG=Scene '%0' does not exist or is not Active.}},0,{@check gt(u(setr,scene,u(mysql,GET`SCENEHERE,u(objid,%l),uldefault(%l/D`CUSTLOC,%l,%#))),0)=@attach %!/INC`MSG=There are no recent scenes here to join!};&D`SCENE`ACTIVE %#=%q<scene>;@attach %!/INC`MESSAGE=%q<scene>,u(moniker,%#) joined Scene %q<scene>!;@select/inline t(get(%#/D`SCENE`SPOOF))=1,{@attach %!/INC`MSG=ALERT: You are currently set to Spoof as: [u(moniker,get(%#/D`SCENE`SPOOF))].}

&Q`GET`SCENEHERE [u(cobj,scene)]=SELECT max(scene_id) from $SCENES$ WHERE room_objid=? AND room_custloc=? AND scene_state='0' AND creation_date >= date_sub(UTC_TIMESTAMP(),INTERVAL 1 HOUR)
&Q`GET`SCENEWHEREIDANDSTATE [u(cobj,scene)]=SELECT scene_id from $SCENES$ WHERE scene_id=? AND scene_state='?'
&Q`GET`OBJIDSINSCENE [u(cobj,scene)]=SELECT DISTINCT objid FROM $PLAYERS$ NATURAL LEFT JOIN $POSES$ WHERE $POSES$.scene_id=?

&INC`SCENE`LEAVE [u(cobj,scene)]=@check u(setr,scene,get(%#/D`SCENE`ACTIVE))=@attach %!/INC`MSG=You are not in any scenes!;@attach %!/INC`MESSAGE=%q<scene>,u(moniker,%#) has left Scene %q<scene>;&D`SCENE`ACTIVE %#

&INC`OWNCHECK [u(cobj,scene)]=@check cor(u(isadmin,%#),eq(u(setr,ownid,u(mysql,get`ownerid,%0)),%1),if(%2,eq(u(mysql,get`plotownerid,%2),%1)))=@attach %!/INC`MSG=Permission denied.
&Q`GET`OWNERID [u(cobj,scene)]=SELECT player_id from $SCENES$ WHERE scene_id=?
&Q`GET`PLOTOWNERID [u(cobj,scene)]=SELECT player_id from $PLOTS$ WHERE plot_id=?

&INC`SCENE`SPOOF [u(cobj,scene)]=@select/inline strlen(%0)=>0,{@check u(setr,accid,u(accid,u(objid,owner(%#))))=@attach %!/INC`MSG=You don't have an account!;@check isdbref(u(setr,spoof,u(namegrab,alts(owner(%#)))))=@attach %!/INC`MSG=ERROR: '%0' is not one of your alts.;&D`SCENE`SPOOF %#=%q<spoof>;@attach %!/INC`MSG=Your poses will now appear to be from %q<t1name>.},{&D`SCENE`SPOOF %#;@attach %!/INC`MSG=Your poses will appear to be from yourself.}

&INC`VALSCENE [u(cobj,scene)]=@check strlen(%0)=@attach %!/INC`MSG=Scene ID field empty.;@check u(valnum,%0)=@attach %!/INC`MSG=Scenes must be addressed by their IDs.;@check gt(u(setr,scene,u(mysql,get`scenecheck,%0)),0)=@attach %!/INC`MSG=Scene '%0' could not be found.
&Q`GET`SCENECHECK [u(cobj,scene)]=SELECT scene_id FROM $SCENES$ WHERE scene_id=?

&INC`SCENE`TITLE [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCENE=%0;@attach %!/INC`OWNCHECK=%q<scene>,%q<pid>;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Title field empty!;@attach %!/INC`DOSQL=SET`SCENETITLE,%1,%q<scene>;@attach %!/INC`MSGSCENE=%q<scene>,[u(moniker,%#)] changed Scene %q<scene>'s Title to: %1

&INC`SCENE`DESC [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCENE=%0;@attach %!/INC`OWNCHECK=%q<scene>,%q<pid>;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Description field empty!;@attach %!/INC`DOSQL=SET`SCENEDESC,%1,%q<scene>;@attach %!/INC`MSGSCENE=%q<scene>,[u(moniker,%#)] changed Scene %q<scene>'s Description to: %1

&INC`SCENE`PLOT [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCENE=%0;@check strlen(%1)=@attach %!/INC`MSG=No plot entered to link!;@check cand(isint(%1),gte(%1,0))=@attach %!/INC`MSG=Plots must be addressed by IDs. Use 0 to set No Plot.;@select/inline gte(%1,0)=1,{@attach %!/INC`VALPLOT=%1};@attach %!/INC`OWNCHECK=%q<scene>,%q<pid>,%1;@attach %!/INC`DOSQL=SET`SCENEPLOT,%1,%q<scene>;@attach %!/INC`MSGSCENE=%q<scene>,[u(moniker,%#)] changed Scene %q<scene>'s Plot to: %1 - [u(mysql,get`plottitle,%1)]

&Q`SET`SCENETITLE [u(cobj,scene)]=UPDATE $SCENES$ SET scene_title=? WHERE scene_id=?
&Q`SET`SCENEDESC [u(cobj,scene)]=UPDATE $SCENES$ SET scene_desc=? WHERE scene_id=?
&Q`SET`SCENEPLOT [u(cobj,scene)]=UPDATE $SCENES$ SET plot_id=? WHERE scene_id=?

&INC`SCENE`FINISH [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCENE=u(strfirstof,%0,get(%#/D`SCENE`ACTIVE));@attach %!/INC`OWNCHECK=%q<scene>,%q<pid>;@attach %!/INC`DOSQL=SET`SCENESTATE,3,%q<scene>;@@ @attach %!/INC`DOSQL=SET`SCENEFINISH;@attach %!/INC`MESSAGE=%q<scene>,[u(moniker,%#)] has marked Scene %q<scene> Finished!
&INC`SCENE`PAUSE [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCENE=u(strfirstof,%0,get(%#/D`SCENE`ACTIVE));@attach %!/INC`OWNCHECK=%q<scene>,%q<pid>;@attach %!/INC`DOSQL=SET`SCENESTATE,1,%q<scene>;@attach %!/INC`MESSAGE=%q<scene>,[u(moniker,%#)] has marked Scene %q<scene> Paused!
&INC`SCENE`CONTINUE [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCENE=u(strfirstof,%0,get(%#/D`SCENE`ACTIVE));@attach %!/INC`OWNCHECK=%q<scene>,%q<pid>;@attach %!/INC`DOSQL=SET`SCENESTATE,0,%q<scene>;@attach %!/INC`MESSAGE=%q<scene>,[u(moniker,%#)] has continued Scene %q<scene>! It will now record new poses.

&Q`SET`SCENESTATE [u(cobj,scene)]=UPDATE $SCENES$ SET scene_state='?' WHERE scene_id=?

&INC`SCENE`UNDO [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(firstof,get(%#/D`SCENE`SPOOF),u(objid,%#));@attach %!/INC`VALSCENE=get(%#/D`SCENE`ACTIVE);@check eq(u(mysql,get`scenestate,%q<scene>),0)=@attach %!/INC`MSG=That scene is not Active!;@check gt(u(setr,undo,u(mysql,get`lastpose,%q<scene>,%q<pid>,0)),0)=@attach %!/INC`MSG=You have no recent poses that can be undone.;@attach %!/INC`DOSQL=SET`IGNORE,1,%q<undo>;@attach %!/INC`MSGSCENE=%q<scene>,[u(moniker,%#)] undid %p last pose: %q<undo>

&INC`SCENE`REDO [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(firstof,get(%#/D`SCENE`SPOOF),u(objid,%#));@attach %!/INC`VALSCENE=get(%#/D`SCENE`ACTIVE);@check eq(u(mysql,get`scenestate,%q<scene>),0)=@attach %!/INC`MSG=That scene is not Active!;@check gt(u(setr,undo,u(mysql,get`lastpose,%q<scene>,%q<pid>,1)),0)=@attach %!/INC`MSG=You have no recent poses that can be restored.;@attach %!/INC`DOSQL=SET`IGNORE,0,%q<undo>;@attach %!/INC`MSGSCENE=%q<scene>,[u(moniker,%#)] restored an earlier Ignored pose: %q<undo>

&Q`GET`LASTPOSE [u(cobj,scene)]=SELECT max(pose_id) FROM $POSES$ WHERE scene_id=? and player_id=? AND pose_ignore=? AND pose_time >= date_sub(UTC_TIMESTAMP(),INTERVAL 1 HOUR)
&Q`SET`IGNORE [u(cobj,scene)]=UPDATE $POSES$ SET pose_ignore=? WHERE pose_id=?

&INC`SCENE`RECALL [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCENE=u(strfirstof,%0,get(%#/D`SCENE`ACTIVE));@select/inline gt(strlen(%1),0)=1,{@check u(valnum,%1)=@attach %!/INC`MSG=The number of poses must be a whole, positive number.;th u(setq,poses,%1)},0,{th u(setq,poses,15)};@pemit %#=u(HEADER,Scene %q<scene>[if(strlen(u(setr,title,u(mysql,get`scenetitle,%q<scene>))),:%B%q<title>)]);th u(setq,target,%#);@select/inline v(game)=PennMUSH,{th mapsql(FUN`RECALL,u(SQL`FORMAT,GET`SCENERECALL,%q<scene>,%q<poses>));@pemit %#=u(SUBHEADER)},RhostMUSH,{@dolist/inline u(mysql,GET`RECALLIDS,%q<scene>,%q<poses>)={th u(setq,det,u(mysql2,GET`RECALLDET,%d0));th u(FUN`RECALL,,elements(%q<det>,1,^),,u(mysql,GET`RECALLPOSE,%d0),elements(%q<det>,2,^))};@pemit %#=u(SUBHEADER)}

&FUN`RECALL [u(cobj,scene)]=pemit(%q<target>,u(SEPARATOR,u(moniker,%1) Posed on [u(fancytime,%4,%q<target>)])%R%3)

&Q`GET`SCENETITLE [u(cobj,scene)]=SELECT scene_title FROM $SCENES$ WHERE scene_id=?
&Q`GET`SCENERECALL [u(cobj,scene)]=SELECT * FROM (SELECT objid,pose_id,pose,UNIX_TIMESTAMP(pose_time) from $POSES$ NATURAL LEFT JOIN $PLAYERS$ WHERE $POSES$.scene_id=? AND $POSES$.pose_ignore=0 ORDER BY $POSES$.pose_id desc limit ?) sub ORDER BY pose_id ASC

&Q`GET`RECALLIDS [u(cobj,scene)]=SELECT * FROM (SELECT pose_id from $POSES$ NATURAL LEFT JOIN $PLAYERS$ WHERE $POSES$.scene_id=? AND $POSES$.pose_ignore=0 ORDER BY $POSES$.pose_id desc limit ?) sub ORDER BY pose_id ASC
&Q`GET`RECALLDET [u(cobj,scene)]=SELECT objid,UNIX_TIMESTAMP(pose_time) FROM $POSES$ NATURAL LEFT JOIN $PLAYERS$ WHERE pose_id=?
&Q`GET`RECALLPOSE [u(cobj,scene)]=SELECT pose FROM $POSES$ WHERE pose_id=?


&INC`SCENE`MAIN [u(cobj,scene)]=@select/inline t(strlen(%0))[isint(%0)]=0*,{@attach %!/INC`SCENE`PLAYERLIST=u(objid,%#),%1},11,{@attach %!/INC`SCENE`DETAILS=%0,%1},10,{@attach %!/INC`SCENE`PLAYERLIST=%0,%1}

&INC`SCENE`OLD [u(cobj,scene)]=@attach %!/INC`SCENE`MAIN=%0,4

&INC`SCENE`PLAYERLIST [u(cobj,scene)]=@attach %!/INC`CHECKPC=u(strfirstof,%0,%#),1;@attach %!/INC`USERCHECK=%q<t1objid>;@pemit %#=u(HEADER,if(strmatch(%q<t1objid>,u(objid,%#)),Your,%q<t1name>'s) [if(%1,Finished,Incomplete)] Scenes);th u(setq,target,%#);@pemit %#=ansi(u(color,%#,SCENESYS,COLUMN_NAMES),align(4 43 20 8,ID,Title,Owner,if(%1,Date,Status)));@select/inline v(game)=PennMUSH,{@dolist/inline u(mysql,GET`SCENEMINE%1,%q<pid>,%q<pid>)={th mapsql(FUN`SCENEMINE%1,u(SQL`FORMAT,GET`SCENEMINE3%1,##))};@pemit %#=u(SUBHEADER)},RhostMUSH,{@dolist/inline u(mysql,GET`SCENEMINE%1,%q<pid>,%q<pid>)={th u(setq,det,u(mysql2,GET`SCENEMINE3%1,##));@pemit %#=u(FUN`SCENEMINE%1,,elements(%q<det>,1,^),elements(%q<det>,2,^),elements(%q<det>,3,^),elements(%q<det>,4,^))}};@pemit %#=u(SUBHEADER)

&FUN`SCENEMINE [u(cobj,scene)]=pemit(%#,align(4 43 20 8,u(pueblize,%1,+scene %1),%2,%3,switch(%4,0,Active,1,Paused,2,Unfinished,3,Finished,???)))

&FUN`SCENEMINE4 [u(cobj,scene)]=pemit(%#,align(4 43 20 8,u(pueblize,%1,+scene %1),%2,%3,u(choosegame,timefmt($m/$d/$y,%4,u(gettz,%#)),ptimefmt($m/$d/$y,%4,u(gettz,%#)))))

&Q`GET`SCENEMINE [u(cobj,scene)]=SELECT DISTINCT scenes.scene_id FROM $SCENES$ AS scenes LEFT JOIN $POSES$ AS poses ON scenes.scene_id=poses.scene_id WHERE (scenes.player_id=? OR poses.player_id=?) AND scene_state!='3'
&Q`GET`SCENEMINE4 [u(cobj,scene)]=SELECT DISTINCT scenes.scene_id FROM $SCENES$ AS scenes LEFT JOIN $POSES$ AS poses ON scenes.scene_id=poses.scene_id WHERE (scenes.player_id=? OR poses.player_id=?) AND scene_state='3'

&Q`GET`SCENEMINE3 [u(cobj,scene)]=SELECT scene_id,scene_title,player_name,scene_state FROM $SCENES$ NATURAL LEFT JOIN $PLAYERS$ WHERE scene_id=?

&Q`GET`SCENEMINE34 [u(cobj,scene)]=SELECT scene_id,scene_title,player_name,UNIX_TIMESTAMP(creation_date) FROM $SCENES$ NATURAL LEFT JOIN $PLAYERS$ WHERE scene_id=?

&INC`SCENE`DETAILS [u(cobj,scene)]=@attach %!/INC`VALSCENE=%0;th u(setq,ownerid,u(mysql,get`ownerid,%q<scene>));@pemit %#=u(HEADER,Scene %q<scene>[if(strlen(u(setr,title,u(mysql,get`scenetitle,%q<scene>))),:%B%q<title>)]);@pemit %#=ansi(h,Started:) [u(fancytime,u(mysql,get`scenedate,%q<scene>),%#)]%R[ansi(h,Description:)] [u(mysql,get`scenedesc,%q<scene>)]%R[ansi(h,Owner:)]%B[u(mysql,get`playername,%q<ownerid>)]%R[if(strlen(u(setr,tp,u(mysql,get`tinyplot,%q<scene>))),ansi(h,Tinyplot:) %q<tp>%R)][ansi(h,Status:)] [switch(u(mysql,get`scenestate,%q<scene>),0,Active,1,Paused,2,Unfinished,3,Finished)];@pemit %#=u(SEPARATOR,Players);@pemit %#=ansi(u(color,%#,SCENE,COLUMN_NAMES),align(30 30 15,ansi(h,Name),ansi(h,Status),ansi(h,Poses)));@pemit %#=u(SEPARATOR);@dolist/inline/delimit | [u(mysql2,GET`PLAYERCOUNT,%q<scene>)]={@pemit %#=align(30 30 15,elements(##,2,^),if(eq(%q<scene>,get(elements(##,1,^)/D`SCENE`ACTIVE)),Active,Inactive),u(mysql,get`playerposecount,%q<scene>,elements(##,3,^)))};@select/inline gt(u(setr,total,u(mysql,get`sceneposecount,%q<scene>)),0)=1,{@pemit %#=align(30 >30 15,,Total:,%q<total>)};@pemit %#=u(SEPARATOR);@pemit %#=ansi(h,URL:) [u(weblink,http://[u(game_config,SCENESYS,URL)][u(game_config,SCENESYS,PATH)]%q<scene>)];@pemit %#=u(SUBHEADER)

&Q`GET`SCENEDESC [u(cobj,scene)]=SELECT scene_desc FROM $SCENES$ WHERE scene_id=?
&Q`GET`SCENEDATE [u(cobj,scene)]=SELECT UNIX_TIMESTAMP(creation_date) FROM $SCENES$ WHERE scene_id=?
&Q`GET`PLAYERNAME [u(cobj,scene)]=SELECT player_name from $PLAYERS$ WHERE player_id=?
&Q`GET`PLAYERPOSECOUNT [u(cobj,scene)]=SELECT count(pose_id) FROM $POSES$ WHERE scene_id=? and player_id=?
&Q`GET`SCENEPOSECOUNT [u(cobj,scene)]=SELECT count(pose_id) FROM $POSES$ WHERE scene_id=?
&Q`GET`TINYPLOT [u(cobj,scene)]=SELECT plot_title FROM $SCENES$ NATURAL LEFT JOIN $PLOTS$ WHERE scene_id=?

&Q`GET`PLAYERCOUNT [u(cobj,scene)]=SELECT DISTINCT Objid,player_name,$PLAYERS$.player_id FROM $PLAYERS$ NATURAL LEFT JOIN $POSES$ WHERE scene_id=? ORDER BY player_name

&INC`SCENE`WHO [u(cobj,scene)]=@attach %!/INC`VALSCENE=u(strfirstof,%0,get(%#/D`SCENE`ACTIVE));@attach %!/INC`MSG=Joined participants for Scene %q<scene>: [u(strfirstof,u(itemize,iter(lsearch(all,type,player,elock,D`SCENE`ACTIVE:%q<scene>),name(%i0)[if(strmatch(%l,loc(%i0)),,%b\(not present\))],%b,|),|,and,\,),Nobody!)]

&INC`SCENE`MOVE [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCENE=%0;@attach %!/INC`OWNCHECK=%q<scene>,%q<pid>;@check isic(%l)=@attach %!/INC`MSG=ERROR: This location is not IC!;@attach %!/INC`DOSQL=SET`RELOCATE,u(objid,%l),name(%l),uldefault(%l/D`CUSTLOC,%l,%#),%q<scene>;@attach %!/INC`MESSAGE=%q<scene>,{[u(moniker,%#)] has changed the scene's location to: %l - [name(%l)]}
&INC`SCENE`TINYPLOT [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCENE=%0;@attach %!/INC`OWNCHECK=%q<scene>,%q<pid>;@attach %!/INC`VALPLOT=%1;@attach %!/INC`DOSQL=SET`SCENEPLOT,%q<plot>,%q<scene>;@attach %!/INC`MSGSCENE=%q<scene>,{[u(moniker,%#)] linked Scene %q<scene> to Tinyplot %q<plot>}
&INC`SCENE`NOTINYPLOT [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCENE=%0;@attach %!/INC`OWNCHECK=%q<scene>,%q<pid>;@check u(setr,plot,u(mysql,get`sceneplot,%q<scene>))=@attach %!/INC`MSG=That scene is not linked to any Tinyplots!;@attach %!/INC`DOSQL=SET`NOSCENEPLOT,%q<scene>;

&Q`SET`RELOCATE [u(cobj,scene)]=UPDATE $SCENES$ SET room_objid=?,room_name=?,room_custloc=? WHERE scene_id=?
&Q`SET`SCENEPLOT [u(cobj,scene)]=UPDATE $SCENES$ SET plot_id=? WHERE scene_id=?
&Q`SET`NOSCENEPLOT [u(cobj,scene)]=UPDATE $SCENES$ SET plot_id=NULL WHERE scene_id=?

&INC`SCENE`DELETE [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCENE=%0;@check u(isadmin,%#)=@attach %!/INC`MSG=Permission denied.;@attach %!/INC`VERIFY={ansi(hr,WARNING:) This will delete Scene %q<scene>. All poses will be lost. This will not affect the Wiki's copy. Are you sure? Enter the same command to verify.},DELETE SCENE %q<scene>;@attach %!/INC`MESSAGE=%q<scene>,{[u(moniker,%#)] has deleted Scene %q<scene>.};@attach %!/INC`DOSQL=DELETE`SCENE,%q<scene>;

&Q`DELETE`SCENE [u(cobj,scene)]=DELETE FROM $SCENES$ WHERE scene_id=?

&INC`SCENES`ADD [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`CHECKTIME=trim(before(%0,/));@check strlen(u(setr,title,trim(after(%0,/))))=@attach %!/INC`MSG=You need to give the scene a title!;@stop u(charsearch,%q<title>,/ %r %t)=@attach %!/INC`MSG=The following characters are not permitted in titles: /, \%r, \%t.;@check strlen(u(setr,desc,%1))=@attach %!/INC`MSG=Your scene needs a description!;@attach %!/INC`DOSQL=INSERT`NEWSCHEDULE,%q<pid>,u(trimtabs,u(trimlines,%q<title>)),u(trimtabs,u(trimlines,%q<desc>)),%q<time>;th u(setq,schedid,u(mysql,get`newschedule,%q<pid>));@trigger u(cobj,bbs)/INC`POST=BB,u(game_config,SCENESYS,BOARD),#%q<schedid>: %q<title>,u(FUN`FORMATPOST,%q<title>,[u(moniker,%#)],u(fancytime,%q<time>,,UTC),%q<desc>),%#;@attach %!/INC`MSG=You schedule a new scene- Scenes %q<schedid>: %q<title> on [u(fancytime,%q<time>,%#)];@wait 1={@attach %!/INC`DOSQL=SET`SCHEDULEBB,u(setr,postid,elements(first(u(wildgrepi,u(game_config,SCENESYS,BOARD),~`*`HDR,#%q<schedid>:*)),2,`)),%q<schedid>}

&INC`SCENES`TAG [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCHED=%0;@stop u(mysql,get`tagcheck,%q<schedule>,%q<pid>)=@attach %!/INC`MSG=You are already signed up for that scene.;@attach %!/INC`DOSQL=INSERT`NEWTAG,%q<pid>,%q<schedule>;@attach %!/INC`MSG=You have tagged Scheduled Scene %0!

&INC`SCENES`UNTAG [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCHED=%0;@check u(mysql,get`tagcheck,%q<schedule>,%q<pid>)=@attach %!/INC`MSG=You are not signed up for that scene.;@attach %!/INC`DOSQL=DELETE`TAG,%q<pid>,%q<schedule>;@attach %!/INC`MSG=You have untagged Scheduled Scene %0!
&Q`INSERT`NEWTAG [u(cobj,scene)]=INSERT INTO $TAGS$ (player_id,schedule_id) VALUES (?,?)
&Q`DELETE`TAG [u(cobj,scene)]=DELETE FROM $TAGS$ WHERE player_id=? AND schedule_id=?
&Q`GET`TAGCHECK [u(cobj,scene)]=SELECT tag_id from $TAGS$ WHERE schedule_id=? AND player_id=?

&Q`INSERT`NEWSCHEDULE [u(cobj,scene)]=INSERT INTO $SCHEDULE$ (player_id,schedule_title,schedule_desc,schedule_date) VALUES (?,?,?,FROM_UNIXTIME('?'))
&Q`GET`NEWSCHEDULE [u(cobj,scene)]=SELECT max(schedule_id) FROM $SCHEDULE$ WHERE player_id=?
&Q`SET`SCHEDULEBB [u(cobj,scene)]=UPDATE $SCHEDULE$ SET post_id=? WHERE schedule_id=?

&FUN`FORMATPOST [u(cobj,scene)]=ansi(h,Title:) %0%R[ansi(h,Posted by:)] %1%R[ansi(h,Scheduled Time:)] %2%R[repeat(-,78)]%R%3

&INC`CHECKTIME [u(cobj,scene)]=@check strlen(%0)=@attach %!/INC`MSG=You didn't enter a date!;@check gt(u(setr,time,convtime(%0,u(gettz,%#))),0)=@attach %!/INC`MSG=The entered date was not recognized. Did you typo? Dates should be in abbreviated 24-hour <month> <day> <hour>:<minute> format using YOUR timezone (@tz)\, such as Jun 26 7:00 or Oct 31 13:00.;@select/inline t(%1)=0,{@check gt(%q<time>,secs())=@attach %!/INC`MSG=That would be in the past!}

&INC`SCENES`MAIN [u(cobj,scene)]=@select/inline gt(strlen(%0),0)=1,{@attach %!/INC`SCENES`DETAILS},0,{@attach %!/INC`SCENES`LIST}

&INC`SCENES`MINE [u(cobj,scene)]=@attach %!/INC`SCENES`LIST=1

&INC`SCENES`LIST [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@pemit %#=u(HEADER,if(%0,Your%b)Current Schedule - [u(choosegame,timefmt($Z,,u(gettz,%#)),ptimefmt($Z,,u(gettz,%#)))]);@dolist/inline if(%0,u(filter,SCENESMINE,u(mysql,GET`UPCOMING),%b,%b,alts(%#)),u(mysql,GET`UPCOMING))={th u(setq,time,u(mysql,get`scheduletime,##));@select/inline not(strmatch(%q<last>,u(setr,last,u(choosegame,timefmt($a $b $d $Y $Z,%q<time>,u(gettz,%#)),ptimefmt($a $b $d $Y $Z,%q<time>,u(gettz,%#))))))=1,{@pemit %#=u(HEADER,%q<last>)};@pemit %#=u(FUN`FORMATSCHEDULE2,%q<pid>,##,u(mysql,get`scheduletitle,##),u(mysql,get`scheduleplayername,##),%q<time>,u(mysql,get`schedownerid,##))};@pemit %#=u(SUBHEADER)

&FIL`SCENESMINE [u(cobj,scene)]=cor(t(words(setinter(u(mysql,get`SCHEDOWNEROBJ,%0),%1))),t(words(setinter(u(mysql,get`tagobjids,%0),%1))))

&FUN`FORMATSCHEDULE2 [u(cobj,scene)]=align(3 4 40 20 7,ljust(switch(1,eq(%0,%5),ansi(hg,*),t(words(setinter(u(mysql,get`playerobjid,%5),alts(u(objid,%#))))),ansi(hg,*),t(u(mysql,get`tagcheck,%1,%0)),ansi(hr,*),t(words(setinter(u(mysql,get`tagobjids,%1),alts(u(objid,%#))))),ansi(c,*)),1)[u(mysql,get`tagcount,%1)],u(pueblize,%1,+schedule %1),%2,%3,elements(u(fancytime,%4,%#),3))

&Q`GET`TAGCOUNT [u(cobj,scene)]=SELECT count(tag_id) FROM $TAGS$ WHERE schedule_id=?
&Q`GET`TAGOBJIDS [u(cobj,scene)]=SELECT DISTINCT objid from $TAGS$ NATURAL LEFT JOIN $PLAYERS$ WHERE schedule_id=?

&Q`GET`UPCOMING [u(cobj,scene)]=SELECT schedule_id FROM $SCHEDULE$ WHERE schedule_date >= UTC_TIMESTAMP() - INTERVAL 10 HOUR ORDER by schedule_date

&Q`GET`FULLSCHEDULE [u(cobj,scene)]=SELECT schedule_id,schedule_title,player_name,UNIX_TIMESTAMP(schedule_date) FROM $SCHEDULE$ NATURAL LEFT JOIN $PLAYERS$ WHERE schedule_date >= UTC_TIMESTAMP() - INTERVAL 10 HOUR ORDER BY schedule_date

&FUN`FORMATSCHEDULE [u(cobj,scene)]=if(not(strmatch(%q<last>,u(setr,last,u(choosegame,timefmt($a $b $d $Y $Z,%4,u(gettz,%q<target>)),ptimefmt($a $b $d $Y $Z,%4,u(gettz,%q<target>)))))),pemit(%q<target>,u(HEADER,%q<last>)))[pemit(%q<target>,align(4 43 20 7,u(pueblize,%1,+schedule %1),%2,%3,elements(u(fancytime,%4,%q<target>),3)))]

&INC`VALSCHED [u(cobj,scene)]=@check strlen(%0)=@attach %!/INC`MSG=Schedule ID field empty.;@check u(valnum,%0)=@attach %!/INC`MSG=Schedule scenes must be addressed by their IDs.;@check gt(u(setr,schedule,u(mysql,get`schedulecheck,%0)),0)=@attach %!/INC`MSG=Scheduled scene '%0' could not be found.;th u(setq,bb,u(game_config,SCENESYS,BOARD));th u(setq,post,u(mysql,get`postid,%q<schedule>))
&Q`GET`SCHEDULECHECK [u(cobj,scene)]=SELECT schedule_id FROM $SCHEDULE$ WHERE schedule_id=?
&Q`GET`POSTID [u(cobj,scene)]=SELECT post_id FROM $SCHEDULE$ WHERE schedule_id=?

&INC`OWNSCHED [u(cobj,scene)]=@check cor(u(isadmin,%#),eq(u(mysql,get`schedownerid,%0),%1),if(%2,eq(u(mysql,get`plotrunner,%2),%0)))=@attach %!/INC`MSG=Permission denied.
&Q`GET`SCHEDOWNERID [u(cobj,scene)]=SELECT player_id from $SCHEDULE$ WHERE schedule_id=?
&Q`GET`SCHEDOWNEROBJID [u(cobj,scene)]=SELECT player_id from $SCHEDULE$ NATURAL LEFT JOIN $PLAYERS$ WHERE schedule_id=?

&INC`SCENES`DETAILS [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCHED=%0;@pemit %#=u(HEADER,Scheduled ID %q<schedule>: [u(mysql,get`scheduletitle,%q<schedule>)]);@pemit %#=ansi(h,Owner:) [u(mysql,get`scheduleplayername,%q<schedule>)]%R[u(mysql,get`scheduledesc,%q<schedule>)];@pemit %#=u(SEPARATOR,Scheduled Time);@pemit %#=align(-38 -38,u(choosegame,timefmt(u(setr,fmt,$a $b $d $I:$M$p $Z),u(setr,time,u(mysql,get`scheduletime,%q<schedule>)),UTC),ptimefmt(u(setr,fmt,$a $b $d $I:$M$p $Z),u(setr,time,u(mysql,get`scheduletime,%q<schedule>)),UTC)),u(choosegame,timefmt(%q<fmt>,%q<time>,u(gettz,%#)),ptimefmt(%q<fmt>,%q<time>,u(gettz,%#))));@select/inline cand(words(u(setr,interest,u(mysql,get`interest,%q<schedule>))),cor(u(isadmin,%#),eq(u(mysql,get`schedownerid,%q<schedule>),%q<pid>)))=1,{@pemit %#=u(SEPARATOR,Interested Players);@pemit %#=u(table,iter(%q<interest>,u(moniker,%i0),%b,|),30,u(width,%#),|)};@pemit %#=u(SUBHEADER)

&FUN`INTERESTPLAY [u(cobj,scene)]=nspemit(%#,if(u(isobjid,%2),%1,%1 (deleted)))
&Q`GET`INTEREST [u(cobj,scene)]=SELECT objid from $TAGS$ NATURAL LEFT JOIN $PLAYERS$ WHERE schedule_id=?

&Q`GET`SCHEDULETITLE [u(cobj,scene)]=SELECT schedule_title FROM $SCHEDULE$ WHERE schedule_id=?
&Q`GET`SCHEDULEDESC [u(cobj,scene)]=SELECT schedule_desc FROM $SCHEDULE$ WHERE schedule_id=?
&Q`GET`SCHEDULEPLAYERNAME [u(cobj,scene)]=SELECT player_name FROM $SCHEDULE$ NATURAL LEFT JOIN $PLAYERS$ WHERE schedule_id=?
&Q`GET`SCHEDULETIME [u(cobj,scene)]=SELECT UNIX_TIMESTAMP(schedule_date) FROM $SCHEDULE$ WHERE schedule_id=?

&INC`SCENES`DESC [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCHED=%0;@attach %!/INC`OWNSCHED=%q<schedule>,%q<pid>;@check strlen(%1)=@attach %!/INC`MSG=What will you replace the text with?;@attach %!/INC`DOSQL=SET`SCHEDULEDESC,trim(trim(%1,%r),%t),%q<schedule>;th u(setr,desc,u(mysql,get`scheduledata,%q<schedule>));&%q<post> %q<bb>=u(FUN`FORMATPOST,u(mysql,get`scheduletitle,%q<schedule>),[u(moniker,%#)],u(fancytime,u(mysql,get`scheduletime,%q<schedule>),u(scene)),%1);@attach %!/INC`MSG=The schedule description was modified!

&Q`SET`SCHEDULEDESC [u(cobj,scene)]=UPDATE $SCHEDULE$ SET schedule_desc=? WHERE schedule_id=?

&INC`SCENES`TITLE [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCHED=%0;@attach %!/INC`OWNSCHED=%q<schedule>,%q<pid>;@check strlen(%1)=@attach %!/INC`MSG=What will you replace the text with?;@stop u(charsearch,%1,%r %t)=@attach %!/INC`MSG=The following characters are not permitted in titles: \%r, \%t.;@attach %!/INC`DOSQL=SET`SCHEDULETITLE,u(setr,title,trim(trim(%1,%r),%t)),%q<schedule>;&%q<post> %q<bb>=u(FUN`FORMATPOST,%1,[u(moniker,%#)],u(fancytime,u(mysql,get`scheduletime,%q<schedule>),u(scene)),u(mysql,get`scheduledesc,%q<schedule>));&%q<post>`HDR %q<bb>=#%q<schedule>: %q<title>;@attach %!/INC`MSG=The schedule title was modified!

&Q`SET`SCHEDULETITLE [u(cobj,scene)]=UPDATE $SCHEDULE$ SET schedule_title=? WHERE schedule_id=?

&INC`SCENES`PLOT [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCHED=%0;@check strlen(%1)=@attach %!/INC`MSG=No plot entered to link!;@check cand(isint(%1),gte(%1,0))=@attach %!/INC`MSG=Plots must be addressed by IDs. Use 0 to set No Plot.;@select/inline gte(%1,0)=1,{@attach %!/INC`VALPLOT=%1};@attach %!/INC`OWNSCHED=%q<schedule>,%q<pid>,%1;@attach %!/INC`DOSQL=SET`SCHEDULEPLOT,%1,%q<schedule>;@attach %!/INC`MSGSCENE=%q<scene>,[u(moniker,%#)] changed Schedule'd Scene %q<schedule>'s Plot to: %1 - [u(mysql,get`plottitle,%1)]

&Q`SET`SCHEDULEPLOT [u(cobj,scene)]=UPDATE $SCHEDULE$ SET plot_id=? WHERE schedule_id=?

&INC`SCENES`RESCHEDULE [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCHED=%0;@attach %!/INC`OWNSCHED=%q<schedule>,%q<pid>;@attach %!/INC`CHECKTIME=%1;@attach %!/INC`DOSQL=SET`SCHEDULEDATE,%q<time>,%q<schedule>;&%q<post> %q<bb>=u(FUN`FORMATPOST,u(mysql,get`scheduletitle,%q<schedule>),[u(moniker,%#)],u(fancytime,u(mysql,get`scheduletime,%q<schedule>),u(scene)),u(mysql,get`scheduledesc,%q<schedule>));@attach %!/INC`MSG=The schedule date was modified!
&Q`SET`SCHEDULEDATE [u(cobj,scene)]=UPDATE $SCHEDULE$ SET schedule_date=FROM_UNIXTIME(?) WHERE schedule_id=?

&INC`SCENES`MAIL [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCHED=%0;@attach %!/INC`OWNSCHED=%q<schedule>,%q<pid>;@check words(u(setr,mailto,u(filter,ISOBJID,setunion(u(mysql,get`taggedobj,%q<schedule>),setunion(u(mysql,get`schedownerobj,%q<schedule>),u(objid,%#))))))=@attach %!/INC`MSG=No valid recipients to mail to.;@check strlen(%1)=@attach %!/INC`MSG=ERROR: No message text.;@force/inplace %#={@mail %q<mailto>=%1}
&Q`GET`TAGGEDOBJ [u(cobj,scene)]=SELECT DISTINCT players.objid FROM $TAGS$ AS tags LEFT JOIN $PLAYERS$ as players ON tags.player_id=players.player_id WHERE schedule_id=?
&Q`GET`SCHEDOWNEROBJ [u(cobj,scene)]=SELECT objid from $SCHEDULE$ NATURAL LEFT JOIN $PLAYERS$ WHERE schedule_id=?

&INC`SCENES`SUMMON [u(cobj,scene)]=@check isdbref(u(cobj,grid))=@attach %!/INC`MSG=ERROR: Grid and Navigation System not installed. Cannot +summon.;@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCHED=%0;@attach %!/INC`OWNSCHED=%q<schedule>,%q<pid>;@check words(u(setr,invites,u(filter,u(setr,tagged,u(filter,ISOBJID,u(mysql,get`taggedobj,%q<schedule>))),%b,%b,%l)))=@attach %!/INC`MSG=No valid recipients to invite or they are already here.;@dolist/inline %q<invites>={@force %#=+summon ##}

&FIL`NOTHERE [u(cobj,scene)]=not(strmatch(loc(%0),%1))

&INC`SCENES`DELETE [u(cobj,scene)]=@attach %!/INC`USERCHECK=u(objid,%#);@attach %!/INC`VALSCHED=%0;@attach %!/INC`OWNSCHED=%q<schedule>,%q<pid>;@attach %!/INC`VERIFY={[ansi(hr,WARNING:)] This will remove Schedule Scene %q<schedule>. Are you sure? Enter the same command in ten seconds to verify!},DELETE %q<schedule>;@attach %!/INC`DOSQL=DEL`SCHEDULE,%q<schedule>;@attach %!/INC`MSG=The scheduled scene was deleted!;@select/inline strlen(%q<post>)=>0,{@attach %!/WIPE=%q<bb>,~`%q<post>}
&Q`DEL`SCHEDULE [u(cobj,scene)]=DELETE FROM $SCHEDULE$ WHERE schedule_id=?

&INC`PLOT`MAIN [u(cobj,scene)]=@select/inline gt(strlen(%0),0)=1,{@attach %!/INC`PLOT`DETAILS},0,{@attach %!/INC`PLOT`LIST}

&INC`PLOT`ALL [u(cobj,scene)]=@attach %!/INC`PLOT`LIST=%0,1

&INC`PLOT`LIST [u(cobj,scene)]=@pemit %#=u(HEADER,if(%1,All,Current) Plots);@pemit %#=align(4 25 20 20 6,ID,Title,Runner,Schedule,Type);th u(setq,target,%#);@select/inline v(game)=PennMUSH,{th mapsql(FUN`FORMATPLOT,u(SQL`FORMAT,if(%1,get`allplots,get`runningplots)))},RhostMUSH,{@dolist/inline/delimit | [u(mysql2,if(%1,get`allplots,get`runningplots))]={th u(FUN`FORMATPLOT,,elements(%d0,1,^),elements(%d0,2,^),elements(%d0,3,^),elements(%d0,4,^),elements(%d0,5,^),elements(%d0,6,^))}};@pemit %#=u(SUBHEADER)

&Q`GET`ALLPLOTS [u(cobj,scene)]=SELECT plot_id,plot_title,player_name,UNIX_TIMESTAMP(start_date),UNIX_TIMESTAMP(end_date),plot_type FROM $PLOTS$ NATURAL LEFT JOIN $PLAYERS$

&Q`GET`RUNNINGPLOTS [u(cobj,scene)]=SELECT plot_id,plot_title,player_name,UNIX_TIMESTAMP(start_date),UNIX_TIMESTAMP(end_date),plot_type FROM $PLOTS$ NATURAL LEFT JOIN $PLAYERS$ WHERE end_date > NOW()

&FUN`FORMATPLOT [u(cobj,scene)]=pemit(%q<target>,align(4 25 20 20 6,u(pueblize,%1,+plot %1),%2,%3,elements(u(fancytime,%4,%q<target>),1 2) - [elements(u(fancytime,%5,%q<target>),1 2)],%6))

&INC`VALPLOT [u(cobj,scene)]=@check strlen(%0)=@attach %!/INC`MSG=Plot ID field empty.;@check u(valnum,%0)=@attach %!/INC`MSG=Plots must be addressed by their IDs.;@check gt(u(setr,plot,u(mysql,get`plotcheck,%0)),0)=@attach %!/INC`MSG=Plot '%0' could not be found.
&Q`GET`PLOTCHECK [u(cobj,scene)]=SELECT plot_id FROM $PLOTS$ WHERE plot_id=?

&INC`PLOT`DETAILS [u(cobj,scene)]=@attach %!/INC`VALPLOT=%0;@pemit %#=u(HEADER,Plot ID %q<plot>: [u(mysql,get`plottitle,%q<plot>)]);@pemit %#=ansi(h,Runner:) [u(mysql,get`plotrunnername,%q<plot>)]%R[ansi(h,Schedule:)] [u(fancytime,u(mysql,get`plotstart,%q<plot>),%#)] to [u(fancytime,u(mysql,get`plotend,%q<plot>),%#)]%R[ansi(h,Type:)] [u(mysql,get`plottype,%q<plot>)]%R[ansi(h,Status:)] [u(mysql,get`plotstatus,%q<plot>)]%R[u(mysql,get`plotdesc,%q<plot>)];@pemit %#=u(HEADER)
&Q`GET`PLOTRUNNER [u(cobj,scene)]=SELECT player_id FROM $PLOTS$ WHERE plot_id=?
&Q`GET`PLOTRUNNERNAME [u(cobj,scene)]=SELECT player_name FROM $PLOTS$ NATURAL LEFT JOIN $PLAYERS$ WHERE plot_id=?
&Q`GET`PLOTTITLE [u(cobj,scene)]=SELECT plot_title FROM $PLOTS$ WHERE plot_id=?
&Q`GET`PLOTSTART [u(cobj,scene)]=SELECT UNIX_TIMESTAMP(start_date) FROM $PLOTS$ WHERE plot_id=?
&Q`GET`PLOTEND [u(cobj,scene)]=SELECT UNIX_TIMESTAMP(end_date) FROM $PLOTS$ WHERE plot_id=?
&Q`GET`PLOTTYPE [u(cobj,scene)]=SELECT plot_type FROM $PLOTS$ WHERE plot_id=?
&Q`GET`PLOTSTATUS [u(cobj,scene)]=SELECT plot_status FROM $PLOTS$ WHERE plot_id=?
&Q`GET`PLOTDESC [u(cobj,scene)]=SELECT plot_desc FROM $PLOTS$ WHERE plot_id=?

&INC`PLOT`ADD [u(cobj,scene)]=@attach %!/INC`CHECKPC=before(%0,/),1;@check gt(u(setr,pid,u(mysql,get`playerid,%q<t1objid>)),0)=@attach %!/INC`MSG=%q<t1name> is not setup for SceneSys!;@check strlen(after(%0,/))=@attach %!/INC`MSG=The Plot needs a title!;@check strlen(%1)=@attach %!/INC`MSG=The Plot needs a description!;@attach %!/INC`DOSQL=NEW`PLOT,%q<pid>,after(%0,/),%1;th u(setq,plot,u(mysql,get`newplot,%q<pid>));@attach %!/INC`MSG=Plot added!

&Q`NEW`PLOT [u(cobj,scene)]=INSERT INTO $PLOTS$ (player_id,plot_title,plot_desc,start_date,end_date,plot_status) VALUES (?,?,?,UTC_TIMESTAMP(),UTC_TIMESTAMP() + INTERVAL 1 MONTH,'Approved')
&Q`GET`NEWPLOT [u(cobj,scene)]=SELECT plot_id FROM $PLOTS$ WHERE player_id=? ORDER BY plot_id LIMIT 1

&INC`PLOT`TITLE [u(cobj,scene)]=@attach %!/INC`VALPLOT=%0;@check strlen(%1)=@attach %!/INC`MSG=The new title field is empty!;@attach %!/INC`DOSQL=SET`PLOTTITLE,%1,%q<plot>;@attach %!/INC`MSG=The Plot title was changed.
&INC`PLOT`DESC [u(cobj,scene)]=@attach %!/INC`VALPLOT=%0;@check strlen(%1)=@attach %!/INC`MSG=The new description field is empty!;@attach %!/INC`DOSQL=SET`PLOTDESC,%1,%q<plot>;@attach %!/INC`MSG=The Plot description was changed.
&Q`SET`PLOTTITLE [u(cobj,scene)]=UPDATE $PLOTS$ SET plot_title=? WHERE plot_id=?
&Q`SET`PLOTDESC [u(cobj,scene)]=UPDATE $PLOTS$ SET plot_desc=? WHERE plot_id=?

&INC`PLOT`START [u(cobj,scene)]=@attach %!/INC`VALPLOT=%0;@attach %!/INC`CHECKTIME=%1,1;@attach %!/INC`DOSQL=SET`PLOTSTART,%q<time>,%q<plot>;@attach %!/INC`MSG=The plot's Start date was set to [u(fancytime,%q<time>,%#)]
&INC`PLOT`END [u(cobj,scene)]=@attach %!/INC`VALPLOT=%0;@attach %!/INC`CHECKTIME=%1,1;@attach %!/INC`DOSQL=SET`PLOTEND,%q<time>,%q<plot>;@attach %!/INC`MSG=The plot's End date was set to [u(fancytime,%q<time>,%#)]
&Q`SET`PLOTSTART [u(cobj,scene)]=UPDATE $PLOTS$ SET start_date=FROM_UNIXTIME(?) WHERE plot_id=?
&Q`SET`PLOTEND [u(cobj,scene)]=UPDATE $PLOTS$ SET end_date=FROM_UNIXTIME(?) WHERE plot_id=?

&INC`PLOT`TYPE [u(cobj,scene)]=@attach %!/INC`VALPLOT=%0;@check strlen(%1)=@attach %!/INC`MSG=You must include a status! Your choices are Minor, Major, and Global.;@attach %!/INC`PARTIAL=%1,MINOR|MAJOR|GLOBAL,|,Status,Status;@attach %!/INC`DOSQL=SET`PLOTTYPE,capnames(%q<status>),%q<plot>;@attach %!/INC`MSG=The Status was changed to [capnames(%q<status>)].
&Q`SET`PLOTTYPE [u(cobj,scene)]=UPDATE $PLOTS$ SET plot_type=? WHERE plot_id=?

&INC`PLOT`RUNNER [u(cobj,scene)]=@attach %!/INC`VALPLOT=%0;@attach %!/INC`CHECKPC=%1,1;@check gt(u(setr,pid,u(mysql,get`playerid,%q<t1objid>)),0)=@attach %!/INC`MSG=%q<t1name> is not setup for SceneSys!;@attach %!/INC`DOSQL=SET`PLOTRUNNER,%q<pid>,%q<plot>
&Q`SET`PLOTRUNNER [u(cobj,scene)]=UPDATE $PLOTS$ SET player_id=? WHERE plot_id=?

&INC`PLOT`STATUS [u(cobj,scene)]=@attach %!/INC`VALPLOT=%0;@check strlen(%1)=@attach %!/INC`MSG=You must include a status! Your choices are Approved, Running, Paused, Finished, and Canceled.;@attach %!/INC`PARTIAL=%1,APPROVED|RUNNING|PAUSED|FINISHED|CANCELED,|,Status,Status;@attach %!/INC`DOSQL=SET`PLOTSTATUS,capnames(%q<status>),%q<plot>;@attach %!/INC`MSG=The Status was changed to [capnames(%q<status>)].
&Q`SET`PLOTSTATUS [u(cobj,scene)]=UPDATE $PLOTS$ SET plot_status=? WHERE plot_id=?

&INC`PLOT`DELETE [u(cobj,scene)]=@attach %!/INC`VALPLOT=%0;@attach %!/INC`VERIFY={ansi(hr,WARNING:) This will DELETE Plot %q<plot>. Are you SURE? enter the same command again within ten seconds to verify!},DELETE PLOT %q<plot>;@attach %!/INC`DOSQL=DELETE`PLOT,%q<plot>;@attach %!/INC`MSG=Plot deleted!
&Q`DELETE`PLOT [u(cobj,scene)]=DELETE FROM $PLOTS$ WHERE plot_id=?

&Q`INSTALL`PLAYERS [u(cobj,scene)]=CREATE TABLE IF NOT EXISTS $PLAYERS$ (player_id BIGINT(31) UNSIGNED NOT NULL AUTO_INCREMENT,objid VARCHAR(30) NOT NULL,player_name VARCHAR(60) NOT NULL DEFAULT 'Unset',PRIMARY KEY(player_id),UNIQUE KEY player_objid (objid)) ENGINE=InnoDB DEFAULT CHARSET=latin1 AUTO_INCREMENT=1

&Q`INSTALL`PLOTS [u(cobj,scene)]=CREATE TABLE IF NOT EXISTS $PLOTS$ (plot_id BIGINT(31) UNSIGNED NOT NULL AUTO_INCREMENT,player_id BIGINT(31) UNSIGNED NOT NULL,plot_title VARCHAR(40),plot_desc text,start_date DATETIME,end_date DATETIME,plot_type VARCHAR(20),plot_status VARCHAR(20),PRIMARY KEY(plot_id),FOREIGN KEY(player_id) REFERENCES $PLAYERS$(player_id) ON UPDATE CASCADE) ENGINE=InnoDB DEFAULT CHARSET=latin1 AUTO_INCREMENT=1

&Q`INSTALL`SCENES [u(cobj,scene)]=CREATE TABLE IF NOT EXISTS $SCENES$ (scene_id BIGINT(31) UNSIGNED NOT NULL AUTO_INCREMENT,player_id BIGINT(31) UNSIGNED,scene_title VARCHAR(255),plot_id BIGINT(31),scene_desc text,scene_state enum('0','1','2','3') NOT NULL DEFAULT '0',room_objid VARCHAR(80),room_name VARCHAR(80),room_custloc VARCHAR(80),creation_date DATETIME,finish_date DATETIME,PRIMARY KEY(scene_id),FOREIGN KEY(player_id) REFERENCES $PLAYERS$(player_id) ON UPDATE CASCADE) ENGINE=InnoDB DEFAULT CHARSET=latin1 AUTO_INCREMENT=1

&Q`INSTALL`POSES [u(cobj,scene)]=CREATE TABLE IF NOT EXISTS $POSES$ (pose_id BIGINT(31) UNSIGNED NOT NULL AUTO_INCREMENT,player_id BIGINT(31) UNSIGNED NOT NULL,pose text,scene_id BIGINT(31) UNSIGNED,pose_time DATETIME,pose_ignore BOOL NOT NULL DEFAULT FALSE,PRIMARY KEY(pose_id),FOREIGN KEY(scene_id) REFERENCES $SCENES$(scene_id) ON UPDATE CASCADE ON DELETE CASCADE,FOREIGN KEY(player_id) REFERENCES $PLAYERS$(player_id) ON UPDATE CASCADE) ENGINE=InnoDB DEFAULT CHARSET=latin1 AUTO_INCREMENT=1

&Q`INSTALL`SCHEDULE [u(cobj,scene)]=CREATE TABLE IF NOT EXISTS $SCHEDULE$ (schedule_id BIGINT(31) UNSIGNED NOT NULL AUTO_INCREMENT,player_id BIGINT(31) UNSIGNED NOT NULL,schedule_title VARCHAR(255),schedule_desc text,schedule_date DATETIME,post_id varchar(10),plot_id BIGINT(31),PRIMARY KEY(schedule_id),FOREIGN KEY(player_id) REFERENCES $PLAYERS$(player_id) ON UPDATE CASCADE) ENGINE=InnoDB DEFAULT CHARSET=latin1 AUTO_INCREMENT=1

&Q`INSTALL`TAGS [u(cobj,scene)]=CREATE TABLE IF NOT EXISTS $TAGS$ (tag_id BIGINT(31) UNSIGNED NOT NULL AUTO_INCREMENT,schedule_id BIGINT(31) UNSIGNED NOT NULL,player_id BIGINT(31) UNSIGNED NOT NULL,PRIMARY KEY(tag_id),FOREIGN KEY(player_id) REFERENCES $PLAYERS$(player_id) ON UPDATE CASCADE ON DELETE CASCADE,FOREIGN KEY(schedule_id) REFERENCES $SCHEDULE$(schedule_id) ON UPDATE CASCADE ON DELETE CASCADE) ENGINE=InnoDB DEFAULT CHARSET=latin1 AUTO_INCREMENT=1

&SQL`TABLES [u(cobj,scene)]=PLAYERS|PLOTS|SCENES|POSES|SCHEDULE|TAGS
&SQL`TABLE`SCENES [u(cobj,scene)]=scene_scenes
&SQL`TABLE`PLAYERS [u(cobj,scene)]=scene_players
&SQL`TABLE`POSES [u(cobj,scene)]=scene_poses
&SQL`TABLE`SCHEDULE [u(cobj,scene)]=scene_schedule
&SQL`TABLE`PLOTS [u(cobj,scene)]=scene_plots
&SQL`TABLE`TAGS [u(cobj,scene)]=scene_tags

@trigger [u(cobj,scene)]/TRG`INSTALL

+help/add +scene=[u(cobj,scene)]/HLP`+SCENE
+help/category +scene=Roleplaying
+help/metatags +scene=logging scenesys
&HLP`+SCENE [u(cobj,scene)]=SceneSys is a scene scheduling, tracking, and logging tool that works alongside [mudname()]'s Wiki. The original version was designed by Mercutio @ M*U*S*H and some of his code remains - credits are due.%R%R[ansi(hc,Setting up)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+scene/setup)] - Get your own unique Char_id.%R[ansi(h,+scene/charid)] - Show your own unique Char_id.%RChar_id is used by the wiki log viewer and must be on your character's wiki page.)]%R%R[ansi(hc,Running/Logging Commands)]%RThese commands are for anyone who wants to start and manage a scene.%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+scene/create <title text>)] - Begins a new scene.%R[ansi(h,+scene/loudcreate <title text>)] - Like /create but announces it on the RP channel.%R[ansi(h,+scene/title <id>=<text>)] - Changes a scene's title.%R[ansi(h,+scene/desc <id>=<text>)] - Gives a scene a description/short summary.%R[ansi(h,+scene/plot <id>=<plot id>)] - Links a scene to a plot\, if you own the scene or run the plot. Set plot to 0 to remove it from any plots.%R[ansi(h,+scene/finish \[<id>\])] - Finishes a scene. ID will be current scene if not specified.%R[ansi(h,+scene/pause \[<id>\])] - Pauses a scene. ID will be current scene if not specified.%R[ansi(h,+scene/continue \[<id>\])] - Resumes a scene that was paused or finished. ID will be current scene if not specified.%R[ansi(h,+scene/move <id>)] - Relocate a scene to your current location\, including virtual room locations.)]%R%R[ansi(hc,Participant Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+scene/join \[<id>\])] - Joins a scene. Your poses will then be recorded. ID is unneeded if the scene just began in your current location.%R[ansi(h,+scene/leave)] - Stops your poses from being logged.%R[ansi(h,+scene/undo)] - Marks your last post to be ignored by the logger so it won't make it to the wiki. Used to fix poses made in error\, typos\, etc.%R[ansi(h,+scene/recall <id>=<# of lines>)] - Displays previous recorded poses from a scene.%R[ansi(h,+scene/spoof <altname>)] - Record poses as one of your alts. The character must be on your same account and must be setup for scenesys. This will NOT respect hidden alts settings and will NOT be mirrored properly in +pot which will still see you as you. All recorded poses will be as the given alt. use this command with no arguments to clear spoof settings.%RTo be recorded\, you must be joined to a scene and A\) Your pose must be in the location the scene is set to\, B\) Your pose must be at least ten words long. C\) The scene must be not be paused or finished.)]%R%R[ansi(hc,Listing Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+scene)] - Displays all scenes you are currently participating in or running that aren't finished.%R[ansi(h,+scene/old)] - Show all of the finished scenes you've been in or run.%R[ansi(h,+scene <id>)] - Show details of a particular scene.%R[ansi(h,+scene/who \[<id>\])] - Quick listing of who has this scene as their active scene.)]

+shelp/add +scene=[u(cobj,scene)]/SHLP`+SCENE
+shelp/category +Scene=Roleplaying
&SHLP`+SCENE [u(cobj,scene)]=

+help/add +schedule=[u(cobj,scene)]/HLP`+SCHEDULE
+help/category +schedule=Roleplaying
+help/metatags +schedule=+schedule calendar events
&HLP`+SCHEDULE [u(cobj,scene)]=SceneSys is a scene scheduling, tracking, and logging tool that works alongside [mudname()]'s Wiki. The original version was designed by Mercutio @ M*U*S*H and some of his code remains - credits are due.%R[ansi(hc,See Also:)] [u(pueblize,+help +scene)], [u(pueblize,help convtime())]%R[ansi(hc,Aliases:)] +schedule%R%R[ansi(hc,Scheduling Commands)]%RThese commands are for anyone who wants to schedule and manage a scene.%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+schedule/add <time>/<title>=<description>)] - Schedules a scene. <time> must be in a format accepted by the convtime() function\, and is from the perspective of your +tz setting. As an example\, now would be: [u(choosegame,timefmt($b $d $H:$M,,u(gettz,%#)),ptimefmt($b $d $H:$M,,u(gettz,%#)))]. It will default to the current year if none is provided.%R[ansi(h,+schedule/reschedule <id>=<time>)] - Changes a scene's scheduled time. <time> is same as adding.%R[ansi(h,+schedule/delete <id>)] - Remove a scheduled scene.%R[ansi(h,+schedule/title <id>=<text>)] - Changes a Scheduled scene's title.%R[ansi(h,+schedule/desc <id>=<text>)] - Changes a scheduled scene's description/short summary.%R[ansi(h,+schedule/plot <id>=<plot id>)] - Links a scene to a plot. Set plot to 0 to remove it from any plots.%R[ansi(h,+schedule/mail <id>=<message>)] - @mails all who have tagged a scene.%R[ansi(h,+schedule/invite <id>)] - Automatically sends out summon requests to taggers.%R%RAll changes made to the +schedule data will be reflected in the announcement post automatically. Deleting the scene will also delete the respective post.)]%R%R[ansi(hc,General Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+schedule)] - Views a list of scheduled scenes. This uses your +tz settings to show them in your own Timezone. A red * means you tagged the scene. If you own the scene\, the displayed number shows number of taggers.%R[ansi(h,+schedule <id>)] - View details about a scheduled Scene. If you're the owner\, it also shows players who've tagged your scene.%R[ansi(h,+schedule/tag <id>)] - 'Tag' a scene to show your interest.%R[ansi(h,+schedule/untag <id>)] - Remove your tag from a scene.)]

+help/add +plot=[u(cobj,scene)]/HLP`+PLOT
+help/category +plot=Roleplaying
&HLP`+PLOT [u(cobj,scene)]=SceneSys is a scene scheduling, tracking, and logging tool that works alongside [mudname()]'s Wiki. The original version was designed by Mercutio @ M*U*S*H and some of his code remains - credits are due.%R[ansi(hc,See Also:)] [u(pueblize,+help +scene,+help +scene)]%R%R[ansi(hc,Commands)]%R[align(5 [sub(u(width,%#),6)],,{[ansi(h,+plot)] - Show all current plots.%R[ansi(h,+plot/all)] - Show all plots in the system.%R[ansi(h,+plot <id>)] - Show a plot's details.})]

+shelp/add +plot=[u(cobj,scene)]/SHLP`+PLOT
+shelp/category +plot=Roleplaying
&SHLP`+PLOT [u(cobj,scene)]=[ansi(hc,Administrator Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+plot/add <runner>/<title>=<description>)] - Creates a new plot.%R[ansi(h,+plot/desc <id>=<description>)] - Change a plot's description.%R[ansi(h,+plot/type <id>=<type>)] - Change a plot's type.%R[ansi(h,+plot/title <id>=<title>)] - Change a plot's title.%R[ansi(h,+plot/runner <id>=<player>)] - Change a plot's runner.%R[ansi(h,+plot/start <id>=<time>)] - Change a plot's start date. Uses the same format as +schedule scheduling.%R[ansi(h,+plot/end <id>=<time>)] - Change a plot's end date.%R[ansi(h,+plot/status <id>=<status>)] - Change a plot's status. This can be anything.%R[ansi(h,+plot/delete <id>)] - Deletes a plot. Do not use except for newly created mistakes! This is not how to finish a plot!%R%RPlots are 'finished' when their end date have passed.)]

@@ POSE ORDER TRACKER - POT
+help/add +pot=[u(cobj,scene)]/HLP`+POT
+help/category +pot=Roleplaying
&HLP`+POT [u(cobj,scene)]=+pot, or [ansi(h,Po)]se [ansi(h,T)]racker, is a system that stores poses within the room for easy retrieval for review. Sometimes, poses are lost in spam or scroll, or a player enters late and needs to see what's going on, or was disconnected when someone else posed, and that's what this system's here to aid with.%R%R[ansi(hc,Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+pot)] - Show either +pot/list or +pot/brief\, depending on /mode. \(See below.\)%R[ansi(h,+pot/list)] - show the last 15 \(or set max\) poses.%R[ansi(h,+pot/list <number>)] - Show a specified amount of poses.%R[ansi(h,+pot/max <number>)] - Set a new default number of poses to show.%R[ansi(h,+pot/brief)] - Show when people last posed and their last +summary made after that pose if they have one.%R[ansi(h,+pot/mode)] - Change the behavior of +pot. By default\, +pot uses +pot/list display style. Change mode and it will use +pot/brief instead.%R%R[ansi(h,+pot/toggle)] - THINGS used as rooms will not support +pot by default. Object owners can use +pot/toggle to enable pose recording.)]

&CONFLICT`SCENESYS [u(cobj,migrate)]=@dolist/inline [u(coi,scene)] [u(coi,pot)]={@halt %i0;@set %i0=NO_COMMAND;@tel %i0=u(cobj,oldbox)}