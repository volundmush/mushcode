@@ DEPENDENCIES - CORE

@switch/inline isdbref(u(pot))=0,{@tel create(Pose Order Tracker <POT>)=config(master_room)}
&pot u(coi)=locate(config(master_room),Pose Order Tracker <POT>,TXxi)
@parent u(pot)=u(coi)
@set u(pot)=WIZARD !NO_COMMAND

&FUN`MAIN u(pot)=null(if(ISROOMIC(%l),setq(type,switch(before(%u,%B),@EMIT,|,SAY,",SEMIPOSE,;,POSE,:))[attrib_set(%l/[setr(potattr,POT`POSES`[nextslot(%l,POT`POSES)])],speak(%#,%q<type>[after(%u,%B)]))][attrib_set(%l/%q<potattr>`BY,%#)][attrib_set(%l/%q<potattr>`WHEN,secs())][iter(filterbool(#lambda/gte(sub(secs(),get(%l/\%0`WHEN)),mul(3600,v(VAR`TIMEOUTHOURS))),lattr(%l/POT`POSES`*)),wipe(%l/%i0))]))

&GFN`ISROOMIC u(pot)=or(gt(get(%0/IC),0),hastype(%0,ROOM))

&CMD`+POT u(pot)=$^\+pot(?\:/(\S+))?(?\: +(.+))?$:@assert or(ISROOMIC(%l),isadmin(%#))=@nspemit %#=This room is not IC!;@assert or(not(strlen(%1)),match(setr(choices,setunion(get(u(pot)/VAR`PLAYFLAGS),if(isadmin(%#),get(u(pot)/VAR`ADMINFLAGS)),|,|)),ucstr(%1),|))=@nspemit %#=announce(JOBS)%BERROR: Invalid switch for +pot! Your choices are: [itemize(%q<choices>,|,and,\,)];@include u(pot)/INC`[strfirstof(%1,MAIN)]
@set u(pot)/CMD`+pot=regexp

&INC`MAIN u(pot)=@assert words(setr(listposes,filterbool(#lambda/lte(sub(secs(),get(%l/POT`POSES`\%0`WHEN)),mul(3600,v(VAR`TIMEOUTHOURS))),sort(iter(if(gt(nattr(%l/POT`POSES`*),100),xattr(%l/POT`POSES`*,sub(nattr(%l/POT`POSES`*),70),100),lattr(%l/POT`POSES`*)),last(%i0,`)),n))))=@nspemit %#=No recent poses tracked in this location.;@nspemit %#=header(Recent Poses);th null(iter(revwords(elements(revwords(%q<listposes>),lnum(1,default(%#/POT`MAX,15)))),[nspemit(%#,ansi(hc,<[name(get(%l/POT`POSES`%i0`BY))]'s [if(and(isadmin(%#),get(get(%l/POT`POSES`%i0`BY)/POT`PRIVACY)),[ansi(hr,PRIVATE)]%B)]Pose from [timestring(sub(secs(),get(%l/POT`POSES`%i0`WHEN)))] Ago>))][if(or(isadmin(%#),not(get(get(%l/POT`POSES`%i0`BY)/POT`PRIVACY))),nspemit(%#,get(%l/POT`POSES`%i0)),nspemit(%#,<Pose hidden by privacy settings>))]));@nspemit %#=header(End of Poses)

&INC`BRIEF u(pot)=@assert words(setr(listposes,filterbool(#lambda/lte(sub(secs(),get(%l/POT`POSES`\%0`WHEN)),mul(3600,v(VAR`TIMEOUTHOURS))),sort(iter(lattr(%l/POT`POSES`*),last(%i0,`)),n))))=@nspemit %#=No recent poses tracked in this location.;@nspemit %#=[header(Summary of Recent Poses)];th null(iter(revwords(elements(revwords(%q<listposes>),lnum(1,default(%#/POT`MAX,15)))),nspemit(%#,ansi(hc,<[name(get(%l/POT`POSES`%i0`BY))]'s [if(and(isadmin(%#),get(get(%l/POT`POSES`%i0`BY)/POT`PRIVACY)),[ansi(hr,PRIVATE)]%B)]Pose from [timestring(sub(secs(),get(%l/POT`POSES`%i0`WHEN)))] Ago>))[if(or(isadmin(%#),not(get(get(%l/POT`POSES`%i0`BY)/POT`PRIVACY))),nspemit(%#,left(get(%l/POT`POSES`%i0),68)...),nspemit(%#,<Pose hidden by privacy settings>))]));@nspemit %#=header(End of Poses)

&INC`LAST u(pot)=@assert words(setr(listposes,revwords(filterbool(#lambda/lte(sub(secs(),get(%l/POT`POSES`\%0`WHEN)),mul(3600,v(VAR`TIMEOUTHOURS))),sort(iter(lattr(%l/POT`POSES`*),last(%i0,`)),n)))))=@nspemit %#=No recent poses tracked in this location.;@assert strlen(%2)=@nspemit %#=Whose pose do you wish to recall?;@include u(ccs)/INC`CHECKPC=%2,1,POT;@assert strlen(setr(pose,first(filterbool(#lambda/strmatch(%q<t1>,get(%l/POT`POSES`\%0`BY)),%q<listposes>))))=@nspemit %#=%q<t1name> has no recent poses in this location!;@assert or(isadmin(%#),not(get(%q<t1>/POT`PRIVACY)))=@nspemit %#=That player has chosen to keep their poses private.;@nspemit %#=header(name(%q<t1>)'s Pose from [timestring(sub(secs(),get(%l/POT`POSES`%q<pose>`WHEN)))] Ago);@nspemit %#=get(%l/POT`POSES`%q<pose>);@nspemit %#=header(End of Pose)

&INC`PRIVATE u(pot)=@switch/inline 1=or(strmatch(1,%2),strmatch(Yes,%2)),{@nspemit %#=Your poses are now considered private.;th attrib_set(%#/POT`PRIVACY,1)},or(strmatch(0,%2),strmatch(No,%2)),{@nspemit %#=Your poses are now considered public.;th attrib_set(%#/POT`PRIVACY,0)},{@nspemit %#=You must enter Yes or 1 to set your poses private, or No or 0 to set them Public.}

&INC`MAX u(pot)=@assert and(isint(%2),gt(%2,0))=@nspemit %#=You must enter a whole number greater than 0 for your maximum poses!;th attrib_set(%#/POT`MAX,%2);@nspemit %#=You will now see only the last %2 poses in +pot.

&STARTUP u(pot)=@dolist SAY SEMIPOSE POSE @EMIT={@hook/before %i0=u(pot),FUN`MAIN};@attribute/access/retroactive POT=WIZARD MORTAL_DARK;@trigger u(pot)/TRG`SELFCLEAN

&TRG`SELFCLEAN u(pot)=@switch/inline gt(v(VAR`SELFCLEAN),0)=1,{@dolist/inline lsearch(all,eroom,\[nattr(##/POT`POSES`*)\])=@trigger u(pot)/TRG`DOCLEAN=%i0;@dolist/inline lsearch(all,ething,\[nattr(##/POT`POSES`*)\])=@trigger u(pot)/TRG`DOCLEAN=%i0};@wait mul(60,v(var`checkminutes))=@trigger v(pot)/TRG`SELFCLEAN

&TRG`DOCLEAN u(pot)=@dolist/inline filterbool(#lambda/gte(sub(secs(),get(%0/\%0`WHEN)),mul(3600,v(VAR`TIMEOUTHOURS))),lattr(%0/POT`POSES`*))=@wipe %0/%i0

&VAR`SELFCLEAN u(pot)=1
&VAR`PLAYFLAGS u(pot)=BRIEF|PRIVATE|LAST|MAX
&VAR`TIMEOUTHOURS u(pot)=2
&VAR`CHECKMINUTES u(pot)=10