@@ DEPENDENCIES - KLS (Key Lock System)
@@ Without the KLS, the LOCK/UNLOCK flags will NOT function.

@switch/inline isdbref(u(cmo))=0,{@tel create(Channel Management Object <CMO>)=config(master_room)}
&cmo u(coi)=locate(config(master_room),Channel Management Object <CMO>,TXxi)
@parent u(cmo)=u(coi)
@set u(cmo)=WIZARD !NO_COMMAND
&cmo`staffrep u(coi)=**Staff Alerts**
&cmo`public u(coi)=Public
&cmo`roll u(coi)=Dice Log
&cmo`rp u(coi)=RP

@switch/inline isdbref(u(cmo))=1,{@switch/inline isdbref(u(cdb))=0,{@tel create(Channel Database Object <CDB>)=u(cmo)}}
&cdb u(coi)=locate(u(cmo),Channel Database Object <CDB>,TXxi)
@parent u(cdb)=u(coi)
@set u(cmo)=WIZARD !NO_COMMAND

&CMD`+CHANNEL u(cmo)=$^(?s)(?\:\+)?(?\:channel|chan)(?\:/(\S+)?)?(?\: +(.+?))?(?\:/(.*?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(cmo)/VAR`CHANNEL`PLAYFLAGS),if(isadmin(%#),get(u(cmo)/VAR`CHANNEL`ADMINFLAGS)),|,|),|,CHANNEL,switch,switch;@include u(cmo)/INC`CHANNEL`[strfirstof(%q<switch>,MAIN)]=%2,%4,%3
@set u(cmo)/CMD`+CHANNEL=regexp

&VAR`CHANNEL`PLAYFLAGS u(cmo)=CODENAME|RECALL|COLOR|CLONE
&VAR`CHANNEL`ADMINFLAGS u(cmo)=ADD|DELETE|STAFFTAG|RENAME|CONFIG||MOGCONF|INIT

&RFN`MSGHEAD u(cmo)=msghead(v(VAR`MSGHEAD))
&VAR`MSGHEAD u(cmo)=CHANNEL
&RFN`MSGHEAD u(cdb)=msghead(v(VAR`MSGHEAD))
&VAR`MSGHEAD u(cdb)=CHANNEL

th attrib_set(u(cco-db)/VAR`CATEGORIES,setunion(get(u(cco-db)/VAR`CATEGORIES),CHANNEL,|,|))

&INC`CHANNEL`MAIN u(cmo)=@switch/inline strlen(%0)=0,{@nspemit %#=header(mudname() Channel Status);@nspemit %#=align(3 20 9 41,ON?,NAME,USERS,DESCRIPTION);@dolist/inline/delimit | [objeval(%#,channels(|))]={th setq(id,u(u(cmo)/FUN`FINDCHANNEL,%i0));@nspemit %#=align(3 20 9 41,rjust(cstatus(%i0,%#),3),ljust(pueblize(%i0,+channel [stripansi(%i0)]),31),ljust(rjust(words(filtervis(%#,cwho(%i0,on),CHANNEL,%i0)),3,0)/[rjust(words(filtervis(%#,cwho(%i0,all),CHANNEL,%i0)),3,0)],8),cdesc(%i0))};@nspemit %#=header()},{@include u(ccs)/INC`PARTIAL=%0,objeval(%#,channels(|)),|,CHANNEL,channel,channel;@nspemit %#=header(Channel Info for: %q<channel>);@break not(strmatch(parent(setr(mog,cmogrifier(%q<channel>))),u(cdb)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Channel is not initialized. Please use +channel/init %q<channel>;@nspemit %#=OWNER: [name(cowner(%q<channel>))]%RBUFFER: [cbuffer(%q<channel>)] - [cmsgs(%q<channel>)] Messages stored!%RFLAGS: [clflags(%q<channel>)]%RDESC: [cdesc(%q<channel>)];@switch/inline t(get(%q<mog>/CONF`CODENAME))=0,{@nspemit %#=ansi(hg,ONLINE USERS:) [words(filtervis(%#,cwho(%q<channel>,on),CHANNEL,%q<channel>))] - [itemize(iter(filtervis(%#,cwho(%q<channel>,on),CHANNEL,%q<channel>),if(valnum(conn(%i0)),name(%i0),ansi(hx,name(%i0)))[if(strlen(clflags(%q<channel>,%i0)),\([clflags(%q<channel>,%i0)]\))],%b,|),|,and,\,)]%R;@nspemit %#=ansi(hx,OFFLINE USERS:) [words(filtervis(%#,cwho(%q<channel>,off),CHANNEL,%q<channel>))] - [itemize(iter(filtervis(%#,cwho(%q<channel>,off),CHANNEL,%q<channel>),name(%i0)[if(strlen(clflags(%q<channel>,%i0)),\([clflags(%q<channel>,%i0)]\))],%b,|),|,and,\,)]},1,{@nspemit %#=ONLINE USERS (ALIAS): [words(filtervis(%#,cwho(%q<channel>,all),CHANNEL,%q<channel>))] - [itemize(iter(filtervis(%#,cwho(%q<channel>,all),CHANNEL,%q<channel>),strfirstof(get(%i0/D`CHANNEL`%q<mog>`CODENAME),name(%i0))[if(strlen(clflags(%q<channel>,%i0)),\([clflags(%q<channel>,%i0)]\))],%b,|),|,and,\,)];@switch/inline isadmin(%#)=1,{@nspemit %#=subheader(Staff Only Section);@nspemit %#=USERS: [words(cwho(%q<channel>,all))] - [itemize(iter(cwho(%q<channel>,all),if(valnum(conn(%i0)),name(%i0),ansi(hx,name(%i0)))[if(strlen(get(%i0/D`CHANNEL`%q<mog>`CODENAME)),([get(%i0/D`CHANNEL`%q<mog>`CODENAME)]))][if(strlen(clflags(%q<channel>,%i0)),\([clflags(%q<channel>,%i0)]\))],%b,|),|,and,\,)]}};@nspemit %#=header()}

&INC`CHANNEL`RECALL u(cmo)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: No channel entered to recall.;@include u(ccs)/INC`PARTIAL=%0,objeval(%#,channels(|)),|,CHANNEL,channel,channel;@force/inline %#=@chan/recall %q<channel>=sub(secs(),convtime(get(%#/lastlogout)))s

&INC`CHANNEL`COLOR u(cmo)=@force/inline %#=+color/channel %0=%1

&INC`CHANNEL`ADD u(cmo)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Channel name empty!;@assert valid(channel,%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Channel name was not accepted. A channel of that name already exists or the entered name was too long or had unacceptable characters.;@switch/inline t(strlen(%1))=1,{@break strmatch(ansi(%1,test),setr(error,#-*))=@nspemit %#=u(RFN`MSGHEAD) ERROR: '%1' was not accepted by ansi()! Error returned: %q<error>};@channel/add if(strlen(%1),ansi(%1,%0),%0)=v(VAR`CHANNEL`DEFAULTPRIVS);@include u(cmo)/INC`CHANNEL`INIT;@nspemit %#=u(RFN`MSGHEAD) Channel [if(strlen(%1),ansi(%1,%0),%0)] created!;@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=ansi(h,%n) has created the [if(strlen(%1),ansi(%1,%0),%0)] channel!}

&INC`CHANNEL`INIT u(cmo)=@assert match(stripansi(objeval(%#,channels(|))),%0,|)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Channel not found.;th setq(mog,firstof(locate(u(cdb),MOG-[stripansi(%0)],TXxi),create(MOG-[stripansi(%0)])));@set %q<mog>=WIZARD;@tel %q<mog>=u(cdb);@parent %q<mog>=u(cdb);@channel/buffer %0=10;@channel/mogrifier %0=%q<mog>;@switch/inline %q<switch>=INIT,{@nspemit %#=u(RFN`MSGHEAD) Channel initiation of '%0' complete!}

&VAR`CHANNEL`DEFAULTPRIVS u(cmo)=PQ

&INC`CHANNEL`DELETE u(cmo)=@include u(ccs)/INC`PARTIAL=%0,stripansi(objeval(%#,channels(|))),|,CHANNEL,channel,channel;@include u(ccs)/INC`VERIFY={[ansi(hr,WARNING:)] This will delete channel %q<name>. Are you sure? Enter the same command within ten seconds to continue!},CHANNEL DELETE %q<channel>,CHANNEL;@switch/inline cand(not(regmatchi(%q<channel>,^\\d+X)),isdbref(setr(mog,cmogrifier(%q<channel>))))=1,{@set %q<mog>=!SAFE;@nuke %q<mog>;@nuke %q<mog>};@channel/delete %q<channel>;@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=ansi(h,%n) has deleted the %q<channel> channel!}

&INC`CHANNEL`RENAME u(cmo)=@include u(ccs)/INC`PARTIAL=%0,stripansi(objeval(%#,channels(|))),|,CHANNEL,channel,channel;@assert cor(isadmin(%#),objeval(%#,testlock(clock(%q<channel>/Mod),%#)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Permission denied.;@assert strlen(%1)=@nspemit %#=u(RFN`MSGHEAD) ERROR: New Name field Empty.;@assert valid(channel,stripansi(%1),%q<channel>)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Either a channel by that name already exists, or the entered new name wasn't an acceptable channel name.;@channel/name %q<channel>=trim(%1);@nspemit %#=u(RFN`MSGHEAD) You have renamed channel %q<channel> to: [trim(%1)]!;@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=ansi(h,%n) has renamed the %q<name> channel to [trim(%1)]!}

&INC`CHANNEL`CLONE u(cmo)=@include u(ccs)/INC`PARTIAL=%0,stripansi(objeval(%#,setr(channels,channels(|)))),|,CHANNEL,channel,channel;th setq(mog,cmogrifier(%q<channel>));@assert cor(isadmin(%#),elock(%q<mog>/Muzzle,%#))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Permission denied.;th setq(sups,regraballi(stripansi(%q<channels>),v(REG`SUP),|,|));th setq(newnum,add(lmath(max,iter(%q<sups>,before(%i0,X),|,%b)),1));th setq(newname,%q<newnum>X-[elements(%q<channels>,match(%q<channels>,%q<channel>,|),|)]);@channel/add %q<newname>=v(VAR`CHANNEL`DEFAULTPRIVS);@assert match(stripansi(channels(|)),%q<newname>,|)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Channel could not be created. Contact coder.;@channel/buffer %q<newname>=10;@nspemit %#=u(RFN`MSGHEAD) Channel %q<channel> cloned to %q<newname>!;th setq(cwho,cwho(%q<channel>,On,1));@chan/mogrifier %q<newname>=cmogrifier(%q<channel>);@dolist/inline %q<cwho>={@chan/on %q<newname>=%i0};@nscemit/noisy %q<newname>=%q<channel> forked for: [strfirstof(%1,general convenience)]. To leave type [pueblize(@chan/off [stripansi(%q<newname>)])];@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=ansi(h,%n) cloned %q<channel> to %q<newname>.}

&REG`SUP u(cmo)=\d+X\-(.+)

&INC`CHANNEL`CONFIG u(cmo)=@include u(ccs)/INC`PARTIAL=%0,stripansi(objeval(%#,channels(|))),|,CHANNEL,channel,channel;@assert cor(isadmin(%#),objeval(%#,testlock(clock(%q<channel>/Mod),%#)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Permission denied.;@break not(strmatch(parent(setr(mog,cmogrifier(%q<channel>))),u(cdb)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Channel is not initialized. Please use +channel/init %q<channel>;@include u(ccs)/INC`PARTIAL=%2,CODENAME,|,CHANNEL,parameter,parameter;@switch/inline strlen(%1)=0,{&CONF`%q<parameter> %q<mog>;@nspemit %#=u(RFN`MSGHEAD) You cleared Channel %q<channel>'s %q<parameter> Setting.},{@include u(cmo)/INC`CHANNEL`CONFIG`%q<parameter>;&CONF`%q<parameter> %q<mog>=%q<entry>;@nspemit %#=u(RFN`MSGHEAD) You set Channel %q<channel>'s %q<parameter> Setting to: %q<entry>};@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=ansi(h,%n) [if(strlen(%1),set,cleared)] the %q<channel> Channel's %q<parameter> Setting[if(strlen(%1),%bto %1!,.)]}

&INC`CHANNEL`CONFIG`CODENAME u(cmo)=th setq(entry,t(%1))

&INC`CHANNEL`MOGCONF u(cmo)=@include u(ccs)/INC`PARTIAL=%0,stripansi(objeval(%#,channels(|))),|,CHANNEL,channel,channel;@assert cor(isadmin(%#),objeval(%#,testlock(clock(%q<channel>/Mod),%#)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Permission denied.;@break not(strmatch(parent(setr(mog,cmogrifier(%q<channel>))),u(cdb)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Channel is not initialized. Please use +channel/init %q<channel>;@switch/inline gt(strlen(before(%2,/)),0)=0,{@include u(cmo)/INC`CHANNEL`MOGCONF`SHOWCONFIG},1,{@include u(ccs)/INC`PARTIAL=before(%2,/),v(VAR`MOGTYPES),|,CHANNEL,mogrifier,mogrifier;@switch/inline gt(strlen(after(%2,/)),0)=1,{@include u(cmo)/INC`CHANNEL`MOGCONF`SUBCONFIG},0,{@include u(cmo)/INC`CHANNEL`MOGCONF`MAINCONFIG}}

&INC`CHANNEL`MOGCONF`MAINCONFIG u(cmo)=@switch/inline strlen(%1)=0,{@include u(ccs)/INC`VERIFY={ansi(hr,WARNING:) This will clear the Channel %q<channel> %q<mogrifier> Mogrifier List, which is currently [setr(msg,'[get(%q<mog>/MOGUSE`%q<mogrifier>)]'[if(not(hasattr(%q<mog>/MOGUSE`%q<mogrifier>)),%b(inherited))].)] Are you sure? Enter the same command again within ten seconds to continue!},CLEAR %q<mog> %q<mogrifier>,CHANNEL;&MOGCONF`%q<mogrifier> %q<mog>;@nspemit %#=announce(CHANNELS) You clear Channel %q<channel>'s Mogrifier list.;@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=ansi(h,%n) cleared Channel '%q<channel>''s Mogrifier list, which was %q<msg>}},{@assert lmath(min,iter(lcstr(%1),match(lcstr(get(%q<mog>/MOGORDER`%q<mogrifier>)),%i0)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: One or more of the entered mogrifier types was not recognized.;th setq(oldmog,get(%q<mog>/MOGUSE`%q<mogrifier>),inherited,hasattr(%q<mog>/MOGUSE`%q<mogrifier>));&MOGUSE`%q<mogrifier> %q<mog>=setr(newmog,filterbool(#lambda/match(lcstr(%1),\%0),lcstr(get(%q<mog>/MOGORDER`%q<mogrifier>))));@nspemit %#=announce(CHANNELS) You set Channel %q<channel>'s Mogrifier list to: %q<newmog>;@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=ansi(h,%n) set Channel '%q<channel>''s Mogrifier list, which was '%q<oldmog>'[if(not(%q<inherited>),%b(inherited))], to: %q<newmog>}

&INC`CHANNEL`MOGCONF`SUBCONFIG u(cmo)=@include u(ccs)/INC`PARTIAL=after(%2,/),v(VAR`MOGTYPES`%q<mogrifier>),|,CHANNEL,parameter,parameter;@switch/inline strlen(%1)=0,{&MOGCONF`%q<mogrifier>`%q<parameter> %q<mog>;@nspemit %#=u(RFN`MSGHEAD) You cleared Channel %q<channel>'s %q<mogrifier> Mogrifier %q<parameter> Setting.},{@include u(cmo)/INC`CHANNEL`MOGCONF`%q<mogrifier>`%q<parameter>;&MOGCONF`%q<mogrifier>`%q<parameter> %q<mog>=%q<entry>;@nspemit %#=u(RFN`MSGHEAD) You set Channel %q<channel>'s %q<mogrifier> Mogrifier %q<parameter> Setting to: %q<entry>};@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=ansi(h,%n) [if(strlen(%1),set,cleared)] the %q<channel> Channel's %q<mogrifier> Mogrifier %q<parameter> Setting[if(strlen(%1),%bto %1!,.)]}

&INC`CHANNEL`MOGCONF`TITLE`TRIM u(cmo)=@assert cand(isint(%1),gte(%1,0))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Trim length Mogrifier setting must be a whole number 0 or greater.;th setq(entry,abs(%1))
&INC`CHANNEL`MOGCONF`TITLE`STAFFTAG u(cmo)=@assert cand(isint(%1),gte(%1,0))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Stafftag Mogrifier setting must be boolean 1 or 0.;th setq(entry,t(%1))

&VAR`MOGTYPES u(cmo)=BLOCK|CHANNAME|TITLE|PLAYERNAME|SPEECHTEXT|MESSAGE
&VAR`MOGTYPES`BLOCK u(cmo)=BADWORDS
&VAR`MOGTYPES`CHANNAME u(cmo)=
&VAR`MOGTYPES`TITLE u(cmo)=TRIM|STAFFTAG
&VAR`MOGTYPES`PLAYERNAME u(cmo)=
&VAR`MOGTYPES`SPEECHTEXT u(cmo)=
&VAR`MOGTYPES`MESSAGE u(cmo)=

&INC`CHANNEL`MOGCONF`SHOWCONFIG u(cmo)=@nspemit %#=header(%q<channel>: Mogrifiers);@dolist/inline/delimit | [v(VAR`MOGTYPES)]={@nspemit %#=separator(capnames(%i0));@nspemit %#=Order: [get(%q<mog>/MOGORDER`%i0)] [if(not(hasattr(%q<mog>/MOGORDER`%i0)),(inherited))]%RIn Use: [get(%q<mog>/MOGUSE`%i0)] [if(not(hasattr(%q<mog>/MOGUSE`%i0)),(inherited))];@nspemit %#=* OPTIONS *;@nspemit %#=iter(v(VAR`MOGTYPES`%i0),%i0: [get(%q<mog>/MOGCONF`%i1`%i0)] [if(not(hasattr(%q<mog>/MOGCONF`%i1`%i0)),(inherited))],|,%R)};@nspemit %#=header()

&INC`CHANNEL`CODENAME u(cmo)=@include u(ccs)/INC`PARTIAL=%0,stripansi(objeval(%#,channels(|))),|,CHANNEL,channel,channel;@break not(strmatch(parent(setr(mog,cmogrifier(%q<channel>))),u(cdb)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Channel is not initialized. Please use +channel/init %q<channel>;@assert get(%q<mog>/CONF`CODENAME)=@nspemit %#=u(RFN`MSGHEAD) ERROR: That channel does not use codenames.;@assert strlen(%1)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Nothing entered for an codename!;@break gt(strlen(%1),config(player_name_len))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Codename is beyond the allowed [config(player_name_len)] characters.;@assert cor(valid(playername,stripansi(%1)),if(pmatch(%1),strmatch(%#,pmatch(%1)),0))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Codenames must follow the same name rules as names, and cannot match another player's name.;@break words(setr(list,setdiff(lsearch(all,type,player,elock,D`CHANNEL`%q<mog>`CODENAME`NOCOLOR:[stripansi(trim(%1))]),%#)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Cannot use that codename: in use by [itemize(iter(%q<list>,name(%i0),%b,|),|,and,\,)];@nspemit %#=u(RFN`MSGHEAD) Codename set for %q<channel> to: %1 - make sure this is a reasonable codename!;&D`CHANNEL`%q<mog>`CODENAME %#=trim(%1);&D`CHANNEL`%q<mog>`CODENAME`NOCOLOR %#=stripansi(trim(%1))

&INC`CHANNEL`STAFFTAG u(cmo)=@include u(ccs)/INC`PARTIAL=%0,stripansi(objeval(%#,channels(|))),|,CHANNEL,channel,channel;@break not(strmatch(parent(setr(mog,cmogrifier(%q<channel>))),u(cdb)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Channel is not initialized. Please use +channel/init %q<channel>;@switch/inline t(match(get(%q<mog>/VAR`STAFFTAG),%:))=1,{&VAR`STAFFTAG %q<mog>=setdiff(get(%q<mog>/VAR`STAFFTAG),%:);@nspemit %#=u(RFN`MSGHEAD) You will no longer have a Staff tag on %q<channel>},0,{&VAR`STAFFTAG %q<mog>=setunion(get(%q<mog>/VAR`STAFFTAG),%:);@nspemit %#=u(RFN`MSGHEAD) You will now have a Staff tag on %q<channel>}



@@ MUZZLE SECTION
&CMD`+MUZZLE u(cmo)=$^(?s)(?\:\+)?(muzzle|unmuzzle)(?\:/(\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%2,setunion(get(u(cmo)/VAR`MUZZLE`PLAYFLAGS),if(isadmin(%#),get(u(cmo)/VAR`MUZZLE`ADMINFLAGS)),|,|),|,MUZZLE,switch,switch;@include u(cmo)/INC`%1`[strfirstof(%q<switch>,MAIN)]=%3,%4,%5
@set u(cmo)/CMD`+MUZZLE=regexp

&VAR`MUZZLE`ADMINFLAGS u(cmo)=ACCOUNT

&INC`MUZZLE`MAIN u(cmo)=@include u(cmo)/INC`MUZZLE`START=%0,%1,%2,0
&INC`MUZZLE`ACCOUNT u(cmo)=@include u(cmo)/INC`MUZZLE`START=%0,%1,%2,1
&INC`UNMUZZLE`MAIN u(cmo)=@include u(cmo)/INC`MUZZLE`START=%0,%1,%2,0,1
&INC`UNMUZZLE`ACCOUNT u(cmo)=@include u(cmo)/INC`MUZZLE`START=%0,%1,%2,0,1

&INC`MUZZLE`START u(cmo)=@switch/inline strlen(%0)=0,{@include u(cmo)/INC`MUZZLE`SHOW},{}

@switch/inline t(%3)=1,{@switch/inline valnum(%0)=1,{@assert hasattr(u(adb)/setr(accid,trim(%0)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Account not found.},0,{@include u(ccs)/INC`CHECKPC=%0,1,CHANNEL;@break isadmin(%q<t1>)=@nspemit %#=u(RFN`MSGHEAD) ERROR: %q<t1name> is staff.;@assert setr(accid,accid(%q<t1>))=@nspemit %#=u(RFN`MSGHEAD) ERROR: %q<t1name> has no account to Muzzle!}},0,{@include u(ccs)/INC`CHECKPC=%0,1,CHANNEL;@break isadmin(%q<t1>)=@nspemit %#=u(RFN`MSGHEAD) ERROR: %q<t1name> is staff.};@switch/inline %1=ALL,{@assert isadmin(%#)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Permission denied.;th setq(allmode,1)},{@include u(ccs)/INC`PARTIAL=%1,objeval(%#,channels(|)),|,CHANNEL,channel,channel;@break not(strmatch(parent(setr(mog,cmogrifier(%q<channel>))),u(cdb)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Channel is not initialized. Please use +channel/init %q<channel>;@assert cor(isadmin(%#),elock(%q<mog>/Muzzle,%#))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Permission denied.};@switch/inline t(%3)=0,{@assert strlen(%2)=@nspemit %#=u(RFN`MSGHEAD) ERROR: No Muzzle duration entered.;@assert gt(stringsecs(%2),0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Invalid duration entered. Please see [pueblize(help stringsecs(),help stringsecs())] for more information.}




&INC`MUZZLE`MAIN u(cmo)=@switch/inline strlen(%0)=0,{@include u(cmo)/INC`MUZZLE`SHOWALL},{@include u(ccs)/INC`CHECKPC=%0,1,CHANNEL;@break isadmin(%q<t1>)=@nspemit %#=u(RFN`MSGHEAD) ERROR: %q<t1name> is staff.;@include u(ccs)/INC`PARTIAL=%1,objeval(%#,channels(|)),|,CHANNEL,channel,channel;@break not(strmatch(parent(setr(mog,cmogrifier(%q<channel>))),u(cdb)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Channel is not initialized. Please use +channel/init %q<channel>;@assert cor(isadmin(%#),elock(%q<mog>/Muzzle,%#))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Permission denied.;@break gt(u(u(cmo)/FUN`GETMUZZLE,%q<mog>,%q<t1objid>),0)=@nspemit %#=msghead(ERROR) %q<t1name> is already Muzzled from %q<channel>.;@assert strlen(%2)=@nspemit %#=u(RFN`MSGHEAD) ERROR: No Muzzle duration entered.;@assert gt(stringsecs(%2),0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Invalid duration entered. Please see [pueblize(help stringsecs(),help stringsecs())] for more information.;&[setr(attr,MUZZLE`[nextslot(%q<mog>,MUZZLE)])] %q<mog>=%q<t1objid>;&%q<attr>`DURATION %q<mog>=add(secs(),stringsecs(%2));@nspemit %#=u(RFN`MSGHEAD) You have Muzzled %q<t1name> from %q<channel> for [timestring(stringsecs(%2))] - Until [convsecs(add(secs(),stringsecs(%2)))].;@include u(ccs)/INC`MSG=v(VAR`MSGHEAD),%q<t1>,{You have been Muzzled from %q<channel> for [timestring(stringsecs(%2))] - Until [convsecs(add(secs(),stringsecs(%2)))].};@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=[ansi(h,%n)] has Muzzled %q<t1name> from %q<channel> for [timestring(stringsecs(%2))] - Until [convsecs(add(secs(),stringsecs(%2)))].}}

&INC`MUZZLE`ACCOUNT u(cmo)=@switch/inline strlen(%0)=0,{@include u(cmo)/INC`MUZZLE`SHOWALL`ACCOUNT},{@include u(ccs)/INC`CHECKPC=%0,1,CHANNEL;@break isadmin(%q<t1>)=@nspemit %#=u(RFN`MSGHEAD) ERROR: %q<t1name> is staff.;@assert setr(accid,accid(%q<t1>))=@nspemit %#=u(RFN`MSGHEAD) ERROR: %q<t1name> has no account to Muzzle!;@include u(ccs)/INC`PARTIAL=%1,objeval(%#,channels(|)),|,CHANNEL,channel,channel;@break not(strmatch(parent(setr(mog,cmogrifier(%q<channel>))),u(cdb)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Channel is not initialized. Please use +channel/init %q<channel>;@assert cor(isadmin(%#),elock(%q<mog>/Muzzle,%#))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Permission denied.;@break gt(u(u(cmo)/FUN`GETACCMUZZLE,%q<mog>,%q<t1objid>),0)=@nspemit %#=u(RFN`MSGHEAD) %q<t1name> is already Account Muzzled from %q<channel>.;@assert strlen(%2)=@nspemit %#=u(RFN`MSGHEAD) ERROR: No Muzzle duration entered.;@assert gt(stringsecs(%2),0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Invalid duration entered. Please see [pueblize(help stringsecs(),help stringsecs())] for more information.;&[setr(attr,ACCMUZZLE`%q<accid>)] %q<mog>=%q<t1objid>;&%q<attr>`DURATION %q<mog>=add(secs(),stringsecs(%2));@nspemit %#=u(RFN`MSGHEAD) You have Account Muzzled %q<t1name> from %q<channel> for [timestring(stringsecs(%2))] - Until [convsecs(add(secs(),stringsecs(%2)))].;@include u(ccs)/INC`MSG=v(VAR`MSGHEAD),get(u(adb)/%q<accid>),{You have been Muzzled from %q<channel> ON ALL CHARACTERS for [timestring(stringsecs(%2))] - Until [convsecs(add(secs(),stringsecs(%2)))].};@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=ansi(h,%n) has Account Muzzled %q<t1name> from %q<channel> for [timestring(stringsecs(%2))] - Until [convsecs(add(secs(),stringsecs(%2)))].}}

&INC`MUZZLE`SHOWALL u(cmo)=th setq(cids,filterbool(#lambda/valnum(\%0),iter(objeval(%#,channels(|)),u(FUN`FINDCHANNEL,%i0),|,|),|,|));@nspemit %#=header(mudname() Channel Muzzles);@dolist/inline/delimit %q<cids>={@nspemit %#=pueblize(setr(cname,elements(channels(|),match(channels(|),get(u(cmo)/%i0),|),|)),+channel [stripansi(%q<channel>)]): [itemize(iter(lsearch(all,eplayer,\[gt(get(##/D`CHANNEL`%i0`MUZZLE),secs())\]),ansi(h,name(%i0)) for [convsecs(get(%i0/D`CHANNEL`%i1`MUZZLE))] \([timestring(sub(get(%i0/D`CHANNEL`%i1`MUZZLE),secs()))]\),%b,|),|,and,\,)]};@nspemit %#=header()

&INC`UNMUZZLE`MAIN u(cmo)=@switch/inline strlen(%0)=0,{@include u(cmo)/INC`MUZZLE`SHOWALL},{@include u(ccs)/INC`CHECKPC=%0,1,CHANNEL;@break isadmin(%q<t1>)=@nspemit %#=u(RFN`MSGHEAD) ERROR: %q<t1name> is staff.;@include u(ccs)/INC`PARTIAL=%1,objeval(%#,channels(|)),|,CHANNEL,channel,channel;@break not(strmatch(parent(setr(mog,cmogrifier(%q<channel>))),u(cdb)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Channel is not initialized. Please use +channel/init %q<channel>;@assert cor(isadmin(%#),elock(%q<mog>/Muzzle,%#))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Permission denied.;@assert gt(u(u(cmo)/FUN`GETMUZZLE,%q<mog>,%q<t1objid>),0)=@nspemit %#=u(RFN`MSGHEAD) %q<t1name> is not Muzzled from %q<channel>.;th setr(attr,wildgrepi(%q<mog>,MUZZLE`*,%q<t1objid>));@wipe %q<mog>/%q<attr>;@nspemit %#=u(RFN`MSGHEAD) You have unMuzzled %q<t1name> from %q<channel>.;@include u(ccs)/INC`MSG=v(VAR`MSGHEAD),%q<t1>,{You have been unMuzzled from %q<channel>.};@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=[ansi(h,%n)] has unMuzzled %q<t1name> from %q<channel>.}}

&INC`UNMUZZLE`ACCOUNT u(cmo)=@switch/inline strlen(%0)=0,{@include u(cmo)/INC`MUZZLE`SHOWALL`ACCOUNT},{@include u(ccs)/INC`CHECKPC=%0,1,CHANNEL;@break isadmin(%q<t1>)=@nspemit %#=u(RFN`MSGHEAD) ERROR: %q<t1name> is staff.;@assert setr(accid,accid(%q<t1>))=@nspemit %#=u(RFN`MSGHEAD) ERROR: %q<t1name> has no account to Muzzle!;@include u(ccs)/INC`PARTIAL=%1,objeval(%#,channels(|)),|,CHANNEL,channel,channel;@break not(strmatch(parent(setr(mog,cmogrifier(%q<channel>))),u(cdb)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Channel is not initialized. Please use +channel/init %q<channel>;@assert cor(isadmin(%#),elock(%q<mog>/Muzzle,%#))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Permission denied.;@assert gt(u(u(cmo)/FUN`GETACCMUZZLE,%q<mog>,%q<accid>),0)=@nspemit %#=u(RFN`MSGHEAD) %q<t1name> is not Account Muzzled from %q<channel>.;@wipe %q<mog>/ACCMUZZLE`%q<accid>;@nspemit %#=u(RFN`MSGHEAD) You have un-Account Muzzled %q<t1name> from %q<channel>.;@include u(ccs)/INC`MSG=v(VAR`MSGHEAD),get(u(adb)/%q<accid>),{You have been unMuzzled from %q<channel> ON ALL CHARACTERS.};@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=ansi(h,%n) has un-Account Muzzled %q<t1name> from %q<channel>.}}

&INC`CHECKNAME u(cmo)=@assert strlen(setr(name,stripansi(trim(%0))))=@nspemit %#=u(RFN`MSGHEAD) Channel Name field empty.;@assert valid(channel,%q<name>)

@@ +clock section
&CMD`+CLOCK u(cmo)=$^(?\:\+)?(?\:chanlock|clock)(?\:/(\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(cmo)/VAR`CLOCK`PLAYFLAGS),if(isadmin(%#),get(u(cmo)/VAR`CLOCK`ADMINFLAGS)),|,|),|,CHANNEL,switch,switch;@include u(cmo)/INC`CLOCK=%2,%3,%4,%5
@set u(cmo)/CMD`+CLOCK=regexp

&VAR`CLOCK`PLAYFLAGS u(cmo)=VIEW
&VAR`CLOCK`ADMINFLAGS u(cmo)=JOIN|SPEAK|SEE|HIDE|MOD|MUZZLE

&INC`CLOCK u(cmo)=@assert strlen(%0)=@nspemit %#=u(rfn`msghead) ERROR: Channel empty.;@include u(ccs)/INC`PARTIAL=%0,objeval(%#,channels(|)),|,CHANNEL,channel,channel;@break not(strmatch(parent(setr(mog,cmogrifier(%q<channel>))),u(cdb)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Channel is not initialized. Please use +channel/init %q<channel>;@switch/inline %q<switch>=VIEW,{@include u(cmo)/INC`CLOCK`VIEW},{@assert cor(isdbref(lockfilter(clock(%q<channel>/mod),%#)),hasflag(%#,WIZARD))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Permission Denied for %q<channel>.;@switch/inline gt(strlen(%2),0)=1,{th setq(dbs,setunion(u(u(kls)/FUN`LISTKEYS),u(u(gso)/FUN`LISTGROUPS)));@assert eq(words(setr(choices,setdiff(iter(%2,if(match(%q<dbs>,setr(grab,namegrab(%q<dbs>,before(%i0,/)))),%q<grab>,before(%i0,/))),%q<dbs>))),0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Following Master Keys not found: [itemize(%q<choices>,|,and,\,)];@dolist/inline %2={@assert isdbref(setr(grab,namegrab(%q<dbs>,before(%i0,/))))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Master Key Not Found: [before(%i0,/)];@assert match(u(u(kls)/FUN`LISTLOCKS,%q<grab>),strfirstof(after(%i0,/),MEMBER))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Lock Type for [before(%i0,/)] Not Found: [after(%i0,/)]};@switch/inline t(strlen(%q<switch>))=1,{th setq(lockstring,if(strlen(%2),iter(%2,@[namegrab(%q<dbs>,before(%i0,/))][if(strlen(after(%i0,/)),/[after(%i0,/)],/Member)],%B,|)));@switch/inline %q<switch>=MUZZLE,{@lock/user:muzzle %q<mog>=%q<lockstring>},{@clock/%q<switch> %q<channel>=%q<lockstring>}},{@break strmatch(%2,*/*)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Auto-set mode does not support custom indirect locks.;@include u(cmo)/INC`CLOCK`AUTOSET};@nspemit %#=u(RFN`MSGHEAD) Channel %q<channel> [if(strlen(%q<switch>),%q<switch>%b,Fully-)]Locked to: [if(strlen(%2),itemize(%2,%b,and,\,),Nothing!)];@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=ansi(h,%n) [if(strlen(%q<switch>),%q<switch>%b,Fully-)]Locked Channel %q<channel> to: [if(strlen(%2),itemize(%2,%b,and,\,),Nothing!)]}}}

&INC`CLOCK`VIEW u(cmo)=@nspemit %#=header(Channel Locks - %q<channel>);th setq(mog,cmogrifier(stripansi(%q<channel>)));@dolist/inline SEE JOIN SPEAK HIDE MOD MUZZLE={@nspemit %#=align(8 69,%i0,switch(%i0,muzzle,lock(%q<mog>/muzzle),clock(%q<channel>/%i0)))};@nspemit %#=separator(Dbrefs as Names);@dolist/inline SEE JOIN SPEAK HIDE MOD MUZZLE={@nspemit %#=align(8 69,%i0,regeditalli(switch(%i0,muzzle,lock(%q<mog>/muzzle),clock(%q<channel>/%i0)),(#\\d+),name($0)))};@nspemit %#=header()

&INC`CLOCK`AUTOSET u(cmo)=@clock/see %q<channel>=if(strlen(%2),iter(%2,@[namegrab(%q<dbs>,before(%i0,/))]/MEMBER,%B,|));@clock/join %q<channel>=if(strlen(%2),iter(%2,@[namegrab(%q<dbs>,before(%i0,/))]/MEMBER,%B,|));@clock/speak %q<channel>=if(strlen(%2),iter(%2,@[namegrab(%q<dbs>,before(%i0,/))]/MEMBER,%B,|));@clock/hide %q<channel>=if(strlen(%2),iter(%2,@[namegrab(%q<dbs>,before(%i0,/))]/MODERATE,%B,|));@lock/user:muzzle %q<mog>=if(strlen(%2),iter(%2,@[namegrab(%q<dbs>,before(%i0,/))]/MODERATE,%B,|))

&STARTUP u(cmo)=@hook/ignore @channel=u(cmo),HOOK`IGNORECHECK;@trigger u(cmo)/TRG`CLEANMUZZLE
&HOOK`IGNORECHECK u(cmo)=if(cand(cor(strmatch(first(after(%u,/)),a*),strmatch(first(after(%u,/)),ren*)),hastype(%#,PLAYER)),0[nspemit(%#,u(RFN`MSGHEAD) ERROR: Please use +channel for this purpose!)],if(cand(strmatch(first(after(%u,/)),wh*),not(isadmin(%#)),if(isdbref(setr(cmog,cmogrifier(rest(%u)))),t(get(%q<cmog>/CONF`CODENAME)),0)),0[nspemit(%#,u(RFN`MSGHEAD) ERROR: @channel/who disabled for CODENAME channel anonymity on this channel! Use +channel <name> for that!)],1))

&TRG`CLEANMUZZLE u(cmo)=@dolist lthings(u(cdb))=@trigger %i0/TRG`CLEANME;@wait 7200=@trigger me/TRG`CLEANMUZZLE

&VAR`ALERTMODE u(cmo)=1
&VAR`ALERTSCHANNEL u(cmo)=u(CMO`STAFFREP)

&GFN`CHANCOLOR u(cmo)=localize(if(isdbref(setr(mog,cmogrifier(stripansi(%0)))),custcolor(%@,CHANNEL`%q<mog>,,1)))

@@ Mogrify and CDB section
@lock/user:muzzle u(cdb)=FLAG^ROYALTY|FLAG^WIZARD
@lset u(cdb)/muzzle=v

&FUN`GETMUZZLE u(cmo)=lmath(max,iter(wildgrepi(%0,MUZZLE`*,%1),get(%0/%i0`DURATION)))
&FUN`GETACCMUZZLE u(cmo)=get(%0/ACCMUZZLE`%1`DURATIOn)

&MOGRIFY`PLAYERNAME u(cdb)=localize(null(iter(v(MOGUSE`PLAYERNAME),setr(playname,u(FUN`PLAYERNAME`%i0,strfirstof(%q<playname>,%0)))))[strfirstof(%q<playname>,%0)])
&MOGORDER`PLAYERNAME u(cdb)=codename
&MOGUSE`PLAYERNAME u(cdb)=codename

&FUN`PLAYERNAME`CODENAME u(cdb)=if(v(CONF`CODENAME),default(%#/D`CHANNEL`[num(me)]`CODENAME,%0))

&MOGRIFY`TITLE u(cdb)=localize(null(iter(v(MOGUSE`TITLE),setr(title,u(FUN`TITLE`%i0,strfirstof(%q<title>,%0)))))[strfirstof(%q<title>,%0)])
&MOGORDER`TITLE u(cdb)=noansi trim stafftag
&MOGUSE`TITLE u(cdb)=noansi trim stafftag

&FUN`TITLE`STAFFTAG u(cdb)=if(cand(v(MOGCONF`TITLE`STAFFTAG),match(v(VAR`STAFFTAG),%:),isadmin(%#)),\[[ansi(hR,Staff)]\][if(strlen(%0),%B%0)],%0)
&MOGCONF`TITLE`STAFFTAG u(cdb)=1
&FUN`TITLE`NOANSI u(cdb)=stripansi(%0)
&FUN`TITLE`TRIM u(cdb)=trim(if(v(MOGCONF`TITLE`TRIM),left(%0,v(MOGCONF`TITLE`TRIM)),%0))
&MOGCONF`TITLE`TRIM u(cdb)=0

&MOGRIFY`BLOCK u(cdb)=localize(null(iter(v(MOGUSE`BLOCK),if(strlen(setr(block,u(FUN`BLOCK`%i0,%2,%1))),ibreak())))%q<block>)
&MOGORDER`BLOCK u(cdb)=muzzle accmuzzle nostaffspoof nocodename badcodename badwords
&MOGUSE`BLOCK u(cdb)=muzzle accmuzzle nostaffspoof nocodename badcodename badwords

&FUN`BLOCK`ACCMUZZLE u(cdb)=if(cand(setr(clear,gt(setr(auntil,u(u(cmo)/FUN`GETACCMUZZLE,num(me),setr(accid,accid(%:)))),secs())),not(isadmin(%#))),u(RFN`MSGHEAD) You have been Account Muzzled from %1 until [fancytime(%q<auntil>,%#)])[null(if(cand(%q<auntil>,not(%q<clear>)),wipe(me/ACCMUZZLE`%q<accid>)))]

&FUN`BLOCK`MUZZLE u(cdb)=if(cand(setr(clear,gt(setr(until,u(u(cmo)/FUN`GETMUZZLE,num(me),%:)),secs())),not(isadmin(%#))),u(RFN`MSGHEAD) You have been Muzzled from %1 until [convsecs(%q<until>)])[null(if(cand(%q<until>,not(%q<clear>)),wipe(me/[first(wildgrepi(me,MUZZLE`*,%:))])))]

&FUN`BLOCK`NOCODENAME u(cdb)=if(cand(v(CONFIG`CODENAME),not(strlen(setr(codename,get(%#/D`CHANNEl`[num(me)]`CODENAME)))),not(isadmin(%#))),u(RFN`MSGHEAD) That Channel uses codenames\, not names. Please set your codename using +channel/codename %1=<codename here>)

&FUN`BLOCK`BADCODENAME u(cdb)=if(cand(v(CONFIG`CODENAME),cand(not(valid(playername,stripansi(%q<codename>))),not(valid(playername,stripansi(%q<codename>),%#))),v(VAR`CODENAME2)),u(RFN`MSGHEAD) Your codename may not match another player's name!)

&FUN`BLOCK`NOSTAFFSPOOF u(cdb)=switch(strmatch(%4,*\[Staff\]*)[isadmin(%#)],10,u(RFN`MSGHEAD) ERROR: You are not staff. Please remove the \[Staff\] from your channel title.)

&FUN`BLOCK`BADWORDS u(cdb)=if(regmatchi(%0,%\b%([v(MOGCONF`BLOCK`BADWORDS)]%)%\b,1:x),u(RFN`MSGHEAD) Your message was blocked by the bad words filter because it contains the following: %qx)
&MOGCONF`BLOCK`BADWORDS u(cdb)=loli|lolicon|lolicons

&MOGRIFY`MESSAGE u(cdb)=localize(null(iter(v(MOGUSE`MESSAGE),setr(message,u(FUN`MESSAGE`%i0,strfirstof(%q<message>,%0)))))[strfirstof(%q<message>,%0)])
&MOGORDER`MESSAGE u(cdb)=url
&MOGUSE`MESSAGE u(cdb)=url

&FUN`MESSAGE`URL u(cdb)=regeditalli(%0,v(VAR`REGEXP`WWW),weblink($0,$0))
&VAR`REGEXP`WWW u(cdb)=\w+\://\S+

&TRG`CLEANME u(cdb)=@dolist/inline/nobreak lattr(me/MUZZLE`*)={@switch/inline cor(not(isobjid(v(%i0))),lte(v(%i0`DURATION),secs()))=1,{@wipe me/%i0}};@dolist/inline/nobreak lattr(me/ACCMUZZLE`*)={@switch/inline cor(not(words(get(u(adb)/[last(%i0,`)]))),lte(v(%i0),secs()))=1,{@wipe me/%i0}}
