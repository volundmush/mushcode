@@ This patch drastically increases the speed at which Anomaly Jobs does just about anything. Use at your own risk.

&job u(coi)=locate(config(master_room),Job Global Object <JGO>,TXxi)
@edit u(job)/*=@switch,@switch/inline
@edit get(u(job)/va)/*=@switch,@switch/inline
@edit u(job)/*=@force,@force/inplace
@edit u(job)/*=@dolist,@dolist/inline/nobreak
@edit get(u(job)/va)/*=@trigger %va,@include/clearregs/localize/nobreak %va

@parent get(u(job)/vb)=u(coi)
@set get(u(job)/vb)=WIZARD
&HOOK_APR locate(get(u(job)/vc),APPS,TXxi)=@force %1={+approve [setr(by,get(%0/OPENED_BY))];+info/approve %q<by>/[iter(lattr(%q<by>/D`INFOFILE`*),get(%q<by>/%i0),%B,|)];@@ @switch/inline t(strlen(setr(chans,switch(getsphere(%q<by>),RACE1,Channel|List,Race2,Channel|list))))=1,{@dolist/inline/delimit | %q<chans>={@chan/on %i0=%q<by>}}}

&JOBS u(ap)=BOTDAS
&JOBSN u(ap)=1
&JOBSWIDTH u(ap)
&JOBSWIDTH_O u(ap)
&JOBSKIN u(ap)=VOLUND
&FN_STAFFALL get(u(job)/va)=isadmin(%0)

@set get(u(job)/vb)=ROYALTY
@switch/inline isdbref(u(bbs))=1,{@wait 0=&BBPOCKET [get(u(job)/vc)]=[u(bbs-db)]}

&TRIG_POST get(u(job)/va)=@switch/inline [u(%va/FN_HASATTRP,%0,PLETTER_%1)]=1,{@wait 1={@trigger %vb/TRIG_POST=[get(%0/POST_[case(%1,APR,APPROVE,DEL,DELETE,DNY,DENY,COM,COMPLETE,APPROVE)])],[switch(%1,APR,A,DNY,D,COM,C,?)]:[name(first(get(%0/OPENED_BY)))][if(gt(words(get(%0/OPENED_BY)),1),+)]-[get(%0/TITLE)],[u(parent(%0)/PLETTER_%1,%0,%1,%2,%3)],%0}}

&TRIG_POST get(u(job)/vb)=+bbpost %0/%1=%2;@switch/inline gt(match(get(u(bbs)/VAR`BB`PLAYCOMMS),COMMENT,|),0)=1,{@wait 1={th setq(newpost,match(u(u(bbs)/FUN`LISTPOSTS,%0),last(get(me/D`BB`LASTPOST))));@dolist/inline/nobreak sortkey(#lambda/last(\%0,_),lattr(%3/COMMENT_*))={+bbcomment [first(get(me/D`BB`LASTPOST))]/%q<newpost>=[elements(get(%3/%i0),4,|)] ([elements(get(%3/%i0),3,|)]) [switch(elements(get(%3/%i0),1,|),CRE,Created,ADD,Commented,MAI,Mailed,APR,Approved,COM,Completed,DEN,Denied,DEL,Deleted,elements(get(%3/%i0),1,|))] On [convsecs(elements(get(%3/%i0),2,|))]:%R[after(get(%3/%i0),elements(get(%3/%i0),1 2 3 4,|,|)|)]}}}

&FN_LINKJOB get(u(job)/va)=[tag(A,XCH_CMD="[ifelse(t(strlen(%1)),%1,%0)]")]%0[endtag(A)]
@edit get(u(job)/vc)/BLETTER_*={name(%0)},{u(%va/FN_LINKJOB,name(%0),+[name(%0)])}
@edit get(u(job)/vc)/BLETTER_CLN={name(%2)},{u(%va/FN_LINKJOB,name(%2),+[name(%2)])}
@edit get(u(job)/vc)/BLETTER_MRG={name(%2)},{u(%va/FN_LINKJOB,name(%2),+[name(%2)])}
@edit get(u(job)/va)/*_JOBLIST={last(name(%0))},{u(%va/FN_LINKJOB,last(name(%0)),+[name(%0)])}

&TRIG_BROADCAST get(u(job)/va)=@switch isdbref(%1)=1,{@pemit/list [setq(0,parent(%0))][setq(J,%0)][setq(0,ifelse(isdbref(%q0),%q0,%vc))][filter(%va/FIL_BROADCAST,setunion(lwho(),))]=[announce(JOB)]%b[u(%q0/BLETTER_%2,%0,%1,%3,%4)]},{@pemit/list [setq(0,%0)][filter(%va/FIL_BROADCAST,setunion(lwho(),))]=[announce(JOB)]%b%1}

&VOLUND_BANNER get(u(job)/va)=setq(0,%0)[setq(x,u(%va/FN_ITEMIZE,map(MAP_NAME,get(%q0/OPENED_BY),%b,|),|))][setq(y,u(%va/FN_ITEMIZE,map(MAP_NAME,get(%q0/TAGGED_FOR),%b,|),|))][ljust(rjust(ansi(hc,Bucket:),10)%b[u(%va/FN_BUCKETNAME,%q0)],div(u(FN_FLEXWIDTH,80),2))][rjust(ansi(hc,Due On:),12)]%b[ifelse(get(%q0/DUE_ON),ifelse(gt(secs(),get(%q0/DUE_ON)),OVERDUE!,convsecs(get(%q0/DUE_ON))),-)]%r[ljust([rjust(ansi(hc,Title:),10)]%b[get(%q0/TITLE)],div(u(FN_FLEXWIDTH,80),2))][rjust(ansi(hc,Status:),12)]%b[switch(get(%q0/PRIORITY),1,Green,2,Yellow,3,Red)]%b%([last(get(%va/STATUS_[get(%q0/STATUS)]),|)]%)%r[ljust(rjust(ansi(hc,Opened On:),10) [convsecs(get(%q0/OPENED_ON))],div(u(FN_FLEXWIDTH,80),2))][rjust(ansi(hc,Assigned To:),12)]%b[ifelse(isdbref(get(%q0/ASSIGNED_TO)),name(get(%q0/ASSIGNED_TO)),Nobody)]%r[rjust(ansi(hc,Opened By:),10)]%b%qx[ifelse(words(%qy),%r[rjust(ansi(hc,Tagged:),10)]%b%qy,)]
&VOLUND_BREAK get(u(job)/va)=subheader(%0)
&VOLUND_BUCKETHEADER get(u(job)/va)=u(FN_HEADER,Bucket List)%r[ifelse(u(%va/FN_WIZONLY,%#),[ansi(hc,[ljust(Name,9)][ljust(Flags,6)]%b%b[ljust(Description,u(FN_FLEXWIDTH,30))][rjust(#Jobs,5)][rjust(Pct,5)][space(3)]C%b%bA%b%bD%b%bDue[space(3)]ARTS)],[ansi(hc,[ljust(NAME,8)][ljust(Description,u(FN_FLEXWIDTH,50))]%b[rjust(Viewing,20)])])]%r[u(FN_BREAK)]
&VOLUND_BUCKETLIST get(u(job)/va)=ifelse(u(%va/FN_WIZONLY,%#),ljust(name(%0),6)%b%b[ifelse(u(%va/FN_MONITORCHECK,%0,%#),V,-)][ifelse(u(%va/IS_HIDDEN,%0),H,-)][ifelse(u(%va/IS_LOCKED,%0),ifelse(u(%va/FN_HASATTRP,%0,CHECKOUT),C,L),-)][ifelse(u(%va/IS_TAGGED,%0),T,-)][ifelse(u(%va/IS_PUBLIC,%0),M,-)][ifelse(u(%va/IS_PUBLISHED,%0),P,-)][ifelse(u(%va/IS_SUMMARY,%0),S,-)]%b%b[u(%va/FN_STREXACT,get(%0/DESC),u(FN_FLEXWIDTH,30))][rjust(words(remove(children(%0),#-1)),5)][rjust(mul(round(fdiv(words(remove(children(%0),#-1)),max(1,words(lcon(%va)))),2),100)%%,5)]%b%b[rjust(u(%va/FN_FIND-BBNUM,get(%0/POST_COMPLETE)),2)]%b[rjust(u(%va/FN_FIND-BBNUM,get(%0/POST_APPROVE)),2)]%b[rjust(u(%va/FN_FIND-BBNUM,get(%0/POST_DENY)),2)][rjust(default(%0/TURNAROUND,0),5)][rjust(ifelse(u(%va/FN_HASATTR,%0,STAT_ART),[round(fdiv(fdiv(first(get(%0/STAT_ART)),rest(get(%0/STAT_ART))),86400),0)]d,-),7)],ljust(name(%0),6)%b%b[u(%va/FN_STREXACT,get(%0/DESC),u(FN_FLEXWIDTH,50))]%b[rjust(ifelse(u(%va/FN_MONITORCHECK,%0,%#),Yes,-),20)])
&VOLUND_FLAGS get(u(job)/va)=ifelse(u(me/IS_LOCKED,%0),u(me/FN_FLAG,ifelse(u(%va/FN_HASATTRP,%0,CHECKOUT),CHECKED OUT,LOCKED),hr),)[ifelse(u(me/IS_PUBLIC,%0),u(me/FN_FLAG,Myjobs),)][ifelse(u(me/IS_PUBLISHED,%0),u(me/FN_FLAG,Published),)]
&VOLUND_FOOTER get(u(job)/va)=subheader(%0)
&VOLUND_HEADER get(u(job)/va)=header(%0)
&VOLUND_JOBLIST get(u(job)/va)=setq(j,secure(ifelse(u(%va/FN_HASATTRP,%1,JOBS),lcstr(mid(get(%1/JOBS),0,20)),get(%va/JOBS_DEFAULT))))[setq(1,ifelse(get(%0/DUE_ON),ifelse(gt(secs(),get(%0/DUE_ON)),r,switch(get(%0/PRIORITY),1,g,2,y,3,r,g)),switch(get(%0/PRIORITY),1,g,2,y,3,r)))][ifelse(u(%va/FN_ISNEW,%0,%1),[ansi(hr,*)]%b,%b%b)][ansi(h%q1,[rjust([u(%va/FN_LINKJOB,last(name(%0)),+[name(%0)])],5)]%b[iter(lnum(strlen(%qj)),[u(%va/DISPLAY_[mid(%qj,##,1)],%0,%1)])])]
&VOLUND_JOBSHEADER get(u(job)/va)=u(FN_HEADER,Anomaly Jobs [u(VERSION)])%r[setq(j,secure(switch(u(%va/FN_HASATTRP,%0,JOBS),1,lcstr(mid(get(%0/JOBS),0,20)),u(JOBS_DEFAULT))))][ansi(hc,*%b%b[ljust(Job#,5)][iter(lnum(strlen(%qj)),u(HEADER_[mid(%qj,##,1)]))])]%r[u(FN_BREAK)]
&VOLUND_MYBANNER get(u(job)/va)=setq(0,%0)[setq(x,u(%va/FN_ITEMIZE,map(MAP_NAME,get(%q0/OPENED_BY),%b,|),|))][ljust(rjust(ansi(hc,Bucket:),10)%b[u(%va/FN_BUCKETNAME,%q0)],div(u(FN_FLEXWIDTH,80),2))][rjust(ansi(hc,Due On:),12)]%b[ifelse(get(%q0/DUE_ON),ifelse(gt(secs(),get(%q0/DUE_ON)),OVERDUE!,convsecs(get(%q0/DUE_ON))),-)]%r[ljust(rjust(ansi(hc,Title:),10)%b[get(%q0/TITLE)],div(u(FN_FLEXWIDTH,80),2))][rjust(ansi(hc,Assigned To:),12)]%b[ifelse(isdbref(get(%q0/ASSIGNED_TO)),name(get(%q0/ASSIGNED_TO)),Nobody)]%r[ljust([rjust(ansi(hc,Opened On:),10)]%b[convsecs(get(%q0/OPENED_ON))],div(u(FN_FLEXWIDTH,80),2))][rjust(ansi(hc,Status:),12)]%b[switch(get(%q0/PRIORITY),1,Green,2,Yellow,3,Red)]%b%r[rjust(ansi(hc,Opened By:),10)]%b%qx
&VOLUND_MYJOBLIST get(u(job)/va)=ifelse(u(FN_ISNEW,%0,%1),[ansi(hr,*)],%b)%b[setq(j,TDAS)][ansi(n,rjust([last(name(%0))],5)%b[iter(lnum(strlen(%qj)),u(DISPLAY_[mid(%qj,##,1)],%0,%1))])]
&VOLUND_MYJOBSHEADER get(u(job)/va)=u(FN_HEADER,Anomaly Jobs [u(VERSION)])%r[setq(j,TDAS)][ansi(ch,ljust(*%b%b[ljust(Job#,5)][iter(lnum(strlen(%qj)),u(HEADER_[mid(%qj,##,1)]))],[u(FN_FLEXWIDTH,79)]))]%r[u(FN_BREAK)]
&VOLUND_MYSUMMARY get(u(job)/va)=%r[u(FN_BREAK,MySummary)]%r[u(parent(%0)/MYSUMMARY,%0)]
&VOLUND_READ get(u(job)/va)=repeat(-,u(FN_FLEXWIDTH,79))%r[ifelse(u(%va/EDIT_ACCESS,%#),[ansi(h,%[)][ifelse(and(or(u(%va/IS_PUBLISHED,%1),switch(extract(get(%1/%0),3,1,|),u(%1/OPENED_BY),1,0),hasflag(%1/%0,no_inherit)),u(%va/IS_PUBLIC,%1)),ansi(hc,rest(%0,_)+),ansi(hc,rest(%0,_)-))][ansi(h,%])]%b,)][ansi(h,extract(get(%1/%0),4,1,|)%badded on%b[convsecs(extract(get(%1/%0),2,1,|))]:%b )][edit(last(get(%1/%0),|),@@PIPE@@,|)]
&VOLUND_SKIN get(u(job)/va)=1
&VOLUND_STAFFSUM get(u(job)/va)=%r[repeat(-,u(FN_FLEXWIDTH,79))]%r[rjust(ansi(hc,DB#:),10)] %0[space(10)][rjust(ansi(hc,Comments:),10)] [setr(z,words(lattr(%0/COMMENT_*)))]%r%r[rjust(ansi(hc,Players:),10)] %(Players contributing to this job%)%r[setq(y,setunion(iter(lattr(%0/COMMENT_*),extract(get(%0/##),4,1,|),%b,|),,|))][u(%va/fn_columns,%qy,20,|,11)][ifelse(u(%va/FN_HASATTR,%0,LIST_READERS),%r[rjust(ansi(hc,Readers:),10)]%b%(Players who have read this job in the past%)%r[setq(z,iter(get(%0/LIST_READERS),first(##,|)))][u(%va/fn_columns,map(%va/MAP_NAME,%qz,%b,|),20,|,11)],)]%r[rjust(ansi(hc,Stats:),10)]%r[u(%va/fn_columns,iter(ifelse(u(%va/FN_HASATTR,%0,LIST_STATS),sort(get(%0/LIST_STATS)),),[first(##,|)]%b[last(##,|)],%b,|),20,|,11)]%r
&VOLUND_SUMMARY get(u(job)/va)=%r[repeat(-,u(FN_FLEXWIDTH,79))]%r[u(parent(%0)/SUMMARY,%0)]

&FIL_PUBLISHED get(u(job)/va)=cor(u(%q0/PUBLISH),hasflag(%q0/%0,no_inherit),u(%va/FN_MYACCESSCHECK,parent(%q0),%#,%q0),match(get(%q0/OPENED_BY),extract(get(%q0/%0),3,1,|)))

@@ COMMUNICATIONS - +JOB
+help/addmain Communications/+job=[u(help)]/HLP`+JOB
&HLP`+JOB u(help)=+job is used to contact the Storytellers, similar to trouble tickets.%R[ansi(hc,Aliases:)] job, request, +request%R%R[ansi(hc,Basic Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+job)] - Shows all Job Categories and their statistics.%R[ansi(h,+job <category>)] - Shows all your pending jobs for <Category>.%R[ansi(h,+job/all <category>)] - Shows all your jobs, even finished ones.%R[ansi(h,+job\[/all\] <category>=<page>)] - Shows a specific page, works with /all.%R[ansi(h,+job <category>/<#>)] - Display's a job, showing comments, votes, etc.%R[ansi(h,+job/add <category>/<text>)] - Creates a new job in <category>.%R[ansi(h,+job/add <category>/<title>=<text>)] - Creates a new job but uses a custom title.%R[ansi(h,+job/comment <category>/<#>=<text>)] - Appends a comment to a job. These can't be edited or deleted for security purposes.%R[ansi(h,+job/log <category>/<#>)] - Show events that happened to a job.})]%R%R[ansi(hc,Handler Commands)]%RShould you be appointed Handler to a job (or whole category):%R[align(5 [sub(width(%#),6)],,{[ansi(h,+job <category>)] - Shows your jobs and any you handle. If you handle the category, shows them all.%R[ansi(h,+job <category>/<#>)] - You can check any jobs you handle.%R[ansi(h,+job/vote <category>/<#>=<vote>)] - Vote on a job. <vote> can be Yes, No, or Undecided. Cannot vote on your own jobs!})]%R%RLegend for +job display-%R* - Updates or changes made since you last checked the job.%RP - Job is pending.%RA - job was approved.%RD - job was denied.%RC - job was canceled.

+shelp/addmain Communications/+job=[u(help)]/SHLP`+JOB
&SHLP`+JOB u(help)=[ansi(hc,Finishing a Job)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+job/approve <category>/<#>=<text>)]%R[ansi(h,+job/deny <category>/<#>=<text>)]%R[ansi(h,+job/cancel <category>/<#>=<text>)] - Approves, Denies, or Cancels a Job for a given reason.})]%R%R[ansi(hc,Handlers)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+job/claim <category>)] - Assign yourself to handle a category.%R[ansi(h,+job/unclaim <category>)] - Remove your handler status from a category.%R[ansi(h,+job/claim <category>/<#>)] - Claims a Job. You're responsible for finishing it!%R[ansi(h,+job/unclaim <category>/<#>)] - Remove your handler status from a job.%R[ansi(h,+job/claim <category>=<player>)] - Assign someone else to handle a category.%R[ansi(h,+job/unclaim <Category>=<player>)] - Remove <player>'s handler status from a category.%R[ansi(h,+job/claim <category>/<#>=<player>)] - Assign <player> to handle a job.%R[ansi(h,+job/unclaim <category>/<#>=<player>)] - Remove <player>'s handler status from a job.})]%R%R[ansi(hc,Misc)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+job/revive <category>/<#>=<text>)] - Returns a Job to the Pending status, essentially undoing Approve, Deny, or Cancel. Must give a reason.%R[ansi(h,+job/delete <category>/<#>)]%RDeletes a job. Permanently. Not necessary or ideal unless the Category has over 1500 of them.})]%R%R[ansi(hc,Wizard Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+job/catmake <text>)] - Creates a new Category. It's set visible by default.%R[ansi(h,+job/catdelete <name>)] - Deletes a category and ALL JOBS related to it. BAD, DO NOT TOUCH.%R[ansi(h,+job/catrename <text>=<text>)] - Renames a Category.%R[ansi(h,+job/catvisible <category>=<bool>)] - Sets whether a category's visible to players or not. <bool> must be 1 (true) or 0 (false)%R[ansi(h,+job/priority <category>=<number>)] - Sets the priority of a board. Whole numbers, matched by colors on the Job System Object.})]




