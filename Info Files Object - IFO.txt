@@ DEPENDENCIES - CORE

@switch/inline isdbref(u(ifo))=0,{@tel create(Info Files Object <IFO>)=config(master_room)}
&ifo u(coi)=locate(config(master_room),Info Files Object <IFO>,TXxi)
@parent u(ifo)=u(coi)
@set u(ifo)=WIZARD !NO_COMMAND

&CMD`+INFO u(ifo)=$^(?s)(?\:\+)?info(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(ifo)/VAR`PLAYFLAGS),if(isadmin(%#),get(u(ifo)/VAR`ADMINFLAGS)),|,|),|,INFO,switch,switch;@include u(ifo)/INC`[strfirstof(%q<switch>,MAIN)]=%2,%3
@set u(ifo)/CMD`+INFO=regexp

&VAR`PLAYFLAGS u(ifo)=SET|GET|RENAME|DELETE|PRIVATE
&VAR`ADMINFLAGS u(ifo)=LOCK|UNLOCK|LSET|HSET|LHSET|HIDE

&RFN`HEADER u(ifo)=header(%0,,INFO`BORDER,INFO`BORDERDOT,INFO`BORDERTEXT)
&RFN`SUBHEADER u(ifo)=subheader(%0,,INFO`BORDER,INFO`BORDERDOT,INFO`BORDERTEXT)
&RFN`SEPARATOR u(ifo)=separator(%0,,INFO`BORDER,INFO`BORDERDOT,INFO`BORDERTEXT)
&RFN`MSGHEAD u(ifo)=msghead(v(VAR`MSGHEAD))
&VAR`MSGHEAD u(ifo)=INFO

th attrib_set(u(cco-db)/VAR`CATEGORIES,setunion(get(u(cco-db)/VAR`CATEGORIES),INFO,|,|))
&VAR`CATEGORIES`INFO u(cco-db)=BORDER|BORDERDOT|BORDERTEXT|PRIVATE
&COLOR`INFO`PRIVATE u(pco)=hx

&INC`TARGET u(ifo)=@switch/inline strmatch(%0,*/*)=1,{@include u(ccs)/INC`CHECKPC=before(%0,/),1,INFO;th setq(target,%q<t1>)},0,{th setq(target,%#)}

&INC`FILENAME u(ifo)=th setq(filename,if(strmatch(%0,*/*),trim(after(%0,/)),trim(%0)))

&FUN`LIST u(ifo)=if(words(setr(fulllist,filterbool(#lambda/cand(if(getstat(%0/\%0`FLAGS,Private),cor(strmatch(%0,%#),isadmin(%#)),1),if(getstat(%0/\%0`FLAGS,Hidden),isadmin(%#),1)),lattr(%0/D`INFOFILE`*),%b,|)),|),if(%1,sort(iter(%q<fulllist>,get(%0/%i0),|,|),i,|,|),sortkey(#lambda/last(%0,`),%q<fulllist>,i,|,|)))

&FUN`FINDFILE u(ifo)=first(filterbool(#lambda/strmatch(%1,get(%0/\%0)),lattr(%0/D`INFOFILE`*)))

&INC`LOCK u(ifo)=@include u(ifo)/INC`TARGET;@include u(ifo)/INC`FILENAME;@assert strlen(%q<filename>)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Info name empty.;@dolist/inline/nobreak/delimit | %q<filename>={@include u(ifo)/INC`PARTIALMATCH=%i0;@break getstat(%q<target>/%q<attr>`FLAGS,Locked)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Info File %q<filename> is already Locked!;th setstat(%q<target>/%q<attr>`FLAGS,Locked,1);th setstat(%q<target>/%q<attr>`FLAGS,LockedBy,%:);th setstat(%q<target>/%q<attr>`FLAGS,LockedOn,secs());@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=u(RFN`MSGHEAD) You Locked your %q<filename> Info File},{@nspemit %#=u(RFN`MSGHEAD) You Locked [name(%q<target>)]'s %q<filename> Info File!;@nspemit %q<target>=u(RFN`MSGHEAD) %n Locked your %q<filename> Info File!};@switch/inline t(v(VAR`ANNOUNCEAPPROVE))=1,{@nscemit/noisy u(VAR`ANNOUNCECHANNEL)=ansi(h,INFO:) [ansi(h,%n)] just Locked [name(%q<target>)]'s %q<filename> Info File!}}

&INC`UNLOCK u(ifo)=@include u(ifo)/INC`TARGET;@include u(ifo)/INC`FILENAME;@assert strlen(%q<filename>)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Info name empty.;@dolist/inline/nobreak/delimit | %q<filename>={@include u(ifo)/INC`PARTIALMATCH=%i0;@assert getstat(%q<target>/%q<attr>`FLAGS,Locked)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Info File %q<filename> is not Locked!;th setstat(%q<target>/%q<attr>`FLAGS,Locked,0);th delstat(%q<target>/%q<attr>`FLAGS,LockedBy);th delstat(%q<target>/%q<attr>`FLAGS,LockedOn);@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=u(RFN`MSGHEAD) You Unlocked your %q<filename> Info File},{@nspemit %#=u(RFN`MSGHEAD) You Unlocked [name(%q<target>)]'s %q<filename> Info File!;@nspemit %q<target>=u(RFN`MSGHEAD) %n Unlocked your %q<filename> Info File!};@switch/inline t(v(VAR`ANNOUNCEAPPROVE))=1,{@nscemit/noisy u(VAR`ANNOUNCECHANNEL)=ansi(h,INFO:) [ansi(h,%n)] just Unlocked [name(%q<target>)]'s %q<filename> Info File!}}

&VAR`ANNOUNCEAPPROVE u(ifo)=1
&VAR`ANNOUNCECHANNEL u(ifo)=u(CMO`STAFFREP)

&INC`DELETE u(ifo)=@include u(ifo)/INC`TARGET;@include u(ifo)/INC`FILENAME;@assert strlen(%q<filename>)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Info name empty.;@dolist/delimit | %q<filename>={@include u(ifo)/INC`PARTIALMATCH=%i0;@switch/inline cor(strmatch(%#,%q<target>),isadmin(%#))=0,{@break 1=@nspemit %#=u(RFN`MSGHEAD) ERROR: You may not delete another's Info files.};@break cand(getstat(%q<target>/%q<attr>`FLAGS,Locked),not(isadmin(%#)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Locked Infos must be Unlocked by admin first.;@wipe %q<target>/%q<attr>;@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=u(RFN`MSGHEAD) You deleted your %q<filename> Info File},{@nspemit %#=u(RFN`MSGHEAD) You deleted [name(%q<target>)]'s %q<filename> Info File!;@nspemit %q<target>=u(RFN`MSGHEAD) %n deleted your %q<filename> Info File!}}

&INC`PARTIALMATCH u(ifo)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Info name empty.;@include u(ccs)/INC`PARTIAL=%0,u(u(ifo)/FUN`LIST,%q<target>,1),|,INFO,filename,filename;th setq(attr,u(u(ifo)/FUN`FINDFILE,%q<target>,%q<filename>))

&INC`GET u(ifo)=@include u(ifo)/INC`TARGET;@include u(ifo)/INC`FILENAME;@dolist/inline/nobreak/delimit | %q<filename>={@include u(ifo)/INC`PARTIALMATCH=%i0;@nspemit %#=u(RFN`HEADER,Unformatted Contents of %q<filename>);@nspemit %#=decompose(get(%q<target>/%q<attr>`CONTENTS));@nspemit %#=u(RFN`HEADER)}

&INC`MAIN u(ifo)=@include u(ifo)/INC`TARGET;@switch/inline gt(strlen(%1),0)=1,{@include u(ifo)/INC`SET},0,{@include u(ifo)/INC`LIST}

&INC`LIST u(ifo)=@include u(ifo)/INC`FILENAME;@switch/inline gt(strlen(%q<filename>),0)=1,{@include u(ifo)/INC`LIST`CONTENTS},0,{@include u(ifo)/INC`LIST`FILES}

&INC`LIST`FILES u(ifo)=@nspemit %#=u(RFN`HEADER,name(%q<target>)'s Info Files);@nspemit %#=table(iter(u(u(ifo)/FUN`LIST,%q<target>),pueblize(left(switch(1,t(getstat(%q<target>/%i0`FLAGS,Hidden)),ansi(hr,get(%q<target>/%i0)),t(getstat(%q<target>/%i0`FLAGS,Private)),ansi(custcolor(%#,INFO`PRIVATE),get(%q<target>/%i0)),get(%q<target>/%i0)),18),+info [if(strmatch(%#,%q<target>),get(%q<target>/%i0),[name(%q<target>)]/[get(%q<target>/%i0)])])[if(getstat(%q<target>/%i0`FLAGS,Locked),\([ansi(hw,+)]\))],|,|),24,width(%#),|);@nspemit %#=u(RFN`HEADER,Legend: + - Locked)

&INC`LIST`CONTENTS u(ifo)=@dolist/inline/nobreak/delimit | %q<filename>={@include u(ifo)/INC`PARTIALMATCH=%i0;@nspemit %#=u(RFN`HEADER,name(%q<target>)'s [get(%q<target>/%q<attr>)] +info);@nspemit %#=get(%q<target>/%q<attr>`CONTENTS);@switch/inline isadmin(%#)=1,{@nspemit %#=separator(Admin Info)%R[ansi(h,Last set by:)] [name(getstat(%q<target>/%q<attr>`FLAGS,SetBy))] [ansi(h,On:)] [convsecs(getstat(%q<target>/%q<attr>`FLAGS,SetOn))];@switch/inline gt(setr(locked,getstat(%q<target>/%q<attr>`FLAGS,Locked)),0)=1,{@nspemit %#=ansi(h,Locked by:) [name(getstat(%q<target>/%q<attr>`FLAGS,LockedBy))] [ansi(h,On:)] [convsecs(getstat(%q<target>/%q<attr>`FLAGS,LockedOn))]};@switch/inline gt(setr(hidden,getstat(%q<target>/%q<attr>`FLAGS,Hidden)),0)=1,{@nspemit %#=ansi(h,Hidden by:) [name(getstat(%q<target>/%q<attr>`FLAGS,HiddenBy))] [ansi(h,On:)] [convsecs(getstat(%q<target>/%q<attr>`FLAGS,HiddenOn))]}};@nspemit %#=u(RFN`HEADER,if(%q<locked>,Admin Locked File))}

&INC`PRIVATE u(ifo)=@include u(ifo)/INC`TARGET;@include u(ifo)/INC`FILENAME;@assert strlen(%q<filename>)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Info name empty.;@dolist/inline/nobreak/delimit | %q<filename>={@include u(ifo)/INC`PARTIALMATCH=%i0;@assert cor(strmatch(%#,%q<target>),isadmin(%#))=@nspemit %#=u(RFN`MSGHEAD) ERROR: You may not modify another's Info files.;@assert cor(not(getstat(%q<target>/%q<attr>`FLAGS,Locked)),isadmin(%#))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Locked files may only be changed by Admin.;@switch/inline t(getstat(%q<target>/%q<attr>`FLAGS,Private))=1,{th setstat(%q<target>/%q<attr>`FLAGS,Private,0);@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=u(RFN`MSGHEAD) You revealed your %q<filename> Info File},{@nspemit %#=u(RFN`MSGHEAD) You revealed [name(%q<target>)]'s %q<filename> Info File!;@nspemit %q<target>=u(RFN`MSGHEAD) %n revealed your %q<filename> Info File!}},0,{th setstat(%q<target>/%q<attr>`FLAGS,Private,1);@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=u(RFN`MSGHEAD) You hid your %q<filename> Info File},{@nspemit %#=u(RFN`MSGHEAD) You hid [name(%q<target>)]'s %q<filename> Info File!;@nspemit %q<target>=u(RFN`MSGHEAD) %n hid your %q<filename> Info File!}}}

&INC`HIDE u(ifo)=@include u(ifo)/INC`TARGET;@include u(ifo)/INC`FILENAME;@assert strlen(%q<filename>)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Info name empty.;@dolist/inline/nobreak/delimit | %q<filename>={@include u(ifo)/INC`PARTIALMATCH=%i0;@switch/inline t(getstat(%q<target>/%q<attr>`FLAGS,Hidden))=1,{th setstat(%q<target>/%q<attr>`FLAGS,Hidden,0);@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=u(RFN`MSGHEAD) You unhid your %q<filename> Info File},{@nspemit %#=u(RFN`MSGHEAD) You unhid [name(%q<target>)]'s %q<filename> Info File!}},0,{th setstat(%q<target>/%q<attr>`FLAGS,Hidden,1);th setstat(%q<target>/%q<attr>`FLAGS,HiddenBy,%:);th setstat(%q<target>/%q<attr>`FLAGS,HiddenOn,secs());@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=u(RFN`MSGHEAD) You hid your %q<filename> Info File},{@nspemit %#=u(RFN`MSGHEAD) You hid [name(%q<target>)]'s %q<filename> Info File!}}}

&INC`LSET u(ifo)=@include u(ifo)/INC`SET=%0,%1,1
&INC`HSET u(ifo)=@include u(ifo)/INC`SET=%0,%1,0,1
&INC`LHSET u(ifo)=@include u(ifo)/INC`SET=%0,%1,1,1

&INC`SET u(ifo)=@include u(ifo)/INC`TARGET;@include u(ifo)/INC`FILENAME;@assert strlen(%q<filename>)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Info file name empty.;@switch/inline gt(strlen(setr(attr,u(u(ifo)/FUN`FINDFILE,%q<target>,%q<filename>))),0)=1,{@assert cor(strmatch(%#,%q<target>),isadmin(%#))=@nspemit %#=u(RFN`MSGHEAD) ERROR: You may not change another's Info files.;@switch/inline getstat(%q<target>/%q<attr>`FLAGS,Locked)=1,{@assert isadmin(%#)=@nspemit %#=u(RFN`MSGHEAD) ERROR: That Info File may not be changed by you.};@break cand(getstat(%q<target>/%q<attr>`FLAGS,Hidden),not(isadmin(%#)))=@nspemit %#=u(RFN`MSGHEAD) ERROR: This command sealed by order of the Maiden of Secrets.},0,{@break gt(strlen(%q<filename>),18)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Info names are limited to 18 characters or less.;@break charin(%q<filename>,| / \\ %r %t)=@nspemit %#=u(RFN`MSGHEAD) ERROR: The following characters are not permitted in Info file names: |, / \\, linebreaks, and indents.;@assert strlen(%1)=u(RFN`MSGHEAD) ERROR: Text field empty. To delete an +info file, use +info/delete.};&[strfirstof(%q<attr>,setr(attr,D`INFOFILE`[nextslot(%q<target>,D`INFOFILE)]))] %q<target>=%q<filename>;&%q<attr>`CONTENTS %q<target>=%1;th setstat(%q<target>/%q<attr>`FLAGS,SetBy,%:);th setstat(%q<target>/%q<attr>`FLAGS,SetOn,secs());@switch/inline t(%2)=1,{th setstat(%q<target>/%q<attr>`FLAGS,Locked,1);th setstat(%q<target>/%q<attr>`FLAGS,LockedBy,%:);th setstat(%q<target>/%q<attr>`FLAGS,LockedOn,secs())};@switch/inline t(%3)=1,{th setstat(%q<target>/%q<attr>`FLAGS,Hidden,1);th setstat(%q<target>/%q<attr>`FLAGS,HiddenBy,%:);th setstat(%q<target>/%q<attr>`FLAGS,HiddenOn,secs())};@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=u(RFN`MSGHEAD) You [if(%2,lock)]set your %q<filename> Info File},{@nspemit %#=u(RFN`MSGHEAD) You [if(%2,lock)]set [name(%q<target>)]'s %q<filename> Info File!;@switch/inline %3=0,{@nspemit %q<target>=u(RFN`MSGHEAD) %n [if(%2,lock)]set your %q<filename> Info File!}}

@@ CHARACTER - +INFO
+help/addmain Character/+info=[u(ifo)]/HLP`+INFO
&HLP`+INFO u(ifo)=The Info system allows Players to store notes about their character. These notes can be used for a number of things, such as tracking details of resources, backgrounds, cheat sheets, or other notes that might be several paragraphs in length. By default they are visible to all players.%R%R[ansi(hc,Concepts)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,infonames)] - Info files can't be very long, may not contain special characters, and are not case sensitive.%R[ansi(h,filelists)] - Some commands let you specify a list of files. Seperate their names with | symbols, such as file1|file2.%R[ansi(h,private)] - Private files are visible only to you and staff.%R[ansi(h,locked)] - Locked files have been verified by staff and are read-only.})]%R%R[ansi(hc,Managing Infos)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+info <infoname>=<text>)] - Creates or replaces an info file.%R[ansi(h,+info/delete <filelist>)] - Deletes a file or series of files.%R[ansi(h,+info/private <filelist>)] - Hides (or reveals hidden) info files.})]%R%R[ansi(hc,Viewing Infos)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+info)] - Show all of your Info files.%R[ansi(h,+info <filelist>)] - Display an info's contents.%R[ansi(h,+info <player>/)] - Show a player's visible files. The / distinguishes between filename and playername!%R[ansi(h,+info <player>/<filelist>)] - Display a player's info contents.%RUse the /get switch as you would reading files to retrieve a decompile'd version for easy editing.})]

+shelp/addmain Character/+info=[u(ifo)]/SHLP`+INFO
&SHLP`+INFO u(ifo)=Staff have many additional abilities with +info. For starters, Staff may target ANY info command on ANY player by using <player>/<infoname> where a player would just use <infoname>, and can see every Info file - hidden or not. This means staff can create and edit info files, hide them, delete them, etc. In addition, Staff see extra information regarding who set and locked info files when they view them and can use the following commands:%R%R[ansi(hc,Staff Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+info/lock <player>/<filelist>)] - Lock one or more info files.%R[ansi(h,+info/unlock <player>/<filelist>)] - Unlock one or more info files.%R[ansi(h,+info/hide <player>/<filelist>)] - Hide one or more info files so players can't see them. use again to unhide.%R[ansi(h,+info/lset <player>/<file>=<text>)] - Set and lock a file simultaneously.%R[ansi(h,+info/lhset <player>/<file>=<text>)] - Set, lock, and hide a file simultaneously.%R%RHidden files cannot be seen or targeted by players. If they attempt to set one's text by luck or other means, they will receive a mysterious error message.})]