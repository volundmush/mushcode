
@tel create(Group System Object <GSO>)=config(master_room)
&gso u(coi)=locate(config(master_room),Group System Object <GSO>,TXxi)
@parent u(gso)=u(coi)
@set u(gso)=WIZARD !NO_COMMAND

@tel create(Group Database Object <GDB>)=u(gso)
&gdb u(coi)=locate(u(gso),Group Database Object <GDB>,TXxi)
@parent u(gdb)=u(coi)
@set u(gdb)=WIZARD
@power u(gdb)=many_attribs

&CMD`+GROUP u(gso)=$^(?s)(?\:\+)?g(roups|roup|create|rename|alias|set|div|undiv|gag|ungag|ictitle|ooctitle|invite|invites|uninvite|join|refuse|add|leave|kick|auth|rank|icon|icoff|oocon|oocoff|disband|ooc|radio|ic)(?\:/(\s+))?(?\: +(.+?))?(?\:=(.*))?$:@include u(gso)/INC`CHOOSE=+g,%1,%2,%3,%4
@set u(gso)/CMD`+GROUP=regexp

&INC`CHOOSE u(gso)=@switch/regexp/inline 1=strmatch(%0,+g),{@assert or(not(strlen(%2)),match(setr(choices,setunion(get(u(gso)/VAR`PLAYFLAGS`G`%1),if(isadmin(%#),get(u(gso)/VAR`ADMINFLAGS`G`%1)),|,|)),ucstr(%2),|))=@nspemit %#=announce(GROUPS)%BERROR: Invalid switch for %0%1! Your choices are: [itemize(%q<choices>,|,and,\,)];@include u(gso)/INC`CHOOSE`G`%1=%2,%3,%4,G},{@nspemit %#=announce(GROUPS)%BERROR: Unknown code error. Please contact staff.}

&INC`CHOOSE`G`CREATE u(gso)=@include u(gso)/INC`CODE`CREATE=%1,%2,%3

&INC`CODE`CREATE u(gso)=@assert isadmin(%#)=@nspemit %#=announce(GROUPS)%BERROR: Only Admin can make Groups.;@assert strlen(%0)=@nspemit %#=announce(GROUPS)%BERROR: Alias field empty.;@assert regmatchi(%0,^(\[A-Z\]\{1,7\})$)=@nspemit %#=announce(GROUPS)%BERROR: Aliases may only be seven or fewer alphabetic characters (A-Z) with no spaces or symbols.;@switch/inline gt(v(VAR`CODE`PERNMODE),0)=1,{@break strmatch(sqlify(%0),DTU)=@nspemit %#=announce(GROUPS)%BERROR: DTU is an unacceptable ALIAS.};@break gt(words(setr(exist,u(u(gso)/FUN`FINDGROUP,%0))),0)=@nspemit %#=announce(GROUPS)%BERROR: A Group by that Alias already exists: [get(u(gdb)/%q<exist>)] - [get(u(gdb)/%q<exist>`NAME)];@assert strlen(%1)=@nspemit %#=announce(GROUPS)%BERROR: Name field empty.;@include u(gso)/INC`CODE`DOCREATE=%0,%1;@nspemit %#=announce(GROUP) You have created the [ansi(hw,%q<galias1> - %q<gname1>)];@include u(gso)/INC`CODE`MAKELOG=%:,%q<gid1>,{The group was created.}

&FUN`FINDGROUP u(gso)=first(filterbool(#lambda/isint(\%0),before(strfirstof(first(grepi(u(gdb),*`NAME,%0)),first(grepi(u(gdb),*,%0))),`)))

&VAR`DEFAULTS`SETAUTH u(gso)=DESC~0|TYPE~0|MOTD~0|DIV~0|INV~0|KICK~0|ADD~0|IC~10|OOC~10|PROMOTE~0|GAG~0|SET~0|AUTH~0|ICTITLE~0|ICTITLEOTHER~0|OOCTITLE~1|OOCTITLEOTHER~0

&VAR`CODE`PERNMODE u(gso)=0

&VAR`DEFAULTS`SETOP u(gso)=IC~1|OOC~1|SRANK~1|COLOR~h

&INC`CODE`DOCREATE u(gso)=@assert strlen(%0);@assert strlen(%1);&[setr(gid1,add(1,lmath(max,lattr(u(gdb)/*))))] u(gdb)=setr(glias1,%0);&%q<gid1>`NAME u(gdb)=setr(gname1,%1);th setstat(%#/D`GROUP,Group,%q<gid1>)

&INC`CODE`MAKELOG u(gso)=&[setr(log,%1`LOG`[add(1,lmath(max,iter(lattr(u(gdb)/%1`LOG`*),last(%i0,`))))])] u(gdb)=%2;&%q<log>`SUBMITTER u(gdb)=%0;&%q<log>`SUBMITTED u(gdb)=secs()

&INC`CODE`VALGROUP u(gso)=@assert strlen(%0)=@nspemit %#=announce(GROUPS)%BERROR: No group entered to check.;@assert valnum(%1)=@nspemit %#=announce(GROUPS)%BERROR: Code error, contact staff.;@switch/inline gt(setr(gid%1,firstof(u(u(gso)/FUN`FINDGROUP,%0),if(hasattr(u(gdb)/%0),%0,0))),0)=0,{@switch/inline gt(setr(gid%1,u(u(gso)/FUN`FINDGROUP,grab(iter(lattr(u(gdb)/*,|),get(u(gdb)/%i0),|,|),%0*,|))),0)=0,{@assert gt(setr(gid%1,u(u(gso)/FUN`FINDGROUP,grab(iter(lattr(u(gdb)/*`NAME,|),get(u(gdb)/%i0),|,|),%0*,|))),0)=@nspemit %#=announce(GROUPS)%BERROR: Group or Sub-Group not found.}};th setq(gname%1,get(u(gdb)/%q<gid%1>),galias%1,get(u(gdb)/%q<gid%1>))

&INC`CHOOSE`G`RENAME u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`RENAME=%1,%2,%3
&INC`CODE`RENAME u(gso)=@assert isgroupmember(%:,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: You are not a member of %q<gname1>.;@assert u(u(gso)/FUN`GRPAUTH,%:,%q<gid1>,RENAME)=@nspemit %#=announce(GROUPS)%BERROR: You lack permission to do that.;@assert strlen(%0)=@nspemit %#=announce(GROUPS)%BERROR: Name field empty.;@break words(grepi(u(gdb)/*`NAME,%0))=@nspemit %#=announce(GROUPS)%BERROR: A Group already has that name.;&%q<gid1>`NAME u(gdb)=%0;@include u(gso)/INC`CODE`MAKELOG=%:,q<gid1>,{Group renamed to: %0};@include u(gso)/INC`CODE`CHAT={%n Renamed the Group to %0},OOC,1

&INC`CHOOSE`G`ALIAS u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`ALIAS=%1,%2,%3
&INC`CODE`ALIAS u(gso)=@assert isadmin(%#)=@nspemit %#=announce(GROUPS)%BERROR: Only Admin may re-Alias Groups.;@assert strlen(%0)=@nspemit %#=announce(GROUPS)%BERROR: ALIAS field empty.;@assert regmatchi(%0,^(\[A-Z\]\{1,5\})$)=@nspemit %#=announce(GROUPS)%BERROR: Aliases may only be 7 or fewer alphabetic characters (A-Z) with no spaces or symbols.;@break words(grepi(u(gdb)/*,%0))=@nspemit %#=announce(GROUPS)%BERROR: A Group already has that Alias.;&%q<gid1> u(gdb)=ucstr(%0);@include u(gso)/INC`CODE`MAKELOG=%:,%q<gid1>,{Alias changed to: %0};@include u(gso)/INC`CODE`CHAT={%n changed the Group's Alias to [ucstr(ansi(hw,%0))]},OOC,1

&GFN`ISGROUPMEMBER u(gso)=if(isadmin(%0),1,if(valnum(%1),match(get(u(gdb)/%1`MEMBERS),%0),if(eq(0,%1),if(u(u(gso)/FUN`FINDGROUP,%1),isgroupmember(%0,u(u(gso)/FUN`FINDGROUP,%1)),0),0)))

&INC`CHOOSE`G`DISBAND u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`DISBAND=%1,%2,%3
&INC`CODE`DISBAND u(gso)=@assert isadmin(%#)=@nspemit %#=announce(GROUPS)%BERROR: Only Staff may Disband Groups.;@include u(ccs)/INC`VERIFY={WARNING: This will completely disband %q<gname1> and all Divisions. ARe you sure you want to continue? Enter the same command again within 10 seconds to Verify!},FAC DELETE %q<gid1>;@include u(gso)/INC`CODE`CHAT={ansi(hw,ALERT:) %n has Disbanded the Group!},OOC,1;@dolist lsearch(all,eplayer,\[hasattr(##/D`GROUP`%q<gid1>)\])={@wipe %i0/D`GROUP`%q<gid1>};@set u(gdb)=!SAFE;@wipe u(gdb)/%q<gid1>;@set u(gdb)=SAFE

&INC`CHOOSE`G`DIV u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`DIV=%1,%2,%3
&INC`CODE`DIV u(gso)=@assert isadmin(%#)=@nspemit %#=announce(GROUPS)%BERROR: Need WRITE permission to Division groups.;@include u(gso)/INC`CODE`VALGROUP=%0,2;@break strmatch(setr(errcheck,u(v(sql)/QUERY,GROUP`DIV,%q<gid2>,%q<gid1>)),#-*)=@nspemit %#=SQL ERROR: %q<errcheck>;@nspemit %#=[u(u(ccs)/FUN`ANNOUNCE,GROUP)] You made [ansi(hw,%q<gname1>)] a division of [ansi(hw,%q<grout1name>)].;@include u(gso)/INC`CODE`MAKELOG=%q<gid1>,%q<galias1>,%q<gname1>,%:,{%q<gname1> was made a division of %q<grout1name>};@include u(gso)/INC`CODE`CHAT={%N has joined %q<gname1> and %q<grout1name>},OOC,1

&INC`CHOOSE`G`UNDIV u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`UNDIV=%1,%2,%3
&INC`CODE`UNDIV u(gso)=@assert isadmin(%#)=@nspemit %#=announce(GROUPS)%BERROR: Need WRITE permission to seperate Groups.;@assert gt(setr(divid,sql(SELECT DIVISON_OF FROM GROUP_MAIN WHERE GROUP_ID=%q<gid1>)),0)=@nspemit %#=That group is not a division.;@include u(gso)/INC`CODE`VALGROUP=%q<divid>,2;@break strmatch(setr(errcheck,u(v(sql)/QUERY,GROUP`UNDIV,%q<gid1>)),#-*)=@nspemit %#=SQL ERROR: %q<errcheck>;@include u(gso)/INC`CODE`CHAT={%N has seperated %q<gname1> from %q<grout1name>},OOC,1;@include u(gso)/INC`CODE`MAKELOG=%q<gid1>,%q<galias1>,%q<gname1>,%:,{%q<gname1> was made standalone.}

&FUN`GRPAUTH u(gso)=if(isadmin(%0),1,lte(u(u(gso)/FUN`GETRANK,%1,%0),u(u(gso)/FUN`GETAUTH,%1,%2)))

&FUN`GETRANK u(gso)=if(isadmin(%0),-1,strfirstof(getstat(u(gdb)/%1`RANK,%0),getstat(u(gso)/VAR`DEFAULTS`SETOP,SRANK)))

&FUN`DIVAUTH u(gso)=or(isadmin(%#),if(setr(did,sql(SELECT DIVISON_OF FROM GROUP_MAIN WHERE GROUP_ID=%1)),if(lte(strfirstof(sql(SELECT RANK FROM GROUP_MEM WHERE GROUP_ID=%q<did> AND PC_ID=%0),99),sql(SELECT OP_CONTENTS FROM GROUP_MAIN_AUTH WHERE GROUP_ID=%q<did> AND OP_TYPE='DIV')),if(setr(ndid,sql(SELECT DIVISION_OF FROM GROUP_MAIN WHERE GROUP_ID=%q<did>)),u(u(gso)/FUN`DIVAUTH,%0,%q<ndid>),0),0),0))

&INC`CHOOSE`G`GAG u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`GAG=%1,%2,%3
&INC`CODE`GAG u(gso)=@assert isgroupmember(%:,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: You are not a member of %q<gname1>.;@assert u(u(gso)/FUN`GRPAUTH,%:,%q<gid1>,GAG)=@nspemit %#=announce(GROUPS)%BERROR: You lack permission to do that.;@include u(ccs)/INC`CHECKPC=%0,1,GROUPS;@assert isgroupmember(%q<t1objid>,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: Must target a group member.;@assert lt(u(u(gso)/FUN`GETRANK,%q<gid1>,%:),u(u(gso)/FUN`GETRANK,%q<gid1>,%q<t1objid>))=@nspemit %#=announce(GROUPS)%BERROR: Only subordinates may be gagged.;@break gt(getstat(u(gdb)/%q<gid1>`GAG,%q<t1objid>),-2)=@nspemit %#=announce(GROUPS)%B%q<t1name> is already gagged.;th setstat(u(gdb)/%q<gid1>`GAG,%q<1objid>,u(u(gso)/FUN`GETRANK,%q<gid1>,%:))

&INC`CHOOSE`G`UNGAG u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`UNGAG=%1,%2,%3
&INC`CODE`UNGAG u(gso)=@assert isgroupmember(%:,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: You are not a member of %q<gname1>.;@assert u(u(gso)/FUN`GRPAUTH,%:,%q<gid1>,GAG)=@nspemit %#=announce(GROUPS)%BERROR: You lack permission to do that.;@include u(ccs)/INC`CHECKPC=%0,1,GROUPS;@assert isgroupmember(%q<t1objid>,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: Must target a group member.;@assert lt(u(u(gso)/FUN`GETRANK,%q<gid1>,%:),u(u(gso)/FUN`GETRANK,%q<gid1>,%q<t1objid>))=@nspemit %#=announce(GROUPS)%BERROR: Only subordinates may be ungagged.;@assert lte(u(u(gso)/FUN`GETRANK,%q<gid1>,%:),setr(gagger,getstat(u(gdb)/%q<gid1>`GAG,%q<t1objid>)))=@nspemit %#=announce(GROUPS)%BERROR: That person was Gagged by Rank: %q<gagger>;th delstat(u(gdb)/%q<gid1>`GAG,%q<t1objid>)

&INC`CHOOSE`G`ROUP u(gso)=@switch/inline gt(strlen(%1),0)=1,{@include u(gso)/INC`CODE`SWITCH=%1,%2,%3,%4,%5,%6,%7,%8},0,{@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`WHO=%1,%2,%3}

&INC`CHOOSE`G`WHO u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`WHO=%1,%2,%3

&INC`CODE`SWITCH u(gso)=@include u(gso)/INC`CODE`VALGROUP=%0,1;@include u(gso)/INC`CODE`WHO=0,1;@switch/inline isgroupmember(%:,%q<gid1>)=1,{@nspemit %#=announce(GROUPS) You are now managing: [ansi(hw,%q<gname1>)];th setstat(%#/D`GROUP,Group,%q<gid1>)}

&INC`CODE`WHO u(gso)=@nspemit %#=header(MEMBERS: %q<gname1>);@switch/inline and(gt(strlen(setr(type,getstat(u(gdb)/%q<gid1>`TYPE))),0),hasattr(u(gso)/INC`CODE`WHO`%q<type>))=1,{@include u(gso)/INC`CODE`WHO`%q<type>},0,{@nspemit %#=ljust(NAME,30)SEX%B[ljust(RANK,5)][ljust(TITLE,20)]%BLAST ON%R[subheader()];th iter(sortkey(#lambda/u(u(gso)/FUN`GETRANK,%q<gid1>,\%0),filterbool(#lambda/isobjid(\%0),get(u(gdb)/%q<gid1>`MEMBERS))),nspemit(%#,ljust(pueblize(name(%i0),+finger [name(%i0)]),30)[center(left(get(%i0/SEX),1),3)][ljust(rjust(u(u(gso)/FUN`GETRANK,%q<gid1>,%i0),3),6)][ljust(left(getstat(%i0/D`GROUP`%q<gid1>,ICTITLE),20),24)][if(gte(conn(%i0),0),ansi(hg,ago(idle(%i0))),ansi(hx,elements(get(%i0/LAST),2 3)))]))};@nspemit %#=header()

@@ @switch/inline gt(sql(SELECT count\(GROUP_ID\) FROM GROUP_MAIN WHERE DIVISION_OF=%q<gid1>),0)=1,{@nspemit %#=[header(Divisions)]%R[ljust(ansi(h,Alias),10)][ljust(ansi(h,Name),25)][ljust(ansi(h,Type),15)][ansi(h,Members)];th iter(lnum(1,ceil(fdiv(sql(SELECT count\(GROUP_ID\) from GROUP_MAIN WHERE DIVISION_OF=%q<gid1>)),30)),nspemit(%#,iter(sql(SELECT GROUP_ID\,ALIAS\,NAME FROM GROUP_MAIN WHERE DIVISION_OF=%q<gid1> ORDER BY ALIAS LIMIT 30 OFFSET [mul(sub(inum(0),1),10)],|,^),ljust(pueblize(ansi(strfirstof(sql(SELECT OP_CONTENTS FROM GROUP_MAIN_OP WHERE OP_TYPE='COLOR' AND GROUP_ID=[elements(%i0,1,^)]),h),elements(%i0,2,^)),=[elements(%i0,2,^)]/who),10)[ljust(pueblize(ansi(strfirstof(sql(SELECT OP_CONTENTS FROM GROUP_MAIN_OP WHERE OP_TYPE='COLOR' AND GROUP_ID=[elements(%i0,1,^)]),h),elements(%i0,3,^)),=[elements(%i0,2,^)]/who),25)][ljust(sql(SELECT OP_CONTENTS FROM GROUP_MAIN_OP WHERE OP_TYPE='TYPE' AND GROUP_ID=[elements(%i0,1,^)]),15)][words(filterbool(#lambda/hasflag(\%0,CONNECTED),sql(SELECT DISTINCT PC_MAIN.OBJID FROM PC_MAIN INNER JOIN GROUP_MEM ON GROUP_MEM.PC_ID=PC_MAIN.PC_ID WHERE GROUP_MEM.GROUP_ID=[elements(%i0,1,^)])))]/[sql(SELECT count\(PC_ID\) FROM GROUP_MEM WHERE GROUP_ID=[elements(%i0,1,^)])],|,%R)))};@nspemit %#=[header()]

&INC`CHOOSE`G`ROUPS u(gso)=@include u(gso)/INC`CODE`GROUPLIST

&FUN`GETOP u(gso)=strfirstof(getstat(u(gdb)/%0`OP,%1),getstat(u(gso)/VAR`DEFAULTS`SETOP,%1))

&INC`CODE`GROUPLIST u(gso)=@nspemit %#=header(List of Groups)%R[ljust(ansi(h,Alias),10)][ljust(ansi(h,Name),40)][ljust(ansi(h,Type),15)][ansi(h,Members)];th iter(filterbool(#lambda/isint(\%0),lattr(u(gdb)/*)),nspemit(%#,ljust(pueblize(ansi(setr(color,strfirstof(u(u(gso)/FUN`GETOP,%i0,COLOR),h)),get(u(gdb)/%i0)),+group [get(u(gdb)/%i0)]),10)[ljust(pueblize(ansi(%q<color>,get(u(gdb)/%i0`NAME)),+group [get(u(gdb)/%i0)]),40)][ljust(get(u(gdb)/%i0`TYPE),15)][words(filterbool(#lambda/hasflag(\%0,CONNECTED),setr(members,filterbool(#lambda/isobjid(\%0),get(u(gdb)/%i0`MEMBERS)))))]/[words(%q<members>)]));@nspemit %#=header()

&INC`CHOOSE`G`SET u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`SET=%1,%2,%3
&INC`CODE`SET u(gso)=@switch/inline gt(strlen(%0),0)=1,{@assert u(u(gso)/FUN`GRPAUTH,%:,%q<gid1>,SET)=@nspemit %#=announce(GROUPS)%BERROR: You lack permission to do that.;@assert strlen(%0)=@nspemit %#=announce(GROUPS)%BERROR: Option field empty.;@assert or(match(setr(choices,get(u(gso)/VAR`OPTIONS`SETOP)),setr(c,ucstr(%0)),|),isadmin(%#))=@nspemit %#=announce(GROUPS)%BERROR: That is not a valid choice. Your options are: [itemize(%q<choices>,|,and,\,)];th setstat(u(gdb)/%q<gid1>`OP,%qc,%1);@include u(gso)/INC`CODE`MAKELOG=%:,%q<gid1>,{Option %qc: %1};@nspemit %#=announce(GROUP) You have changed %q<gname1>'s %qc OPTION to: %1;@include u(gso)/INC`CODE`CHAT={ansi(hw,ALERT:) %n changed %qc OPTION to: %1},OOC,1},0,{@nspemit %#=header(%q<galias1> - SET);th setq(spaces,add(2,lmath(max,iter(setr(fields,setunion(iter(get(u(gso)/VAR`DEFAULTS`SETOP),before(%i0,~),|,|),iter(get(u(gdb)/%q<gid1>`OP),before(%i0,~),|,|))),strlen(%i0)))));@nspemit %#=table(iter(%q<fields>,ljust(rjust(if(match(get(u(gso)/VAR`MAIN`SETOP),%i0,|),ansi(h,%i0),ansi(hr,%i0)),%q<spaces>),%q<space>):%B[left(u(u(gso)/FUN`GETOP,%q<gid1>,%i0),10)],,|),38,78,|);@nspemit %#=%R%R[ansi(h,AVAILABLE OPTIONS:)] [itemize(get(u(gso)/VAR`OPTIONS`SETOP),|,and,\,)];@nspemit %#=header()}


&INC`CHOOSE`G`ICTITLE u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`ICTITLE=%1,%2,%3
&INC`CHOOSE`G`OOCTITLE u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`OOCTITLE=%1,%2,%3

&INC`CODE`ICTITLE u(gso)=@include u(gso)/INC`CODE`DOTITLE=%0,%1,IC
&INC`CODE`OOCTITLE u(gso)=@include u(gso)/INC`CODE`DOTITLE=%0,%1,OOC
&INC`CODE`DOTITLE u(gso)=@assert isgroupmember(%:,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: You are not a member of %q<gname1>.;@assert u(u(gso)/FUN`GRPAUTH,%:,%q<gid1>,%2TITLE[if(and(strlen(%0),strlen(%1)),OTHER)])=@nspemit %#=announce(GROUPS)%BERROR: You lack permission to do that.;@switch/inline and(strlen(%0),strlen(%1))=1,{@include u(ccs)/INC`CHECKPC=%0,1,GROUPS;@assert isgroupmember(%q<t1objid>,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: Must target a group member.;th setq(target,%q<t1objid>);@assert strlen(setr(title,%1))=@nspemit %#=announce(GROUPS)%BERROR: What title will you bestow?},0,{th setq(target,%:);@assert strlen(setr(title,%0))=@nspemit %#=announce(GROUPS)%BERROR: What will you title yourself?};@break gt(%q<title>,20)=@nspemit %#=announce(GROUPS)%BERROR: Titles may not exceed 20 characters in length.;th setstat(%q<target>/D`GROUP`%q<gid1>,%2TITLE,%q<title>);@nspemit %#=announce(GROUPS) You have set [if(strmatch(%:,%q<target>),your,%q<t1name>'s)] %2TITLE in [ansi(hw,%q<gname1>)] to: %1;@switch/inline strmatch(%#,%q<t1>)=0,{@nspemit %q<t1>=announce(GROUPS) %n has set your %2TITLE in [ansi(hw,%q<gname1>)] to: %1};@include u(gso)/INC`CODE`MAKELOG=%:,%q<gid1>,{if(strmatch(%:,%q<target>),%n,%q<t1name>)'s %2TITLE set to: %1}

&INC`CHOOSE`G`ICON u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`CHANON=%1,-
&INC`CHOOSE`G`OOCON u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`CHANON=%1,=

&INC`CODE`CHANON u(gso)=@assert isgroupmember(%:,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: You are not a member of that group.;@break gt(getstat(%#/D`GROUP`%q<gid1>,setr(type,if(strmatch(%1,=),OOC,IC))),0)=@nspemit %#=announce(GROUPS)%BERROR: You are already on that group's %q<type> Channel.;th setstat(%#/D`GROUP`%q<gid1>,%q<type>,1);@include u(gso)/INC`CODE`CHAT={ansi(hw,ALERT:) %n has joined the channel.},if(strmatch(%1,=),OOC,IC),1;@nspemit %#=announce(GROUPS) You have joined the [if(strmatch(%1,=),OOC,IC)] channel of %q<gname1> 

&INC`CHOOSE`G`ICOFF u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`CHANOFF=%1,-
&INC`CHOOSE`G`OOCOFF u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`CHANOFF=%1,=
&INC`CODE`CHANOFF u(gso)=@assert isgroupmember(%:,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: You are not a member of that group.;@assert gt(getstat(%#/D`GROUP`%q<gid1>,setr(type,if(strmatch(%1,=),OOC,IC))),0)=@nspemit %#=announce(GROUPS)%BERROR: You are not on that group's %q<type> Channel.;th setstat(%#/D`GROUP`%q<gid1>,%q<type>,0);@include u(gso)/INC`CODE`CHAT={ansi(hw,ALERT:) %n has left the channel.},%q<type>,1;@nspemit %#=announce(GROUPS) You have left the %q<type> channel of %q<gname1>

&FUN`GETAUTH u(gso)=strfirstof(getstat(u(gdb)/%0`AUTH,%1),getstat(u(gso)/VAR`DEFAULTS`SETAUTH,%1))

&INC`CHOOSE`G`AUTH u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`AUTH=%1,%2,%3
&INC`CODE`AUTH u(gso)=@switch/inline gt(strlen(%0),0)=1,{@assert isgroupmember(%:,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: You are not a member of that group.;@assert u(u(gso)/FUN`GRPAUTH,%:,%q<gid1>,AUTH)=@nspemit %#=announce(GROUPS)%BERROR: You lack permission to do that.;@assert strlen(%0)=@nspemit %#=announce(GROUPS)%BERROR: No Auth type entered.;@assert or(match(setr(choices,setunion(iter(get(u(gso)/VAR`DEFAULTS`SETAUTH),before(%i0,~),|,|),iter(get(u(gdb)/%q<gid1>`AUTH),before(%i0,~),|,|),|,|)),%0,|),isadmin(%#)) Your choices are: [itemize(%q<choices>,|,and,\,)];@assert or(lte(setr(rank,u(u(gso)/FUN`GETRANK,%q<gid1>,%:)),getstat(u(gdb)/%q<gid1>`AUTHSET,%2)),isadmin(%#))=@nspemit %#=announce(GROUPS)%BERROR: That was set by one of higher rank.;@assert strlen(%1)=@nspemit %#=announce(GROUPS)%BERROR: No new rank value entered.;@assert isint(%1)=@nspemit %#=announce(GROUPS)%BERROR: Ranks must be whole numbers.;@assert or(lte(%q<rank>,%1),isadmin(%#))=@nspemit %#=announce(GROUPS)%BERROR: Cannot set the requirement higher than your own rank.;th setstat(u(gdb)/%q<gid1>`AUTH,%0,%1);th setstat(u(gdb)/%q<gid1>`AUTHBY,%0,bound(%q<rank>,0));@include u(gso)/INC`CODE`MAKELOG=%:,%q<gid1>,{Changed %q<galias1> [ucstr(%0)] AUTH to: %1};@include u(gso)/INC`CODE`CHAT={ansi(hw,ALERT:) %n changed [ucstr(%0)] AUTH to: %1},OOC,1},0,{@nspemit %#=header(%q<galias1> - AUTH);th setq(spaces,add(2,lmath(max,iter(setr(fields,setunion(setr(auths,iter(get(u(gso)/VAR`DEFAULTS`SETAUTH),before(%i0,~),|,|)),iter(get(u(gdb)/%q<gid1>`AUTH),before(%i0,~),|,|),|,|)),strlen(%i0),|))));@nspemit %#=table(iter(%q<fields>,ljust(rjust(if(match(iter(get(u(gso)/VAR`DEFAULTS`SETAUTH),before(%i0,~),|,|),%i0,|),ansi(h,%i0),ansi(hr,%i0)),%q<spaces>),%q<space>):%B[u(u(gso)/FUN`GETAUTH,%q<gid1>,%i0)],|,|),38,78,|);@nspemit %#=%R[ansi(h,AVAILABLE OPTIONS:)] [itemize(%q<auths>,|,and,\,)];@nspemit %#=header()}

&INC`CHOOSE`G`INVITE u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`INVITE=%1,%2,%3
&INC`CODE`INVITE u(gso)=@assert isgroupmember(%:,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: You are not a member of that group.;@assert u(u(gso)/FUN`GRPAUTH,%:,%q<gid1>,INV)=@nspemit %#=announce(GROUPS)%BERROR: You do not have permission to do that.;@switch/inline gt(strlen(%0),0)=1,{@include u(ccs)/INC`CHECKPC=%0,1,GROUPS;@break isgroupmember(%q<t1objid>,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: They are already a member of the Group.;@break match(get(u(gdb)/%q<gid1>`INVITES),%q<t1objid>)=@nspemit %#=announce(GROUPS)%BERROR: %q<t1name> has already been invited to %q<gname1>.;&%q<gid1>`INVITES u(gdb)=setunion(get(u(gdb)/%q<gid1>`INVITES),%q<t1objid>);@nspemit %q<t1>=%n has invited you to join the %q<gname1>! To accept, type [pueblize(ansi(h,+gjoin %q<galias1>),+gjoin %q<galias1>)] or Refuse using [pueblize(ansi(h,+grefuse %q<galias1>),+grefuse %q<galias1>)].;@include u(gso)/INC`CODE`MAKELOG=%:,%q<gid1>,{Invite sent to %q<t1name>.};@nspemit %#=announce(GROUPS) You have invited %q<t1name> to the %q<gname1>},0,{@include u(gso)/INC`MAIN`SHOWINVITES}

&INC`CHOOSE`G`UNINVITE u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`UNINVITE=%1,%2,%3
&INC`CODE`UNINVITE u(gso)=@assert isgroupmember(%:,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: You are not a member of that group.;@assert u(u(gso)/FUN`GRPAUTH,%:,%q<gid1>,INV)=@nspemit %#=announce(GROUPS)%BERROR: You do not have permission to do that.;@switch/inline gt(strlen(%0),0)=1,{@include u(ccs)/INC`CHECKPC=%0,1,GROUPS;@break isgroupmember(%q<t1objid>,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: They're already a member of the group.;@break match(get(u(gdb)/%q<gid1>`INVITES),%q<t1objid>)=@nspemit %#=announce(GROUPS)%BERROR: %q<t1name> has not been invited to %q<gname1>.;&%q<gid1>`INVITES u(gdb)=setdiff(get(u(gdb)/%q<gid1>`INVITES),%q<t1objid>);@nspemit %q<t1>=announce(GROUPS) %n withdrew your invitation to join the %q<gname1>!;@include u(gso)/INC`CODE`MAKELOG=%:,%q<gid1>,{Invite withdrawn from %q<t1name>.}},0,{@include u(gso)/INC`MAIN`SHOWINVITES}

&INC`MAIN`SHOWINVITES u(gso)=@nspemit %#=header(%q<gname1>'s Pending Invites);@nspemit %#=itemize(iter(setr(invites,filterbool(#lambda/isobjid(\%0),get(u(gdb)/%q<gid1>`INVITES))),name(%i0),%b,|),|,and,\,);@nspemit %#=header();&%q<gid1>`INVITES u(gdb)=%q<invites>

&INC`CHOOSE`G`ADD u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`ADD=%1,%2,%3
&INC`CODE`ADD u(gso)=@assert isgroupmember(%:,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: You are not a member of that group.;@assert u(u(gso)/FUN`GRPAUTH,%:,%q<gid1>,ADD)=@nspemit %#=announce(GROUPS)%BERROR: You do not have permission to do that.;@include u(ccs)/INC`CHECKPC=%0,1,GROUPS;@break match(get(u(gdb)/%q<gid1>`MEMBERS),%q<t1objid>)=@nspemit %#=announce(GROUPS)%BERROR: They are already a member of the Group.;&%q<gid1>`MEMBERS u(gdb)=setunion(get(u(gdb)/%q<gid1>`MEMBERS),%q<t1objid>);@nspemit %q<t1>=announce(GROUPS) %n added you to the %q<gname1> Group!;th setstat(%q<t1>/D`GROUP,Group,%q<gid1>);th setstat(%q<t1>/D`GROUP`%q<gid1>,IC,1);th setstat(%q<t1>/D`GROUP`%q<gid1>,OOC,1);@include u(gso)/INC`CODE`MAKELOG=%:,%q<gid1>,{Added %q<t1name>.};@include u(gso)/INC`CODE`CHAT={ansi(hw,ALERT:) %n added %q<t1name> to the Group!},OOC,1

&INC`CHOOSE`G`JOIN u(gso)=@switch/inline gt(strlen(%1),0)=1,{@include u(gso)/INC`CODE`VALGROUP=%1,1;@include u(gso)/INC`CODE`JOIN=%1,%2,%3},0,{@include u(gso)/INC`CODE`MYINVITES}
&INC`CODE`JOIN u(gso)=@break match(get(u(gdb)/%q<gid1>`MEMBERS),%:)=@nspemit %#=announce(GROUPS)%BERROR: You are already a member of the Group.;@assert match(get(u(gdb)/%q<gid1>`INVITES),%:)=@nspemit %#=announce(GROUPS)%BERROR: You have not been invited to join that Group.;&%q<gid1>`MEMBERS u(gdb)=setunion(get(u(gdb)/%q<gid1>`MEMBERS),%:);@nspemit %#=announce(GROUPS) You have joined the %q<gname1> Group!;th setstat(%#/D`GROUP,Group,%q<gid1>);th setstat(%#/D`GROUP`%q<gid1>,IC,1);th setstat(%#/D`GROUP`%q<gid1>,OOC,1);@include u(gso)/INC`CODE`MAKELOG=%:,%q<gid1>,{%q<t1name> joined.};@include u(gso)/INC`CODE`CHAT={ansi(hw,ALERT:) %n joined the Group!},OOC,1


&INC`CHOOSE`G`REFUSE u(gso)=@switch/inline gt(strlen(%1),0)=1,{@include u(gso)/INC`CODE`VALGROUP=%1,1;@include u(gso)/INC`CODE`REFUSE=%1,%2,%3},0,{@include u(gso)/INC`CODE`MYINVITES}
&INC`CODE`REFUSE u(gso)=@break match(get(u(gdb)/%q<gid1>`MEMBERS),%:)=@nspemit %#=announce(GROUPS)%BERROR: You are already a member of the Group.;@assert match(get(u(gdb)/%q<gid1>`INVITES),%:)=@nspemit %#=announce(GROUPS)%BERROR: You have not been invited to join that Group.;&%q<gid1>`MEMBERS u(gdb)=setdiff(get(u(gdb)/%q<gid1>`MEMBERS),%:);@nspemit %#=announce(GROUPS) You have rejected joining the %q<gname1> Group!;@include u(gso)/INC`CODE`MAKELOG=%:,%q<gid1>,{%q<t1name> rejected joining.};@include u(gso)/INC`CODE`CHAT={ansi(hw,ALERT:) %n rejected [poss(%#)] invitation!},OOC,1


&INC`CODE`MYINVITES u(gso)=@assert words(setr(groups,wildgrepi(u(gdb),*,*%:*)))=@nspemit %#=announce(GROUPS) You have no pending invites.;@nspemit %#=header(Your Group Invites);@nspemit %#=iter(%q<groups>,pueblize(get(u(gdb)/%i0) - [get(u(gdb)/%i0`NAME)],+gjoin [get(u(gdb)/%i0)]),,%R);@nspemit %#=header()

&INC`CHOOSE`G`LEAVE u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`LEAVE=%1,%2,%3
&INC`CODE`LEAVE u(gso)=@assert match(get(u(gdb)/%q<gid1>`MEMBERS),%:)=@nspemit %#=announce(GROUPS)%BERROR: You are not a member of that group.;@break eq(0,u(u(gso)/FUN`GETRANK,%:,%q<gid1>))=@nspemit %#=announce(JOBS)%BERROR: Leaders may not leave their groups. Please contact staff.;&%q<gid1>`MEMBERS u(gdb)=setdiff(get(u(gdb)/%q<gid1>`MEMBERS),%:);@nspemit %#=announce(GROUPS) You left the %q<gname1> Group!;@include u(gso)/INC`CODE`MAKELOG=%:,%q<gid1>{%q<t1name> left the group.};@include u(gso)/INC`CODE`CHAT={ansi(hw,ALERT:) %n left the Group},OOC,1

&INC`CHOOSE`G`KICK u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`KICK=%1,%2,%3
&INC`CODE`KICK u(gso)=@assert isgroupmember(%:,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: You are not a member of that group.;@assert u(u(gso)/FUN`GRPAUTH,%:,%q<gid1>,KICK)=@nspemit %#=announce(GROUPS)%BERROR: You do not have permission to do that.;@include u(ccs)/INC`CHECKPC=%0,1,GROUPS;@assert match(get(u(gdb)/%q<gid1>`MEMBERS),%q<t1objid>)=@nspemit %#=announce(GROUPS)%BERROR: They are not a member of the Group.;@assert lte(u(u(gso)/FUN`GETRANK,%:,%q<gid1>),u(u(gso)/FUN`GETRANK,%q<t1objid>,%q<gid1>))=@nspemit %#=announce(GROUPS)%BERROR: Cannot kick those of higher rank.;&%q<gid1>`MEMBERS u(gdb)=setdiff(get(u(gdb)/%q<gid1>`MEMBERS),%q<t1objid>);@nspemit %q<t1>=announce(GROUPS) %n removed you from the %q<gname1> Group!;@include u(gso)/INC`CODE`MAKELOG=%:,%q<gid1>{%q<t1name> was kicked.};@include u(gso)/INC`CODE`CHAT={ansi(hw,ALERT:) %n removed %q<t1name> from the Group},OOC,1

&INC`CHOOSE`G`RANK u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`RANK=%1,%2,%3
&INC`CODE`RANK u(gso)=@assert isgroupmember(%:,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: You are not a member of that group.;@assert u(u(gso)/FUN`GRPAUTH,%:,%q<gid1>,RANK)=@nspemit %#=announce(GROUPS)%BERROR: You do not have permission to do that.;@include u(ccs)/INC`CHECKPC=%0,1,GROUPS;@assert strlen(%1)=@nspemit %#=announce(GROUPS)%BERROR: Rank field empty.;@assert and(gte(%1,0),isint(%1))=@nspemit %#=announce(GROUPS)%BERROR: Ranks must be whole numbers.;@assert lt(u(u(gso)/FUN`GETRANK,%:,%q<gid1>),%1)=@nspemit %#=announce(GROUPS)%BERROR: New Rank must be less than your own.;@assert lt(u(u(gso)/FUN`GETRANK,%:,%q<gid1>),u(u(gso)/FUN`GETRANK,%q<t1objid>,%q<gid1>))=@nspemit %#=announce(GROUPS)%BERROR: Cannot change the rank of superiors.;th setstat(u(gdb)/%q<gid1>`RANK,%q<t1objid>,%1);@nspemit %q<t1>=announce(GROUPS) %n has Changed your rank in the %q<gname1> Group to %1!;@include u(gso)/INC`CODE`MAKELOG=%:,%q<gid1>{Changed %q<t1name>'s Rank to %1};@include u(gso)/INC`CODE`CHAT={ansi(hw,ALERT:) %n set %q<t1name>'s Rank to %1},OOC,1

&INC`CHOOSE`G`RADIO u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`CHAT=%1[if(strlen(%2),=%2)],IC,0
&INC`CHOOSE`G`IC u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`CHAT=%1[if(strlen(%2),=%2)],IC,0

&INC`CHOOSE`G`OOC u(gso)=@include u(gso)/INC`CODE`VALGROUP=strfirstof(getstat(%#/D`GROUP,Group),0),1;@include u(gso)/INC`CODE`CHAT=%1[if(strlen(%2),=%2)],OOC,0

&INC`CODE`CHAT u(gso)=@assert strlen(%0)=@nspemit %#=What will you say?;@switch/inline gt(%2,0)=0,{@assert getstat(u(gdb)/%q<gid1>`OP,setr(chan,%1))=@nspemit %#=announce(GROUPS)%BERROR: That channel is not enabled.;@assert strfirstof(getstat(%#/D`GROUP,%q<chan>),1)=@nspemit %#=announce(GROUPS)%BERROR: You have universally disabled Group %q<chan> Channels!;@assert u(u(gso)/FUN`GRPAUTH,%:,%q<gid1>,%q<chan>)=@nspemit %#=You lack permission to do that!;@break strlen(getstat(u(gdb)/%q<gid1>`GAG,%:))=@nspemit %#=announce(GROUPS)%BERROR: You have been Gagged from the Group's channels.;@assert getstat(%#/D`GROUP`%q<gid1>,%q<chan>)=@nspemit %#=announce(GROUPS)%BERROR: Cannot use a channel while it is disabled.};@assert words(setr(list,filterbool(#lambda/and(isgroupmember(objid(\%0),%q<gid1>),firstof(getstat(\%0/D`GROUP,%q<chan>),1),firstof(getstat(\%0/D`GROUP`%q<gid1>,%q<chan>),1)),setunion(lwho(),lwho()))));th setq(header,ansi(c,<)[ansi(strfirstof(u(u(gso)/FUN`GETOP,%q<gid1>,COLOR),h),ucstr(get(u(gdb)/%q<gid1>)))]-[if(gt(%2,0),ansi(hr,SYS),ansi(hx,%q<chan>))][ansi(c,>)]);@switch/inline gt(%2,0)=0,{th if(%2,,setq(title,getstat(%#/D`GROUP`%q<gid>,%q<chan>TITLE)));@nspemit/list %q<list>=%q<header>%B[if(strlen(%q<title>),%q<title>%B)][speak(%#,%0)]},1,{@nspemit/list %q<list>=%q<header>%B%0}

&CMD`CHAT u(gso)=$^(?s)(-|=)(\w+)(?\: +(.+))?$:@include u(gso)/INC`CODE`VALGROUP=%2,1;@assert isgroupmember(%:,%q<gid1>)=@nspemit %#=announce(GROUPS)%BERROR: You do not belong to that group!;@assert strlen(%3)=@nspemit %#=What will you say?;@include u(gso)/INC`CODE`CHAT=%3,if(strmatch(=,%1),OOC,IC),0
@set u(gso)/CMD`CHAT=regexp

&PLAYER`CONNECT`GROUPCHECK u(gso)=@dolist iter(wildgrep(u(gdb)/*`INVITES,*%0*),before(%i0,`))={@nspemit %#=announce(GROUP) The [get(u(gdb)/%i0`NAME)] Have invited you to join their group! To accept, type [pueblize(ansi(h,+gjoin [get(u(gdb)/%i0)]),+gjoin %i0)]}

&STARTUP u(gso)=@trigger u(gso)/TRG`DOCLEANUP

&TRG`DOCLEANUP u(gso)=@dolist filterbool(#lambda/isnum(\%0),lattr(u(gdb)/*))={&%i0`MEMBERS u(gdb)=filterbool(#lambda/isobjid(\%0),get(u(gdb)/%i0`MEMBERS));&%i0`INVITES u(gdb)=filterbool(#lambda/isobjid(\%0),get(u(gdb)/%i0`INVITES));&%i0`RANK u(gdb)=filterbool(#lambda/isobjid(before(\%0,~)),get(u(gdb)/%i0`RANK))};@wait 7200=@trigger u(gso)/TRG`DOCLEANUP
