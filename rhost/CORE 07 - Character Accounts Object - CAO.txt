@@ DEPENDENCIES - Core

@switch/inline isdbref(u(cao))=0,{@tel create(Character Accounts Object <CAO>)=globalroom()}
&cao [u(coi)]=locate(globalroom(),Character Accounts Object <CAO>,Ti)
@wait 0=@parent [u(cao)]=[u(coi)]
@set [u(cao)]=INHERIT !NO_COMMAND SIDEFX
@power [u(cao)]=no_pay debit

@switch/inline isdbref(u(adb))=0,{@switch/inline isdbref(u(cao))=1,{@tel create(Account Database <ADB>)=u(cao)}}
&adb [u(coi)]=locate(u(cao),Account Database <ADB>,Ti)
@wait 0=@parent u(adb)=[u(coi)]
@set u(adb)=INHERIT

&STARTUP [u(cao)]=@dolist/inline lattr(me/^STARTUP`\\w+$,,,1)=@trigger me/##

lattr(me/^STARTUP`\\w+$,,,1)
@@ ****** COMMAND SECTION *******
&CMD`+ACCOUNT [u(cao)]=$^(?\:\+)?(account)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%2,setunion(get(u(cao)/VAR`ACCOUNT`PLAYFLAGS),if(isadmin(%#),get(u(cao)/VAR`ACCOUNT`ADMINFLAGS)),|,|),|,ACCOUNT,switch,switch;@include u(cao)/INC`ACCOUNT`[strfirstof(%q<switch>,MAIN)]=%3,%4
@set u(cao)/CMD`+ACCOUNT=regexp

@@ ****** ACCOUNT SECTION *******
&FILTER`OBJID [u(cao)]=hastype(%0,PLAYER)

&FUN`FINDACCOUNT [u(cao)]=first(regrepi(u(adb),ACC`\\d+,(\\s)?%0(\\s)?,1))

&INC`ACCOUNT`MAIN [u(cao)]=@switch/inline isadmin(%#)=1,{@switch/inline strlen(%0)=0,{@assert/inline words(petr(accounts,sortattr(lattr(u(adb)/^ACC`\\d+$,,,1))))=@pemit %#=announce(ACCOUNT) ERROR: There are no accounts entered in the system.;@pemit %#=header(mudname() Player Accounts)%R%B%B[ljust(ID,4)]Characters%R[subheader()];@dolist/inline %q<accounts>={@pemit %#=rjust(last(##,`),4). [itemize(iter(filter(FILTER`OBJID,get(u(adb)/##)),name(%i0),,|),|,and,\,)]};@pemit %#=header()},{@switch/inline valnum(%0)=1,{@assert/inline hasattr(u(adb)/[petr(accid,ACC`%0)])=@pemit %#=ERROR: Account ID not found.},{@include u(ccs)/INC`CHECKPC=%0,1,ACCOUNT;@assert/inline gt(petr(accid,u(u(cao)/FUN`FINDACCOUNT,%q<t1>)),0)=@pemit %#=ERROR: %q<t1name> is not bound to an account.};@include u(cao)/INC`ACCOUNT`SHOW}},{@include u(ccs)/INC`CHECKPC=%#,1,ACCOUNT;@assert/inline gt(petr(accid,u(u(cao)/FUN`FINDACCOUNT,%q<t1>)),0)=@pemit %#=ERROR: You are not bound to an Account. Please check with an Admin to correct this!;@include u(cao)/INC`ACCOUNT`SHOW}

&INC`ACCOUNT`SHOW [u(cao)]=@assert/inline words(petr(pcs,filter(FILTER`OBJID,get(u(adb)/%q<accid>))))=@pemit %#=announce(ACCOUNT) ERROR: No PCs bound to this account.;@pemit %#=header(Account [last(%q<accid>,`)][if(strlen(petr(email,get(u(adb)/%q<accid>`EMAIL))),:%B%q<email>)] Details)%R[ljust(DBREF,7)][ljust(NAME,30)][ljust(STATUS,10)][ljust(IP,22)]LAST ON%R[subheader()];@dolist/inline %q<pcs>={@pemit %#=rjust(##,5) %B[ljust(name(##),30)][ljust(switch(1,isadmin(##),ansi(hy,ADM),haspower(##,BUILDER),ansi(hm,BUI),approved(##),ansi(hg,APP),ansi(hr,UNAPP)),10)][ljust(get(##/LASTIP),22)][if(gte(conn(##),0),ansi(hg,ago(idle(##))),ansi(hx,elements(get(##/LAST),2 3)))]};@pemit %#=header()

&INC`ACCOUNT`NEW [u(cao)]=@break/inline petr(accid,u(u(cao)/FUN`FINDACCOUNT,%#))=@pemit %#=announce(ACCOUNT) ERROR: You are already bound to Account %q<accid>!;th petq(accid,nextslot(u(adb),ACC));&ACC`%q<accid> [u(adb)]=%#;@pemit %#=announce(ACCOUNT) You are now bound to Account %q<accid>!;@switch/inline gt(v(VAR`ACCOUNT`ALERTMODE),0)=1,{@cemit/noisy u(VAR`ACCOUNT`ALERTSCHANNEL)=%n Bound %oself to new Account: %q<accid>}

&INC`ACCOUNT`BIND [u(cao)]=@include u(ccs)/INC`CHECKPC=%0,1,ACCOUNT;@switch/inline valnum(%1)=1,{@assert/inline hasattr(u(adb)/[petr(accid,ACC`%1)])=@pemit %#=announce(ACCOUNT) ERROR: Account ID not found.},{@assert/inline strlen(%1)=announce(ACCOUNT) ERROR: Destination Account Empty. It must be an Account ID, player name, or NEW;@switch/inline %1=NEW,{th petq(accid,ACC`[nextslot(u(adb),ACC)])},{@include u(ccs)/INC`CHECKPC=%1,2,ACCOUNT;@assert/inline gt(strlen(petr(accid,u(u(cao)/FUN`FINDACCOUNT,%q<t2>))),0)=@pemit %#=announce(ACCOUNT) ERROR: That player doesn't have an account.}};@switch/inline gt(strlen(petr(oldid,u(u(cao)/FUN`FINDACCOUNT,%q<t1>))),0)=1,{&%q<oldid> [u(adb)]=setdiff(get(u(adb)/%q<oldid>),%q<t1>)};&%q<accid> [u(adb)]=setunion(get(u(adb)/%q<accid>),%q<t1>);@pemit %q<t1>=announce(ACCOUNT) You have been Bound to [pueblize(Account %q<accid>,+account %q<accid>)] by %n.;@pemit %#=announce(ACCOUNT) You have Bound %q<t1name> to Account [last(%q<accid>,`)];@switch/inline gt(v(VAR`ACCOUNT`ALERTMODE),0)=1,{@cemit/noisy u(VAR`ACCOUNT`ALERTSCHANNEL)=%n Bound %q<t1name> to Account [last(%q<accid>,`)]}

&INC`ACCOUNT`UNBIND [u(cao)]=@include u(ccs)/INC`CHECKPC=%0,1,ACCOUNT;@assert/inline words(petr(accounts,regrepi(u(adb),ACC`\\d+,(\\s)?%q<t1>(\\s)?,1)))=@pemit %#=announce(ACCOUNT) ERROR: %q<t1name> is not a member of any accounts.;@dolist %q<accounts>={&## [u(adb)]=filter(FILTER`OBJID,setdiff(get(u(adb)/##),%q<t1>))};@pemit %#=announce(ACCOUNT) You have removed %q<t1name> from Accounts: [itemize(%q<accounts>,%b,and,\,)];@pemit %q<t1>=announce(ACCOUNT) [ansi(h,%n)] has removed you from Accounts: [itemize(%q<accounts>,%b,and,\,)];@switch/inline gt(v(VAR`ACCOUNT`ALERTMODE),0)=1,{@cemit/noisy u(VAR`ACCOUNT`ALERTSCHANNEL)=%n removed %q<t1name> to from Accounts: [itemize(%q<accounts>,%b,and,\,)]}


&VAR`ACCOUNT`ALERTMODE [u(cao)]=1
&VAR`ACCOUNT`ALERTSCHANNEL [u(cao)]=u(CMO`STAFFREP)

&INC`ACCOUNT`UNBOUND [u(cao)]=@assert/inline gt(words(petr(unbound,lsearch(all,eplayer,\[not(u(u(cao)/FUN`FINDACCOUNT,objid(##)))\]))),0)=@pemit %#=announce(ACCOUNT) There are no accountless PCs in the Database.;@pemit %#=header(Unbound Characters)%R[ljust(DBREF,7)][ljust(NAME,30)][ljust(STATUS,10)][ljust(IP,22)]LAST ON%R[subheader()];@dolist/inline %q<unbound>={@pemit %#=rjust(num(%i0),5) %B[ljust(pueblize(name(%i0),+finger [name(%i0)]),30)][ljust(switch(1,isadmin(%i0),ansi(hy,ADM),haspower(%i0,BUILDER),ansi(hm,BUI),approved(%i0),ansi(hg,APP),ansi(hr,UNAPP)),10)][ljust(pueblize(get(%i0/LASTIP),+ip [name(%i0)]),22)][if(gte(conn(%i0),0),ansi(hg,ago(idle(%i0))),ansi(hx,elements(get(%i0/LAST),2 3)))]};@pemit %#=header()

&INC`ACCOUNT`EMAIL [u(cao)]=@switch/inline config(player_creation)=Yes,{@assert/inline gt(petr(accid,u(u(cao)/FUN`FINDACCOUNT,%#)),0)=@pemit %#=ERROR: This character is not bound to an Account.;&%q<accid>`EMAIL u(adb)=%0;@pemit %#=announce(ACCOUNT) You have [if(strlen(%0),set your Account email to: %0,unset your Account email.)]},{@assert/inline isadmin(%#)=@pemit %#=announce(ACCOUNT) Only Staff may set emails!;@include u(ccs)/INC`CHECKPC=%0,1,ACCOUNT;@assert/inline gt(petr(accid,u(u(cao)/FUN`FINDACCOUNT,%q<t1>)),0)=@pemit %#=ERROR: %q<t1name> is not bound to an Account.;&%q<accid>`EMAIL u(adb)=%1;@pemit %#=announce(ACCOUNT) %q<t1name>'s Email has been set to: %1}

&VAR`ACCOUNT`PLAYFLAGS [u(cao)]=EMAIL|NEW
&VAR`ACCOUNT`ADMINFLAGS [u(cao)]=BIND|UNBIND|UNBOUND

&STARTUP`CLEANACCOUNTS [u(cao)]=@trigger u(cao)/TRG`ACCOUNT`CLEANACCOUNTS

&TRG`ACCOUNT`CLEANACCOUNTS [u(cao)]=@dolist lattr(u(adb)/^ACC`\\d+$,,,1)={&## [u(adb)]=filter(FILTER`OBJID,get(u(adb)/##));@assert hasattr(u(adb)/##)=@wipe u(adb)/##`*};@wait 7200=@trigger u(cao)/TRG`ACCOUNT`CLEANACCOUNTS

@@ ****** APPROVAL SECTION *********
&CMD`+APPROVE [u(cao)]=$^(?\:\+)?(approve|unapprove)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%2,setunion(get(u(cao)/VAR`%1`PLAYFLAGS),if(isadmin(%#),get(u(cao)/VAR`%1`ADMINFLAGS)),|,|),|,%1,switch,switch;@include u(cao)/INC`%1`[strfirstof(%q<switch>,MAIN)]=%3,%4
@set u(cao)/CMD`+APPROVE=regexp

&INC`APPROVE`MAIN [u(cao)]=@switch/inline and(isadmin(%#),strlen(%0))=1,{@include u(ccs)/INC`CHECKPC=%0,1,APPROVE;@break/inline approved(%q<t1>)=@pemit %#=announce(APPROVE) ERROR: They are already approved!;@include u(cao)/INC`APPROVE`DOAPPROVE=%q<t1>;@pemit %#=announce(APPROVE) You have approved %q<t1name> for play!;@switch/inline gt(v(VAR`APPROVE`ALERTMODE),0)={@cemit/noisy u(VAR`APPROVE`ALERTSCHANNEL)=%n has approved %q<t1name> for play!;@force/inline %q<t1>=+recall;@cemit/noisy u(VAR`APPROVE`ANNOUNCECHANNEL)=ansi(h,%n) just approved %q<t1name> for play!}},{@pemit %#=announce(APPROVE) You [if(approved(%#),ansi(hg,are),ansi(hr,are not))] are approved for play!}

&INC`APPROVE`DOAPPROVE [u(cao)]=@set %0=!WANDERER
&INC`APPROVE`DOUNAPPROVE [u(cao)]=@set %0=WANDERER

&INC`UNAPPROVE`MAIN [u(cao)]=@switch/inline isadmin(%#)=1,{@include u(ccs)/INC`CHECKPC=%0,1,APPROVE;@assert/inline approved(%q<t1>)=@pemit %#=announce(APPROVE) ERROR: They are not approved!;@include u(cao)/INC`APPROVE`DOUNAPPROVE=%q<t1>;@pemit %#=announce(APPROVE) You have unapproved %q<t1name> for play!;@switch/inline gt(v(VAR`APPROVE`ALERTMODE),0)={@cemit/noisy u(VAR`APPROVE`ALERTSCHANNEL)=%n has unapproved %q<t1name> for play!}},{@pemit %#=announce(APPROVE) You [if(approved(%#),ansi(hg,are),ansi(hr,are not))] are approved for play!}

&GFN`APPROVED [u(cao)]=or(not(hasflag(%0,WANDERER)),isadmin(%0))

&APPROVE [u(ae)]=approved(%#)
&isadmin [u(ae)]=isadmin(%#)

&VAR`APPROVE`ALERTMODE [u(cao)]=1
&VAR`APPROVE`ALERTSCHANNEL [u(cao)]=u(CMO`STAFFREP)
&VAR`APPROVE`ANNOUNCECHANNEL [u(cao)]=u(CMO`PUBLIC)

@@ ********* STAFF SECTION ********
&CMD`+STAFF [u(cao)]=$^(?\:\+)?(staff|admin|wizlist)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%2,setunion(get(u(cao)/VAR`STAFF`PLAYFLAGS),if(isadmin(%#),get(u(cao)/VAR`STAFF`ADMINFLAGS)),|,|),|,%1,switch,switch;@include u(cao)/INC`STAFF`[strfirstof(%q<switch>,MAIN)]=%3,%4
@set u(cao)/CMD`+STAFF=regexp

&GFN`ISADMIN [u(cao)]=or(gte(bittype(%0),5),strlen(getstat(u(cao)/VAR`STAFF`WIZLIST,%0)))

&FUN`STAFF`CHSTAFF [u(cao)]=gte(bittype(%0),7)

&INC`WIZLIST`MAIN [u(cao)]=@include u(cao)/INC`STAFF`MAIN
&INC`ADMIN`MAIN [u(cao)]=@include u(cao)/INC`STAFF`MAIN

&VAR`STAFF`ADMINFLAGS [u(cao)]=ADD|REM|ORDER|ON|OFF|VAC

&INC`STAFF`MAIN [u(cao)]=@assert/inline words(petr(staff,filter(#lambda/isdbref([lit(%0)]),iter(sortby(#lambda/[lit([ncomp(last(%0,~),last(%1,~))])],get(u(cao)/VAR`STAFF`WIZLIST),|,|),before(%i0,~),|,|),|,|)),|)=@pemit %#=announce(STAFF) ERROR: There are no Staff registered!;@pemit %#=header(mudname() Staff);@pemit %#=printf($-3s $-13s $-5s $-12s $-15s $-12s $-11s,Bit,Name,Alias,Position,Status,Duty,Idle/LastOn);@pemit %#=subheader();@dolist/inline/delimit | %q<staff>=@pemit %#=u(u(cao)/FUN`Staff`WIZLIST,##,%#);@pemit %#=header()

&FUN`STAFF`WIZLIST [u(cao)]=printf($-3s $-13s $-5s $-12s $-15s $-12s $-6s $-4s,bittype(%0),left(name(%0),13),left(get(%0/ALIAS),5),left(default(%0/POSITION,???),12),left(strfirstof(get(%0/STATUS),),15),if(get(%0/VACATION),ansi(c,On Vacation),if(or(hasflag(%0,DARK),not(hasflag(%0,CONNECTED)),hidden(%0)),ansi(hx,Offline),if(get(%0/duty),ansi(hg,On Duty),ansi(hx,Off Duty)))),lastidle(%0,%1),if(and(isadmin(%1),or(hidden(%0),hasflag(%0,DARK))),switch(1,hasflag(%0,DARK),DARK,hidden(%0),HIDE)))

&INC`STAFF`ADD [u(cao)]=@include u(ccs)/INC`CHECKPC=%0,1,STAFF;@break/inline strlen(getstat(u(cao)/VAR`STAFF`WIZLIST,%q<t1>))=@pemit %#=announce(STAFF) ERROR: They are already on the Staff list!;@switch/inline gt(strlen(%1),0)=1,{@assert/inline valnum(%1)=@pemit %#=announce(STAFF) ERROR: Order Must be a whole positive number.;th setstat(u(cao)/VAR`STAFF`WIZLIST,%q<t1>,%1)},0,{th setstat(u(cao)/VAR`STAFF`WIZLIST,%q<t1>,petr(order,add(1,iter(get(u(cao)/VAR`STAFF`WIZLIST),last(%i0,~)))))};@pemit %#=announce(STAFF) You have added %q<t1name> to Staff!;@pemit %q<t1>=announce(STAFF) You have been added to Staff!;@switch/inline gt(v(VAR`STAFF`ALERTMODE),0)=1,{@cemit/noisy u(VAR`STAFF`ALERTSCHANNEL)=ansi(h,%n) has added %q<t1name> to Staff!}

&INC`STAFF`REM [u(cao)]=@include u(ccs)/INC`CHECKPC=%0,1,STAFF;@assert/inline strlen(getstat(u(cao)/VAR`STAFF`WIZLIST,%q<t1>))=@pemit %#=announce(STAFF) ERROR: They are not on the Staff list!;@pemit %#=announce(STAFF) You have removed %q<t1name> from Staff!;@pemit %q<t1>=announce(STAFF) You have been removed from Staff!;th delstat(u(cao)/VAR`STAFF`WIZLIST,%q<t1>);@switch/inline gt(v(VAR`STAFF`ALERTMODE),0)=1,{@cemit/noisy u(VAR`STAFF`ALERTSCHANNEL)=ansi(h,%n) has removed %q<t1name> from Staff!}

&INC`STAFF`VAC [u(cao)]=@switch/inline t(default(%#/VACATION,0))=0,{@pemit %#=announce(STAFF) You go On Vacation!;&vacation %#=1},1,{@pemit %#=announce(STAFF) You come off Vacation!;&vacation %#=0}

&INC`STAFF`ORDER [u(cao)]=@include u(ccs)/INC`CHECKPC=%0,1,STAFF;@assert/inline strlen(getstat(u(cao)/VAR`STAFF`WIZLIST,%q<t1>))=@pemit %#=announce(STAFF) ERROR: They are not on the Staff list!;@assert/inline strlen(%1)=@pemit %#=announce(STAFF) ERROR: Order field empty.;@assert/inline valnum(%1)=@pemit %#=announce(STAFF) ERROR: Order Must be a whole positive number.;th setstat(u(cao)/VAR`STAFF`WIZLIST,%q<t1>,%1);@pemit %#=announce(STAFF) You have set %q<t1name>'s Staff order to %1!;@pemit %q<t1>=%n has set your Staff order to %1!;@switch/inline gt(v(VAR`STAFF`ALERTMODE),0)=1,{@cemit/noisy u(VAR`STAFF`ALERTSCHANNEL)=ansi(h,%n) has set %q<t1name>'s Staff order to %1!}

&INC`STAFF`ON [u(cao)]=@switch/inline t(default(%#/DUTY,0))=1,{@pemit %#=announce(STAFF) You are already On-duty!},0,{@pemit %#=announce(STAFF) You are now ON DUTY!;&duty %#=1;@switch/inline nor(hasflag(%#,DARK),hidden(%#))=1,{@cemit/noisy u(CMO`PUBLIC)=[ansi(h,%n)] is now ON DUTY!}}

&INC`STAFF`OFF [u(cao)]=@switch/inline t(default(%#/DUTY,0))=0,{@pemit %#=announce(STAFF) You are already Off-duty!},1,{@pemit %#=announce(STAFF) You are now OFF DUTY!;&duty %#=0;@switch/inline nor(hasflag(%#,DARK),hidden(%#))=1,{@cemit/noisy u(CMO`PUBLIC)=[ansi(h,%n)] is now OFF DUTY!}}

&VAR`STAFF`ALERTMODE [u(cao)]=1
&VAR`STAFF`ALERTSCHANNEL [u(cao)]=u(CMO`STAFFREP)

&STARTUP`CLEANSTAFF [u(cao)]=@trigger u(cao)/TRG`STAFF`CLEANSTAFF
&TRG`STAFF`CLEANSTAFF [u(cao)]=&VAR`STAFF`WIZLIST [u(cao)]=filter(#lambda/isdbref(before([lit(%0)],~)),get(u(cao)/VAR`STAFF`WIZLIST),|,|);@wait 7200=@trigger u(cao)/TRG`STAFF`CLEANSTAFF

@@ ********* BUILDER SECTION ********
&CMD`+BUILDER [u(cao)]=$^(?\:\+)?(builder)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%2,setunion(get(u(cao)/VAR`BUILDER`PLAYFLAGS),if(isadmin(%#),get(u(cao)/VAR`BUILDER`ADMINFLAGS)),|,|),|,BUILDER,switch,switch;@include u(cao)/INC`BUILDER`[strfirstof(%q<switch>,MAIN)]=%3,%4
@set u(cao)/CMD`+BUILDER=regexp

&INC`BUILDER`MAIN [u(cao)]=@switch/inline isadmin(%#)=1,{@switch/inline strlen(%0)=0,{@assert/inline words(petr(build,lsearch(all,eplayer,\[haspower(##,BUILDER)\])))=@pemit %#=announce(BUILDER) There are no Builder Characters.;@pemit %#=header(mudname() Builder Characters);@pemit %#=iter(%q<build>,ansi(h,%i0 - [name(%i0)]) - [powers(%i0)],%b,%R);@pemit %#=header()},{@include u(ccs)/INC`CHECKPC=%0,1,BUILDER;@pemit %#=header(%q<t1name>'s Belongings);@dolist/inline THING ROOM EXIT=@switch/inline gt(words(petr(search,lsearch(%q<t1>,type,%i0))),0)=1,{@pemit %#=subheader(Type: %i0);@dolist/inline %q<search>=@pemit %#=[rjust(pueblize(%i0,brief %i0),7)] - [name(%i0)]};@pemit %#=header()}},0,{@pemit %#=announce(BUILDER) You [if(haspower(%#,BUILDER),ansi(hg,are),ansi(hr,are not))] A Builder!}

&INC`BUILDER`ADD [u(cao)]=@include u(ccs)/INC`CHECKPC=%0,1,BUILDER;@break/inline haspower(%q<t1>,BUILDER)=@pemit %#=announce(BUILDER) ERROR: They are already a Builder.;@set %q<t1>=!UNREGISTERED;@power %q<t1>=builder no_quota tel_anywhere open_anywhere link_anywhere;give %q<t1>=100000;@pemit %#=announce(BUILDER) You have made %q<t1name> a Builder.;@pemit %q<t1>=announce(BUILDER) %n has granted you Builder powers.;@switch/inline gt(v(VAR`BUILDER`ALERTMODE),0)={@cemit/noisy u(VAR`BUILDER`ALERTSCHANNEL)=%n has made %q<t1name> a Builder.}

&INC`BUILDER`REM [u(cao)]=@include u(ccs)/INC`CHECKPC=%0,1,BUILDER;@assert/inline haspower(%q<t1>,BUILDER)=@pemit %#=announce(BUILDER) ERROR: They are not a Builder.;@set %q<t1>=!UNREGISTERED;@power %q<t1>=!builder !no_quota !tel_anywhere !open_anywhere !link_anywhere;@switch/inline lt(money(%q<t1>),100000)=1,{give %q<t1>=100000};give %q<t1>=-99000;@set %q<t1>=UNREGISTERED;@pemit %#=announce(BUILDER) You have revoked %q<t1name>'s Builder powers.;@pemit %q<t1>=announce(BUILDER) %n has revoked your Builder powers.;@switch/inline gt(v(VAR`BUILDER`ALERTMODE),0)={@cemit/noisy u(VAR`BUILDER`ALERTSCHANNEL)=%n has revoked %q<t1name>'s Builder powers.}

&VAR`BUILDER`ADMINFLAGS [u(cao)]=ADD|REM

&VAR`BUILDER`ALERTMODE [u(cao)]=1
&VAR`BUILDER`ALERTSCHANNEL [u(cao)]=u(CMO`STAFFREP)

@@ ******** IDLERS SECTION ******
&CMD`+IDLERS [u(cao)]=$^(?\:\+)?(idlers)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%2,setunion(get(u(cao)/VAR`%1`PLAYFLAGS),if(isadmin(%#),get(u(cao)/VAR`%1`ADMINFLAGS)),|,|),|,%1,switch,switch;@include u(cao)/INC`%1`[strfirstof(%q<switch>,MAIN)]=%3,%4
@set u(cao)/CMD`+IDLERS=regexp

&INC`IDLERS`MAIN [u(cao)]=@switch/inline gt(strlen(%0),0)=1,{@assert/inline valnum(petr(days,%0))=@pemit %#=announce(IDLERS) ERROR: Day field must be a whole, positive integer!},{th petq(days,30)};@assert/inline words(petr(idlers,sortkey(#lambda/convtime(get([lit(%0)]/LASTLOGOUT)),setdiff(lsearch(all,eplayer,\[and(gt(sub(secs(),convtime(get(##/LASTLOGOUT))),mul(86400,%q<days>)),not(haspower(##,GUEST)),not(isadmin(##)))\]),u(ap)),n,%b,|)))=@pemit %#=announce(IDLERS) Nobody's been offline longer than %q<days>.;@pemit %#=header(Players over %q<days> Idle)%R[ljust(DBREF,7)][ljust(NAME,30)][ljust(STATUS,10)][ljust(IP,22)]LAST ON%R[subheader()];th iter(lnum(1,ceil(fdiv(words(petr(list,%q<idlers>),|),30))),nspemit(%#,iter(elements(%q<list>,lnum(add(1,mul(sub(inum(0),1),30)),mul(inum(0),30)),|,|),rjust(num(%i0),5) %B[ljust(pueblize(name(%i0),+finger [name(%i0)]),30)][ljust(if(isadmin(%i0),ansi(hy,ADM),if(approved(%i0),ansi(hg,APP),ansi(hr,UNAPP))),10)][ljust(pueblize(get(%i0/LASTIP),+ip [name(%i0)]),22)][if(gte(conn(%i0),0),ansi(hg,ago(idle(%i0))),ansi(hx,elements(get(%i0/LAST),2 3)))],|,%R)));@pemit %#=header()

&VAR`IDLERS`ADMINFLAGS [u(cao)]=
&VAR`IDLERS`PLAYFLAGS [u(cao)]=