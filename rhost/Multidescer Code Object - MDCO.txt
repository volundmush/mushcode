@switch/inline isdbref(u(desc))=0,{@tel create(Multidescer Code Object <DESC>)=globalroom()}
&desc [u(coi)]=locate(globalroom(),Multidescer Code Object <DESC>,Ti)
@wait 0=@parent [u(desc)]=[u(coi)]
@set [u(desc)]=INHERIT SAFE !NO_COMMAND

&CMD`+DESC [u(desc)]=$^(?s)(?\:\+)?desc(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(desc)/VAR`PLAYFLAGS),if(isadmin(%#),get(u(desc)/VAR`ADMINFLAGS)),|,|),|,DESC,switch,switch;@include u(desc)/INC`[strfirstof(%q<switch>,MAIN)]=%2,%3
@set u(desc)/CMD`+DESC=regexp

&VAR`PLAYFLAGS [u(desc)]=SAVE|DELETE|NOISY|VIEW|RENAME
&VAR`ADMINFLAGS [u(desc)]=

&INC`MAIN [u(desc)]=@switch/inline strlen(%0)=0,{@pemit %#=header(Your Saved Descriptions);@pemit %#=table(iter(lattr(%#/D`DESC`*),get(%#/%i0),%b,|),26,78,|);@pemit %#=header()},{@assert/inline strlen(petr(attr,u(FUN`LOCDESC,%#,%0)))=@pemit %#=announce(DESC) ERROR: Desc '%0' not found!;@switch/inline %2=1,{@remit %l=roomannounce(DESC) [ansi(h,%n)] changed to %p '[get(%#/%q<attr>)]' Description!},{@pemit %#=announce(DESC) You change to your '[get(%#/%q<attr>)]' Description!};@cpattr %#/%q<attr>`DESC=%#/DESCRIBE}

&INC`NOISY [u(desc)]=@include u(desc)/INC`MAIN=%0,%1,1

&FUN`LOCDESC [u(desc)]=regrepi(%0,D`DESC`*,^%1$)

&GFN`MDESC [u(desc)]=if(strlen(u(FUN`LOCDESC,%0,%1)),get(%0/[u(FUN`LOCDESC,%0,%1)]`DESC))

&INC`SAVE [u(desc)]=@assert/inline strlen(%0)=@pemit %#=announce(DESC) ERROR: Desc name field empty!;@switch/inline strlen(petr(attr,u(FUN`LOCDESC,%#,%0)))=0,{th petq(attr,D`DESC`[nextslot(%#,D`DESC)])},{@include u(ccs)/INC`VERIFY={[ansi(hr,WARNING:)] This will overwrite the existing file. Enter the same command within ten seconds to verify.},DESC %0,DESC};@switch/inline strlen(%1)=0,{&%q<attr> %#=%0;@cpattr %#/DESCRIBE=%#/%q<attr>`DESC;@pemit %#=announce(DESC) Your current Description has been saved as '%0'!},{&%q<attr> %#=%0;&%q<attr>`DESC %#=%1;@pemit %#=announce(DESC) The entered description was saved as '%0'!}

&INC`DELETE [u(desc)]=@assert/inline strlen(%0)=@pemit %#=announce(DESC) ERROR: Desc name field empty!;@assert/inline strlen(petr(attr,u(FUN`LOCDESC,%#,%0)))=@pemit %#=announce(DESC) ERROR: Desc not found!;@include u(ccs)/INC`VERIFY={[ansi(hr,WARNING:)] Deleting Desc '%0'. This cannot be undone. Are you sure you want to do this? Enter the same command again within ten seconds to verify.},DESC DELETE %0,DESC;th petq(name,get(%#/%q<attr>));@pemit %#=announce(DESC) Desc '%q<name>' DELETED.;@wipe %#/%q<attr>

&INC`VIEW [u(desc)]=@assert/inline strlen(%0)=@pemit %#=announce(DESC) ERROR: Desc name field empty!;@assert/inline strlen(petr(attr,u(FUN`LOCDESC,%#,%0)))=@pemit %#=announce(DESC) ERROR: Desc not found!;@pemit %#=header(Viewing Description: '[get(%#/%q<attr>)]');@pemit %#=get(%#/%q<attr>`DESC);@pemit %#=header()

&INC`RENAME [u(desc)]=@assert/inline strlen(%0)=@pemit %#=announce(DESC) ERROR: Desc name field empty!;@assert/inline strlen(petr(attr,u(FUN`LOCDESC,%#,%0)))=@pemit %#=announce(DESC) ERROR: Desc not found!;@assert/inline strlen(%1)=@pemit %#=announce(DESC) ERROR: New desc name field empty!;@break/inline strlen(petr(attr2,u(FUN`LOCDESC,%#,%1)))=@pemit %#=announce(DESC) ERROR: A desc with that name already exists!;@pemit %#=announce(DESC) Desc '%0' renamed to '%1'!;&%q<attr> %#=%1