@@ DEPENDENCIES - Core

@switch/inline isdbref(u(cao))=0,{@tel create(Character Accounts Object <CAO>)=config(master_room)}
&cao u(coi)=locate(config(master_room),Character Accounts Object <CAO>,TXxi)
@parent u(cao)=u(coi)
@set u(cao)=WIZARD !NO_COMMAND
@power u(cao)=no_pay debit queue

@switch/inline isdbref(u(adb))=0,{@switch/inline isdbref(u(cao))=1,{@tel create(Account Database <ADB>)=u(cao)}}
&adb u(coi)=locate(u(cao),Account Database <ADB>,TXxi)
@parent u(adb)=u(coi)
@set u(adb)=WIZARD
@power u(adb)=many_attribs

@switch/inline isdbref(u(ipdb))=0,{@switch/inline isdbref(u(cao))=1,{@tel create(IP Database <IPDB>)=u(cao)}}
&ipdb u(coi)=locate(u(cao),IP Database <IPDB>,TXxi)
@parent u(ipdb)=u(coi)
@set u(ipdb)=WIZARD
@power u(ipdb)=many_attribs

@switch/inline isdbref(u(bandb))=0,{@switch/inline isdbref(u(cao))=1,{@tel create(Ban Database <BANDB>)=u(cao)}}
&bandb u(coi)=locate(u(cao),Ban Database <BANDB>,TXxi)
@parent u(bandb)=u(coi)
@set u(bandb)=WIZARD
@power u(bandb)=many_attribs

@mvattr u(cao)/VAR`STAFF`WIZLIST=u(pco)/WIZLIST
@wipe u(cao)
@set u(cao)=SAFE

@@ ****** COMMAND SECTION *******
&CMD`+ACCOUNT u(cao)=$^(?\:\+)?(account)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%2,setunion(get(u(cao)/VAR`ACCOUNT`PLAYFLAGS),if(isadmin(%#),get(u(cao)/VAR`ACCOUNT`ADMINFLAGS)),|,|),|,ACCOUNT,switch,switch;@include u(cao)/INC`ACCOUNT`[strfirstof(%q<switch>,MAIN)]=%3,%4
@set u(cao)/CMD`+ACCOUNT=regexp

@@ ****** ACCOUNT SECTION *******
&RFN`ACCOUNT`MSGHEAD u(cao)=msghead(v(VAR`ACCOUNT`MSGHEAD))
&VAR`ACCOUNT`MSGHEAD u(cao)=ACCOUNT

&RFN`ACCOUNT`HEADER u(cao)=header(%0,,ACCOUNT`BORDER,ACCOUNT`BORDERDOT,ACCOUNT`BORDERTEXT)
&RFN`ACCOUNT`SEPARATOR u(cao)=separator(%0,,ACCOUNT`BORDER,ACCOUNT`BORDERDOT,ACCOUNT`BORDERTEXT)
&RFN`ACCOUNT`SUBHEADER u(cao)=subheader(%0,,ACCOUNT`BORDER,ACCOUNT`BORDERDOT,ACCOUNT`BORDERTEXT)

&FILTER`OBJID u(cao)=isobjid(%0)

&FUN`FINDACCOUNT u(cao)=if(%1,first(grepi(u(adb),*,%0)),get(%0/D`ACCOUNT))

&AGFN`ACCID u(cao)=udefault(u(cao)/FUN`FINDACCOUNT,0,if(%1,%0,objid(pmatch(%0))),%1)
&AGFN`ALTS u(cao)=filterbool(#lambda/isobjid(\%0),get(u(adb)/[accid(%0)]))

&INC`ACCOUNT`MAIN u(cao)=@switch/inline isadmin(%#)=1,{@switch/inline strlen(%0)=0,{@include u(cao)/INC`ACCOUNT`LIST},{@switch/inline valnum(%0)=1,{@assert hasattr(u(adb)/[setr(accid,%0)])=@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) ERROR: Account ID not found.},{@include u(ccs)/INC`CHECKPC=%0,1,ACCOUNT;@assert gt(setr(accid,u(u(cao)/FUN`FINDACCOUNT,%q<t1objid>)),0)=@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) ERROR: %q<t1name> is not bound to an account.};@include u(cao)/INC`ACCOUNT`SHOW}},{@include u(ccs)/INC`CHECKPC=%#,1,ACCOUNT;@assert gt(setr(accid,u(u(cao)/FUN`FINDACCOUNT,%q<t1objid>)),0)=@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) ERROR: You are not bound to an Account. Please check with an Admin to correct this!;@include u(cao)/INC`ACCOUNT`SHOW}

&INC`ACCOUNT`LIST u(cao)=@assert words(setr(accounts,sort(filterbool(#lambda/cand(isint(\%0),hasattrval(u(adb)/\%0)),lattr(u(adb)/*)),n)))=@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) ERROR: There are no accounts entered in the system.;@nspemit %#=u(RFN`ACCOUNT`HEADER,mudname() Player Accounts);@nspemit %#=align(-5 25 [sub(width(%#),32)],ACC,Email,Characters);@nspemit %#=u(RFN`ACCOUNT`SEPARATOR);@dolist/inline %q<accounts>={@nspemit %#=align(-5 25 [sub(width(%#),32)],pueblize(%i0,+account %i0),default(u(adb)/%i0`EMAIL,Unset),strfirstof(itemize(iter(filterbool(FILTER`OBJID,get(u(adb)/%i0)),pueblize(name(%i0),+finger [name(%i0)]),,|),|,and,\,),OLD: [itemize(iter(get(u(adb)/%i0`OLD),ansi(c,after(%i0,~)),|,|),|,and,\,)]))};@nspemit %#=u(RFN`ACCOUNT`SUBHEADER,+account <id or player> for details)

&INC`ACCOUNT`SHOW u(cao)=th setq(pcs,filterbool(FILTER`OBJID,get(u(adb)/%q<accid>)));@@ @assert words(setr(pcs,filterbool(FILTER`OBJID,get(u(adb)/%q<accid>))))=@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) ERROR: No PCs bound to this account.;@nspemit %#=u(RFN`ACCOUNT`HEADER,Account %q<accid>[if(strlen(setr(email,get(u(adb)/%q<accid>`EMAIL))),:%B%q<email>)] Details);@nspemit %#=align(-6 3 3 21 8 25 6,Dbref,Sta,Alt,Name,New Mail,IP,LastOn);@nspemit %#=u(RFN`ACCOUNT`SEPARATOR);@dolist/inline %q<pcs>={@nspemit %#=align(-6 3 -3 21 -8 25 6,num(%i0),switch(1,isadmin(%i0),ansi(hy,ADM),haspower(%i0,BUILDER),ansi(hm,BUI),approved(%i0),ansi(hg,APP),ansi(hr,---)),default(%i0/D`ALTS,0),pueblize(name(%i0),+finger [name(%i0)]),elements(maildstats(%i0),5),pueblize(get(%i0/LASTIP),+ip [name(%i0)]),if(gte(conn(%i0),0),ansi(hg,ago(idle(%i0))),ansi(hx,elements(get(%i0/LAST),2 3))))};@switch/inline cand(isadmin(%#),gt(words(setr(old,filterbool(#lambda/not(isobjid(before(\%0,~))),get(u(adb)/%q<accid>`OLD),|,|)),|),0))=1,{@nspemit %#=u(RFn`ACCOUNT`SEPARATOR,Old Characters);@nspemit %#=align(-6 3 3 21 8 21,Dbref,,,Name,,Objid);@dolist/inline/delimit | [sortkey(#lambda/after(\%0,~),%q<old>,i,|,|)]={@nspemit %#=align(-6 3 -3 21 8  21,before(%i0,:),,,after(%i0,~),,before(%i0,~))}};@nspemit %#=u(RFN`ACCOUNT`HEADER)

&INC`ACCOUNT`NEW u(cao)=@break setr(accid,u(u(cao)/FUN`FINDACCOUNT,%:))=@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) ERROR: You are already bound to Account %q<accid>!;@assert strmatch(config(player_creation),yes)=@nspemit %#=u(RFN`MSGHEAD) ERROR: This game does not allow open character creation. Please contact staff to receive an account.;@include u(cao)/INC`ACCOUNT`ADDTOACCOUNT=setr(accid,nextslot(u(adb))),%:;@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) You are now bound to Account %q<accid>!;@switch/inline gt(v(VAR`ACCOUNT`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ACCOUNT`ALERTSCHANNEL)=%n Bound %oself to new Account: %q<accid>}

&INC`ACCOUNT`BIND u(cao)=@include u(ccs)/INC`CHECKPC=%0,1,ACCOUNT;@switch/inline valnum(%1)=1,{th setq(accid,abs(%1))},{@assert strlen(%1)=u(RFN`ACCOUNT`MSGHEAD) ERROR: Destination Account Empty. It must be an Account ID, player name, or NEW;@switch/inline %1=NEW,{th setq(accid,nextslot(u(adb)))},{@include u(ccs)/INC`CHECKPC=%1,2,ACCOUNT;@assert gt(setr(accid,u(u(cao)/FUN`FINDACCOUNT,%q<t2objid>)),0)=@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) ERROR: That player doesn't have an account.}};@switch/inline gt(setr(oldid,u(u(cao)/FUN`FINDACCOUNT,%q<t1objid>)),0)=1,{@include u(cao)/INC`ACCOUNT`REMFROMACCOUNT=%q<oldid>,%q<t1objid>};@include u(cao)/INC`ACCOUNT`ADDTOACCOUNT=%q<accid>,%q<t1objid>;@nspemit %q<t1>=u(RFN`ACCOUNT`MSGHEAD) You have been Bound to [pueblize(Account %q<accid>,+account %q<accid>)] by %n.;@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) You have Bound %q<t1name> to [pueblize(Account %q<accid>,+account %q<accid>)];@switch/inline gt(v(VAR`ACCOUNT`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ACCOUNT`ALERTSCHANNEL)=%n Bound %q<t1name> to [pueblize(Account %q<accid>,+account %q<accid>)]};

&INC`ACCOUNT`ADDTOACCOUNT u(cao)=&%0 u(adb)=setunion(get(u(adb)/%0),objid(%1));&D`ACCOUNT %1=%0;th setstat(u(adb)/%0`OLD,objid(%1),name(%1))
&INC`ACCOUNT`REMFROMACCOUNT u(cao)=&%0 u(adb)=setdiff(get(u(adb)/%0),objid(%1));&D`ACCOUNT %1;@switch/inline t(%1)=1,{th delstat(u(adb)/%0`OLD,objid(%1))}

&INC`ACCOUNT`UNBIND u(cao)=@include u(ccs)/INC`CHECKPC=%0,1,ACCOUNT;@assert words(setr(accounts,filterbool(#lambda/match(get(u(adb)/\%0),%q<t1objid>),lattr(u(adb)/*))))=@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) ERROR: %q<t1name> is not a member of any accounts.;@dolist/inline %q<accounts>={@include u(cao)/INC`ACCOUNT`REMFROMACCOUNT=%i0,%q<t1objid>};@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) You have removed %q<t1name> from Accounts: [itemize(%q<accounts>,%b,and,\,)];@nspemit %q<t1>=u(RFN`ACCOUNT`MSGHEAD) [ansi(h,%n)] has removed you from Accounts: [itemize(%q<accounts>,%b,and,\,)];@switch/inline gt(v(VAR`ACCOUNT`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ACCOUNT`ALERTSCHANNEL)=%n removed %q<t1name> to from Accounts: [itemize(%q<accounts>,%b,and,\,)]}

&VAR`ACCOUNT`ALERTMODE u(cao)=1
&VAR`ACCOUNT`ALERTSCHANNEL u(cao)=u(CMO`STAFFREP)

&INC`ACCOUNT`UNBOUND u(cao)=@assert words(setr(unbound,lsearch(all,type,player,elock,!D`ACCOUNT:?*)))=@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) There are no accountless PCs in the Database.;@nspemit %#=u(RFN`ACCOUNT`HEADER,Unbound Characters);@nspemit %#=align(-6 3 3 27 25 9,Dbref,Sta,Alt,Name,IP,Last On);@nspemit %#=u(RFN`ACCOUNT`SEPARATOR);@dolist/inline %q<unbound>={@nspemit %#=align(-6 3 -3 27 25 9,num(%i0),switch(1,isadmin(%i0),ansi(hy,ADM),haspower(%i0,BUILDER),ansi(hm,BUI),approved(%i0),ansi(hg,APP),ansi(hr,---)),default(%i0/D`ALTS,0),pueblize(name(%i0),+finger [name(%i0)]),pueblize(get(%i0/LASTIP),+ip [name(%i0)]),if(gte(conn(%i0),0),ansi(hg,ago(idle(%i0))),ansi(hx,elements(get(%i0/LAST),2 3))))};@nspemit %#=u(RFN`ACCOUNT`HEADER)

&INC`ACCOUNT`EMAIL u(cao)=@switch/inline config(player_creation)=Yes,{@assert setr(accid,accid(%:))=@nspemit %#=ERROR: This character is not bound to an Account.;&%q<accid>`EMAIL u(adb)=%0;@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) You have [if(strlen(%0),set your Account email to: %0,unset your Account email.)]},{@assert isadmin(%#)=@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) Only Staff may set emails!;@include u(ccs)/INC`CHECKPC=%0,1,ACCOUNT;@assert setr(accid,accid(%q<t1objid>))=@nspemit %#=ERROR: %q<t1name> is not bound to an Account.;&%q<accid>`EMAIL u(adb)=%1;@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) %q<t1name>'s Email has been set to: %1}

&INC`ACCOUNT`DELETE u(cao)=@assert strlen(%0)=@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) ERROR: Account ID empty.;@assert valnum(%0)=@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) ERROR: Accounts must be whole, positive numbers.;@assert hasattr(u(adb)/%0)=@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) ERROR: Account not found.;@include u(ccs)/INC`VERIFY={ansi(hr,WARNING:) This will delete Account %0. All records will be deleted. This will not delete any characters! Are you sure? Enter the command again within ten seconds to continue.},ACCOUNT DELETE %0,ACCOUNT;@wipe u(adb)/%0;@nspemit %#=u(RFN`ACCOUNT`MSGHEAD) Account '%0' deleted!;@switch/inline gt(v(VAR`ACCOUNT`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ACCOUNT`ALERTSCHANNEL)=%n deleted Account %0!}

&VAR`ACCOUNT`PLAYFLAGS u(cao)=EMAIL|NEW
&VAR`ACCOUNT`ADMINFLAGS u(cao)=BIND|UNBIND|UNBOUND|DELETE

&PLAYER`CREATE`ACCOUNT u(cao)=@assert cand(gt(words(setr(pcs,setdiff(filterbool(#lambda/not(haspower(\%0,GUEST)),iter(filterbool(#lambda/strmatch(get(u(ipdb)/\%0),get(%0/LASTIP)),iter(grepi(u(ipdb),*`*`TYPE,SUCCESS),elements(%i0,1 2,`,`))),first(%i0,`))),%0))),0),eq(words(setdiff(setunion(iter(%q<pcs>,u(u(cao)/FUN`FINDACCOUNT,objid(%i0))),),0)),1))={@wait 2=@nspemit %0=u(RFN`ACCOUNT`MSGHEAD) Welcome to [mudname()]! No account could be set for this PC based on IP records. Please contact Admin if this is an alt. If it is a new character, please type [pueblize(+account/new,+account/new)]};@break gt(words(setr(accid,setdiff(setunion(iter(%q<pcs>,accid(%i0)),),0))),1)={@wait 1=@nspemit %0=u(RFN`ACCOUNT`MSGHEAD) No account could be determined for this PC because your IP address matches characters from multiple accounts. Please contact an Admin to have your account set if this is an alt.};@include u(cao)/INC`ACCOUNT`ADDTOACCOUNT=%q<accid>,%0;@nspemit %0=u(RFN`ACCOUNT`MSGHEAD) You have been Added to Account %q<accid> based on your IP records. If this is in error, please contact an admin to have it corrected.;@switch/inline gt(v(VAR`ACCOUNT`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ACCOUNT`ALERTSCHANNEL)=[name(%0)] was automatically bound to [pueblize(Account %q<accid>,+account %q<accid>)]}

&PLAYER`CONNECT`ACCOUNT u(cao)=@break haspower(%0,GUEST);@assert gt(sub(secs(),csecs(%0)),300);@break accid(%0);@trigger u(cao)/PLAYER`CREATE`ACCOUNT=%0

&OBJECT`RENAME`FIXACCOUNT u(cao)=@switch/inline %2=PLAYER,{@assert setr(accid,accid(%0));th setstat(u(adb)/%q<accid>`OLD,%0,%1)}

&OBJECT`DESTROY`CLEANACCOUNTS u(cao)=@switch/inline %2=PLAYER,{@assert setr(accid,accid(%0,1));@include u(cao)/INC`ACCOUNT`REMFROMACCOUNT=%q<accid>,%0}

@@ ****** ALTS SECTION *******
&RFN`ALTS`MSGHEAD u(cao)=msghead(v(VAR`ALTS`MSGHEAD))
&VAR`ALTS`MSGHEAD u(cao)=ALTS

&CMD`+ALTS u(cao)=$^(?\:\+)?(alts)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%2,setunion(get(u(cao)/VAR`ALTS`PLAYFLAGS),if(isadmin(%#),get(u(cao)/VAR`ALTS`ADMINFLAGS)),|,|),|,ALTS,switch,switch;@include u(cao)/INC`ALTS`[strfirstof(%q<switch>,MAIN)]=%3,%4
@set u(cao)/CMD`+ALTS=regexp

&INC`ALTS`MAIN u(cao)=@include u(ccs)/INC`CHECKPC=strfirstof(%0,%#),1,ALTS;th setq(who,filtervis(%#,lwhoid(%#),WHO));@assert get(%q<t1>/D`ALTS)=@nspemit %#=u(RFN`ALTS`MSGHEAD) [name(%q<t1>)] is not on any alts lists.;@nspemit %#=u(RFN`ALTS`MSGHEAD) Alts of [ansi(h,%q<t1name>)] ([hideidle(%q<t1>)]): [u(FUN`ALTS`LIST,%q<t1>)]

&INC`ALTS`LIST u(cao)=@assert setr(accid,accid(%:))=@nspemit %#=u(RFN`ALTS`MSGHEAD) ERROR: This character is not bound to an account.;@assert strlen(%0)=@nspemit %#=u(RFN`ALTS`MSGHEAD) ERROR: List or target field empty. Please enter a name or a number to list this alt under.;@switch/inline cand(strlen(%0),strlen(%1))=1,{@include u(ccs)/INC`CHECKPC=%0,1,ALTS;@assert strmatch(accid(%q<t1objid>),%q<accid>)=@nspemit %#=u(RFN`ALTS`MSGHEAD) ERROR: %q<t1name> is not one of your registered characters.;@assert cand(gte(setr(list,%1),0),isint(%1))=@nspemit %#=u(RFN`ALTS`MSGHEAD) ERROR: Alts list must be a whole number 0 or greater.},0,{@include u(ccs)/INC`CHECKPC=%#,1,ALTS;@assert cand(gte(setr(list,%0),0),isint(%0))=@nspemit %#=u(RFN`ALTS`MSGHEAD) ERROR: Alts list must be a whole number 0 or greater.};&D`ALTS %q<t1>=%q<list>;@nspemit %#=u(RFN`ALTS`MSGHEAD) Okay! %q<t1name> will [if(%q<list>,now appear in Alts List #%q<list>.,not appear in any Alts lists.)]

&FUN`ALTS`LIST u(cao)=if(get(%0/D`ALTS),strfirstof(itemize(iter(setdiff(filterbool(#lambda/strmatch(default(%0/D`ALTS,0),default(\%0/D`ALTS,0)),get(u(adb)/[strfirstof(accid(objid(%0)),0)])),objid(%0),%b,namei),name(%i0)[if(not(%1),\([hideidle(%i0)]\))],%b,|),|,and,\,),None),None)

&VAR`ALTS`PLAYFLAGS u(cao)=LIST
&VAR`ALTS`ADMINFLAGS u(cao)=

@@ ******* IP SECTION *********
&CMD`+IP u(cao)=$^(?\:\+)?(ip|sitematch|matchsite)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%2,setunion(get(u(cao)/VAR`IP`PLAYFLAGS),if(isadmin(%#),get(u(cao)/VAR`IP`ADMINFLAGS)),|,|),|,IP,switch,switch;@include u(cao)/INC`IP`[strfirstof(%q<switch>,MAIN)]=%3,%4
@set u(cao)/CMD`+IP=regexp

&INC`IP`MAIN u(cao)=@break haspower(%#,GUEST)=@nspemit %#=ERROR: Guests cannot use +ip.;@switch/inline isadmin(%#)=0,{@include u(cao)/INC`IP`IPSHOW=%#,0},{@include u(ccs)/INC`CHECKPC=strfirstof(%0,%#),1,IP;@break haspower(%q<t1>,GUEST)=@nspemit %#=ERROR: To match Guests, please use +ip/guests;@include u(cao)/INC`IP`IPSHOW=%q<t1>,0}

&INC`IP`GUESTS u(cao)=@switch/inline isadmin(%#)=0,{@include u(cao)/INC`IP`IPSHOW=%#,1},{@include u(ccs)/INC`CHECKPC=%0,1,IP;@include u(cao)/INC`IP`IPSHOW=%q<t1>,1}

&INC`IP`IP u(cao)=@assert strlen(%0)=@nspemit %#=msghead(IP) ERROR: An IP address is required.;@include u(cao)/INC`IP`IPSHOW=%0,1,1

&INC`IP`IPSHOW u(cao)=@assert gt(words(setr(ips,if(%2,%0,setunion(iter(filterbool(#lambda/strmatch(get(u(ipdb)/\%0`TYPE),SUCCESS),lattr(u(ipdb)/%0`*)),get(u(ipdb)/%i0)),)))),0)=@nspemit %#=msghead(IP) ERROR: No IP logs found to match against.;th setq(pcs,filterbool(#lambda/hastype(\%0,PLAYER),unique(iter(filterbool(#lambda/strmatch(get(u(ipdb)/\%0`TYPE),SUCCESS),iter(%q<ips>,grepi(u(ipdb),*`*,%i0))),first(%i0,`)),namei)));@switch/inline %1=0,{th setq(pcs,filterbool(#lambda/not(haspower(\%0,GUEST)),%q<pcs>))};@assert words(%q<pcs>)=@nspemit %#=msghead(IP) ERROR: No Matching Logs found.;@nspemit %#=header(Site matches for [if(%2,%0,name(%0))][if(%1,%B\(Showing Guests\))]);th iter(lnum(1,ceil(fdiv(words(setr(list,%q<pcs>),|),30))),nspemit(%#,iter(elements(%q<list>,lnum(add(1,mul(sub(inum(0),1),30)),mul(inum(0),30)),%B,|),ljust(ansi(h,pueblize(name(%i0),+finger [name(%i0)])\(%i0\)),50,.)[get(%i0/LAST)]%RRecent Sites: [edit(setunion(iter(lattr(u(ipdb)/%i0`*),get(u(ipdb)/%i0)),),get(%i0/LASTIP),ansi(hc,get(%i0/LASTIP)))]%R[ansi(h,Successful Logins:)]%R[iter(filterbool(#lambda/strmatch(SUCCESS,get(u(ipdb)/\%0`TYPE)),sort(lattr(u(ipdb)/%i0`*))),From [get(u(ipdb)/%i0)] On [convsecs(get(u(ipdb)/%i0`WHEN))],%B,%R)]%R[ansi(h,Failed Logins:)]%R[iter(filterbool(#lambda/strmatch(FAILURE,get(u(ipdb)/\%0`TYPE)),sort(lattr(u(ipdb)/%i0`*))),From [get(u(ipdb)/%i0)] On [convsecs(get(u(ipdb)/%i0`WHEN))] Because [get(u(ipdb)/%i0`REASON)],%B,%R)]%R,|,%R)));@nspemit %#=header()

&TRG`IP`CLEANCONNLOGS u(cao)=@assert gt(words(setr(logs,sortkey(#lambda/last(\%0,`),lattr(u(ipdb)/[num(%0)]`*),n))),10);@dolist elements(%q<logs>,1 2)={@wipe u(ipdb)/%i0}

&PLAYER`CONNECT`IPLOG u(cao)=&[setr(iplog,num(%0)`[nextslot(u(ipdb),num(%0))])] u(ipdb)=get(%0/LASTIP);&%q<iplog>`WHEN u(ipdb)=secs();&%q<iplog>`TYPE u(ipdb)=SUCCESS;&%q<iplog>`SITE u(ipdb)=get(%0/LASTSITE);@trigger u(cao)/TRG`IP`CLEANCONNLOGS=%0

&SOCKET`LOGINFAIL`IPLOG u(cao)=@switch/inline isdbref(%4)=1,{&[setr(iplog,num(%4)`[nextslot(u(ipdb),num(%4))])] u(ipdb)=%1;&%q<iplog>`WHEN u(ipdb)=secs();&%q<iplog>`TYPE u(ipdb)=FAILURE;&%q<iplog>`SITE u(ipdb)=host(%0);&%q<iplog>`REASON u(ipdb)=%3;@trigger u(cao)/TRG`IP`CLEANCONNLOGS=%4};@select/inline 1=strmatch(#1,%4),{@nscemit/noisy u(VAR`IP`ALERTSCHANNEL)=ansi(hrf,WARNING:) 'IP %1 (descriptor: %0) - Host [host(%0)]' attempted and failed to login to the God (#1) Character!;@pemit/port %0={ansi(hrf,WARNING:) Unauthorized access attempt for an admin character detected. This action has been logged.}},isadmin(%4),{@nscemit/noisy u(VAR`IP`ALERTSCHANNEL)=ansi(hrf,WARNING:) 'IP %1 (descriptor: %0) - Host [host(%0)]' attempted and failed to login to ADMIN %4 [pueblize(name(%4),+ip [name(%4)])]!;@pemit/port %0={ansi(hrf,WARNING:) Unauthorized access attempt for an admin character detected. This action has been logged.}},isdbref(%4),{@switch/inline gt(v(VAR`IP`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`IP`ALERTSCHANNEL)=ansi(hr,WARNING:) [pueblize(name(%4),+ip [name(%4)])] has failed to login %2 times. Reason: %3. [if(strmatch(get(%4/LASTSITE),host(%0)),,ansi(hr,ALERT:) Connection attempt does not match previous LASTSITE. Descriptor: %0. IP: %1. HOST: [host(%0)])]}}

&VAR`IP`ALERTMODE u(cao)=1
&VAR`IP`ALERTSCHANNEL u(cao)=u(CMO`STAFFREP)

&VAR`IP`ADMINFLAGS u(cao)=GUESTS|IP

&OBJECT`DESTROY`IPLOGS u(cao)=@switch/inline %2=PLAYER,{@wipe u(ipdb)/[before(%0,:)]}

@@ Convert from old Ip to new IP.
@@ @dolist lsearch(all,eplayer,\[words(lattr(##/D`IPLOG`*))\])={@include/nobreak u(ccs)/INC`MVTREE=%i0,D`IPLOG,u(ipdb),%i0}

@@ ***** BANLIST SECTION *******
&RFN`BANLIST`MSGHEAD u(cao)=msghead(v(VAR`BANLIST`MSGHEAD))
&VAR`BANLIST`MSGHEAD u(cao)=BANLIST

&RFN`BANLIST`HEADER u(cao)=header(%0,,BANLIST`BORDER,BANLIST`BORDERDOT,BANLIST`BORDERTEXT)
&RFN`BANLIST`SEPARATOR u(cao)=separator(%0,,BANLIST`BORDER,BANLIST`BORDERDOT,BANLIST`BORDERTEXT)
&RFN`BANLIST`SUBHEADER u(cao)=subheader(%0,,BANLIST`BORDER,BANLIST`BORDERDOT,BANLIST`BORDERTEXT)

&CMD`+BANLIST u(cao)=$^(?\:\+)?(banlist)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@assert isadmin(%#)=@force/inline %#=HUH_COMMAND;@include u(ccs)/INC`PARTIAL=%2,setunion(get(u(cao)/VAR`BANLIST`PLAYFLAGS),if(isadmin(%#),get(u(cao)/VAR`BANLIST`ADMINFLAGS)),|,|),|,BANLIST,switch,switch;@include u(cao)/INC`BANLIST`[strfirstof(%q<switch>,MAIN)]=trim(%3),trim(%4)
@set u(cao)/CMD`+BANLIST=regexp

&VAR`BANLIST`ADMINFLAGS u(cao)=BANIP|BANPLAYER|DELETE|DURATION|COMMENT|MESSAGE

&INC`BANLIST`MAIN u(cao)=@switch/inline t(strlen(%0))=0,{@include u(cao)/INC`BANLIST`LIST},1,{@include u(cao)/INC`BANLIST`DETAILS}

&INC`BANLIST`ID u(cao)=@assert valnum(%0)=@nspemit %#=u(RFN`BANLIST`MSGHEAD) Entry must be in ID number format.;@assert hasattr(u(bandb)/%0)=@nspemit %#=u(RFN`BANLIST`MSGHEAD) Entry '%0' not found.;

&INC`BANLIST`LIST u(cao)=@nspemit %#=u(RFN`BANLIST`HEADER,mudname() Ban List);@nspemit %#=align(4 21 25 25,ID,IP,Affected,Until);@dolist/inline setr(list,sort(reglattr(u(bandb)/^\\d+$)))={@nspemit %#=align(4 21 25 25,%i0,get(u(bandb)/%i0),get(u(bandb)/%i0`AFFNAMES),setq(col,if(gte(get(u(bandb)/%i0`UNTIL),secs()),n,hr))[ansi(%q<col>,convsecs(get(u(bandb)/%i0`UNTIL)))])};@nspemit %#=u(RFN`BANLIST`SUBHEADER,words(%q<list>) Entries)

&INC`BANLIST`DETAILS u(cao)=@include u(cao)/INC`BANLIST`ID;@nspemit %#=u(RFN`BANLIST`HEADER,Banlist Entry %0);@nspemit %#=IP: [get(u(bandb)/%0)];@nspemit %#=AFFECTED: [get(u(bandb)/%0`AFFNAMES)];@nspemit %#=Set by [name(get(u(bandb)/%0`SETBY))] on [convsecs(get(u(bandb)/%0`SETON))];@dolist/inline sortkey(#lambda/last(\%0,`),lattr(u(bandb)/%0`COMMENT`*))={@nspemit %#=u(RFN`BANLIST`SEPARATOR,Comment [last(%i0,`)]);@nspemit %#=name(get(u(bandb)/%i0`BY)) commented on [convsecs(get(u(bandb)/%i0`ON))]:%R[get(u(bandb)/%i0)]};@nspemit %#=u(RFN`BANLIST`HEADER)

&INC`BANLIST`BANIP u(cao)=@assert hasflag(%#,Wizard)=@nspemit %#=u(RFN`BANLIST`MSGHEAD) Permission denied.;@assert strlen(setr(ip,%0))=@nspemit %#=u(RFN`BANLIST`MSGHEAD) ERROR: No IP entered to ban.;@include u(cao)/INC`BANLIST`BAN=%0,trim(before(%1,/)),trim(after(%1,/))

&INC`BANLIST`BANPLAYER u(cao)=@assert hasflag(%#,Wizard)=@nspemit %#=u(RFN`BANLIST`MSGHEAD) Permission denied.;@include u(ccs)/INC`CHECKPC=%0,1,BANLIST;th setq(ip,get(%q<t1>/lastip));@include u(cao)/INC`BANLIST`BAN=%0,trim(before(%1,/)),trim(after(%1,/))

&INC`BANLIST`BAN u(cao)=@include u(cao)/INC`BANLIST`BADMATCH=%q<ip>;@break words(setr(banned,u(FUN`CHECKBAN,%q<ip>)))=@nspemit %#=u(RFn`BANLIST`MSGHEAD) The IP matches (without wildcards) existing entries in the Ban Database: [itemize(%q<banned>)];@assert strlen(%1)=@nspemit %#=u(RFN`BANLIST`MSGHEAD) ERROR: Duration value empty.;@assert isint(setr(dur,stringsecs(%1)))=@nspemit %#=u(RFN`BANLIST`MSGHEAD) ERROR: Duration did not compute to a time value. see help stringsecs() for proper syntax.;@assert strlen(%2)=@nspemit %#=u(RFN`BANLIST`MSGHEAD) ERROR: A comment is necessary to apply a ban.;th setq(affects,u(FUN`BANAFFECTS,%q<ip>));@include u(ccs)/INC`VERIFY={WARNING: This will affect sites used by [setr(affnames,strfirstof(itemize(iter(%q<affects>,name(%i0),%b,|),|,and,\,),<nobody>))] for [etime(%q<dur>)]. These characters will be @booted upon completion. Are you sure? Enter again within ten seconds to verify.},BAN %q<affects> %q<dur>,BANLIST;&[setr(banid,nextslot(u(bandb)))] u(bandb)=%q<ip>;@include u(cao)/INC`BANLIST`COMMENT`MAKE=%q<banid>,%2;&%q<banid>`AFFECTED u(bandb)=%q<affects>;&%q<banid>`AFFNAMES u(bandb)=%q<affnames>;&%q<banid>`UNTIL u(bandb)=setr(end,add(secs(),%q<dur>));&%q<banid>`SETON u(bandb)=secs();&%q<banid>`SETBY u(bandb)=%:;@dolist/inline filterbool(#lambda/conn(\%0),%q<affects>)={@nspemit %i0=You have been banned from [mudname()] until [convsecs(%q<end>)].;@boot %i0};@nscemit/noisy u(VAR`IP`ALERTSCHANNEL)={[ansi(h,\[%n\])] banned %q<ip> ([ansi(h,%q<affnames>)]) until [convsecs(%q<end>)] for: %2};@nspemit %#=u(RFN`BANLIST`MSGHEAD) Banned.

&INC`BANLIST`BADMATCH u(cao)=th foreach(#lambda/setr(quest,add(%q<quest>,strmatch(?,\%0))),%0);@break %q<quest>=@nspemit %#=u(RFn`BANLIST`MSGHEAD) ERROR: Question marks are not allowed in IP entries.;th foreach(#lambda/setr(aster,add(%q<aster>,strmatch(*,\%0))),%0);@break gt(%q<aster>,1)=@nspemit %#=u(RFN`BANLIST`MSGHEAD) ERROR: no more than one asterisk per match.;@break cor(strmatch(*.,%0),strmatch(*.,%0))=@nspemit %#=u(RFN`BANLIST`MSGHEAD) ERROR: That would ban EVERYONE.;@break and(%q<aster>,not(strmatch(*,right(%0,1))))=@nspemit %#=u(RFN`BANLIST`MSGHEAD) ERROR: Asterisks may only be at the end of an IP entry.

&INC`BANLIST`DELETE u(cao)=@assert hasflag(%#,Wizard)=@nspemit %#=u(RFN`BANLIST`MSGHEAD) Permission denied.;@include u(cao)/INC`BANLIST`ID;@include u(ccs)/INC`VERIFY={ansi(hr,WARNING:) This will delete Banlist entry %0. Are you sure? Enter again within ten seconds to verify.},BANLIST DELETE %0,BANLIST;@nscemit/noisy u(VAR`IP`ALERTSCHANNEL)={[ansi(h,\[%n\])] lifted BanID %0: [get(u(bandb)/%0)] ([get(u(bandb)/%0`AFFNAMES)]).};@nspemit %#=u(RFN`BANLIST`MSGHEAD) Unbanned.;@wipe u(bandb)/%0

&INC`BANLIST`DURATION u(cao)=@assert hasflag(%#,Wizard)=@nspemit %#=u(RFN`BANLIST`MSGHEAD) Permission denied.;@include u(cao)/INC`BANLIST`ID;@assert strlen(%1)=@nspemit %#=u(RFN`BANLIST`MSGHEAD) ERROR: Duration value empty.;@assert isint(setr(dur,stringsecs(%1)))=@nspemit %#=u(RFN`BANLIST`MSGHEAD) ERROR: Duration did not compute to a time value. see help stringsecs() for proper syntax.;@nscemit/noisy u(VAR`IP`ALERTSCHANNEL)={[ansi(h,\[%n\])] reset ban duration for BanID %0: [get(u(bandb)/%0)] ([get(u(bandb)/%0`AFFNAMES)]) to [etime(%q<dur>)] - until [convsecs(add(secs(),%q<dur>))]};&%0`UNTIL u(bandb)=add(secs(),%q<dur>);@nspemit %#=u(RFN`BANLIST`MSGHEAD) Duration reset.

&INC`BANLIST`COMMENT u(cao)=@include u(cao)/INC`BANLIST`ID;@assert strlen(%1)=@nspemit %#=u(RFN`BANLIST`MSGHEAD) Comment text field empty.;@include u(cao)/INC`BANLIST`COMMENT`MAKE;@nspemit %#=u(RFN`BANLIST`MSGHEAD) Comment posted.;@nscemit/noisy u(VAR`IP`ALERTSCHANNEL)={[ansi(h,\[%n\])] new comment posted to [pueblize(+banlist %0,+banlist %0)]}

&INC`BANLIST`COMMENT`MAKE u(cao)=&[setr(attr,%0`COMMENT`[nextslot(u(bandb),%0`COMMENT)])] u(bandb)=%1;&%q<attr>`ON u(bandb)=secs();&%q<attr>`BY u(bandb)=%:

&INC`BANLIST`MESSAGE u(cao)=@assert hasflag(%#,Wizard)=@nspemit %#=u(RFN`BANLIST`MSGHEAD) Permission denied.;@include u(cao)/INC`BANLIST`ID;

&FUN`BANAFFECTS u(cao)=setunion(iter(wildgrepi(u(ipdb),*`*,%0),first(%i0,`)),,,namei)

&FUN`CHECKBAN u(cao)=filterbool(#lambda/eq(comp(%0,get(u(bandb)/\%0),I),0),reglattr(u(bandb)/^\\d+$))

&FUN`CHECKLOGINS u(cao)=filterbool(#lambda/strmatch(%0,get(u(bandb)/\%0)),reglattr(u(bandb)/^\\d+$))

&SOCKET`CONNECT`BANLIST u(cao)=@assert setr(banid,first(u(FUN`CHECKLOGINS,%1)));@switch/inline gt(secs(),get(u(bandb)/%q<banid>`UNTIL))=1,{@nscemit/noisy u(VAR`IP`ALERTSCHANNEL)={ansi(hr,WARNING:) EXPIRED Banned address %1 (match: BanID %q<banid>: [get(u(bandb)/%q<banid>)]) has connected.}},0,{@nspemit/port %0=setr(message,strfirstof(get(u(bandb)/%q<banid>`MESSAGE),u(VAR`BANLIST`MESSAGE)));@boot/port %0;@nscemit/noisy u(VAR`IP`ALERTSCHANNEL)={ansi(hr,WARNING:) Banned address %1 (match: BanID %q<banid>: [get(u(bandb)/%q<banid>)]) attempted to connect. Message sent to them: '%q<message>'}}

&VAR`BANLIST`MESSAGE u(cao)=Your site has been banned from [mudname()]. Contact staff at [v(VAR`BANLIST`EMAIL)] with any inquiries.

&VAR`BANLIST`EMAIL u(cao)=mcmush@gmail.com

@@ ****** APPROVAL SECTION *********
&CMD`+APPROVE u(cao)=$^(?\:\+)?(approve|unapprove)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%2,setunion(get(u(cao)/VAR`%1`PLAYFLAGS),if(isadmin(%#),get(u(cao)/VAR`%1`ADMINFLAGS)),|,|),|,%1,switch,switch;@include u(cao)/INC`%1`[strfirstof(%q<switch>,MAIN)]=%3,%4
@set u(cao)/CMD`+APPROVE=regexp

&PLAYER`CREATE`UNREGISTER u(cao)=@set %0=UNREGISTERED

&INC`APPROVE`MAIN u(cao)=@switch/inline cand(isadmin(%#),strlen(%0))=1,{@include u(ccs)/INC`CHECKPC=%0,1,APPROVE;@break approved(%q<t1>)=@nspemit %#=msghead(APPROVE) ERROR: They are already approved!;@include u(cao)/INC`APPROVE`DOAPPROVE=%q<t1>;@nspemit %#=msghead(APPROVE) You have approved %q<t1name> for play!;@switch/inline gt(v(VAR`APPROVE`ALERTMODE),0)={@nscemit/noisy u(VAR`APPROVE`ALERTSCHANNEL)=%n has approved %q<t1name> for play!;@force/inline %q<t1>=+recall;@nscemit/noisy u(VAR`APPROVE`ANNOUNCECHANNEL)=ansi(h,%n) just approved %q<t1name> for play!}},{@nspemit %#=msghead(APPROVE) You [if(approved(%#),ansi(hg,are),ansi(hr,are not))] are approved for play!}

&INC`APPROVE`DOAPPROVE u(cao)=@set %0=!UNREGISTERED
&INC`APPROVE`DOUNAPPROVE u(cao)=@set %0=UNREGISTERED

&INC`UNAPPROVE`MAIN u(cao)=@switch/inline isadmin(%#)=1,{@include u(ccs)/INC`CHECKPC=%0,1,APPROVE;@assert approved(%q<t1>)=@nspemit %#=msghead(APPROVE) ERROR: They are not approved!;@include u(cao)/INC`APPROVE`DOUNAPPROVE=%q<t1>;@nspemit %#=msghead(APPROVE) You have unapproved %q<t1name> for play!;@switch/inline gt(v(VAR`APPROVE`ALERTMODE),0)={@nscemit/noisy u(VAR`APPROVE`ALERTSCHANNEL)=%n has unapproved %q<t1name> for play!}},{@nspemit %#=msghead(APPROVE) You [if(approved(%#),ansi(hg,are),ansi(hr,are not))] are approved for play!}

&GFN`APPROVED u(cao)=cor(not(hasflag(%0,UNREGISTERED)),isadmin(%0))

&APPROVE u(ae)=approved(%#)
&isadmin u(ae)=isadmin(%#)

&VAR`APPROVE`ALERTMODE u(cao)=1
&VAR`APPROVE`ALERTSCHANNEL u(cao)=u(CMO`STAFFREP)
&VAR`APPROVE`ANNOUNCECHANNEL u(cao)=u(CMO`PUBLIC)

@@ ********* STAFF SECTION ********
&CMD`+STAFF u(cao)=$^(?\:\+)?(staff|admin|wizlist)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%2,setunion(get(u(cao)/VAR`STAFF`PLAYFLAGS),if(isadmin(%#),get(u(cao)/VAR`STAFF`ADMINFLAGS)),|,|),|,%1,switch,switch;@include u(cao)/INC`STAFF`[strfirstof(%q<switch>,MAIN)]=%3,%4
@set u(cao)/CMD`+STAFF=regexp

&GFN`ISADMIN u(cao)=orflags(%0,Wr)

&FUN`STAFF`CHSTAFF u(cao)=orflags(%0,W)

&INC`WIZLIST`MAIN u(cao)=@include u(cao)/INC`STAFF`MAIN
&INC`ADMIN`MAIN u(cao)=@include u(cao)/INC`STAFF`MAIN

&VAR`STAFF`ADMINFLAGS u(cao)=ADD|REM|ORDER|ON|OFF|VAC

&INC`STAFF`MAIN u(cao)=@assert words(setr(staff,filterbool(#lambda/isobjid(\%0),iter(sortkey(#lambda/after(\%0,~),get(u(pco)/WIZLIST),n,|,|),before(%i0,~),|,|),|,|)),|)=@nspemit %#=msghead(STAFF) ERROR: There are no Staff registered!;@nspemit %#=header(mudname() Staff);@nspemit %#=align(3 20 3 40 9,Idl,Name,Bit,Position,Duty);@@ @nspemit %#=separator();@dolist/inline/delimit | %q<staff>=@nspemit %#=u(u(cao)/FUN`Staff`WIZLIST,%i0,%#);@switch/inline t(strlen(get(u(pco)/WIZLIST`MSG)))=1,{@nspemit %#=separator();@nspemit %#=u(u(pco)/WIZLIST`MSG)};@nspemit %#=header()

&FUN`STAFF`WIZLIST u(cao)=align(3 20 3 40 9,switch([gte(conn(%0),0)][cor(hidden(%0),hasflag(%0,DARK))][isadmin(%1)],0*,ansi(hx,Off),111,ansi(hx,etime(idle(%0),4)),10*,ansi(hg,etime(idle(%0),4)),ansi(hx,Off)),name(%0),switch(1,hasflag(%0,WIZARD),ansi(hr,WIZ),hasflag(%0,ROYALTY),ansi(hc,ROY),hasflag(%0,STAFF),ansi(hg,STF),hasflag(%0,JUDGE),ansi(hy,JDG),ansi(hx,N/A)),default(%0/JOB,???),switch(hasflag(%0,ON-VACATION)[gte(conn(%0),0)][cor(hidden(%0),hasflag(%0,DARK))][isadmin(%1)],1*,Vacation,0111,ansi(hx,if(get(%0/duty),On,Off)),0110,ansi(hx,Off),00*,ansi(hx,Off),010*,if(get(%0/duty),ansi(hg,On),ansi(hx,Off))))

&INC`STAFF`ADD u(cao)=@include u(ccs)/INC`CHECKPC=%0,1,STAFF;@break strlen(getstat(u(pco)/WIZLIST,%q<t1objid>))=@nspemit %#=msghead(STAFF) ERROR: They are already on the Staff list!;@switch/inline gt(strlen(%1),0)=1,{@assert valnum(%1)=@nspemit %#=msghead(STAFF) ERROR: Order Must be a whole positive number.;th setstat(u(pco)/WIZLIST,%q<t1objid>,%1)},0,{th setstat(u(pco)/WIZLIST,%q<t1objid>,setr(order,add(1,iter(get(u(pco)/WIZLIST),last(%i0,~)))))};@nspemit %#=msghead(STAFF) You have added %q<t1name> to Staff!;@nspemit %q<t1>=msghead(STAFF) You have been added to Staff!;@switch/inline gt(v(VAR`STAFF`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`STAFF`ALERTSCHANNEL)=ansi(h,%n) has added %q<t1name> to Staff!}

&INC`STAFF`REM u(cao)=@include u(ccs)/INC`CHECKPC=%0,1,STAFF;@assert strlen(getstat(u(pco)/WIZLIST,%q<t1objid>))=@nspemit %#=msghead(STAFF) ERROR: They are not on the Staff list!;@nspemit %#=msghead(STAFF) You have removed %q<t1name> from Staff!;@nspemit %q<t1>=msghead(STAFF) You have been removed from Staff!;th delstat(u(pco)/WIZLIST,%q<t1objid>);@switch/inline gt(v(VAR`STAFF`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`STAFF`ALERTSCHANNEL)=ansi(h,%n) has removed %q<t1name> from Staff!}

&INC`STAFF`VAC u(cao)=@switch/inline hasflag(%#,ON-VACATION)=0,{@nspemit %#=msghead(STAFF) You go On Vacation!;@set %#=ON-VACATION},1,{@nspemit %#=msghead(STAFF) You come off Vacation!;@set %#=!ON-VACATION}

&INC`STAFF`ORDER u(cao)=@include u(ccs)/INC`CHECKPC=%0,1,STAFF;@assert strlen(getstat(u(pco)/WIZLIST,%q<t1objid>))=@nspemit %#=msghead(STAFF) ERROR: They are not on the Staff list!;@assert strlen(%1)=@nspemit %#=msghead(STAFF) ERROR: Order field empty.;@assert valnum(%1)=@nspemit %#=msghead(STAFF) ERROR: Order Must be a whole positive number.;th setstat(u(pco)/WIZLIST,%q<t1objid>,%1);@nspemit %#=msghead(STAFF) You have set %q<t1name>'s Staff order to %1!;@nspemit %q<t1>=%n has set your Staff order to %1!;@switch/inline gt(v(VAR`STAFF`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`STAFF`ALERTSCHANNEL)=ansi(h,%n) has set %q<t1name>'s Staff order to %1!}

&INC`STAFF`ON u(cao)=@switch/inline t(default(%#/DUTY,0))=1,{@nspemit %#=msghead(STAFF) You are already On-duty!},0,{@nspemit %#=msghead(STAFF) You are now ON DUTY!;&duty %#=1;@@ @switch/inline nor(hasflag(%#,DARK),hidden(%#))=1,{@nscemit/noisy u(CMO`PUBLIC)=[ansi(h,%n)] is now ON DUTY!}}

&INC`STAFF`OFF u(cao)=@switch/inline t(default(%#/DUTY,0))=0,{@nspemit %#=msghead(STAFF) You are already Off-duty!},1,{@nspemit %#=msghead(STAFF) You are now OFF DUTY!;&duty %#=0;@@ @switch/inline nor(hasflag(%#,DARK),hidden(%#))=1,{@nscemit/noisy u(CMO`PUBLIC)=[ansi(h,%n)] is now OFF DUTY!}}

&VAR`STAFF`ALERTMODE u(cao)=1
&VAR`STAFF`ALERTSCHANNEL u(cao)=u(CMO`STAFFREP)

&OBJECT`DESTROY`CLEANSTAFF u(cao)=@switch/inline %2=PLAYER,{th delstat(u(pco)/WIZLIST,%0)}

&PLAYER`CONNECT`STAFFDUTY u(cao)=@switch/inline t(getstat(u(pco)/WIZLIST,%0))=1,{@wait 3=@nspemit %0=msghead(STAFF) You are [if(hasflag(%0,DARK),DARK,not Dark)], [if(hidden(%0),Hidden,not Hidden)] and [if(hasflag(%0,VACATION),on Vacation,if(default(%0/duty,0),On Duty,Off Duty))].}

@@ ********* BUILDER SECTION ********
@switch/inline isdbref(u(build))=0,{@tel pcreate(Buildstaff,digest(md5,secs()))=u(cao)}
&build u(coi)=locate(u(cao),Buildstaff,PXxi)
@lock/zone u(build)=POWER^BUILDER
@set u(build)=!UNREGISTERED SHARED
@power u(build)=builder no_pay no_quota
@lset u(build)/zone=v

&OBJECT`CREATE`BUILDER u(cao)=@assert hastype(%0,ROOM EXIT);@assert haspower(owner(%0),BUILDER);@chown/preserve %0=u(build)

&CMD`+BUILDER u(cao)=$^(?\:\+)?(builder)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%2,setunion(get(u(cao)/VAR`BUILDER`PLAYFLAGS),if(isadmin(%#),get(u(cao)/VAR`BUILDER`ADMINFLAGS)),|,|),|,BUILDER,switch,switch;@include u(cao)/INC`BUILDER`[strfirstof(%q<switch>,MAIN)]=%3,%4
@set u(cao)/CMD`+BUILDER=regexp

&INC`BUILDER`MAIN u(cao)=@switch/inline isadmin(%#)=1,{@switch/inline strlen(%0)=0,{@assert words(setr(build,lsearch(all,eplayer,\[haspower(##,BUILDER)\])))=@nspemit %#=msghead(BUILDER) There are no Builder Characters.;@nspemit %#=header(mudname() Builder Characters);@nspemit %#=iter(%q<build>,ansi(h,%i0 - [name(%i0)]) - [powers(%i0)],%b,%R);@nspemit %#=header()},{@include u(ccs)/INC`CHECKPC=%0,1,BUILDER;@nspemit %#=header(%q<t1name>'s Belongings);@dolist/inline THING ROOM EXIT=@switch/inline gt(words(setr(search,lsearch(%q<t1>,type,%i0))),0)=1,{@nspemit %#=subheader(Type: %i0);@dolist/inline %q<search>=@nspemit %#=[rjust(pueblize(%i0,brief %i0),7)] - [name(%i0)]};@nspemit %#=header()}},0,{@nspemit %#=msghead(BUILDER) You [if(haspower(%#,BUILDER),ansi(hg,are),ansi(hr,are not))] A Builder!}

&INC`BUILDER`ADD u(cao)=@include u(ccs)/INC`CHECKPC=%0,1,BUILDER;@break haspower(%q<t1>,BUILDER)=@nspemit %#=msghead(BUILDER) ERROR: They are already a Builder.;@set %q<t1>=!UNREGISTERED;@power %q<t1>=builder no_quota tel_anywhere open_anywhere link_anywhere;give %q<t1>=100000;@nspemit %#=msghead(BUILDER) You have made %q<t1name> a Builder.;@nspemit %q<t1>=msghead(BUILDER) %n has granted you Builder powers.;@switch/inline gt(v(VAR`BUILDER`ALERTMODE),0)={@nscemit/noisy u(VAR`BUILDER`ALERTSCHANNEL)=%n has made %q<t1name> a Builder.}

&INC`BUILDER`REM u(cao)=@include u(ccs)/INC`CHECKPC=%0,1,BUILDER;@assert haspower(%q<t1>,BUILDER)=@nspemit %#=msghead(BUILDER) ERROR: They are not a Builder.;@set %q<t1>=!UNREGISTERED;@power %q<t1>=!builder !no_quota !tel_anywhere !open_anywhere !link_anywhere;@switch/inline lt(money(%q<t1>),100000)=1,{give %q<t1>=100000};give %q<t1>=-99000;@nspemit %#=msghead(BUILDER) You have revoked %q<t1name>'s Builder powers.;@nspemit %q<t1>=msghead(BUILDER) %n has revoked your Builder powers.;@switch/inline gt(v(VAR`BUILDER`ALERTMODE),0)={@nscemit/noisy u(VAR`BUILDER`ALERTSCHANNEL)=%n has revoked %q<t1name>'s Builder powers.}

&VAR`BUILDER`ADMINFLAGS u(cao)=ADD|REM

&VAR`BUILDER`ALERTMODE u(cao)=1
&VAR`BUILDER`ALERTSCHANNEL u(cao)=u(CMO`STAFFREP)

@@ ******** IDLERS SECTION ******
&CMD`+IDLERS u(cao)=$^(?\:\+)?(idlers)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%2,setunion(get(u(cao)/VAR`%1`PLAYFLAGS),if(isadmin(%#),get(u(cao)/VAR`%1`ADMINFLAGS)),|,|),|,%1,switch,switch;@include u(cao)/INC`%1`[strfirstof(%q<switch>,MAIN)]=%3,%4
@set u(cao)/CMD`+IDLERS=regexp

&INC`IDLERS`MAIN u(cao)=@switch/inline gt(strlen(%0),0)=1,{@assert valnum(setr(days,%0))=@nspemit %#=msghead(IDLERS) ERROR: Day field must be a whole, positive integer!},{th setq(days,30)};@assert words(setr(idlers,sortkey(#lambda/convtime(get(\%0/LASTLOGOUT)),setdiff(lsearch(all,eplayer,\[cand(gt(sub(secs(),convtime(get(##/LASTLOGOUT))),mul(86400,%q<days>)),not(haspower(##,GUEST)),not(isadmin(##)))\]),u(ap)),n,%b,|)))=@nspemit %#=msghead(IDLERS) Nobody's been offline longer than %q<days>.;@nspemit %#=header(Players over %q<days> Idle)%R[ljust(DBREF,7)][ljust(NAME,30)][ljust(STATUS,10)][ljust(IP,22)]LAST ON%R[subheader()];th iter(lnum(1,ceil(fdiv(words(setr(list,%q<idlers>),|),30))),nspemit(%#,iter(elements(%q<list>,lnum(add(1,mul(sub(inum(0),1),30)),mul(inum(0),30)),|,|),rjust(num(%i0),5) %B[ljust(pueblize(name(%i0),+finger [name(%i0)]),30)][ljust(if(isadmin(%i0),ansi(hy,ADM),if(approved(%i0),ansi(hg,APP),ansi(hr,UNAPP))),10)][ljust(pueblize(get(%i0/LASTIP),+ip [name(%i0)]),22)][if(gte(conn(%i0),0),ansi(hg,ago(idle(%i0))),ansi(hx,elements(get(%i0/LAST),2 3)))],|,%R)));@nspemit %#=header()

&VAR`IDLERS`ADMINFLAGS u(cao)=
&VAR`IDLERS`PLAYFLAGS u(cao)=